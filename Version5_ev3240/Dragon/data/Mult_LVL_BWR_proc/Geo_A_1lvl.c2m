* Deck Geo_A_1lvl 
* CLE-2000 procedure
* Author : R. Guasch
* Purpose : Generate and return 1st level geometry of AT10 assembly
* Consists of the initial cartesian grid to be populated by sub-geometries
*

* -------------------------------
*       PROCEDURE OUTPUT
* -------------------------------
PARAMETER GEOM ::
::: LINKED_LIST GEOM ; ;


* -------------------------------
*    STRUCTURES AND MODULES
* -------------------------------
MODULE  GEO: END: ;

!Assembly scale data

REAL Pitch_A W_gap := 15.24 0.75 ; ! Assembly pitch and water gap thickness
REAL Box_out Box_in Box_thi := 13.74 13.4 0.17 ; ! Outer box outer, inner sides and thickness
REAL Chan_out Chan_in Chan_thi := 3.5 3.34 0.08 ; ! Channel box outer, inner sides and thickness
REAL Pitch_C := 1.29500 ;

! Multiples of cell pitch : used to define 1st level geometry mesh 
REAL 2_Pitch_C 3_Pitch_C 4_Pitch_C ;
EVALUATE 2_Pitch_C := Pitch_C 2.0 * ;
EVALUATE 3_Pitch_C := Pitch_C 3.0 * ;
EVALUATE 4_Pitch_C := Pitch_C 4.0 * ;

! X1 = Water gap + outer box thickness +extra water : in box water not a multiple of Pitch_C
REAL XtraMode ;
EVALUATE XtraMode := Box_in 10.0 Pitch_C * - 2.0 / ;

!Meshing points for macro geometry :
REAL X1 X2 X3 X4  ;
EVALUATE X1 := W_gap Box_thi + XtraMode + ;
EVALUATE X2 := 4.0 Pitch_C * X1 + ;
EVALUATE X3 := 3.0 Pitch_C * X2 + ;
EVALUATE X4 := 3.0 Pitch_C * X3 + ;  

REAL Y1 Y2 Y3 Y4 ;
EVALUATE Y1 := X1 ;
EVALUATE Y2 := 4.0 Pitch_C * Y1 + ;
EVALUATE Y3 := 3.0 Pitch_C * Y2 + ;
EVALUATE Y4 := 3.0 Pitch_C * Y3 + ;

REAL L1 L2 L3 ;
EVALUATE L1 := X2 X1 - ;
EVALUATE L2 := X3 X2 - ;
EVALUATE L3 := X4 X3 - ;

REAL H1 H2 H3 ;
EVALUATE H1 := Y2 Y1 - ;
EVALUATE H2 := Y3 Y2 - ;
EVALUATE H3 := Y4 Y3 - ;

GEOM := GEO: :: CAR2D 5 5 
    EDIT 1
    X- DIAG X+ REFL
    Y- REFL Y+ DIAG
    MIX
    1  2  3  4  5 
       6  7  8  9
          10 11 12
             13 14
                15
    MESHX 0.0 <<X1>> <<X2>> <<X3>> <<X4>> <<Pitch_A>>
    MESHY 0.0 <<Y1>> <<Y2>> <<Y3>> <<Y4>> <<Pitch_A>>
;
! Using dummy mix numbers for 1st level geom.
ECHO "1 level GEOM_A generation done" ;
END: ;
QUIT .