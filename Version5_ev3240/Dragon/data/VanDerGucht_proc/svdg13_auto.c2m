PARAMETER XSM :: ::: XSM_FILE XSM ; ;
*----
*
*  TEST CASE uo2_295_c5 MODIFIED FOR CESCOL
*  UO2 ROWLAND'S BENCHMARK
*  DISTRIBUTED SELF-SHIELDING
*  295-GROUP JEF2.2 DRAGLIB
*
*  Author: A. Hebert
*
*----
*  Define STRUCTURES and MODULES used
*----
LINKED_LIST GEOM TRACK LIBRARY LIBRARY2 CP CALC ;
MODULE LIB: GEO: MAC: SYBILT: AUTO: ASM: FLU: EDI: ;
INTEGER COMB0101 COMB0102 COMB0103 COMB0104 COMB0105 COMB0106 GAIN1
        MODE1 MODE2 := 
        1 2 3 4 5 6 7 8 9 ; (* DISTRIBUTED SELF-SHIELDING *)
REAL RCOMB1 RCOMB2 RCOMB3 RCOMB4 RCOMB5 RCOMB6 ;
*
EVALUATE RCOMB1 RCOMB2 RCOMB3 RCOMB4 RCOMB5 RCOMB6 := 0.2529822 
0.334664 0.3577709 0.3794733 0.3898718 0.40 ;
GEOM := GEO: :: TUBE 9
      R+ REFL
      RADIUS 0.0 <<RCOMB1>> <<RCOMB2>> <<RCOMB3>> <<RCOMB4>>
                 <<RCOMB5>> <<RCOMB6>> 0.45 0.5748331 0.6770275
      MIX <<COMB0101>> <<COMB0102>> <<COMB0103>> <<COMB0104>>
          <<COMB0105>> <<COMB0106>> <<GAIN1>> <<MODE1>> <<MODE2>>
          ;
TRACK := SYBILT: GEOM ::
     TITLE 'UO2 ROWLAND S BENCHMARK'
     MAXR 20 QUA1 5 ;
LIBRARY := LIB: ::
 EDIT 1
 NMIX 9    (*MAXIMUM OF MATERIAL MIXTURES*)
 CTRA APOL (*APOLLO TYPE TRANSPORT CORRECTION*)
 ANIS 2
 SUBG
 ADED 4 NELAS N4N N2N N3N
*
 MIXS LIB: DRAGON FIL: DUMMYDA
 MIX <<COMB0101>> 673.6 (*COMB0101*)
    U238    = '2384001' 2.2604E-2 1
    O16     = '16'      4.6624E-2
 MIX <<COMB0102>> COMB 1 1.0
 MIX <<COMB0103>> COMB 1 1.0
 MIX <<COMB0104>> COMB 1 1.0
 MIX <<COMB0105>> COMB 1 1.0
 MIX <<COMB0106>> COMB 1 1.0
 MIX <<GAIN1>> 600.0 (*GAIN1*)
    Zr0     = '91'      4.3241E-2
 MIX <<MODE1>> 574.0 (*MODE1*)
    H1      = '1'       4.6892E-2
    O16     = '16'      2.3446E-2
 MIX <<MODE2>> 574.0 (*MODE2*)
    H1      = '1'       4.6892E-2
    O16     = '16'      2.3446E-2
 ;
LIBRARY2 := AUTO: LIBRARY TRACK :: EDIT 2 MAXT 50000 ;
LIBRARY2 := MAC: LIBRARY2 ::
     READ INPUT
     MIX <<MODE2>> FIXE
     1.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0 0.0
     0.0 0.0 0.0 0.0 0.0 0.0 0.0
     ;
CP := ASM: LIBRARY2 TRACK :: EDIT 1 PIJ ;
CALC := FLU: CP LIBRARY2 TRACK :: EDIT 1 TYPE S EXTE 1 ;
XSM := EDI: CALC LIBRARY2 TRACK ::
     EDIT 2 SAVE
     MERG  REGI 1 2 3 4 5 6 0 0 0
     SAVE MICR 1 U238 ;
QUIT "LIST" .
