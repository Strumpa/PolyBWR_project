************************************************************************************************************************
*                                                                                                                      *
* Procedure :  SYBNXT_ISO.c2m                                                                                          *
* Purpose   :  Tracking SYBILT for SS and NXT TISO for FLX + MOC Flux calculation                                      *
* Author    :  R. Guasch                                                                                               *
*                                                                                                                      *
* CALL      :   SYBNXT_ISO                                                                                             *
*                                                                                                                      *
************************************************************************************************************************

* ---
* STRUCTURES AND MODULES
* ---
MODULE MCCGT: SYBILT: NXT: USS: ASM: FLU: GREP: END: ;

* ---
* PROCEDURES
PROCEDURE Mix_Cell Geom_FLX Geom_SS ; 
* ---

LINKED_LIST GEOMFLX_sub GEOMSS TRACK_SS TRACK LIBRARY LIBRARY2 SYS FLUX ;
SEQ_BINARY TF_EXC ;

INTEGER COMB0101 COMB0102 COMB0103 COMB0104 GAP CLAD MODE :=
        1 2 3 4 5 6 7 ; !  DISTRIBUTED SELF-SHIELDING 

! Geometric Parameter definition : 
REAL Rcomb1 Rcomb2 Rcomb3 Rcomb4 := 0.313602 0.396678 0.43227 0.4435 ;
REAL Rgap Rclad pitch := 0.4520 0.5140 1.295 ;
REAL Rmode := 0.64 ;
REAL Kinf1 Keff1 ;

* ---
* TRACKING PARAMETERS FOR SSH
* ---
INTEGER iqua2 := 24 ;
INTEGER nseg := 5 ;


* ---
* TRACKING PARAMETERS FOR MOC FLX
* ---
INTEGER an2d := 18 ;  ! ANGULAR QUADRATURE PARAMETER
REAL densur := 35.0 ; ! DENSITY OF INTEGRATION LINES CM^-1

* polar angles for MCCGT:
INTEGER nmu := 4 ;

* size allocation for MCCGT:
INTEGER mcu := 50000 ;

! GEOMETRY DEFINITION

GEOMSS := Geom_SS :: <<Rcomb1>> <<Rcomb2>> <<Rcomb3>> <<Rcomb4>> 
    <<Rgap>> <<Rclad>> <<pitch>> ;

GEOMFLX_sub := Geom_FLX :: <<Rcomb1>> <<Rcomb2>> <<Rcomb3>> <<Rcomb4>>
        <<Rgap>> <<Rclad>> <<Rmode>> <<pitch>> ;


* ---
* SELF-SHIELDING TRACKING
* ---

TRACK_SS := SYBILT: GEOMSS ::
    TITLE 'TRACKING FOR PINCELL SS'
    MAXR 50 QUA2 <<iqua2>> <<nseg>> DP01 MAXZ 200000 
;

* ---
* FLUX CALC TRACKING
* ---

TRACK TF_EXC := NXT: GEOMFLX_sub ::
  EDIT 0
  ALLG
  TISO EQW <<an2d>> <<densur>> REND
  LONG
;

* MOC tracking
TRACK := MCCGT: TRACK TF_EXC ::
  EDIT 0
  CACB <<nmu>> AAC 80 TMT EPSI 1E-5 MCU <<mcu>>
;

! XS Library
LIBRARY := Mix_Cell :: J311_295 <<COMB0101>> <<COMB0102>> <<COMB0103>> <<COMB0104>> 
                                <<GAP>> <<CLAD>> <<MODE>> ;


! UNIVERSAL SELF-SHIELDING 
LIBRARY2 := USS: LIBRARY TRACK_SS ::
          EDIT 0
          PASS 2
          GRMIN 52
;

SYS := ASM: LIBRARY2 TRACK TF_EXC :: ARM EDIT 0 ; 
FLUX := FLU: LIBRARY2 SYS TRACK TF_EXC :: 
        EDIT 1 TYPE K B1 SIGS ;

GREP: FLUX :: GETVAL 'K-EFFECTIVE  ' 1 1 1 >>Keff1<< ;
ECHO "SS tracking = SYBILT and FLX tracking = NXT TISO, MOC used on subdivided geom Keff6=" Keff1 ;

ECHO "SYBNXT_ISO completed" ;


END: ;
