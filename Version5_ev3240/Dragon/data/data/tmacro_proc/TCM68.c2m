*----
*  TEST CASE TCM68
*  Adaptive constant scheme in space and energy in Cartesian
*  geometries.
*----
*  Define STRUCTURES and MODULES used
*----
MODULE GEO: FMAC: MAC: SNT: ASM: FLU: PSOUR: OUT: DELETE: END: UTL:
       HEAT: ABORT: ;
PROCEDURE assertS ;
LINKED_LIST  EMACRO EMACRO2 GMACRO GMACRO2 GEOM ETRACK GTRACK SYSTEM
       EFLUX GFLUX ESOUR GSOUR EEDITS GEDITS DEPOS EFLX GFLX ;
SEQ_ASCII FMAC_M :: FILE './W_6MeV_e_ph_CSD.txt' ;
*----
*  Set and track the geometry
*----
GEOM := GEO: :: CAR1D 1
           X- VOID  X+ VOID
           MIX 1
           MESHX 0.0 0.05
           SPLITX 50
           ;
ETRACK := SNT: GEOM ::
      EDIT 2 
      MAXR 1000000 
      SCHM 3 DIAM 0
      ESCHM 3 EDIAM 0
      NLIVO 
      SN 8 SCAT 8 
      EPSI 1.E-5
      MAXI 300 
      BFPG 
      ;
GTRACK := ETRACK ;
GTRACK := SNT: GTRACK GEOM :: EDIT 2 BTE ;
*----
*  Compute the electron flux
*----
EMACRO := FMAC: FMAC_M ::
  EDIT 2
  PARTICLE BETA
;
EMACRO2 := EMACRO ;
EMACRO := MAC: EMACRO EMACRO2 ::
     MIX 1 1 OLDL
     ;
EMACRO2 := DELETE: EMACRO2 ;
ESOUR := PSOUR: EMACRO ETRACK GEOM ::
     MONO 1
     X-
     INTG 1 1.0
     DIR 1.0 0.0 0.0
     ;
SYSTEM := ASM: EMACRO ETRACK :: EDIT 2 ARM ;
EFLUX := FLU: EMACRO ETRACK SYSTEM ESOUR ::
     EDIT 2 TYPE S EXTE 100
     ;
EEDITS := OUT: EFLUX ETRACK EMACRO GEOM ::
     EDIT 2 COND INTG NONE
     ;
SYSTEM := DELETE: SYSTEM ;

*----
*  Set the photon source
*----
GMACRO := FMAC: FMAC_M ::
  EDIT 2
  PARTICLE GAMA
;

GMACRO2 := GMACRO ;
GMACRO := MAC: GMACRO GMACRO2 ::
     MIX 1 1 OLDL
     ;
GSOUR := PSOUR: GMACRO GTRACK EFLUX ::
   EDIT 1
   PARTICLE BETA
   ;
*----
*  Compute the photon flux
*----
SYSTEM := ASM: GMACRO GTRACK :: EDIT 1 ARM ;
GFLUX := FLU: GMACRO GTRACK SYSTEM GSOUR ::
    EDIT 2 TYPE S EXTE 100
    ;
GEDITS := OUT: GFLUX GTRACK GMACRO GEOM ::
    EDIT 2 COND INTG NONE
    ;
*----
* Asserts
*----
GFLX := GEDITS :: STEP UP GROUP STEP AT 1 ; 
EFLX := EEDITS :: STEP UP GROUP STEP AT 1 ;

assertS GFLX :: 'FLUX-INTG' 1 6.036487E-4 ;
assertS GFLX :: 'FLUX-INTG' 5 7.258679E-4 ;
assertS GFLX :: 'FLUX-INTG' 15 1.017586E-3 ;
assertS EFLX :: 'FLUX-INTG' 1 1.545685E-3 ;
assertS EFLX :: 'FLUX-INTG' 5 1.923795E-3 ;
assertS EFLX :: 'FLUX-INTG' 15 2.576633E-3 ;

ECHO "Test TCM68 completed" ;
END: ;
