*----
*  TEST CASE TMATXS24
*  Electron Beam on an intraoperative breast cancer lumpectomy.
*  NJOY-[CEPXSR]: electron 3MeV - 100 Groups - P2 non-coupled library.
*  This is a highly simplified Mobetron Benchmark. Please, note that
*  deterministic parameters are not physical here.
*  Author: Ahmed Naceur
*----
*  Define STRUCTURES and MODULES used
*----
MODULE       GEO: LIB: MAC: SNT: ASM: FLU: HEAT: OUT: DELETE: 
             END: EDI: PSOUR: ;
LINKED_LIST  MACRO DEPOS MICRO OLDMACRO GEOM TRACK SYSTEM FLUX 
             ESOUR EEDIT EDITION EEDITc GWT0 ;
SEQ_ASCII    edep_mob.txt   :: FILE 'edep_mob.txt' ;
PROCEDURE    assertS ;

* --- Geometry 

GEOM := GEO: :: CAR1D 4
     X- VOID  X+ VOID
     MIX      1   2   3   1
     MESHX 0.0 .6000 1.2000 1.4250 1.5000
     SPLITX 200 150 150 50
     ; 

* --- Tracking       

TRACK := SNT: GEOM ::
      TITLE 'Intraoperative Mobetron Benchmark'
      EDIT 0 MAXR 1000000  SCHM 1 DIAM 1 NLIVO SN 8 SCAT 8 EPSI 1.E-5
      MAXI 300 BFPG ;

* --- Library access

MICRO  := LIB: :: 
          EDIT 1
          NMIX 3 
          ANIS 3
          STERN 1
          MIXS LIB: MATXS2 FIL: _mobetron
          * -- water  -- * 
          MIX  1  300.0 1.0 NOGAS
          H  = h 0.111894 
          O  = o 0.888106 
          * -- aluminium   -- * 
          MIX  2  300.0 2.69890 NOGAS
          Al = al 1.00  
          * -- steel   -- *  
          MIX  3  300.0 8.060 NOGAS 
          Fe =  fe  0.641922
          Cr =  cr  0.1837
          Ni =  ni  0.1235
          Mo =  mo  0.02386
          Mn =  mn  0.01619
          Si =  si  0.005093
          Cu =  cu  0.001734
          W  =  w   0.0011
          Co =  co  0.001052
          V  =  v   0.000508
          C  =  c   0.000445
          N  =  n   0.0004
          P  =  p   0.000200
          S  =  s   0.000175
          As =  as  0.0001207  ;

* --- Library structuring 

MACRO := MICRO :: STEP UP MACROLIB ;

OLDMACRO := MACRO ;
MACRO    := MAC: MACRO OLDMACRO :: 
            MIX 1 1 OLDL 
            MIX 2 2 OLDL
            MIX 3 3 OLDL ;

OLDMACRO := DELETE: OLDMACRO ;

* ---- Electron beam definition

ESOUR := PSOUR: MACRO TRACK GEOM :: EDIT 1 MONO 1 X- INTG 1 1.0 
                                    DIR 1.0 0.0 0.0 ; 

* ---- Electron flux

SYSTEM := ASM: MACRO TRACK :: EDIT 1 ARM ;
FLUX   := FLU: MACRO TRACK SYSTEM ESOUR :: EDIT 1 TYPE S EXTE 100 ;

* --- Recover flux and reaction rates

EEDIT := OUT: FLUX TRACK MACRO GEOM :: EDIT 1 COND INTG NONE ;

* --- Energy deposition from the e- source

EEDITc := EEDIT ;
DEPOS  := HEAT: EEDIT EEDITc :: EDIT 1 NORM 1.00 2.69890 8.060 ;

* --- Save data structure for post-treatement 

edep_mob.txt   := DEPOS ; 

* --- AssertS

GWT0 := EEDIT :: STEP UP GROUP STEP AT 1 ;
assertS GWT0 :: 'FLUX-INTG' 1  3.311044E-03 ;
assertS GWT0 :: 'FLUX-INTG' 30 3.885913E-03 ;
assertS GWT0 :: 'FLUX-INTG' 60 4.248134E-03 ;
assertS GWT0 :: 'H-FACTOR'  1  1.504317E+00 ;
assertS GWT0 :: 'H-FACTOR'  30 1.609681E+00 ;
assertS GWT0 :: 'H-FACTOR'  60 1.639779E+00 ;

* --- Delete data structure 

DEPOS EEDIT FLUX SYSTEM MACRO MICRO TRACK GEOM GWT0 := DELETE: 
  DEPOS EEDIT FLUX SYSTEM MACRO MICRO TRACK GEOM GWT0 ; 

*
ECHO "test TMATXS24 completed" ;
END: ;
