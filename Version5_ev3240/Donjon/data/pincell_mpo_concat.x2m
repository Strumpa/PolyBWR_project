*----
*  TEST CASE pincell_mpo_concat
*  17 x 17 ASSEMBLY WITH TRIVAC
*  Input cross section data interpolated from MPO
*----
*  Define STRUCTURES and MODULES used
*----
MODULE MPO: MCR: GEO: TRIVAT: TRIVAA: FLUD: END: ;
LINKED_LIST PINCELL TRACK MACRO SYSTEM FLUX ;
STRING MyMpo1 := 'MyMpo1.hdf' ;
STRING MyMpo2 := 'MyMpo2.hdf' ;
STRING MyMpoCat := 'MyMpoCat.hdf' ;
HDF5_FILE MyMpo1 :: FILE <<MyMpo1>> ;
HDF5_FILE MyMpo2 :: FILE <<MyMpo2>> ;
HDF5_FILE MyMpoCat :: FILE <<MyMpoCat>> ;
PROCEDURE assertS ;

************************************************************************
*  Concatenation of two MPO files                                      *
************************************************************************
MyMpoCat := MyMpo1 ;
MyMpoCat := MPO:  MyMpoCat MyMpo2 :: EDIT 10 ;

************************************************************************
*  MPO interpolation                                                   *
************************************************************************
MACRO :=  MCR:  MyMpoCat :: EDIT 3 MACRO NMIX 1
   MPO MyMpoCat output_0 MIX 1 SET 'BURN' 900.0 SET 'Dmod' 0.8 ENDMIX ;

************************************************************************
*  numerical solution                                                  *
************************************************************************
REAL Cote := 1.26  ;
REAL Precf := 1.E-5 ;
PINCELL := GEO: :: CAR2D 1 1
     MESHX 0.0 <<Cote>> MESHY 0.0 <<Cote>>
     SPLITX 2 SPLITY 2
     X- REFL X+ REFL    Y- REFL Y+ REFL
     MIX  1
;
TRACK := TRIVAT: PINCELL ::
     TITLE 'HOMOGENIZED PINCELL'
     EDIT 0 MAXR 300 DUAL 3 1 ;
SYSTEM := TRIVAA: MACRO TRACK :: EDIT 0 ;
FLUX := FLUD: SYSTEM TRACK :: EDIT 0
        ACCE 3 3  ADI 4  EXTE 1000 <<Precf>> THER 1000 ;
assertS FLUX :: 'K-EFFECTIVE' 1 1.290027 ;
ECHO "pincell_mpo_concat completed" ;
END: ;
