*----
*
*  TEST CASE uo2_evo_hdf
*  UO2 ROWLAND'S BENCHMARK
*  DISTRIBUTED SELF-SHIELDING
*  99-GROUP JEF2.2 APOLIB-HDF
*
*  Author: A. Hebert
*
*----
*  Define STRUCTURES and MODULES used
*----
LINKED_LIST GEOM TRACK LIBRARY LIBRARY2 CP CALC BURNUP StepList ;
MODULE LIB: GEO: SYBILT: USS: ASM: FLU: GREP: UTL: EVO: DELETE: ;
INTEGER COMB0101 COMB0102 COMB0103 COMB0104 COMB0105 COMB0106 GAIN1
        MODE1 MODE2 := 
        1 2 3 4 5 6 7 8 9 ; (* DISTRIBUTED SELF-SHIELDING *)
REAL RCOMB1 RCOMB2 RCOMB3 RCOMB4 RCOMB5 RCOMB6 ;
PROCEDURE assertS ;
REAL Norm_f2 := 38.743 ;
*
*----
*  Variables
*----
INTEGER istep :=  1 ;
INTEGER nstep := 21 ;
INTEGER nauto :=  3 ;
INTEGER iauto :=  1 ;
INTEGER  valstep valauto ;
REAL    evobeg evoend step2 stepauto ;

StepList := UTL: :: CREA 'ListBU' <<nstep>> =
     9.375   18.75     37.5    75.     112.5
   150.     325.     500.     750.    1000.
  1500.    2000.    2500.    3000.    4000.
  5000.    6000.    7000.    8000.   10000.
 12000.  ;

StepList := UTL: StepList :: CREA 'ListAutop' <<nauto>> =
  4000.    8000.   12000. ;
*
EVALUATE RCOMB1 RCOMB2 RCOMB3 RCOMB4 RCOMB5 RCOMB6 := 0.2529822 
0.334664 0.3577709 0.3794733 0.3898718 0.40 ;
GEOM := GEO: :: TUBE 9
      R+ REFL
      RADIUS 0.0 <<RCOMB1>> <<RCOMB2>> <<RCOMB3>> <<RCOMB4>>
                 <<RCOMB5>> <<RCOMB6>> 0.45 0.5748331 0.6770275
      MIX <<COMB0101>> <<COMB0102>> <<COMB0103>> <<COMB0104>>
          <<COMB0105>> <<COMB0106>> <<GAIN1>> <<MODE1>> <<MODE2>>
          ;
TRACK := SYBILT: GEOM ::
     TITLE 'UO2 ROWLAND S BENCHMARK'
     MAXR 20 QUA1 5 ;

LIBRARY := LIB: ::
 EDIT 2
 NMIX 9    (*MAXIMUM OF MATERIAL MIXTURES*)
 CTRA APOL (*APOLLO TYPE TRANSPORT CORRECTION*)
 ANIS 2
 ADED 4 NELAS N4N N2N N3N
 SUBG      (*HELIOS TYPE PROBABILITY TABLES*)

*----
*  Depletion Chain
*----
 DEPL LIB: APLIB3 FIL: CLA99CEA93:CLA99CEA93_EVO
 MIXS LIB: APLIB3 FIL: CLA99CEA93:CLA99CEA93_SS
 MIX <<COMB0101>> 800.0 (*COMB0101*)
    O16     = O16    4.6624E-2
    U235    = U235   7.0803E-4 1
    U238    = U238   2.2604E-2 1
 MIX <<COMB0102>> COMB <<COMB0101>> 1.0 (*COMB0102*)
 MIX <<COMB0103>> COMB <<COMB0101>> 1.0 (*COMB0103*)
 MIX <<COMB0104>> COMB <<COMB0101>> 1.0 (*COMB0104*)
 MIX <<COMB0105>> COMB <<COMB0101>> 1.0 (*COMB0105*)
 MIX <<COMB0106>> COMB <<COMB0101>> 1.0 (*COMB0106*)
 MIX <<GAIN1>> 600.0 NOEV (*GAIN1*)
    Zr90    = Zr90   2.2247E-2 2
    Zr91    = Zr91   0.4852E-2 2
    Zr92    = Zr92   0.7416E-2 2
    Zr94    = Zr94   0.7515E-2 2
    Zr96    = Zr96   0.1211E-2 2
 MIX <<MODE1>> 293.6 NOEV (*MODE1*)
    H2O     = H2O    3.3494E-2
 MIX <<MODE2>> 293.6 NOEV (*MODE2*)
    H2O     = H2O    3.3494E-2
 ;
 
LIBRARY2 := USS: LIBRARY TRACK :: EDIT 1 TRAN PASS 2 GRMIN 18 ;
CP := ASM: LIBRARY2 TRACK :: EDIT 1 PIJ PNOR HELI ;
CALC := FLU: CP LIBRARY2 TRACK :: EDIT 1 TYPE B B1 SIGS ;
assertS CALC :: K-INFINITY 1 1.349664 ;

ECHO "Sortie de FLU: au BURNUP:  0.000000E+00" ;

EVALUATE evoend := 0. ;
WHILE istep nstep <= DO

  GREP: StepList :: GETVAL 'ListBU' <<istep>> >>step2<< ;

  EVALUATE evobeg := evoend ;
  EVALUATE evoend := step2 Norm_f2 / ;
  ECHO "BURNUP step" istep "between" evobeg "and" evoend "day:" ;
  IF istep 1 = THEN
    BURNUP LIBRARY := EVO: LIBRARY CALC TRACK :: EDIT 3
         DEPL <<evobeg>> <<evoend>> DAY POWR <<Norm_f2>>
         EPS1 .025  EPS2 .1
         EXPM 1.E15 RUNG EXTR NSAT NODI
         ;
  ELSE
    BURNUP LIBRARY := EVO: BURNUP LIBRARY CALC TRACK :: EDIT 2
         DEPL <<evobeg>> <<evoend>> DAY POWR <<Norm_f2>>
         EPS1 .025  EPS2 .1
         EXPM 1.E15 RUNG EXTR NSAT NODI
         ;
  ENDIF ;

*--------------------------------
* Self Shielding
*--------------------------------
    GREP: StepList :: GETVAL 'ListAutop' <<iauto>> >>stepauto<< ;
    EVALUATE valstep := step2 R_TO_I ;
    EVALUATE valauto := stepauto R_TO_I ;
    IF valstep valauto = THEN
      ECHO "StepAuto:" stepauto "MWj/t" ;
      ECHO "Self-shielding calculation" istep "at" evoend "DAY:" ;

      LIBRARY2 := USS: LIBRARY2 LIBRARY TRACK ::
        EDIT 1 TRAN PASS 2 GRMIN 18 ;

      IF iauto nauto < THEN
        EVALUATE iauto := iauto 1 + ;
      ENDIF ;
    ENDIF ;
*--------------------------------

  ECHO "step2=" step2 "evoend=" evoend ;

  CP := DELETE: CP ;
  CP := ASM: LIBRARY TRACK :: PIJ PNOR HELI ;
  CALC := FLU: CALC TRACK LIBRARY CP :: TYPE B B1 SIGS ;

  ECHO "Output of FLU: at burnup:" step2 ;

  BURNUP LIBRARY := EVO: BURNUP LIBRARY CALC TRACK :: EDIT 2
                       SAVE <<evoend>> DAY POWR <<Norm_f2>> ;

  EVALUATE istep := istep 1 + ;

ENDWHILE ;

assertS CALC :: K-INFINITY 1 0.9174420 ;
ECHO "test uo2_evo_hdf completed" ;
QUIT "LIST" .
