* Deck Geo_A_2lvl 
* CLE-2000 procedure
* Author : R. Guasch
* Purpose : Generate and return 2-level geometry of AT10 assembly
* Consists of the initial cartesian grid subdivided into another layer of cartesian grids.
*

* -------------------------------
*       PROCEDURE OUTPUT
* -------------------------------
PARAMETER GEOM_A_2 ::
::: LINKED_LIST GEOM_A_2 ; ;


* -------------------------------
*    STRUCTURES AND MODULES
* -------------------------------
MODULE  GEO: END: ;

!Assembly scale data

REAL Pitch_A W_gap := 15.24 0.75 ; ! Assembly pitch and water gap thickness
REAL Box_out Box_in Box_thi := 13.74 13.4 0.17 ; ! Outer box outer, inner sides and thickness
REAL Chan_out Chan_in Chan_thi := 3.5 3.34 0.08 ; ! Channel box outer, inner sides and thickness
REAL Pitch_C := 1.29500 ;

! Multiples of cell pitch : used to define 1st level geometry mesh 
REAL 2_Pitch_C 3_Pitch_C 4_Pitch_C ;
EVALUATE 2_Pitch_C := Pitch_C 2.0 * ;
EVALUATE 3_Pitch_C := Pitch_C 3.0 * ;
EVALUATE 4_Pitch_C := Pitch_C 4.0 * ;

! X1 = Water gap + outer box thickness +extra water : in box water not a multiple of Pitch_C
REAL XtraMode ;
EVALUATE XtraMode := Box_in 10.0 Pitch_C * - 2.0 / ;

!Meshing points for macro geometry :
REAL X1 X2 X3 X4  ;
EVALUATE X1 := W_gap Box_thi + XtraMode + ;
EVALUATE X2 := 4.0 Pitch_C * X1 + ;
EVALUATE X3 := 3.0 Pitch_C * X2 + ;
EVALUATE X4 := 3.0 Pitch_C * X3 + ;  

REAL Y1 Y2 Y3 Y4 ;
EVALUATE Y1 := X1 ;
EVALUATE Y2 := 4.0 Pitch_C * Y1 + ;
EVALUATE Y3 := 3.0 Pitch_C * Y2 + ;
EVALUATE Y4 := 3.0 Pitch_C * Y3 + ;

REAL L1 L2 L3 ;
EVALUATE L1 := X2 X1 - ;
EVALUATE L2 := X3 X2 - ;
EVALUATE L3 := X4 X3 - ;

REAL H1 H2 H3 ;
EVALUATE H1 := Y2 Y1 - ;
EVALUATE H2 := Y3 Y2 - ;
EVALUATE H3 := Y4 Y3 - ;

REAL x_step y_step ; ! allows for submeshing of in-assembly box water
! in-assembly box water included in geom level with outer assembly water and box
EVALUATE x_step := W_gap Box_thi + ;
EVALUATE y_step := XtraMode Box_thi + ;

! inner CHaNneL box related parameters, used to define 2nd level CHNL Geometry
! CHNL = channel box + surroudning water to fit with pin CARCEL edges 
REAL XCHNL1 XCHNL2 XCHNL3 XCHNL4 XCHNL5 ;
EVALUATE XCHNL1 := L2 Chan_out - 2.0 / ;
EVALUATE XCHNL2 := XCHNL1 Chan_thi + ;
EVALUATE XCHNL3 := XCHNL2 Chan_in + ;
EVALUATE XCHNL4 := XCHNL3 Chan_thi + ;
EVALUATE XCHNL5 := L2 ;

! Pin level common mixes : Clad, Gap, Coolant (intra-pin water) :

INTEGER GAP CLAD COOL ;
EVALUATE GAP CLAD COOL := 5 6 7 ;

! Assembly level common mixes water, channel and box
! Careful : some intra assembly water is treated at the 2nd geometry level
! MODE is associated with H20 OUTSIDE assembly box and INSIDE channel box, the rest is COOL
INTEGER BOX MODE ;
EVALUATE BOX MODE := 60 70 ;

GEOM_A_2 := GEO: :: CAR2D 5 5 
    EDIT 1
    X- DIAG X+ REFL
    Y- REFL Y+ DIAG
    CELL
    WBC1 WB1 WB2 WB3 WBC2
         C01 C02 C03 WR1
            CHNL C04 WR2
                 C05 WR3
                     WTC

    MESHX 0.0 <<X1>> <<X2>> <<X3>> <<X4>> <<Pitch_A>>
    MESHY 0.0 <<Y1>> <<Y2>> <<Y3>> <<Y4>> <<Pitch_A>>
        ::: WTC := GEO: CAR2D 3 3
        MESHX 0.0 <<XtraMode>> <<y_step>> <<X1>>
        MESHY 0.0 <<XtraMode>> <<y_step>> <<Y1>>
        MIX <<COOL>> <<BOX>> <<MODE>> 
             <<BOX>> <<BOX>> <<MODE>> 
            <<MODE>> <<MODE>> <<MODE>>
    ;
    ::: WBC1 := GEO: CAR2D 3 3 
        MESHX 0.0 <<W_gap>> <<x_step>> <<X1>>
        MESHY 0.0 <<W_gap>> <<x_step>> <<Y1>>
        MIX <<MODE>> <<MODE>> <<MODE>> 
            <<MODE>> <<BOX>> <<BOX>>
            <<MODE>> <<BOX>> <<COOL>>
    ;
    ::: WR3 := GEO: CAR2D 3 1
        MESHX 0.0 <<XtraMode>> <<y_step>> <<X1>>
        MESHY 0.0 <<L3>>
        MIX <<COOL>> <<BOX>> <<MODE>>
    ;
    ::: WR2 := GEO: CAR2D 3 1
        MESHX 0.0 <<XtraMode>> <<y_step>> <<X1>>
        MESHY 0.0 <<L2>>
        MIX <<COOL>> <<BOX>> <<MODE>>
    ; 
    ::: WR1 := GEO: CAR2D 3 1
        MESHX 0.0 <<XtraMode>> <<y_step>> <<X1>>
        MESHY 0.0 <<L1>>
        MIX <<COOL>> <<BOX>> <<MODE>>
    ; 
    ::: WB1 := GEO: CAR2D 1 3
        MESHX 0.0 <<L1>>
        MESHY 0.0 <<W_gap>> <<x_step>> <<Y1>>
        MIX <<MODE>> <<BOX>> <<COOL>>
    ;
    ::: WB2 := GEO: CAR2D 1 3
        MESHX 0.0 <<L2>>
        MESHY 0.0 <<W_gap>> <<x_step>> <<Y1>>
        MIX <<MODE>> <<BOX>> <<COOL>>
    ;
    ::: WB3 := GEO: CAR2D 1 3
        MESHX 0.0 <<L3>>
        MESHY 0.0 <<W_gap>> <<x_step>> <<Y1>>
        MIX <<MODE>> <<BOX>> <<COOL>>
    ;
    ::: WBC2 := GEO: CAR2D 3 3
        MESHX 0.0 <<XtraMode>> <<y_step>> <<X1>>
        MESHY 0.0 <<W_gap>> <<x_step>> <<X1>>
        MIX <<MODE>> <<MODE>> <<MODE>> 
            <<BOX>>  <<BOX>> <<MODE>> 
            <<COOL>> <<BOX>> <<MODE>> 
    ;
    ::: CHNL := GEO: CAR2D 5 5
        MESHX 0.0 <<XCHNL1>> <<XCHNL2>> <<XCHNL3>> <<XCHNL4>> <<XCHNL5>>
        MESHY 0.0 <<XCHNL1>> <<XCHNL2>> <<XCHNL3>> <<XCHNL4>> <<XCHNL5>>
        MIX <<COOL>> <<COOL>> <<COOL>> <<COOL>> <<COOL>> 
            <<COOL>> <<BOX>> <<BOX>> <<BOX>> <<COOL>>
            <<COOL>> <<BOX>> <<MODE>> <<BOX>> <<COOL>>
            <<COOL>> <<BOX>> <<BOX>> <<BOX>> <<COOL>>
            <<COOL>> <<COOL>> <<COOL>> <<COOL>> <<COOL>>
    ;
    ::: C01 := GEO: CAR2D 4 4
        MIX 1 2 3 4
            1 2 3 4
            1 2 3 4
            1 2 3 4 
        MESHX 0.0 <<Pitch_C>> <<2_Pitch_C>> <<3_Pitch_C>> <<4_Pitch_C>>
        MESHY 0.0 <<Pitch_C>> <<2_Pitch_C>> <<3_Pitch_C>> <<4_Pitch_C>>
    ;
    ::: C02 := GEO: CAR2D 3 4 
        MIX 1 2 3
            1 2 3
            1 2 3
            1 2 3  
        MESHX 0.0 <<Pitch_C>> <<2_Pitch_C>> <<3_Pitch_C>> 
        MESHY 0.0 <<Pitch_C>> <<2_Pitch_C>> <<3_Pitch_C>> <<4_Pitch_C>>

    ;
    ::: C03 := GEO: CAR2D 3 4
        MIX 1 2 3
            1 2 3
            1 2 3
            1 2 3
        MESHX 0.0 <<Pitch_C>> <<2_Pitch_C>> <<3_Pitch_C>> 
        MESHY 0.0 <<Pitch_C>> <<2_Pitch_C>> <<3_Pitch_C>> <<4_Pitch_C>>
    ;
    ::: C04 := GEO: CAR2D 3 3 
        MIX 1 2 3
            1 2 3
            1 2 3
        MESHX 0.0 <<Pitch_C>> <<2_Pitch_C>> <<3_Pitch_C>>
        MESHY 0.0 <<Pitch_C>> <<2_Pitch_C>> <<3_Pitch_C>>
    ;
    ::: C05 := GEO: CAR2D 3 3
        MIX 1 2 3
            1 2 3
            1 2 3
        MESHX 0.0 <<Pitch_C>> <<2_Pitch_C>> <<3_Pitch_C>>
        MESHY 0.0 <<Pitch_C>> <<2_Pitch_C>> <<3_Pitch_C>>
    ;
;
ECHO "2 level GEOM_A generation done" ;
END: ;
QUIT .