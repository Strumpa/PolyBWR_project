* TEST CASE TDCM60
* 2-D cell calculation on small cell hexagonal geometry
* Fission source problem solved with MoC
*---
*----
*  Define STRUCTURES and MODULES used
*----
LINKED_LIST  GEOCELL TRACK MACRO SYS FLUX EDITION ;
SEQ_BINARY  TRKFIL ;
SEQ_ASCII GD_TDCM60 ;
MODULE  GEO: MAC: G2S: SALT: MCCGT: ASM: FLU: EDI: DELETE: END: ;
SEQ_ASCII TDCM60.ps :: FILE './TDCM60.ps' ;
PROCEDURE assertS ;
INTEGER an2d := 6 ;
REAL densur := 250. ;
*----
* Macroscopic Cross sections
*----
MACRO := MAC: ::
NGRO 2 ANIS 2 NMIX 3 NIFI 1
READ INPUT
MIX 1 TOTAL 0.392175 0.622581
SCAT 2 2 0.001451 0.361893 2 2 0.358282 0.000715
2 2 0.0 0.0 2 2 0.0 0.0
NUSIGF 0.022141 0.496970 CHI 1.0 0.0
MIX 2 TOTAL 0.276383 0.278610
SCAT 2 2 0.000774 0.274505 2 2 0.273807 0.000288
2 2 0.0 0.0 2 2 0.0 0.0
MIX 3 TOTAL 0.439812 1.35565
SCAT 2 2 0.002672 0.411998 2 2 1.33517 0.02128
1 1 0.2 2 2 0.5 0.006    ;
*----
* Geometry definition
*----
GEOCELL := GEO: :: HEXCEL 3
  HBC COMPLETE ALBE 1.0
  RADIUS 0.0 0.455 0.47 0.6605
  SIDE 0.7361215932
  SECT -1 2
  MIX
  1
  2
  3 3 3 3 3 3
  3 3 3 3 3 3
;
*----
* Isotropic tracking 
*----
GD_TDCM60 := G2S: GEOCELL ;
TRACK TRKFIL := SALT: GD_TDCM60 ::
  GAUS 4 TISO <<an2d>> <<densur>>   ;
TRACK := MCCGT: TRACK TRKFIL :: 
  AAC 1 SCR 0 MAXI 1000 GAUS KRYL 30 HDD 0.0 STIS 1 ;
SYS := ASM: MACRO TRACK TRKFIL :: 
  ARM HETE ;
FLUX := FLU: MACRO TRACK SYS TRKFIL :: 
  EDIT 1 TYPE K ;
assertS FLUX :: 'K-INFINITY' 1 1.047191 ;
EDITION := EDI: MACRO TRACK FLUX ::
  EDIT 3 SAVE ;
GD_TDCM60 TRACK TRKFIL SYS FLUX := DELETE: GD_TDCM60 TRACK TRKFIL SYS FLUX ;
*----
* Specular tracking with translation
*----
GEOCELL := GEO: GEOCELL ::
  HBC COMPLETE TRAN
;
GD_TDCM60 TDCM60.ps := G2S: GEOCELL :: DRAWMIX ;
TRACK TRKFIL := SALT: GD_TDCM60 ::
  GAUS 4 TSPC <<an2d>> <<densur>>   ;
TRACK := MCCGT: TRACK TRKFIL :: 
  AAC 1 SCR 0 MAXI 1000 GAUS KRYL 30 HDD 0.0 STIS 1 ;
SYS := ASM: MACRO TRACK TRKFIL :: 
  ARM HETE ;
FLUX := FLU: MACRO TRACK SYS TRKFIL :: 
  TYPE K ;
assertS FLUX :: 'K-INFINITY' 1 1.048457 ;
EDITION := EDI: EDITION MACRO TRACK FLUX ::
  EDIT 3 SAVE ;
ECHO "test TDCM60 completed" ;
END: ;
QUIT "LIST" .
