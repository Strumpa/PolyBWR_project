*=====================================================================*
* FILE:           ssrcell_thm.x2m
* TYPE:           DRAGON/DONJON input file
* AUTHOR:         Cristian Garrido Tamm (cristian.garrido@idom.com)
* PURPOSE:        TODO
* CREATION DATE:  Fri 07 Apr 2023 09:47:29 PM CEST
* LAST MODIFIED:  Sat 08 Apr 2023 01:07:48 PM CEST
*=====================================================================*
*---
* DEFINE MODULES
*---
MODULE END: USPLIT: THM: TRIVAT: TRIVAA: NCR: MACINI: FLUD: ;
*---
* DEFINE PROCEDURES
*---
PROCEDURE HOM_chan HOM_fmap ; 
*---
* DEFINE DATA STRUCTURES
*---
LINKED_LIST CPO GEOM FMAP MATEX THM TRACK MACRO MACRO2 SYSTEM FLUX ;
SEQ_ASCII COMPO :: FILE './_COMPO.txt' ;
PROCEDURE assertS ;
*---
* DEFINE VARIABLES
*---
* Constants
STRING DOUBLE_LINE := 
"====================================================================" ;
STRING SINGLE_LINE := 
"--------------------------------------------------------------------" ;
LOGICAL True False := $True_L $False_L ;
STRING FILENAME := 'ssrcell_thm' ;
* Input data
*GEO
REAL Apothem := 10.0 ; 
REAL maxh := 4000.0 ; 
*TEMPERATURES
REAL Tfuel := 1200.15 ;
REAL Tcool := 1000.15 ;
REAL Tmod := 1000.15 ;
*FUEL MASS
REAL Mass := 4.655149E-3 ; ! kg
*FUEL POWER
REAL Fuelpwr := 38.425 ; ! kW/kg
* Variables
REAL   powi := Fuelpwr Mass * 1.E3 / ;
ECHO "total reactor power=" powi "MW" ;

*---
* BEGIN CALCULATION
*---
ECHO DOUBLE_LINE ;
ECHO 'BEGIN INPUT ' FILENAME ;
ECHO DOUBLE_LINE ;

*---
* RECOVER MULTICOMPO
*---
CPO := COMPO ;

*---
* GEOMETRY
*---
REAL Hside := 2.0 Apothem * 3.0 SQRT / ;
GEOM := HOM_chan ::
    <<Hside>> <<maxh>> ;

*---
* Reactor material index:
*---
GEOM MATEX := USPLIT: GEOM :: NGRP 2 MAXR 400
    NFUEL 1 FMIX 1 ;
    
*--
* Fuel map definition
*--
FMAP MATEX := HOM_fmap MATEX ::
    <<Hside>> <<maxh>> <<powi>> <<Tfuel>> <<Tcool>> <<Tmod>> <<Mass>> ; 

*--
* THM single-stage calculation
*--
THM FMAP := THM: FMAP ::
    EDIT 2
    FPUISS 0.93
    INLET 330000.0 (*Pa*) 318.15 (*K*)
    INLET-Q 7.9770045E-5 (*m2*) 0.375 (*inlet mass flow rate kg/s*)
    CRITFL 8.5E6 (* W/m^2 *)
    ASSMB 1 0
    RADIUS 4.55063E-3 4.60169E-3 5.00184E-3 5.00184E-3 (* m *)
    RODMESH 15 20
    MONO
;

*---
*TRACKING: Raviart-Thomas-Schneider method
*---
TRACK := TRIVAT: GEOM :: EDIT 0
    MAXR 400 DUAL 1 3
    SPN 3 SCAT 4 ;

*---
* XS INTERPOLATION
*---
MACRO := NCR: CPO FMAP ::
    EDIT 100
    MACRO LINEAR
    NMIX 20
    TABLE CPO EDI2B
    MIX 1 
      SET 'TF' <<Tfuel>>
      SET 'TC' <<Tcool>>
      SET 'TM' <<Tmod>>
    ENDMIX ;

MACRO2 MATEX := MACINI: MATEX MACRO :: EDIT 99 FUEL ;

*---
* FLUX SOLUTION
*---
SYSTEM := TRIVAA: MACRO2 TRACK :: EDIT 0 ;
FLUX := FLUD: SYSTEM TRACK :: EDIT 2
      ACCE 3 3 EXTE 1000 1E-04 ADI 6 ;

*---
* END CALCULATION
*---
ECHO DOUBLE_LINE ;
ECHO 'END INPUT ' FILENAME ;
ECHO DOUBLE_LINE ;

assertS FLUX :: 'K-EFFECTIVE' 1 1.282791 ;
ECHO "test ssrcell_thm completed" ;

END: ;
QUIT .
