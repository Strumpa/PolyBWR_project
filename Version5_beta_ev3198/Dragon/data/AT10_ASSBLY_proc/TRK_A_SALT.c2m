* Deck TRK_A_SALT 
* CLE-2000 procedure for SALT: tracking on self-shielding and flux geom
* Currently 3-level BWR geometry treatment limited to surfacic (G2S: --> SALT:) 
*
* Goal : get reference deterministic t=0 results to compare with NXT capabilities.
*
* CALL      :                                                                     
* TRACK TF_EXC TRACK_SS TF_EXC_SS FIG.ps FIG_SS.ps := TRK_A_SALT GEOM GEOM_SS
*              
*       
************************************************************************************

* ---
* INPUT & OUTPUT PARAMETERS
* ---
PARAMETER Geo_G2S    FIG_main.ps   TRACK    TF_EXC
          GeoSS_G2S FIG_SS.ps TRACK_SS TF_EXC_SS
          GEOM       GEOM_SS ::
::: SEQ_ASCII Geo_G2S ;
::: SEQ_ASCII FIG_main.ps ;
::: LINKED_LIST TRACK ;
::: SEQ_BINARY TF_EXC ;
::: SEQ_ASCII GeoSS_G2S ;
::: SEQ_ASCII FIG_SS.ps ;
::: LINKED_LIST TRACK_SS ;
::: SEQ_BINARY TF_EXC_SS ;
::: LINKED_LIST GEOM ;
::: LINKED_LIST GEOM_SS ; ;


* ---
* STRUCTURES AND MODULES
* ---
MODULE G2S: MCCGT: SALT: END: ;

* ---
* TRACKING PARAMTERES
* ---

INTEGER an2d := 18 ;  ! ANGULAR QUADRATURE PARAMETER
REAL densur := 35.0 ; ! DENSITY OF INTEGRATION LINES CM^-1

INTEGER an2d_SS := 12 ;
REAL densur_SS := 20.0 ;

* polar angles for MCCGT:
INTEGER nmu := 4 ;

* size allocation for MCCGT:
INTEGER mcu := 50000 ;

* ---
* FLUX CALC TRACKING
* ---
Geo_G2S FIG_main.ps := G2S: GEOM :: ;

TRACK TF_EXC := SALT: Geo_G2S ::
  EDIT 0
  ALLG
  TSPC EQW2 <<an2d>> <<densur>> REND
  LONG
;

* MOC tracking
TRACK := MCCGT: TRACK TF_EXC ::
  EDIT 0
  CACB <<nmu>> AAC 80 TMT EPSI 1E-5 MCU <<mcu>>
;

* ---
* SELF-SHIELDING TRACKING
* ---
GeoSS_G2S FIG_SS.ps := G2S: GEOM_SS :: ;

TRACK_SS TF_EXC_SS := SALT: GeoSS_G2S ::
  EDIT 0
  ALLG
  TSPC <<an2d_SS>> <<densur_SS>> REND
  LONG
;

END: ;
QUIT .


