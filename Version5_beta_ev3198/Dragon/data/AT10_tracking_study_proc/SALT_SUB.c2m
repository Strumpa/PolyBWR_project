************************************************************************************************************************
*                                                                                                                      *
* Procedure :  SALT_SUB.c2m                                                                                            *
* Purpose   :  Tracking and figure printing for pin cells + MOC Flux calculation, SALT tracking                        *
* Author    :  R. Guasch adapted from L.Fede                                                                           *
*                                                                                                                      *
* CALL      :   SALT_SUB                                                                                               *
*                                                                                                                      *
************************************************************************************************************************

* ---
* STRUCTURES AND MODULES
* ---
MODULE G2S: MCCGT: SALT: PSP: USS: ASM: FLU: GREP: END: DELETE: ;

* ---
* PROCEDURES
PROCEDURE Mix_Cell Geom_FLX Geom_SS ; 
* ---

LINKED_LIST GEOMFLX_sub GEOMSS TRACK_SS TRACK LIBRARY LIBRARY2 SYS FLUX ;

SEQ_ASCII UOX_TBH_SS ;
SEQ_ASCII UOX_TBH_FLX ;
SEQ_BINARY TF_EXC ;
SEQ_BINARY TF_EXC_SS ;

SEQ_ASCII FIG_SS_.ps :: FILE './PIN_AT10_FIG_SS_.ps' ;
SEQ_ASCII FIG_FLX_.ps :: FILE './PIN_AT10_FIG_FLX_.ps' ;

INTEGER COMB0101 COMB0102 COMB0103 COMB0104 GAP CLAD MODE :=
        1 2 3 4 5 6 7 ; !  DISTRIBUTED SELF-SHIELDING 


! Geometric Parameter definition : 
REAL Rcomb1 Rcomb2 Rcomb3 Rcomb4 := 0.313602 0.396678 0.43227 0.4435 ;
REAL Rgap Rclad pitch := 0.4520 0.5140 1.295 ;
REAL Rmode := 0.64 ;

REAL Kinf1 Keff1 ;

* ---
* TRACKING PARAMETERS FOR SSH
* ---
INTEGER an2d_SS := 12 ;
REAL densur_SS := 20.0 ;

* ---
* TRACKING PARAMETERS FOR MOC FLX
* ---
INTEGER an2d := 18 ;  ! ANGULAR QUADRATURE PARAMETER
REAL densur := 35.0 ; ! DENSITY OF INTEGRATION LINES CM^-1

* polar angles for MCCGT:
INTEGER nmu := 4 ;

* size allocation for MCCGT:
INTEGER mcu := 50000 ;

! GEOMETRY DEFINITION

GEOMSS := Geom_SS :: <<Rcomb1>> <<Rcomb2>> <<Rcomb3>> <<Rcomb4>> 
    <<Rgap>> <<Rclad>> <<pitch>> ;

GEOMFLX_sub := Geom_FLX :: <<Rcomb1>> <<Rcomb2>> <<Rcomb3>> <<Rcomb4>>
    <<Rgap>> <<Rclad>> <<Rmode>> <<pitch>> ;


* ---
* SELF-SHIELDING TRACKING
* ---
UOX_TBH_SS FIG_SS_.ps := G2S: GEOMSS :: ;

TRACK_SS TF_EXC_SS := SALT: UOX_TBH_SS ::
  EDIT 0
  ALLG
  TSPC <<an2d_SS>> <<densur_SS>> REND
;


* ---
* FLUX CALC TRACKING
* ---
UOX_TBH_FLX FIG_FLX_.ps := G2S: GEOMFLX_sub :: ;

TRACK TF_EXC := SALT: UOX_TBH_FLX ::
  EDIT 0
  ALLG
  TSPC EQW2 <<an2d>> <<densur>> REND
;

* MOC tracking
TRACK := MCCGT: TRACK TF_EXC ::
  EDIT 0
  CACB <<nmu>> AAC 80 TMT EPSI 1E-5 MCU <<mcu>>
;


! LIBRARY
LIBRARY := Mix_Cell :: J311_295 <<COMB0101>> <<COMB0102>> <<COMB0103>> <<COMB0104>> 
                                <<GAP>> <<CLAD>> <<MODE>> ;


! UNIVERSAL SELF-SHIELDING 
LIBRARY2 := USS: LIBRARY TRACK_SS TF_EXC_SS ::
          EDIT 0
          PASS 2
          GRMIN 52
;

SYS := ASM: LIBRARY2 TRACK TF_EXC :: ARM EDIT 0 ; 
FLUX := FLU: LIBRARY2 SYS TRACK TF_EXC :: 
        EDIT 1 TYPE K B1 SIGS ;

GREP: FLUX :: GETVAL 'K-EFFECTIVE  ' 1 1 1 >>Keff1<< ;
ECHO "SS tracking = SALT and FLX tracking = SALT, MOC used on subdivided geom: Keff2=" Keff1 ;

ECHO "AT10SALT_SUB completed" ;


END: ;
