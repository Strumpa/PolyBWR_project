* TEST CASE TDCM59
* 2-D cell calculation on small cell Cartesian geometry
* Fission source problem solved with MoC
*---
*----
*  Define STRUCTURES and MODULES used
*----
LINKED_LIST  GEOCELL TRACK MACRO SYS FLUX EDITION ;
SEQ_BINARY  TRKFIL ;
SEQ_ASCII GD_TDCM59 ;
MODULE  GEO: MAC: G2S: SALT: MCCGT: ASM: FLU: EDI: DELETE: END: ;
SEQ_ASCII TDCM59.ps :: FILE './TDCM59.ps' ;
PROCEDURE assertS ;
INTEGER an2d := 8 ;
REAL densur := 250. ;
*----
* Macroscopic Cross sections
*----
MACRO := MAC: ::
NGRO 2 ANIS 2 NMIX 3 NIFI 1
READ INPUT
MIX 1 TOTAL 0.392175 0.622581
SCAT 2 2 0.001451 0.361893 2 2 0.358282 0.000715
2 2 0.0 0.0 2 2 0.0 0.0
NUSIGF 0.022141 0.496970 CHI 1.0 0.0
MIX 2 TOTAL 0.276383 0.278610
SCAT 2 2 0.000774 0.274505 2 2 0.273807 0.000288
2 2 0.0 0.0 2 2 0.0 0.0
MIX 3 TOTAL 0.439812 1.35565
SCAT 2 2 0.002672 0.411998 2 2 1.33517 0.02128
1 1 0.2 2 2 0.5 0.006    ;
*----
* Geometry definition
*----
GEOCELL := GEO: :: CARCEL 5
X- ALBE 1.0 X+ ALBE 1.0 MESHX 0.0 1.2647
Y- ALBE 1.0 Y+ ALBE 1.0 MESHY 0.0 1.2647
RADIUS 0.0 0.205 0.41 0.47 0.57 0.70
MIX 1 1 2 3 3 3
;
*----
* Isotropic tracking 
*----
GD_TDCM59 := G2S: GEOCELL ;
TRACK TRKFIL := SALT: GD_TDCM59 ::
  GAUS 4 TISO <<an2d>> <<densur>>   ;
TRACK := MCCGT: TRACK TRKFIL :: 
  AAC 1 SCR 0 MAXI 1000 GAUS KRYL 30 HDD 0.0 STIS 1 ;
SYS := ASM: MACRO TRACK TRKFIL :: 
  ARM HETE ;
FLUX := FLU: MACRO TRACK SYS TRKFIL :: 
  EDIT 1 TYPE K ;
assertS FLUX :: 'K-INFINITY' 1 1.064644 ;
EDITION := EDI: MACRO TRACK FLUX ::
  EDIT 3 SAVE ;
GD_TDCM59 TRACK TRKFIL SYS FLUX := DELETE: GD_TDCM59 TRACK TRKFIL SYS FLUX ;
*----
* Specular tracking with reflection
*----
GEOCELL := GEO: GEOCELL ::
X- REFL X+ REFL
Y- REFL Y+ REFL
;
GD_TDCM59 := G2S: GEOCELL ;
TRACK TRKFIL := SALT: GD_TDCM59 ::
  GAUS 4 TSPC <<an2d>> <<densur>>   ;
TRACK := MCCGT: TRACK TRKFIL :: 
  AAC 1 SCR 0 MAXI 1000 GAUS KRYL 30 HDD 0.0 STIS 1 ;
SYS := ASM: MACRO TRACK TRKFIL :: 
  ARM HETE ;
FLUX := FLU: MACRO TRACK SYS TRKFIL :: 
  TYPE K ;
assertS FLUX :: 'K-INFINITY' 1 1.064723 ;
EDITION := EDI: EDITION MACRO TRACK FLUX ::
  EDIT 3 SAVE ;
GD_TDCM59 TRACK TRKFIL SYS FLUX := DELETE: GD_TDCM59 TRACK TRKFIL SYS FLUX ;
*----
* Specular tracking with translation
*----
GEOCELL := GEO: GEOCELL ::
X- TRAN X+ TRAN
Y- TRAN Y+ TRAN
;
GD_TDCM59 TDCM59.ps := G2S: GEOCELL :: DRAWNOD ;
TRACK TRKFIL := SALT: GD_TDCM59 ::
  GAUS 4 TSPC <<an2d>> <<densur>>   ;
TRACK := MCCGT: TRACK TRKFIL :: 
  AAC 1 SCR 0 MAXI 1000 GAUS KRYL 30 HDD 0.0 STIS 1 ;
SYS := ASM: MACRO TRACK TRKFIL :: 
  ARM HETE ;
FLUX := FLU: MACRO TRACK SYS TRKFIL :: 
  TYPE K ;
assertS FLUX :: 'K-INFINITY' 1 1.064719 ;
EDITION := EDI: EDITION MACRO TRACK FLUX ::
  EDIT 3 SAVE ;
ECHO "test TDCM59 completed" ;
END: ;
QUIT "LIST" .
