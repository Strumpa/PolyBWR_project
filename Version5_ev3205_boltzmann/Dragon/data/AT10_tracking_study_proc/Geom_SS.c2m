***************************************************************************
* Deck Geom_SS                                                            *
*                                                                         *
* Treatment of SELF SHIELDING geometry for BWR pincell                    *
* ATRIUM-10 UOX pincell, geometry provided by M. Hursin (PSI)             *
*                                                                         *
* Author : R. Guasch, adapted from A. Hebert and A. Canbakan              *
*                                                                         *
*                                                                         *
* Dragon Procedure called as :                                            *
* GEOMSS := Geom_SS :: <<Rcomb1>> <<Rcomb2>> <<Rcomb3>> <<Rcomb4>>        *
*        <<Rgap>> <<Rclad>> <<pitch>>;                                    *
*                                                                         *
* Input data :                                                            *
* Rcomb1, Rcomb2, Rcomb3, Rcom4 : radial dicretization for cylindrical    *
* Fuel zone : 50/100, 80/100, 95/100 and 100/100 fuel surface (volume)    *
* Rgap, Rclad : radii for gap and cladding around fuel                    *
* pitch : side length of cartesian cell                                   *
* All dimensions are given in cm.                                         *
*                                                                         *
* Output data :                                                           *
* GEOMSS : geometry used for self shielding calculation                   *
*                                                                         *
***************************************************************************

PARAMETER  GEOMSS  ::  
       EDIT 0 
           ::: LINKED_LIST GEOMSS  ; 
;

* Modules used in this procedure :
MODULE GEO: END: ;

* Input data :

REAL Rcomb1  Rcomb2  Rcomb3  Rcomb4  ;
:: >>Rcomb1<<  >>Rcomb2<<  >>Rcomb3<<  >>Rcomb4<< ;

REAL Rgap Rclad pitch ;
:: >>Rgap<< >>Rclad<< >>pitch<< ;

GEOMSS := GEO: :: CARCEL 6 
  X- REFL X+ REFL
  Y- REFL Y+ REFL
  RADIUS 0.0 <<Rcomb1>> <<Rcomb2>>
             <<Rcomb3>> <<Rcomb4>> <<Rgap>> <<Rclad>>
  MIX 1 2 3 4 5 6 7
  MESHX 0.0 <<pitch>> MESHY 0.0 <<pitch>>
;

