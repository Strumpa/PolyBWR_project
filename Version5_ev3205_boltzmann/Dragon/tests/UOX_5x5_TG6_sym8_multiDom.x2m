*----
*  TEST CASE UOX_5x5_TG6_sym8_multiDom
*  SIMPLE 5 X 5 PWR ASSEMBLY WITH SYBIL
*  Input cross section data from Apex
*----
*  Define STRUCTURES and MODULES used
*----
LINKED_LIST ASSMB TRACK MACRO SYS FLUX ;
HDF5_FILE UOX_5x5 :: FILE './UOX_5x5_TG6_sym8_multiDom.h5' ;
MODULE GEO: HUTL: SPH: SYBILT: ASM: FLU: END: ;
REAL B2 ;
PROCEDURE assertS ;

REAL side1 := 1.26 ;
REAL side2 := side1 1.26 + ;
REAL side3 := side2 1.26 + ;
ASSMB := GEO: :: CAR2D 3 3 (*ASSEMBLY 5 X 5*)
          X- DIAG X+ REFL
          Y- SYME Y+ DIAG
          MESHX 0.0 <<side1>> <<side2>> <<side3>>
          MIX 1 2 4
                3 5
                  6
;

TRACK := SYBILT: ASSMB ::
     EDIT 1
     MAXR 40  (*MAXIMUM OF 40 REGIONS*)
     MAXZ 100000
     TITLE 'SIMPLE 5x5 FUEL ASSEMBLY'
     QUA2 6 6
     DP00 ;

HUTL: UOX_5x5 :: DIR
  GREP '/calc       1/miscellaneous/B2' 1 >>B2<< ;
ECHO 'buckling B2=' B2 ;
UOX_5x5 := SPH: UOX_5x5 TRACK :: EDIT 6 STEP AT 1
  EQUI 'equivalence_P0c_up0' ;
MACRO := SPH: UOX_5x5 :: EDIT 3 STEP AT 1 MACRO SPRD
  EQUI 'equivalence_P0c_up0' ;
SYS := ASM: MACRO TRACK :: PIJ ;
FLUX := FLU: MACRO TRACK SYS :: TYPE K LKRD SIGS BUCK <<B2>> ;
assertS FLUX :: 'K-INFINITY' 1 1.320080 ;

ECHO "UOX_5x5_TG6_sym8_multiDom completed" ;
END: ;
