****************************************************************
* CLE-2000 / DONJON script                                     *
* Purpose : initialize coupled neutronics/TH calculation       *
* Author : A. Hebert adapted by R.Guasch                       *
* Date : 16/09/2024                                            *
* CALL      :                                                  *
*  Fmap Matex Cpo Track := IniDONJON ;                         *
*                                                              *
****************************************************************
PARAMETER  Fmap Matex Cpo Track THData ::
  ::: LINKED_LIST Fmap Matex Cpo Track THData ; ;
STRING name_compo ;
:: >>name_compo<< ;
INTEGER Iz ;
:: >>Iz<< ;
DOUBLE Dmass ; 
:: >>Dmass<< ;
DOUBLE Dheight ; 
:: >>Dheight<< ;
DOUBLE Dpitch ;
:: >>Dpitch<< ;

LINKED_LIST Geom ;
MODULE   TRIVAT: UTL: END: ;

PROCEDURE  GeoCore SetFMap SetLocPara ;
SEQ_ASCII _COMPO :: FILE <<name_compo>> ;
*----
*  Local Parameters
*----
REAL mass := Dmass D_TO_R ;

REAL pitch := Dpitch D_TO_R ;
REAL height := Dheight D_TO_R ;

EVALUATE pitch := pitch 100.0 * ;
EVALUATE height := height 100.0 * ;

INTEGER MaxR := 10000 ; 

REAL Iz1 := Iz I_TO_R ;
*----
*  Recover the Multicompo
*----
STRING DIR := "EDIPARAM" ;
Cpo := _COMPO ;
UTL: Cpo :: DIR ;

REAL BMass := mass Iz1 / ;

Geom Matex := GeoCore :: <<pitch>> <<height>> <<Iz1>> ;

!Track := TRIVAT: Geom :: (*From simplePOW PyGan test*)
!   EDIT 1 MAXR <<MaxR>> DUAL 2 3 ;

Track := TRIVAT: Geom :: (*From pincell_mphy_thm Donjon test*)
   EDIT 1 MAXR <<MaxR>> MCFD 1 ;

*--
* Fuel map definition
*--
Fmap Matex := SetFMap Matex :: <<pitch>> <<height>> <<BMass>> <<Iz1>> ;


* Initialize TH Data : set constant Tf, Tc, Dc on all axial slices
ECHO "Iz1 = " Iz1 ;
Fmap := SetLocPara Fmap THData :: <<Iz1>> ;

END: ;
QUIT "LIST" .
