* UOX_Gd157.c2m, CLE-2000 script to call Dragon5
* Purpose : Test depletion of Gd157 on homogeneous geometry
* Homogeneous UOX + moderator + clad + Gd157
* Reference Serpen2 case : HOM_UOX_Gd157_mc (PyNjoy2016 and oldlib)
* Burnup calculation 


****************************************************************************
************************** VARIABLES DEFINITION ****************************
****************************************************************************

MODULE GEO: SYBILT: LIB: USS: SHI: AUTO: ASM: FLU: COMPO: EDI: EVO: GREP: UTL: DELETE: END: ;
LINKED_LIST GEOM LIBRARY LIBRARY2 TRACK_SS TRACK SYS FLUX ;

PARAMETER COMPO StepList ::
::: LINKED_LIST COMPO ;
::: LINKED_LIST StepList ; ;
STRING name_compo ;
:: >>name_compo<< ;
STRING ssh_module ;
:: >>ssh_module<< ;
STRING ssh_method ;
:: >>ssh_method<< ; 

STRING sat depl_sol ;
:: >>sat<< >>depl_sol<< ;

SEQ_BINARY TF_EXC TF_EXC_SS ;

STRING Library := "J311_295" ;

REAL Kinf ;
LINKED_LIST BURNUP EDIBU ;
SEQ_ASCII _COMPO :: FILE <<name_compo>> ;




****************************************************************************
***************** GEOMETRY DEFINTION OF HOMOGENEOUS UOX+GD *****************
****************************************************************************

GEOM := GEO: :: HOMOGE 
        MIX 1
      ;

****************************************************************************
******************** LIBRARY DEFINTION OF homogeneous Gd pin ***************
****************************************************************************

IF ssh_method "RSE" = THEN

  LIBRARY := LIB: ::
      EDIT 0
      NMIX 1    ! MAXIMUM OF MATERIAL MIXTURES
      RSE 5.0E-02
      ANIS 1

    DEPL LIB: DRAGON FIL: <<Library>>

    MIXS LIB: DRAGON FIL: <<Library>>

    MIX 1 750.0
      O16  = O16   2.955339E-02 
      H1  = H1   2.497844E-02
      U234  = U234   3.482587E-06 1
      U235  = U235   3.664503E-04 1
      U238  = U238   7.765571E-03 1

      Gd157  = Gd157 7.899877E-05 1

      Zr90  = Zr90   2.449439E-03 1
      Zr91  = Zr91   5.341629E-04 1
      Zr92  = Zr92   8.164794E-04 1
      Zr94  = Zr94   8.274287E-04 1
      Zr96  = Zr96   1.333029E-04 1

      Cr50  = Cr50   3.698141E-07
      Cr52  = Cr52   7.131555E-06
      Cr53  = Cr53   8.086515E-07
      Cr54  = Cr54   2.012894E-07

      Fe54  = Fe54   6.253124E-07
      Fe56  = Fe56   9.816079E-06
      Fe57  = Fe57   2.266963E-07
      Fe58  = Fe58   3.016907E-08

      Ni58  = Ni58   2.823166E-06
      Ni60  = Ni60   1.087478E-06
      Ni61  = Ni61   4.727185E-08
      Ni62  = Ni62   1.507292E-07
      Ni64  = Ni64   3.838073E-08

      O17  = O17   1.316917E-08

      Sn112  = Sn112   5.243423E-07
      Sn114  = Sn114   3.567679E-07
      Sn115  = Sn115   1.837913E-07
      Sn116  = Sn116   7.859755E-06
      Sn117  = Sn117   4.151493E-06
      Sn118  = Sn118   1.309232E-05
      Sn119  = Sn119   4.643408E-06
      Sn120  = Sn120   1.761148E-05
      Sn122  = Sn122   2.502800E-06
      Sn124  = Sn124   3.129844E-06

      He4  = He4   2.145381E-06
  ;
ELSEIF ssh_method "PT" = THEN

  LIBRARY := LIB: ::
      EDIT 0
      NMIX 1    ! MAXIMUM OF MATERIAL MIXTURES
      CALENDF 4 PT 
      ANIS 1

    DEPL LIB: DRAGON FIL: <<Library>>

    MIXS LIB: DRAGON FIL: <<Library>>

    MIX 1 750.0
      O16  = O16   2.955339E-02 
      H1  = H1   2.497844E-02
      U234  = U234   3.482587E-06 1
      U235  = U235   3.664503E-04 1
      U238  = U238   7.765571E-03 1

      Gd157  = Gd157 7.899877E-05 1

      Zr90  = Zr90   2.449439E-03 1
      Zr91  = Zr91   5.341629E-04 1
      Zr92  = Zr92   8.164794E-04 1
      Zr94  = Zr94   8.274287E-04 1
      Zr96  = Zr96   1.333029E-04 1

      Cr50  = Cr50   3.698141E-07
      Cr52  = Cr52   7.131555E-06
      Cr53  = Cr53   8.086515E-07
      Cr54  = Cr54   2.012894E-07

      Fe54  = Fe54   6.253124E-07
      Fe56  = Fe56   9.816079E-06
      Fe57  = Fe57   2.266963E-07
      Fe58  = Fe58   3.016907E-08

      Ni58  = Ni58   2.823166E-06
      Ni60  = Ni60   1.087478E-06
      Ni61  = Ni61   4.727185E-08
      Ni62  = Ni62   1.507292E-07
      Ni64  = Ni64   3.838073E-08

      O17  = O17   1.316917E-08

      Sn112  = Sn112   5.243423E-07
      Sn114  = Sn114   3.567679E-07
      Sn115  = Sn115   1.837913E-07
      Sn116  = Sn116   7.859755E-06
      Sn117  = Sn117   4.151493E-06
      Sn118  = Sn118   1.309232E-05
      Sn119  = Sn119   4.643408E-06
      Sn120  = Sn120   1.761148E-05
      Sn122  = Sn122   2.502800E-06
      Sn124  = Sn124   3.129844E-06

      He4  = He4   2.145381E-06
  ;
ELSEIF ssh_method "SUBG" = THEN
  LIBRARY := LIB: ::
      EDIT 0
      NMIX 1    ! MAXIMUM OF MATERIAL MIXTURES
      SUBG
      ANIS 1

    DEPL LIB: DRAGON FIL: <<Library>>

    MIXS LIB: DRAGON FIL: <<Library>>

    MIX 1 750.0
      O16  = O16   2.955339E-02 
      H1  = H1   2.497844E-02
      U234  = U234   3.482587E-06 1
      U235  = U235   3.664503E-04 1
      U238  = U238   7.765571E-03 1

      Gd157  = Gd157 7.899877E-05 1

      Zr90  = Zr90   2.449439E-03 1
      Zr91  = Zr91   5.341629E-04 1
      Zr92  = Zr92   8.164794E-04 1
      Zr94  = Zr94   8.274287E-04 1
      Zr96  = Zr96   1.333029E-04 1

      Cr50  = Cr50   3.698141E-07
      Cr52  = Cr52   7.131555E-06
      Cr53  = Cr53   8.086515E-07
      Cr54  = Cr54   2.012894E-07

      Fe54  = Fe54   6.253124E-07
      Fe56  = Fe56   9.816079E-06
      Fe57  = Fe57   2.266963E-07
      Fe58  = Fe58   3.016907E-08

      Ni58  = Ni58   2.823166E-06
      Ni60  = Ni60   1.087478E-06
      Ni61  = Ni61   4.727185E-08
      Ni62  = Ni62   1.507292E-07
      Ni64  = Ni64   3.838073E-08

      O17  = O17   1.316917E-08

      Sn112  = Sn112   5.243423E-07
      Sn114  = Sn114   3.567679E-07
      Sn115  = Sn115   1.837913E-07
      Sn116  = Sn116   7.859755E-06
      Sn117  = Sn117   4.151493E-06
      Sn118  = Sn118   1.309232E-05
      Sn119  = Sn119   4.643408E-06
      Sn120  = Sn120   1.761148E-05
      Sn122  = Sn122   2.502800E-06
      Sn124  = Sn124   3.129844E-06

      He4  = He4   2.145381E-06
  ;

ENDIF ;
****************************************************************************************
********************* TRAKING GEOM_hom/_SS FOR FLUX/SSH CALCULATION ********************
****************************************************************************************

* ---
* FLUX+SSH TRACKING
* ---


TRACK := SYBILT: GEOM ::
    TITLE 'HOM_UOX_Gd157 test case'
    MAXR 2
    ;


************************************************************************
*  REFERENCE LOCAL PARAMETERS                                          *
************************************************************************
SEQ_ASCII FIG_BWR :: FILE './FIG_BWR_CELL_1level.ps' ;

************************************************************************
*  BEGIN DECLARATION                                                   *
************************************************************************

INTEGER nstep nauto maxcompo ;
GREP: StepList :: LENGTH ListBU >>nstep<< ;
GREP: StepList :: LENGTH ListAutop >>nauto<< ;
GREP: StepList :: LENGTH ListCompo >>maxcompo<< ;
ECHO "nstep=" nstep "nauto=" nauto "maxcompo=" maxcompo ;
************************************************************************
*  END DECLARATION                                                     *
************************************************************************
************************************************************************
*  BEGIN COMPO INITIALIZATION                                          *
************************************************************************
COMPO := COMPO: ::
    EDIT 0
    STEP UP 'EDIBU'
    MAXCAL 5
    COMM 'Evolution burnup pincell' ENDC
    PARA 'Burnup' IRRA
    ISOT 9 U234 U235 U238 Pu239 Pu240 Gd155 Gd157 Xe135 Sm149
    INIT
;

************************************************************************
*  END COMPO INITIALIZATION                                            *
************************************************************************

************************************************************************
*  NOMINAL REFERENCE CALCULATION                                       *
************************************************************************
INTEGER istepNext ;
REAL BUbeg Tbeg ;
REAL BU BUautop BUcompo := 0.0 0.0 0.0 ;
REAL Norm_f2 := 38.6 ; ! SPECIFIC POWER MW/t
REAL BUend Tend := 0. 0. ;
INTEGER istep :=  0 ;
INTEGER iauto :=  1 ;
INTEGER iCompo :=  1 ;
ECHO "start burnup loop" ;
WHILE istep nstep < DO

 EVALUATE istep := istep 1 + ;
 ECHO "burnup step: " istep "/" nstep ;
 
 EVALUATE BUbeg Tbeg := BUend Tend ;
 ECHO "BUbeg = " BUbeg "and Tbeg = " Tbeg ;  
 GREP: StepList :: GETVAL 'ListBU' <<istep>> >>BUend<< ;
 EVALUATE Tend := BUend Norm_f2 / ;
 ECHO "BUend from StepList = " BUend " and computed Tend = " Tend ;
 ECHO "Nominal depletion between" Tbeg " and" Tend "days" ;

*----
* USS
*----
  IF istep 1 = THEN
     ECHO ".perform self-shielding at" Tbeg "days" ;
    IF ssh_module "USS" = THEN
        LIBRARY2 := USS: LIBRARY TRACK ::
                    EDIT 1 PASS 3
                  ;
    ELSEIF ssh_module "SHI" = THEN
        LIBRARY2 := SHI: LIBRARY TRACK :: 
                    EDIT 1 NOLJ
                  ;
    ELSEIF ssh_module "AUTO" = THEN
        LIBRARY2 := AUTO: LIBRARY TRACK :: 
                    EDIT 2 GRMIN 52 PASS 3 MAXT 100000 
                  ;
    ENDIF ;

  ELSE
     GREP: StepList :: GETVAL "ListAutop" <<iauto>> >>BUautop<< ;
     GREP: StepList :: GETVAL "ListBU" <<istep>> >>BU<< ;
     ECHO "HERE, step is =" istep "iauto is =" iauto ;
     ECHO "BU is =" BU " and BUautop =" BUautop ;  
     IF BUautop BU = THEN
       ECHO "perform self-shielding at" Tbeg "days" ;
      IF ssh_module "USS" = THEN
        LIBRARY2 := USS: LIBRARY LIBRARY2 TRACK ::
                    EDIT 1 PASS 3
                  ;
      ELSEIF ssh_module "SHI" = THEN
        LIBRARY2 := SHI: LIBRARY LIBRARY2 TRACK :: 
                    EDIT 1 NOLJ
                  ;
      ELSEIF ssh_module "AUTO" = THEN
        LIBRARY2 := AUTO: LIBRARY LIBRARY2 TRACK :: 
                    EDIT 2 GRMIN 52 PASS 3 MAXT 100000  
                  ;
      ENDIF ;
      EVALUATE iauto := iauto 1 + ;
      ECHO "iauto is = " iauto ;
    ENDIF ;
  ENDIF ;

************************************************************************
*  BEGIN FLUX CALCULATION                                              *
************************************************************************
  SYS := ASM: LIBRARY2 TRACK :: PIJ EDIT 0 ;    

  IF istep 1 = THEN     
     FLUX := FLU: LIBRARY2 SYS TRACK :: 
     EDIT 1 TYPE K ;
  ELSE
     FLUX := FLU: FLUX LIBRARY2 SYS TRACK :: 
     EDIT 1 TYPE K ;
  ENDIF ;
 
  SYS := DELETE: SYS ;

  IF istep 1 = THEN
    IF sat "SAT" = sat "SATOFF" = + THEN
      BURNUP LIBRARY2 := EVO: LIBRARY2 FLUX TRACK ::
        EDIT 0
        <<depl_sol>>
        GLOB
        <<sat>>
        SAVE <<Tbeg>> DAY POWR <<Norm_f2>>
      ;
    ELSE
      BURNUP LIBRARY2 := EVO: LIBRARY2 FLUX TRACK ::
        EDIT 0
        <<depl_sol>>
        GLOB
        SAVE <<Tbeg>> DAY POWR <<Norm_f2>>
      ;
    ENDIF ;
  ELSE
    IF sat "SAT" = sat "SATOFF" = + THEN
      ECHO "sat = " sat ;
      BURNUP LIBRARY2 := EVO: BURNUP LIBRARY2 FLUX TRACK ::
        EDIT 0
        <<depl_sol>>
        GLOB
        <<sat>>
        SAVE <<Tbeg>> DAY POWR <<Norm_f2>>
      ;
    ELSE
      ECHO "no SAT keyword" ; 
      BURNUP LIBRARY2 := EVO: BURNUP LIBRARY2 FLUX TRACK ::
        EDIT 0
        <<depl_sol>>
        GLOB
        SAVE <<Tbeg>> DAY POWR <<Norm_f2>>
      ; 
    ENDIF ;
  ENDIF ;

  GREP: StepList :: GETVAL "ListCompo" <<iCompo>> >>BUcompo<< ;
  GREP: StepList :: GETVAL "ListBU" <<istep>> >>BU<< ;
  ECHO "here istep = " istep "and iCompo =" iCompo BUcompo ;
  IF BUcompo BU = THEN
        EDIBU := EDI: FLUX LIBRARY2 TRACK ::
        EDIT 0
        MICR 9 U234 U235 U238 Pu239 Pu240 Gd155 Gd157 Xe135 Sm149
        COND
        SAVE ON FUEL ;
        COMPO := COMPO: COMPO EDIBU BURNUP LIBRARY2 ::
        EDIT 0
        STEP UP 'EDIBU'
        SET <<Tbeg>> DAY ;

        EDIBU := DELETE: EDIBU ;
        EVALUATE iCompo := iCompo 1 + ;
  ENDIF ;

  GREP: FLUX :: GETVAL 'K-INFINITY  ' 1 1 1 >>Kinf<< ;
  ECHO "+++ Burnup=" BUend " Kinf=" Kinf " at step" istep ;

  IF istep nstep < THEN
    EVALUATE istepNext := istep 1 + ;
    ECHO "istepNext = " istepNext ;
    GREP: StepList :: GETVAL 'ListBU' <<istepNext>> >>BUend<< ;
    ECHO "BUend from StepList = " BUend ;
    EVALUATE Tend := BUend Norm_f2 / ;
    ECHO "Tbeg was = " Tbeg ;
    ECHO "Tend evaluated = " Tend ;
  
    BURNUP LIBRARY2 := EVO: BURNUP LIBRARY2 FLUX TRACK ::
     EDIT 3 DEPL <<Tbeg>> <<Tend>> DAY POWR <<Norm_f2>> 
     EXPM 1.0E15 GLOB ;
  
  ENDIF ;
 
************************************************************************
*  END FLUX CALCULATION                                                *
************************************************************************

ENDWHILE ;
************************************************************************
*  END OF REFERENCE BURNUP LOOP WITH DEPLETION                         *
************************************************************************

************************************************************************
*  EXPORT MULTICOMPO IN ASCII FORMAT                                   *
************************************************************************
_COMPO := COMPO ;

END: ;



