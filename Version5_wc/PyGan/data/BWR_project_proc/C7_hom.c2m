* C7_hom.x2m, CLE-2000 script to call Dragon5
* Purpose : Test Gd depletion on homogeneous geometry
* homogeneous square, homogeneous C7 composition
* Reference Serpen2 case : AT10_C7_hom_mc
* Burnup calculation 


****************************************************************************
************************** VARIABLES DEFINITION ****************************
****************************************************************************

MODULE GEO: G2S: NXT: SYBILT: SALT: TLM: MCCGT: LIB: USS: ASM: FLU: COMPO: EDI: EVO: GREP: UTL: DELETE: END: ;
LINKED_LIST GEOM_hom GEOM_hom_SS LIBRARY LIBRARY2 TRACK_SS TRACK SYS FLUX ;

SEQ_ASCII Geo_G2S GeoSS_G2S ;

SEQ_ASCII FIG_main.ps :: FILE './FIG_main.ps' ;
SEQ_ASCII FIG_SS.ps :: FILE './FIG_selfshielding.ps' ;
PARAMETER COMPO StepList ::
::: LINKED_LIST COMPO ;
::: LINKED_LIST StepList ; ;
STRING name_compo ;
:: >>name_compo<< ;

SEQ_BINARY TF_EXC TF_EXC_SS ;

STRING Library := "J311_295" ;

REAL Kinf ;
LINKED_LIST BURNUP EDIBU ;
SEQ_ASCII _COMPO :: FILE <<name_compo>> ;
****************************************************************************
***************** GEOMETRY DEFINTION OF HOMOGENEOUS UOX+GD *****************
****************************************************************************
REAL Pitch_C := 1.29500 ;



GEOM_hom_SS := GEO: :: CAR2D 1 1
    X- REFL X+ REFL 
    Y- REFL Y+ REFL
    MIX 1
    MESHX 0.0 <<Pitch_C>>
    MESHY 0.0 <<Pitch_C>>
;

GEOM_hom := GEO: :: CAR2D 1 1
    X- REFL X+ REFL 
    Y- REFL Y+ REFL
    MIX 1
    MESHX 0.0 <<Pitch_C>>
    MESHY 0.0 <<Pitch_C>>
;

****************************************************************************
******************** LIBRARY DEFINTION OF homogeneous Gd pin ***************
****************************************************************************


LIBRARY := LIB: ::
    EDIT 0
    NMIX 100    ! MAXIMUM OF MATERIAL MIXTURES
    RSE 1.0E-03 !1.0E-04
    ANIS 3

  DEPL LIB: DRAGON FIL: <<Library>>

  MIXS LIB: DRAGON FIL: <<Library>>

  MIX 1 750.0
    O16  = O16   0.02955338548712106
    Gd154  = Gd154   1.1004278103192103e-05
    Gd155  = Gd155   7.470798365097093e-05
    Gd156  = Gd156   0.0001033293279699805
    Gd157  = Gd157   7.899877184560855e-05
    Gd158  = Gd158   0.0001253890272765292
    Gd160  = Gd160   0.00011034602866474086
    U234  = U234   3.482587194905372e-06
    U235  = U235   0.0003664502612644706
    U238  = U238   0.007765571276708093
    He4  = He4   2.1453806701140634e-06
    Ni60  = Ni60   1.0874776456363846e-06
    Zr94  = Zr94   0.0008274286922000575
    Fe58  = Fe58   3.016906693168572e-08
    Sn112  = Sn112   5.243422989291285e-07
    Sn116  = Sn116   7.859754663712027e-06
    Zr96  = Zr96   0.00013330285725980193
    Cr50  = Cr50   3.698141154931749e-07
    Zr92  = Zr92   0.00081647943973399
    Sn114  = Sn114   3.567679112022196e-07
    Sn117  = Sn117   4.151493388148665e-06
    Zr91  = Zr91   0.0005341628550143306
    Sn122  = Sn122   2.5028000772241687e-06
    O17  = O17   1.3169171610381536e-08
    Sn124  = Sn124   3.129843649130694e-06
    Cr54  = Cr54   2.012894237557363e-07
    Ni62  = Ni62   1.507292113230215e-07
    Cr53  = Cr53   8.086514927664982e-07
    Sn115  = Sn115   1.837912639585452e-07
    Ni61  = Ni61   4.727184640619601e-08
    Ni64  = Ni64   3.838072579008798e-08
    Cr52  = Cr52   7.13156598371902e-06
    Zr90  = Zr90   0.0024494394411665637
    Sn120  = Sn120   1.761147822111496e-05
    Ni58  = Ni58   2.8231658471965945e-06
    Fe54  = Fe54   6.253123805390552e-07
    Fe57  = Fe57   2.2669631197113722e-07
    Fe56  = Fe56   9.81607944647493e-06
    Sn118  = Sn118   1.3092317035740116e-05
    Sn119  = Sn119   4.643407544437486e-06
    H1  = H1_H2O   0.02497844090816797
;
****************************************************************************************
********************* TRAKING GEOM_hom/_SS FOR FLUX/SSH CALCULATION ********************
****************************************************************************************

* ---
* TRACKING PARAMTERES
* ---

INTEGER an2d := 12 ;  ! ANGULAR QUADRATURE PARAMETER 
REAL densur := 10.0 ; ! DENSITY OF INTEGRATION LINES CM^-1

INTEGER iqua2 := 12 ;
INTEGER nseg := 3 ;

* ---
* FLUX CALC TRACKING
* ---
!Geo_G2S FIG_main.ps := G2S: GEOM_hom :: ;

TRACK TF_EXC := NXT: GEOM_hom ::
  EDIT 1
  ALLG
  TSPC EQW2 <<an2d>> <<densur>> REND
;


* ---
* SELF-SHIELDING TRACKING
* ---
!GeoSS_G2S FIG_SS.ps := G2S: GEOM_hom_SS :: ;


TRACK_SS := SYBILT: GEOM_hom_SS ::
    TITLE 'TRACKING FOR PINCELL SS'
    MAXR 50 QUA2 <<iqua2>> <<nseg>> DP01 MAXZ 200000 
;


************************************************************************
*  REFERENCE LOCAL PARAMETERS                                          *
************************************************************************
SEQ_ASCII FIG_BWR :: FILE './FIG_BWR_CELL_1level.ps' ;

************************************************************************
*  BEGIN DECLARATION                                                   *
************************************************************************

INTEGER nstep nauto maxcompo ;
GREP: StepList :: LENGTH ListBU >>nstep<< ;
GREP: StepList :: LENGTH ListAutop >>nauto<< ;
GREP: StepList :: LENGTH ListCompo >>maxcompo<< ;
ECHO "nstep=" nstep "nauto=" nauto "maxcompo=" maxcompo ;
************************************************************************
*  END DECLARATION                                                     *
************************************************************************
************************************************************************
*  BEGIN COMPO INITIALIZATION                                          *
************************************************************************
COMPO := COMPO: ::
    EDIT 0
    STEP UP 'EDIBU'
    MAXCAL 5
    COMM 'Evolution burnup pincell' ENDC
    PARA 'Burnup' IRRA
    ISOT 11 U235 U236 U238 Pu239 Pu240 Pu241 Pu242 Gd155 Gd157 Xe135 Sm149
    INIT
;

************************************************************************
*  END COMPO INITIALIZATION                                            *
************************************************************************

************************************************************************
*  NOMINAL REFERENCE CALCULATION                                       *
************************************************************************
INTEGER istepNext ;
REAL BUbeg Tbeg ;
REAL BU BUautop BUcompo := 0.0 0.0 0.0 ;
REAL Norm_f2 := 38.6 ; ! SPECIFIC POWER MW/t
REAL BUend Tend := 0. 0. ;
INTEGER istep :=  0 ;
INTEGER iauto :=  1 ;
INTEGER iCompo :=  1 ;
ECHO "start burnup loop" ;
WHILE istep nstep < DO

 EVALUATE istep := istep 1 + ;
 ECHO "burnup step: " istep "/" nstep ;
 
 EVALUATE BUbeg Tbeg := BUend Tend ;
 GREP: StepList :: GETVAL 'ListBU' <<istep>> >>BUend<< ;
 EVALUATE Tend := BUend Norm_f2 / ;
 ECHO "Nominal depletion between" Tbeg " and" Tend "days" ;

*----
* USS
*----
  IF istep 1 = THEN
     ECHO ".perform self-shielding at" Tbeg "days" ;
    LIBRARY2 := USS: LIBRARY TRACK_SS ::
        EDIT 1 PASS 3 MAXST 20
        CALC
            REGI W1 U238 ALL
        ENDC
    ;
  ELSE
     GREP: StepList :: GETVAL "ListAutop" <<iauto>> >>BUautop<< ;
     GREP: StepList :: GETVAL "ListBU" <<istep>> >>BU<< ;
     ECHO "HERE, step is =" istep "iauto is =" iauto ;
     ECHO "BU is =" BU " and BUautop =" BUautop ;  
     IF BUautop BU = THEN
       ECHO "perform self-shielding at" Tbeg "days" ;
    LIBRARY2 := USS: LIBRARY LIBRARY2 TRACK_SS ::
        EDIT 1 PASS 3 MAXST 20
        CALC
            REGI W1 U238 ALL
        ENDC
    ;
      EVALUATE iauto := iauto 1 + ;
      ECHO "iauto is = " iauto ;
    ENDIF ;
  ENDIF ;

************************************************************************
*  BEGIN MOC FLUX CALCULATION                                          *
************************************************************************
  SYS := ASM: LIBRARY2 TRACK TF_EXC :: PIJ EDIT 0 ;    

  IF istep 1 = THEN     
     FLUX := FLU: LIBRARY2 SYS TRACK TF_EXC :: 
     EDIT 1 TYPE K B1 SIGS ;
  ELSE
     FLUX := FLU: FLUX LIBRARY2 SYS TRACK TF_EXC :: 
     EDIT 1 TYPE K B1 SIGS ;
  ENDIF ;
 
  SYS := DELETE: SYS ;

  IF istep 1 = THEN
    BURNUP LIBRARY2 := EVO: LIBRARY2 FLUX TRACK ::
      EDIT 0
      SAVE <<Tbeg>> DAY POWR <<Norm_f2>>
    ;
  ELSE
    BURNUP LIBRARY2 := EVO: BURNUP LIBRARY2 FLUX TRACK ::
      EDIT 0
      SAVE <<Tbeg>> DAY POWR <<Norm_f2>>
    ;
  ENDIF ;

  GREP: StepList :: GETVAL "ListCompo" <<iCompo>> >>BUcompo<< ;
  GREP: StepList :: GETVAL "ListBU" <<istep>> >>BU<< ;
  ECHO "here istep = " istep "and iCompo =" iCompo BUcompo ;
  IF BUcompo BU = THEN
        EDIBU := EDI: FLUX LIBRARY2 TRACK ::
        EDIT 0
        MICR 11 U235 U236 U238 Pu239 Pu240 Pu241 Pu242 Gd155 Gd157 Xe135 Sm149
        COND
        SAVE ON FUEL ;
        COMPO := COMPO: COMPO EDIBU BURNUP LIBRARY ::
        EDIT 0
        STEP UP 'EDIBU'
        SET <<Tbeg>> DAY ;

        EDIBU := DELETE: EDIBU ;
        EVALUATE iCompo := iCompo 1 + ;
  ENDIF ;

  GREP: FLUX :: GETVAL 'K-INFINITY  ' 1 1 1 >>Kinf<< ;
  ECHO "+++ Burnup=" BUend " Kinf=" Kinf " at step" istep ;

  IF istep nstep < THEN
    EVALUATE istepNext := istep 1 + ;
    GREP: StepList :: GETVAL 'ListBU' <<istepNext>> >>BUend<< ;
    EVALUATE Tend := BUend Norm_f2 / ;
  
    BURNUP LIBRARY2 := EVO: BURNUP LIBRARY2 FLUX TRACK ::
     EDIT 3 DEPL <<Tbeg>> <<Tend>> DAY POWR <<Norm_f2>> 
     EXPM 1.0E15 GLOB ;
  
  ENDIF ;
 
************************************************************************
*  END FLUX CALCULATION                                                *
************************************************************************

ENDWHILE ;
************************************************************************
*  END OF REFERENCE BURNUP LOOP WITH DEPLETION                         *
************************************************************************

************************************************************************
*  EXPORT MULTICOMPO IN ASCII FORMAT                                   *
************************************************************************
_COMPO := COMPO ;

END: ;



