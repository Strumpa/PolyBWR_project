*----
*  TEST CASE PCC Gd157
*  AT10_45Gd bemchmark
*  295 groups ENDFB-VIII.1 draglib
*  Purpose : Investigate predictor-corrector for Gd cell depletion
*  Compare to Serpent2 : CE-LI method / OpenMC CE-CM method
*   Implementing CECM method
*     - Constant Extrapolaton method up until dt/2
*     - Constant Extrpolation : constant midpoint on corrector
*
* ---
* INPUT & OUTPUT PARAMETERS
* ---
PARAMETER COMPO LIBRARY TRACK TF_EXC TRACK_SS TF_EXC_SS StepList ::
::: LINKED_LIST COMPO ;
::: LINKED_LIST LIBRARY ; 
::: LINKED_LIST TRACK ;
::: SEQ_BINARY TF_EXC ;
::: LINKED_LIST TRACK_SS ;
::: SEQ_BINARY TF_EXC_SS ;
::: LINKED_LIST StepList ; ;
STRING name_compo ;
:: >>name_compo<< ;

STRING deplsolopt glob_opt ;
:: >>deplsolopt<< >>glob_opt<< ; 

LINKED_LIST
  SYS FLUX BURNUP EDITION LIB2 BOSLIB ;
MODULE
  GREP: USS: ASM: FLU: EVO: EDI: COMPO: DELETE: END: ;
*----
*  Define variables
*----
INTEGER
  istep := 1 ;
INTEGER nstep istepNext ncompo ;
REAL
  evobeg evoend evomid BUbeg BUend Kinf ;
SEQ_ASCII _COMPO :: FILE <<name_compo>> ;


REAL Norm_f2 := 26.5 ; ! SPECIFIC POWER MW/t
REAL BUautop BUcompo := 0.0 0.0 ;
INTEGER iauto :=  1 ;
INTEGER iCompo :=  2 ;


*  Create the reactor COMPO
*----
COMPO := COMPO: ::
    EDIT 0
    STEP UP 'EDIBU'
    MAXCAL 5
    COMM 'Evolution burnup pincell' ENDC
    PARA 'Burnup' IRRA
    ISOT 14 U235 U238 Pu239 Pu240 Pu241 Pu242 
          Gd154 Gd155 Gd156 Gd157 Gd158 Gd160 Xe135 Sm149
    INIT
;
*----
*  Self-Shielding calculation SALT
*  Transport calculation      SALT
*  Flux calculation, no leakage model
*----
*--- Perform self-shielding at t=0
EVALUATE evobeg := 0.0 ;
ECHO "Performing self-shielding at t=0 to obtain first BoS Library2" ;

LIB2 := USS: LIBRARY TRACK_SS TF_EXC_SS  ::
            EDIT 0 PASS 4
            ;

SYS := ASM: LIB2 TRACK TF_EXC :: ARM EDIT 0 ;  
ECHO "Performing flux calculation at t=0 to obtain first BoS flux" ;
FLUX := FLU: LIB2 SYS TRACK TF_EXC :: 
    EDIT 1 TYPE K ;

SYS := DELETE: SYS ;

BURNUP BOSLIB := EVO: LIB2 FLUX TRACK ::
    SAVE <<evobeg>> DAY POWR <<Norm_f2>> ;


EDITION := EDI: FLUX LIB2 TRACK ::
        EDIT 0
        MICR 14 U235 U238 Pu239 Pu240 Pu241 Pu242 
        Gd154 Gd155 Gd156 Gd157 Gd158 Gd160 Xe135 Sm149
        MERG MIX 1 0 0 0
        COND
        SAVE ON FUEL 
  ;

COMPO := COMPO: COMPO EDITION BURNUP ::
  EDIT 3
  STEP UP 'EDIBU'
  SET <<evobeg>> DAY
;

EDITION := DELETE: EDITION ;
BOSLIB := DELETE: BOSLIB ;
*----
*  Burnup loop: for first step BURNUP is created
*  while for other steps it is modified
*  two burnup per step:
*  1) get a first approximation of composition at t+dt/2 : deplete t -> t+dt/2
*     followed by a transport calculation
*  2) use mid-stage flux distribution to 
*     perform midpoint correction, deplete t -> t+dt using midpoint reaction rates
*----
GREP: StepList :: LENGTH ListBU >>nstep<< ;
GREP: StepList :: LENGTH ListCompo >>ncompo<< ;
WHILE istep nstep < DO
    EVALUATE istepNext := istep 1 + ;
    ECHO "istep = " istep ;
    ECHO "istepNext = " istepNext ;
    GREP: StepList :: GETVAL 'ListBU' <<istep>> >>BUbeg<< ;
    GREP: StepList :: GETVAL 'ListBU' <<istepNext>> >>BUend<< ;
    GREP: StepList :: GETVAL "ListAutop" <<iauto>> >>BUautop<< ;

    EVALUATE evobeg := BUbeg Norm_f2 / ;
    EVALUATE evoend := BUend Norm_f2 / ;
    EVALUATE evomid := evoend evobeg - 2.0 / ;
    EVALUATE evomid := evomid evobeg + ;

    ECHO "Begin step = " istep ;
    ECHO "Performing evolution stage from Tbeg = " evobeg " to Tend = " evoend ; 
    ECHO "with midpoint at Tmid = " evomid ;
    IF istep 1 > THEN
        ECHO "AT BoS BUautop =" BUautop " and BUbeg =" BUbeg ;
        IF BUautop BUbeg = THEN
            ECHO "Call to USS: at BoS" ;
            LIB2 := USS: LIBRARY LIB2 TRACK_SS TF_EXC_SS  ::
                        EDIT 0 PASS 4
                        ;
            EVALUATE iauto := iauto 1 + ;
        ENDIF ;
    ENDIF ;
    SYS := ASM: LIB2 TRACK TF_EXC :: ARM EDIT 0 ; 
    ECHO "Performing flux calculation at step = " istep " to obtain first BoS flux" ;
    IF istep 1 > THEN
        FLUX := FLU: FLUX LIB2 SYS TRACK TF_EXC :: 
            EDIT 1 TYPE K ; 
    ENDIF ;
    SYS := DELETE: SYS ;
  
    * Normalize BOS rates to total power :
    BURNUP LIB2 := EVO: BURNUP LIB2 FLUX TRACK ::
        SAVE <<evobeg>> DAY POWR <<Norm_f2>>
    ;
    ECHO "Editing burnup object at step : " istep ;
    ECHO "Calling EVO for BOS depletion between Tbeg =" evobeg " and Tmid =" evomid ;
    * First deplete BOS material with BOS flux to obtain first guess of EOS material
    BURNUP LIB2 := EVO: BURNUP LIB2 FLUX TRACK ::
        DEPL <<evobeg>> <<evomid>> DAY POWR <<Norm_f2>> 
        <<deplsolopt>> NOEX <<glob_opt>> 
    ;

    * Perform self-shielding of EOS library ?
    
    ECHO "iauto is = " iauto ;
    ECHO "HERE, step is =" istep "iauto is =" iauto ;
    ECHO "Correspinding BUbeg =" BUbeg ;
    SYS := ASM: LIB2 TRACK TF_EXC :: ARM EDIT 0 ;

    * Perform flux calculation with guess MoS materials to obtain MoS flux
    FLUX := FLU: FLUX LIB2 SYS TRACK TF_EXC :: 
        EDIT 1 TYPE K ;

    SYS := DELETE: SYS ;

    * Renormalize reaction rates @ BoS using MoS rates :
    *BURNUP LIB2 := EVO: BURNUP LIB2 FLUX TRACK ::
    *    SAVE <<evobeg>> DAY POWR <<Norm_f2>> ;

    ECHO "Calling EVO for CM depletion between Tbeg =" evobeg " and Tend =" evoend ;
    BURNUP LIB2 := EVO: BURNUP LIB2 FLUX TRACK ::
        SAVE <<evoend>> DAY POWR <<Norm_f2>>
        DEPL <<evobeg>> <<evoend>> DAY POWR <<Norm_f2>> 
        <<deplsolopt>> NOEX <<glob_opt>>
        ;

    GREP: FLUX :: GETVAL 'K-INFINITY  ' 1 1 1 >>Kinf<< ;
    ECHO "+++ Burnup=" BUend " Kinf=" Kinf " at step" istep ;

    * Renormalize reaction rates # EoS using EoS rates :
    BURNUP LIB2 := EVO: BURNUP LIB2 FLUX TRACK ::
        SAVE <<evoend>> DAY POWR <<Norm_f2>> ;

    IF iCompo ncompo <= THEN
    GREP: StepList :: GETVAL "ListCompo" <<iCompo>> >>BUcompo<< ;
    ECHO "here istep = " istep "and iCompo =" iCompo BUcompo ;
        IF BUcompo BUend = THEN
                ECHO "Saving COMPO at BU =" BUcompo ;
                EDITION := EDI: FLUX LIB2 TRACK ::
                EDIT 0
                MICR 14 U235 U238 Pu239 Pu240 Pu241 Pu242 
                Gd154 Gd155 Gd156 Gd157 Gd158 Gd160 Xe135 Sm149
                MERG MIX 1 0 0 0
                COND
                SAVE ON FUEL 
                ;
                COMPO := COMPO: COMPO EDITION BURNUP LIB2 ::
                EDIT 0
                STEP UP 'EDIBU'
                SET <<evoend>> DAY 
                ;

                EDITION := DELETE: EDITION ;
                EVALUATE iCompo := iCompo 1 + ;
        ENDIF ;
    ENDIF ;
    EVALUATE istep := istepNext ;
ENDWHILE ;



************************************************************************
*  EXPORT MULTICOMPO IN ASCII FORMAT                                   *
************************************************************************
_COMPO := COMPO ;


ECHO "CECM completed" ;
END: ;   
QUIT "LIST" .
