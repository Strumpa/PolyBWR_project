* --------------------------------
*    INPUT & OUTPUT PARAMETERS
* --------------------------------
PARAMETER GEOM_2x2 GEOM_2x2_SS ::
::: LINKED_LIST GEOM_2x2 ; 
::: LINKED_LIST GEOM_2x2_SS ; ;


* -------------------------------
*    STRUCTURES AND MODULES
* -------------------------------
MODULE GEO: END: ECHO: ;

****************************************************************************
********************* GEOMETRY DEFINTION OF 2x2 UOX+Gd *********************
****************************************************************************
REAL Pitch_C := 1.29500 ;
REAL Rgap Rclad := 0.4520 0.5140 ;
REAL Rcomb1 Rcomb2 Rcomb3 Rcomb4 ;
EVALUATE Rcomb4 := 0.4435 ;
EVALUATE Rcomb1 := 0.313602 ;
EVALUATE Rcomb2 := 0.396678 ; 
EVALUATE Rcomb3 := 0.43227 ;


!Mix numbers for C2 fuel 
INTEGER COMB0201 COMB0202 COMB0203 COMB0204 ;
EVALUATE COMB0201 COMB0202 COMB0203 COMB0204 := 1 2 3 4 ;

!Mix numbers for C3 fuel 
INTEGER COMB0301 COMB0302 COMB0303 COMB0304 ;
EVALUATE COMB0301 COMB0302 COMB0303 COMB0304 := 5 6 7 8 ;

!Mix numbers for C4 fuel 
INTEGER COMB0401 COMB0402 COMB0403 COMB0404 ;
EVALUATE COMB0401 COMB0402 COMB0403 COMB0404 := 9 10 11 12 ;

! UOX + Gd Pincell scale data

REAL RcombGd1 RcombGd2 RcombGd3 RcombGd4 RcombGd5 RcombGd6 ;
EVALUATE RcombGd1 RcombGd2 RcombGd3 RcombGd4 RcombGd5 RcombGd6 
         := 0.19834 0.28049 0.34353 0.39668 0.43227 0.4435 ;

! Mix numbers C7 fuel
INTEGER COMB0701 COMB0702 COMB0703 COMB0704 COMB0705 COMB0706 ;
EVALUATE COMB0701 COMB0702 COMB0703 COMB0704 COMB0705 COMB0706 := 13 14 15 16 17 18 ;

! Pin level common mixes : Clad, Gap, Coolant (intra-pin water) :

INTEGER GAP CLAD COOL ;
EVALUATE GAP CLAD COOL := 19 20 21 ;

! Multiples of cell pitch : used to define 2nd level geometry mesh 
REAL 2_Pitch_C 3_Pitch_C 4_Pitch_C ;
EVALUATE 2_Pitch_C := Pitch_C 2.0 * ;
EVALUATE 3_Pitch_C := Pitch_C 3.0 * ;
EVALUATE 4_Pitch_C := Pitch_C 4.0 * ;



GEOM_2x2_SS := GEO: :: CAR2D 2 2
    X- REFL X+ REFL 
    Y- REFL Y+ REFL
    CELL
    C2 C3
    C4 C7
    MESHX 0.0 <<Pitch_C>> <<2_Pitch_C>>
    MESHX 0.0 <<Pitch_C>> <<2_Pitch_C>>
    ::: C2 :=  GEO: CARCEL 6 
                RADIUS 0.0 <<Rcomb1>> <<Rcomb2>>
                <<Rcomb3>> <<Rcomb4>> <<Rgap>> <<Rclad>>
                MIX <<COMB0201>> <<COMB0202>> <<COMB0203>> <<COMB0204>>
                    <<GAP>> <<CLAD>> <<COOL>> 
                    MESHX 0.0 <<Pitch_C>> MESHY 0.0 <<Pitch_C>>
    ;
    ::: C3 := GEO: C2 MIX <<COMB0301>> <<COMB0302>> <<COMB0303>> <<COMB0304>>
                            <<GAP>> <<CLAD>> <<COOL>>  
    ;
    ::: C4 := GEO: C2 MIX <<COMB0401>> <<COMB0402>> <<COMB0403>> <<COMB0404>>
                            <<GAP>> <<CLAD>> <<COOL>>
    ;
    ::: C7 := GEO: CARCEL 8
              RADIUS 0.0 <<RcombGd1>> <<RcombGd2>> <<RcombGd3>> <<RcombGd4>>
                          <<RcombGd5>> <<RcombGd6>> <<Rgap>> <<Rclad>> 
              MIX <<COMB0701>> <<COMB0702>> <<COMB0703>> <<COMB0704>> <<COMB0705>> <<COMB0706>>
                  <<GAP>> <<CLAD>> <<COOL>>
              MESHX 0.0 <<Pitch_C>> MESHY 0.0 <<Pitch_C>>
    ; 
;


GEOM_2x2 := GEO: :: CAR2D 2 2
    X- REFL X+ REFL 
    Y- REFL Y+ REFL
    CELL
    C2 C3
    C4 C7
    MESHX 0.0 <<Pitch_C>> <<2_Pitch_C>>
    MESHX 0.0 <<Pitch_C>> <<2_Pitch_C>>
    ::: C2 :=  GEO: CARCEL 6 
                SECT 4 6
                RADIUS 0.0 <<Rcomb1>> <<Rcomb2>>
                <<Rcomb3>> <<Rcomb4>> <<Rgap>> <<Rclad>>
                MIX <<COMB0201>> <<COMB0202>> <<COMB0203>> <<COMB0204>>
                    <<GAP>> <<CLAD>>
                    <<COOL>> <<COOL>> <<COOL>> <<COOL>>
                    <<COOL>> <<COOL>> <<COOL>> <<COOL>>
                    <<COOL>> <<COOL>> <<COOL>> <<COOL>>  
                MESHX 0.0 <<Pitch_C>> MESHY 0.0 <<Pitch_C>>
    ;
    ::: C3 := GEO: C2 MIX <<COMB0301>> <<COMB0302>> <<COMB0303>> <<COMB0304>>
                            <<GAP>> <<CLAD>> <<COOL>> <<COOL>> <<COOL>> <<COOL>>
                            <<COOL>> <<COOL>> <<COOL>> <<COOL>>
                            <<COOL>> <<COOL>> <<COOL>> <<COOL>>  
    ;
    ::: C4 := GEO: C2 MIX <<COMB0401>> <<COMB0402>> <<COMB0403>> <<COMB0404>>
                            <<GAP>> <<CLAD>> 
                            <<COOL>> <<COOL>> <<COOL>> <<COOL>>
                            <<COOL>> <<COOL>> <<COOL>> <<COOL>>
                            <<COOL>> <<COOL>> <<COOL>> <<COOL>>
    ;
    ::: C7 := GEO: CARCEL 8
              SECT 4 8 
              RADIUS 0.0 <<RcombGd1>> <<RcombGd2>> <<RcombGd3>> <<RcombGd4>>
                          <<RcombGd5>> <<RcombGd6>> <<Rgap>> <<Rclad>> 
              MIX <<COMB0701>> <<COMB0702>> <<COMB0703>> <<COMB0704>> <<COMB0705>> <<COMB0706>>
                  <<GAP>> <<CLAD>> 
                  <<COOL>> <<COOL>> <<COOL>> <<COOL>> <<COOL>> <<COOL>>
                  <<COOL>> <<COOL>> <<COOL>> <<COOL>> <<COOL>> <<COOL>>
              MESHX 0.0 <<Pitch_C>> MESHY 0.0 <<Pitch_C>>
    ;
    
;

END: ;
QUIT .


