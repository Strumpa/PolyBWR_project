*DECK SNFTH3
      SUBROUTINE SNFTH3(NUN,NGEFF,IMPX,INCONV,NGIND,NHEX,LZ,IELEM,
     1 ISPLH,SIDE,NMAT,NPQ,NSCT,MAT,VOL,NCODE,ZCODE,TOTAL,QEXT,DU,DE,
     2 DZ,W,MRMZ,DC,DB,DA,CONNEC,IZGLOB,CONFROM,FUNKNO,MN,DN)
*
*-----------------------------------------------------------------------
*
*Purpose:
* Perform one inner iteration for solving SN equations in 3D hexagonal
* geometry for the HODD method. VOID boundary conditions on sides, and,
* VOID or REFL boundary conditions on top and bottom. Energy-angle
* multithreading. Albedo boundary conditions.
*
*Copyright:
* Copyright (C) 2020 Ecole Polytechnique de Montreal
* This library is free software; you can redistribute it and/or
* modify it under the terms of the GNU Lesser General Public
* License as published by the Free Software Foundation; either
* version 2.1 of the License, or (at your option) any later version
*
*Author(s): A. A. Calloo
*
*Parameters: input
* NUN     total number of unknowns in vector FUNKNO.
* NGEFF   number of energy groups processed in parallel.
* IMPX    print flag (equal to zero for no print).
* INCONV  energy group convergence flag (set to .FALSE. if converged).
* NGIND   energy group indices assign to the NGEFF set.
* NHEX    number of hexagons in X-Y plane.
* LZ      number of meshes along Z axis.
* IELEM   measure of order of the spatial approximation polynomial:
*         =1 constant (classical diamond scheme - default for HODD);
*         =2 linear (default for DG);
*         =3 parabolic;
*         =4 cubic (only for DG).
* SIDE    side of an hexagon.
* NMAT    number of material mixtures.
* ISPLH   splitting option for hexagons.
* NPQ     number of SN directions in 12 dodecants (including zero-weight
*         directions).
* NSCT    maximum number of spherical harmonics moments of the flux.
* MAT     material mixture index in each region.
* VOL     volumes of each region.
* TOTAL   macroscopic total cross sections.
* NCODE   boundary condition indices.
* ZCODE   albedos.
* QEXT    Legendre components of the fixed source.
* DU      first direction cosines ($\\mu$).
* DE      second direction cosines ($\\eta$).
* DZ      third direction cosines ($\\xi$).
* W       weights.
* MRMZ    quadrature index.
* DC      diamond-scheme parameter.
* DB      diamond-scheme parameter.
* DA      diamond-scheme parameter.
* MN      moment-to-discrete matrix.
* DN      discrete-to-moment matrix.
* CONNEC  connectivity matrix for flux swapping -- which lozenges is the
*         lozenge under consideration connected to; in order to pass the
*         flux along. This is dependent on direction.
* IZGLOB  hexagon sweep order depending on direction.
* CONFROM matrix for incoming flux -- which lozenges are feeding into
*         the lozenge under consideration. This is dependent on
*         direction.
*
*Parameters: input/output
* FUNKNO  Legendre components of the flux and boundary fluxes.
*
*-----------------------------------------------------------------------
*
#if defined(_OPENMP)
      USE omp_lib
#endif
*----
*  SUBROUTINE ARGUMENTS
*----
      INTEGER NUN,NGEFF,IMPX,NGIND(NGEFF),NHEX,LZ,IELEM,ISPLH,
     1 NMAT,NPQ,NSCT,IZGLOB(NHEX,6),MAT(ISPLH,ISPLH,3,NHEX,LZ),
     2 NCODE(6),MRMZ(NPQ),CONNEC(3,(NHEX*3)*2,6),CONFROM(2,3,6)
      LOGICAL INCONV(NGEFF)
      REAL SIDE,VOL(ISPLH,ISPLH,3,NHEX,LZ),ZCODE(6),TOTAL(0:NMAT,NGEFF),
     1 QEXT(NUN,NGEFF),DU(NPQ),DE(NPQ),DZ(NPQ),W(NPQ),
     2 DC(ISPLH*ISPLH*3*NHEX,1,NPQ),DB(ISPLH*ISPLH*3*NHEX,LZ,NPQ),
     3 DA(1,LZ,NPQ),FUNKNO(NUN,NGEFF),MN(NPQ,NSCT),DN(NSCT,NPQ)
*----
*  LOCAL VARIABLES
*----
      INTEGER NPQD(12),IIND(12),P
      DOUBLE PRECISION Q(IELEM**3), Q2(IELEM**3,(IELEM**3)+1),
     1 VT, CONST0, CONST1, CONST2
      PARAMETER(IUNOUT=6,PI=3.141592654)
      INTEGER :: ILOZSWP(3,6), IFROMI, IFROMJ
      REAL :: JAC(2,2,3), MUH, ETAH, XIH, AAA, BBB, CCC, DDD, MUHTEMP,
     1 ETAHTEMP
      DOUBLE PRECISION :: THETA, XNI(IELEM,IELEM,ISPLH,LZ),
     1 XNJ(IELEM,IELEM,LZ),XNK(IELEM,IELEM),C1
*----
*  ALLOCATABLE ARRAYS
*----
      INTEGER, ALLOCATABLE, DIMENSION(:,:) :: INDANG
      DOUBLE PRECISION,ALLOCATABLE, DIMENSION(:,:,:,:,:) :: FLUX
      DOUBLE PRECISION,ALLOCATABLE, DIMENSION(:,:,:,:,:,:) :: FLUX_G
      DOUBLE PRECISION,ALLOCATABLE, DIMENSION(:,:,:,:,:,:) :: BFLUX
*----
*  SCRATCH STORAGE ALLOCATION
*----
      ALLOCATE(INDANG(NPQ,12))
      ALLOCATE(BFLUX(2,NHEX*3,ISPLH,IELEM,IELEM,LZ),
     1 FLUX(IELEM**3,NSCT,3*ISPLH**2,NHEX,LZ))
      ALLOCATE(FLUX_G(IELEM**3,NSCT,3*ISPLH**2,NHEX,LZ,NGEFF))
*----
*  CONSTRUCT JACOBIAN MATRIX FOR EACH LOZENGE
*----
      ILOZSWP = RESHAPE((/ 3, 2, 1, 3, 1, 2, 1, 3, 2, 1, 2, 3, 2, 1,
     1   3, 2, 3, 1 /), SHAPE(ILOZSWP))
      JAC = RESHAPE((/ 1., -SQRT(3.), 1., SQRT(3.), 2., 0., 1.,
     1    SQRT(3.), 2., 0., -1., SQRT(3.) /), SHAPE(JAC))
      JAC = (SIDE/2.)*JAC
*----
*  DEFINITION OF CONSTANTS.
*----
      L4=3*(IELEM**3)*(ISPLH**2)*NHEX*LZ*NSCT
      CONST0=2.0D0*DSQRT(3.0D0)
      CONST1=2.0D0*DSQRT(5.0D0)
      CONST2=2.0D0*DSQRT(15.0D0)
*----
*  PARAMETER VALIDATION.
*----
      IF((IELEM.LT.1).OR.(IELEM.GT.3))
     1   CALL XABORT('SNFTH3: INVALID IELEM (DIAM) VALUE. '
     2   //'CHECK INPUT DATA FILE.')
      FLUX_G(:IELEM**3,:NSCT,:3*ISPLH**2,:NHEX,:LZ,:NGEFF)=0.0D0
*----
*  SET DODECANT SWAPPING ORDER.
*----
      NPQD(:12)=0
      INDANG(:NPQ,:12)=0
      IIND(:12)=0
      DO 10 M=1,NPQ
        VU=DU(M)
        VE=DE(M)
        VZ=DZ(M)
        IF(W(M).EQ.0) CYCLE
        THETA=0.0D0
        IF(VE.GT.0.0)THEN
          IF(VU.EQ.0.0)THEN
            THETA = PI/2
          ELSEIF(VU.GT.0.0)THEN
            THETA = ATAN(ABS(VE/VU))
          ELSEIF(VU.LT.0.0)THEN
            THETA = PI - ATAN(ABS(VE/VU))
          ENDIF
        ELSEIF(VE.LT.0.0)THEN
          IF(VU.EQ.0.0)THEN
            THETA = 3*PI/2
          ELSEIF(VU.LT.0.0)THEN
            THETA = PI + ATAN(ABS(VE/VU))
          ELSEIF(VU.GT.0.0)THEN
            THETA = 2.*PI - ATAN(ABS(VE/VU))
          ENDIF
        ENDIF
*---- UNFOLD DODECANTS
        IND=0
        IF(VZ.GE.0.0)THEN
          IF((THETA.GT.0.0).AND.(THETA.LT.(PI/3.)))THEN
            IND=1
          ELSEIF((THETA.GT.(PI/3.)).AND.(THETA.LT.(2.*PI/3.)))THEN
            IND=2
          ELSEIF((THETA.GT.(2.*PI/3.)).AND.(THETA.LT.(PI)))THEN
            IND=3
          ELSEIF((THETA.GT.(PI)).AND.(THETA.LT.(4.*PI/3.)))THEN
            IND=4
          ELSEIF((THETA.GT.(4.*PI/3.)).AND.(THETA.LT.(5.*PI/3.)))THEN
            IND=5
          ELSEIF((THETA.GT.(5.*PI/3.)).AND.(THETA.LT.(2.*PI)))THEN
            IND=6
          ENDIF
        ELSEIF(VZ.LT.0.0)THEN
          IF((THETA.GT.0.0).AND.(THETA.LT.(PI/3.)))THEN
            IND=7
          ELSEIF((THETA.GT.(PI/3.)).AND.(THETA.LT.(2.*PI/3.)))THEN
            IND=8
          ELSEIF((THETA.GT.(2.*PI/3.)).AND.(THETA.LT.(PI)))THEN
            IND=9
          ELSEIF((THETA.GT.(PI)).AND.(THETA.LT.(4.*PI/3.)))THEN
            IND=10
          ELSEIF((THETA.GT.(4.*PI/3.)).AND.(THETA.LT.(5.*PI/3.)))THEN
            IND=11
          ELSEIF((THETA.GT.(5.*PI/3.)).AND.(THETA.LT.(2.*PI)))THEN
            IND=12
          ENDIF
        ENDIF
        IIND(IND)=IND ! assume IIND(I)=I in hexagonal geometry
        NPQD(IND)=NPQD(IND)+1
        INDANG(NPQD(IND),IND)=M
   10 CONTINUE
*----
*  MAIN LOOP OVER DODECANTS.
*----
      DO 310 JND=1,12
      IND=IIND(JND)
      IND2=MOD(IND-1,6)+1
      IF(IND.EQ.0) GO TO 310 ! needed because of S2 LS (8 dir. for 12 dodecants)
*----
*  PRELIMINARY LOOPS FOR SETTING BOUNDARY CONDITIONS.
*----
      IF((NCODE(5).NE.1).or.(NCODE(6).NE.1))THEN
*$OMP  PARALLEL DO
*$OMP1 PRIVATE(M,IG,VZ,M1,E1,IOF,JOF,IEL,JEL,I,J,IL,JL)
*$OMP2 SHARED(FUNKNO) COLLAPSE(2)
      DO 150 IG=1,NGEFF
      DO 140 IPQD=1,NPQD(IND)
      IF(.NOT.INCONV(IG)) GO TO 140
      M=INDANG(IPQD,IND)
      VZ=DZ(M)
*
      IF(VZ.GT.0.0)THEN
         M1=MRMZ(M)
         IF(NCODE(5).NE.4)THEN
           DO IEL=1,IELEM
           DO JEL=1,IELEM
             DO I=1,NHEX
             DO J=1,3
             DO IL=1,ISPLH
             DO JL=1,ISPLH
             IOF=((((((M-1)*NHEX+(I-1))*3+(J-1))*ISPLH+(JL-1))*ISPLH+
     >       (IL-1))*IELEM+(JEL-1))*IELEM+IEL
             JOF=((((((M1-1)*NHEX+(I-1))*3+(J-1))*ISPLH+(JL-1))*ISPLH+
     >       (IL-1))*IELEM+(JEL-1))*IELEM+IEL
             FUNKNO(L4+IOF,IG)=FUNKNO(L4+JOF,IG)
             ENDDO
             ENDDO
             ENDDO
             ENDDO
           ENDDO
           ENDDO
         ENDIF
      ELSEIF(VZ.LT.0.0)THEN
         M1=MRMZ(M)
         IF(NCODE(6).NE.4)THEN
           DO IEL=1,IELEM
           DO JEL=1,IELEM
             DO I=1,NHEX
             DO J=1,3
             DO IL=1,ISPLH
             DO JL=1,ISPLH
             IOF=((((((M-1)*NHEX+(I-1))*3+(J-1))*ISPLH+(JL-1))*ISPLH+
     >       (IL-1))*IELEM+(JEL-1))*IELEM+IEL
             JOF=((((((M1-1)*NHEX+(I-1))*3+(J-1))*ISPLH+(JL-1))*ISPLH+
     >       (IL-1))*IELEM+(JEL-1))*IELEM+IEL
             FUNKNO(L4+IOF,IG)=FUNKNO(L4+JOF,IG)
             ENDDO
             ENDDO
             ENDDO
             ENDDO
           ENDDO
           ENDDO
         ENDIF
      ENDIF
*
  140 CONTINUE
  150 CONTINUE
*$OMP END PARALLEL DO
      ENDIF
*----
*  MAIN SWAPPING LOOPS FOR SN FLUX CALCULATION
*----
*$OMP  PARALLEL DO
*$OMP1 PRIVATE(ITID,FLUX,BFLUX,M,IG,XNI,XNJ,XNK,Q,Q2,IOF,IER,II,JJ,IZ)
*$OMP2 PRIVATE(IEL,JEL,I,J,ISCT,IL,JL,VT,AAA,BBB,CCC,DDD,IHEXI,ILOZI)
*$OMP3 PRIVATE(ILOZ,ISIDEI,IHEXJ,ILOZJ,ISIDEJ,IFROMI,IFROMJ,INDEXI)
*$OMP4 PRIVATE(INDEXJ,MUHTEMP,ETAHTEMP,MUH,ETAH,XIH,I2,I3,J2,J3,K2,C1)
*$OMP5 SHARED(FUNKNO) REDUCTION(+:FLUX_G) COLLAPSE(2)
      DO 300 IG=1,NGEFF
      DO 290 IPQD=1,NPQD(IND)
#if defined(_OPENMP)
        ITID=omp_get_thread_num()
#else
        ITID=0
#endif
      IF(IMPX.GT.5) WRITE(IUNOUT,500) ITID,NGIND(IG),IPQD
      IF(.NOT.INCONV(IG)) GO TO 290
      M=INDANG(IPQD,IND)
      FLUX(:IELEM**3,:NSCT,:3*ISPLH**2,:NHEX,:LZ)=0.0D0
      BFLUX(:2,:NHEX*3,:ISPLH,:IELEM,:IELEM,:LZ)=0.0D0
      IF(W(M).EQ.0.0) GO TO 290
*----
*  LOOP OVER X-Y PLANE AND Z-DIRECTED AXIS.
*----
* SECOND loop over hexagons
      DO 280 II=1,NHEX
      I=IZGLOB(II,IND2)

* THIRD loop over lozenges
      DO 270 JJ=1,3
      J=ILOZSWP(JJ,IND2)
*
      AAA = JAC(1,1,J)
      BBB = JAC(1,2,J)
      CCC = JAC(2,1,J)
      DDD = JAC(2,2,J)
*
* Identify DOMAIN-WISE indices of connected sides
      IHEXI  = CONNEC(1,((I-1)*3*2) + ((J-1)*2) +1,IND2)
      ILOZI  = CONNEC(2,((I-1)*3*2) + ((J-1)*2) +1,IND2)
      ISIDEI = CONNEC(3,((I-1)*3*2) + ((J-1)*2) +1,IND2)
      IHEXJ  = CONNEC(1,((I-1)*3*2) + ((J-1)*2) +2,IND2)
      ILOZJ  = CONNEC(2,((I-1)*3*2) + ((J-1)*2) +2,IND2)
      ISIDEJ = CONNEC(3,((I-1)*3*2) + ((J-1)*2) +2,IND2)
      IFROMI = CONFROM(1,J,IND2)
      IFROMJ = CONFROM(2,J,IND2)
      INDEXI = ((IHEXI-1)*3)+ILOZI
      INDEXJ = ((IHEXJ-1)*3)+ILOZJ
*
      DO 260 IL=1,ISPLH
      I2=IL
      IF(CONFROM(1,J,IND2).EQ.3) I2=ISPLH+1-I2
      DO 250 JL=1,ISPLH
      J2=JL
      IF(CONFROM(2,J,IND2).EQ.4) J2=ISPLH+1-J2
      DO IEL=1,IELEM
      DO JEL=1,IELEM
        IF((NCODE(5).NE.1).or.(NCODE(6).NE.1))THEN
        IOF=((((((M-1)*NHEX+(I-1))*3+(J-1))*ISPLH+(J2-1))*ISPLH+
     1      (I2-1))*IELEM+(JEL-1))*IELEM+IEL
        IF(IND.LT.7) THEN
          XNK(IEL,JEL)=FUNKNO(L4+IOF,IG)*ZCODE(5)
        ELSE
          XNK(IEL,JEL)=FUNKNO(L4+IOF,IG)*ZCODE(6)
        ENDIF
        ELSE
          XNK(IEL,JEL)=0.0
        ENDIF
      ENDDO
      ENDDO
      DO 220 IZ=1,LZ
      K2= IZ
      IF(IND.GE.7) K2=LZ+1-IZ
      IF(JL.EQ.1)THEN
         DO IEL=1,IELEM
            DO JEL=1,IELEM
               XNJ(IEL,JEL,K2) = BFLUX(2,((I-1)*3)+J,I2,IEL,JEL,K2)
            ENDDO
         ENDDO
      ENDIF
      IF(IL.EQ.1)THEN
         DO IEL=1,IELEM
            DO JEL=1,IELEM
               XNI(IEL,JEL,J2,K2) = BFLUX(1,((I-1)*3)+J,J2,IEL,JEL,K2)
            ENDDO
         ENDDO
      ENDIF
*
      MUHTEMP  =  DA(1,K2,M)
      ETAHTEMP =  DB(1,K2,M)
      MUH  = (MUHTEMP*DDD) - (ETAHTEMP*BBB)
      ETAH = (-MUHTEMP*CCC) + (ETAHTEMP*AAA)
      XIH  = DC(1,1,M)
*
      IF(MAT(I2,J2,J,I,K2).EQ.0) GO TO 220

*     -----------------------------------------------------
      DO 170 IEL=1,IELEM**3
      Q(IEL)=0.0D0
      DO 160 P=1,NSCT
      IOF=(((((((K2-1)*NHEX+(I-1))*3+(J-1))*ISPLH+(J2-1))*ISPLH+
     1 (I2-1))*NSCT+(P-1))*IELEM**3)+IEL
      Q(IEL)=Q(IEL)+QEXT(IOF,IG)*MN(M,P)
  160 CONTINUE
  170 CONTINUE
*     -----------------------------------------------------
*
      VT=VOL(I2,J2,J,I,K2)*TOTAL(MAT(I2,J2,J,I,K2),IG)
      CALL XDDSET(Q2,(IELEM**3)*((IELEM**3)+1),0.0D0)
*
*     -----------------------------------------------------
      IF(IELEM.EQ.1) THEN
      Q2(1,1)=2.0D0*ABS(MUH)+2.0D0*ABS(ETAH)+
     1        2.0D0*ABS(XIH)+VT
      Q2(1,2)=2.0D0*ABS(MUH)*XNI(1,1,J2,K2)+
     1        2.0D0*ABS(ETAH)*XNJ(1,1,K2)+
     2        2.0D0*ABS(XIH)*XNK(1,1)+VOL(I2,J2,J,I,K2)*Q(1)
      ELSE IF(IELEM.EQ.2) THEN
      Q2(1,1) = VT
      Q2(1,2) = 2.0D0*3.0D0**(0.5D0)*MUH
      Q2(1,3) = 2.0D0*3.0D0**(0.5D0)*ETAH
      Q2(1,5) = 2.0D0*3.0D0**(0.5D0)*XIH
      Q2(2,1) = 2.0D0*3.0D0**(0.5D0)*MUH
      Q2(2,2) = - VT - 6.0D0*ABS(MUH)
      Q2(2,4) = -2.0D0*3.0D0**(0.5D0)*ETAH
      Q2(2,6) = -2.0D0*3.0D0**(0.5D0)*XIH
      Q2(3,1) = 2.0D0*3.0D0**(0.5D0)*ETAH
      Q2(3,3) = - VT - 6.0D0*ABS(ETAH)
      Q2(3,4) = -2.0D0*3.0D0**(0.5D0)*MUH
      Q2(3,7) = -2.0D0*3.0D0**(0.5D0)*XIH
      Q2(4,2) = -2.0D0*3.0D0**(0.5D0)*ETAH
      Q2(4,3) = -2.0D0*3.0D0**(0.5D0)*MUH
      Q2(4,4) = VT + 6.0D0*ABS(ETAH) + 6.0D0*ABS(MUH)
      Q2(4,8) = 2.0D0*3.0D0**(0.5D0)*XIH
      Q2(5,1) = 2.0D0*3.0D0**(0.5D0)*XIH
      Q2(5,5) = - VT - 6.0D0*ABS(XIH)
      Q2(5,6) = -2.0D0*3.0D0**(0.5D0)*MUH
      Q2(5,7) = -2.0D0*3.0D0**(0.5D0)*ETAH
      Q2(6,2) = -2.0D0*3.0D0**(0.5D0)*XIH
      Q2(6,5) = -2.0D0*3.0D0**(0.5D0)*MUH
      Q2(6,6) = VT + 6.0D0*ABS(MUH) + 6.0D0*ABS(XIH)
      Q2(6,8) = 2.0D0*3.0D0**(0.5D0)*ETAH
      Q2(7,3) = -2.0D0*3.0D0**(0.5D0)*XIH
      Q2(7,5) = -2.0D0*3.0D0**(0.5D0)*ETAH
      Q2(7,7) = VT + 6.0D0*ABS(ETAH) + 6.0D0*ABS(XIH)
      Q2(7,8) = 2.0D0*3.0D0**(0.5D0)*MUH
      Q2(8,4) = 2.0D0*3.0D0**(0.5D0)*XIH
      Q2(8,6) = 2.0D0*3.0D0**(0.5D0)*ETAH
      Q2(8,7) = 2.0D0*3.0D0**(0.5D0)*MUH
      Q2(8,8) = - VT - 6.0D0*ABS(ETAH) - 6.0D0*ABS
     >   (MUH) - 6.0D0*ABS(XIH)
*---------------
      Q2(1,9) = Q(1)*VOL(I2,J2,J,I,K2)
      Q2(2,9) = -Q(2)*VOL(I2,J2,J,I,K2)
      Q2(3,9) = -Q(3)*VOL(I2,J2,J,I,K2)
      Q2(4,9) = Q(4)*VOL(I2,J2,J,I,K2)
      Q2(5,9) = -Q(5)*VOL(I2,J2,J,I,K2)
      Q2(6,9) = Q(6)*VOL(I2,J2,J,I,K2)
      Q2(7,9) = Q(7)*VOL(I2,J2,J,I,K2)
      Q2(8,9) = -Q(8)*VOL(I2,J2,J,I,K2)
      Q2(1,9) = Q2(1,9)
      Q2(2,9) = Q2(2,9) + 2.0D0*3.0D0**(0.5D0)*MUH
     >   *XNI(1,1,J2,K2)
      Q2(3,9) = Q2(3,9) + 2.0D0*3.0D0**(0.5D0)*ETAH
     >   *XNJ(1,1,K2)
      Q2(4,9) = Q2(4,9) - 2.0D0*3.0D0**(0.5D0)*ETAH
     >   *XNJ(2,1,K2) - 2.0D0*3.0D0**(0.5D0)*MUH*X
     >   NI(2,1,J2,K2)
      Q2(5,9) = Q2(5,9) + 2.0D0*3.0D0**(0.5D0)*XNK(1,1)
     >   *XIH
      Q2(6,9) = Q2(6,9) - 2.0D0*3.0D0**(0.5D0)*MUH
     >   *XNI(1,2,J2,K2) - 2.0D0*3.0D0**(0.5D0)*XNK(2,1)*
     >   XIH
      Q2(7,9) = Q2(7,9) - 2.0D0*3.0D0**(0.5D0)*ETAH
     >   *XNJ(1,2,K2) - 2.0D0*3.0D0**(0.5D0)*XNK(1,2)*XIH
      Q2(8,9) = Q2(8,9) + 2.0D0*3.0D0**(0.5D0)*ETAH
     >   *XNJ(2,2,K2) + 2.0D0*3.0D0**(0.5D0)*MUH*XNI
     >   (2,2,J2,K2) + 2.0D0*3.0D0**(0.5D0)*XNK(2,2)*XIH
      ELSE IF(IELEM.EQ.3) THEN
      Q2(1,1) = - VT - 2*ABS(ETAH) - 2*ABS(MUH)
     >    - 2*ABS(XIH)
      Q2(1,3) = -2*5**(0.5D0)*ABS(MUH)
      Q2(1,7) = -2*5**(0.5D0)*ABS(ETAH)
      Q2(1,19) = -2*5**(0.5D0)*ABS(XIH)
      Q2(2,2) = - VT - 2*ABS(ETAH) - 2*ABS(XIH)
      Q2(2,3) = -2*15**(0.5D0)*MUH
      Q2(2,8) = -2*5**(0.5D0)*ABS(ETAH)
      Q2(2,20) = -2*5**(0.5D0)*ABS(XIH)
      Q2(3,1) = 2*5**(0.5D0)*ABS(MUH)
      Q2(3,2) = -2*15**(0.5D0)*MUH
      Q2(3,3) = VT + 2*ABS(ETAH) + 10*ABS(MUH)
     >   + 2*ABS(XIH)
      Q2(3,9) = 2*5**(0.5D0)*ABS(ETAH)
      Q2(3,21) = 2*5**(0.5D0)*ABS(XIH)
      Q2(4,4) = - VT - 2*ABS(MUH) - 2*ABS(XIH)
      Q2(4,6) = -2*5**(0.5D0)*ABS(MUH)
      Q2(4,7) = -2*15**(0.5D0)*ETAH
      Q2(4,22) = -2*5**(0.5D0)*ABS(XIH)
      Q2(5,5) = - VT - 2*ABS(XIH)
      Q2(5,6) = -2*15**(0.5D0)*MUH
      Q2(5,8) = -2*15**(0.5D0)*ETAH
      Q2(5,23) = -2*5**(0.5D0)*ABS(XIH)
      Q2(6,4) = 2*5**(0.5D0)*ABS(MUH)
      Q2(6,5) = -2*15**(0.5D0)*MUH
      Q2(6,6) = VT + 10*ABS(MUH) + 2*ABS(XIH)
      Q2(6,9) = 2*15**(0.5D0)*ETAH
      Q2(6,24) = 2*5**(0.5D0)*ABS(XIH)
      Q2(7,1) = 2*5**(0.5D0)*ABS(ETAH)
      Q2(7,4) = -2*15**(0.5D0)*ETAH
      Q2(7,7) = VT + 10*ABS(ETAH) + 2*ABS(MUH)
     >   + 2*ABS(XIH)
      Q2(7,9) = 2*5**(0.5D0)*ABS(MUH)
      Q2(7,25) = 2*5**(0.5D0)*ABS(XIH)
      Q2(8,2) = 2*5**(0.5D0)*ABS(ETAH)
      Q2(8,5) = -2*15**(0.5D0)*ETAH
      Q2(8,8) = VT + 10*ABS(ETAH) + 2*ABS(XIH)
      Q2(8,9) = 2*15**(0.5D0)*MUH
      Q2(8,26) = 2*5**(0.5D0)*ABS(XIH)
      Q2(9,3) = -2*5**(0.5D0)*ABS(ETAH)
      Q2(9,6) = 2*15**(0.5D0)*ETAH
      Q2(9,7) = -2*5**(0.5D0)*ABS(MUH)
      Q2(9,8) = 2*15**(0.5D0)*MUH
      Q2(9,9) = - VT - 10*ABS(ETAH) - 10*ABS(MU
     >   H) - 2*ABS(XIH)
      Q2(9,27) = -2*5**(0.5D0)*ABS(XIH)
      Q2(10,10) = VT + 2*ABS(ETAH) + 2*ABS(MUH)
      Q2(10,12) = 2*5**(0.5D0)*ABS(MUH)
      Q2(10,16) = 2*5**(0.5D0)*ABS(ETAH)
      Q2(10,19) = 2*15**(0.5D0)*XIH
      Q2(11,11) = VT + 2*ABS(ETAH)
      Q2(11,12) = 2*15**(0.5D0)*MUH
      Q2(11,17) = 2*5**(0.5D0)*ABS(ETAH)
      Q2(11,20) = 2*15**(0.5D0)*XIH
      Q2(12,10) = -2*5**(0.5D0)*ABS(MUH)
      Q2(12,11) = 2*15**(0.5D0)*MUH
      Q2(12,12) = - VT - 2*ABS(ETAH) - 10*ABS(M
     >   UH)
      Q2(12,18) = -2*5**(0.5D0)*ABS(ETAH)
      Q2(12,21) = -2*15**(0.5D0)*XIH
      Q2(13,13) = VT + 2*ABS(MUH)
      Q2(13,15) = 2*5**(0.5D0)*ABS(MUH)
      Q2(13,16) = 2*15**(0.5D0)*ETAH
      Q2(13,22) = 2*15**(0.5D0)*XIH
      Q2(14,14) = VT
      Q2(14,15) = 2*15**(0.5D0)*MUH
      Q2(14,17) = 2*15**(0.5D0)*ETAH
      Q2(14,23) = 2*15**(0.5D0)*XIH
      Q2(15,13) = -2*5**(0.5D0)*ABS(MUH)
      Q2(15,14) = 2*15**(0.5D0)*MUH
      Q2(15,15) = - VT - 10*ABS(MUH)
      Q2(15,18) = -2*15**(0.5D0)*ETAH
      Q2(15,24) = -2*15**(0.5D0)*XIH
      Q2(16,10) = -2*5**(0.5D0)*ABS(ETAH)
      Q2(16,13) = 2*15**(0.5D0)*ETAH
      Q2(16,16) = - VT - 10*ABS(ETAH) - 2*ABS(M
     >   UH)
      Q2(16,18) = -2*5**(0.5D0)*ABS(MUH)
      Q2(16,25) = -2*15**(0.5D0)*XIH
      Q2(17,11) = -2*5**(0.5D0)*ABS(ETAH)
      Q2(17,14) = 2*15**(0.5D0)*ETAH
      Q2(17,17) = - VT - 10*ABS(ETAH)
      Q2(17,18) = -2*15**(0.5D0)*MUH
      Q2(17,26) = -2*15**(0.5D0)*XIH
      Q2(18,12) = 2*5**(0.5D0)*ABS(ETAH)
      Q2(18,15) = -2*15**(0.5D0)*ETAH
      Q2(18,16) = 2*5**(0.5D0)*ABS(MUH)
      Q2(18,17) = -2*15**(0.5D0)*MUH
      Q2(18,18) = VT + 10*ABS(ETAH) + 10*ABS(MU
     >   H)
      Q2(18,27) = 2*15**(0.5D0)*XIH
      Q2(19,1) = 2*5**(0.5D0)*ABS(XIH)
      Q2(19,10) = -2*15**(0.5D0)*XIH
      Q2(19,19) = VT + 2*ABS(ETAH) + 2*ABS(MUH)
     >    + 10*ABS(XIH)
      Q2(19,21) = 2*5**(0.5D0)*ABS(MUH)
      Q2(19,25) = 2*5**(0.5D0)*ABS(ETAH)
      Q2(20,2) = 2*5**(0.5D0)*ABS(XIH)
      Q2(20,11) = -2*15**(0.5D0)*XIH
      Q2(20,20) = VT + 2*ABS(ETAH) + 10*ABS(XIH
     >   )
      Q2(20,21) = 2*15**(0.5D0)*MUH
      Q2(20,26) = 2*5**(0.5D0)*ABS(ETAH)
      Q2(21,3) = -2*5**(0.5D0)*ABS(XIH)
      Q2(21,12) = 2*15**(0.5D0)*XIH
      Q2(21,19) = -2*5**(0.5D0)*ABS(MUH)
      Q2(21,20) = 2*15**(0.5D0)*MUH
      Q2(21,21) = - VT - 2*ABS(ETAH) - 10*ABS(M
     >   UH) - 10*ABS(XIH)
      Q2(21,27) = -2*5**(0.5D0)*ABS(ETAH)
      Q2(22,4) = -2*5**(0.5D0)*ABS(XIH)
      Q2(22,13) = 2*15**(0.5D0)*XIH
      Q2(22,22) = - VT - 2*ABS(MUH) - 10*ABS(XI
     >   H)
      Q2(22,24) = -2*5**(0.5D0)*ABS(MUH)
      Q2(22,25) = -2*15**(0.5D0)*ETAH
      Q2(23,5) = -2*5**(0.5D0)*ABS(XIH)
      Q2(23,14) = 2*15**(0.5D0)*XIH
      Q2(23,23) = - VT - 10*ABS(XIH)
      Q2(23,24) = -2*15**(0.5D0)*MUH
      Q2(23,26) = -2*15**(0.5D0)*ETAH
      Q2(24,6) = 2*5**(0.5D0)*ABS(XIH)
      Q2(24,15) = -2*15**(0.5D0)*XIH
      Q2(24,22) = 2*5**(0.5D0)*ABS(MUH)
      Q2(24,23) = -2*15**(0.5D0)*MUH
      Q2(24,24) = VT + 10*ABS(MUH) + 10*ABS(XIH
     >   )
      Q2(24,27) = 2*15**(0.5D0)*ETAH
      Q2(25,7) = -2*5**(0.5D0)*ABS(XIH)
      Q2(25,16) = 2*15**(0.5D0)*XIH
      Q2(25,19) = -2*5**(0.5D0)*ABS(ETAH)
      Q2(25,22) = 2*15**(0.5D0)*ETAH
      Q2(25,25) = - VT - 10*ABS(ETAH) - 2*ABS(M
     >   UH) - 10*ABS(XIH)
      Q2(25,27) = -2*5**(0.5D0)*ABS(MUH)
      Q2(26,8) = 2*5**(0.5D0)*ABS(XIH)
      Q2(26,17) = -2*15**(0.5D0)*XIH
      Q2(26,20) = 2*5**(0.5D0)*ABS(ETAH)
      Q2(26,23) = -2*15**(0.5D0)*ETAH
      Q2(26,26) = VT + 10*ABS(ETAH) + 10*ABS(XI
     >   H)
      Q2(26,27) = 2*15**(0.5D0)*MUH
      Q2(27,9) = -2*5**(0.5D0)*ABS(XIH)
      Q2(27,18) = 2*15**(0.5D0)*XIH
      Q2(27,21) = -2*5**(0.5D0)*ABS(ETAH)
      Q2(27,24) = 2*15**(0.5D0)*ETAH
      Q2(27,25) = -2*5**(0.5D0)*ABS(MUH)
      Q2(27,26) = 2*15**(0.5D0)*MUH
      Q2(27,27) = - VT - 10*ABS(ETAH) - 10*ABS(
     >   MUH) - 10*ABS(XIH)
      Q2(1,28) = -Q(01)*VOL(I2,J2,J,I,K2)
      Q2(2,28) = -Q(02)*VOL(I2,J2,J,I,K2)
      Q2(3,28) = Q(03)*VOL(I2,J2,J,I,K2)
      Q2(4,28) = -Q(04)*VOL(I2,J2,J,I,K2)
      Q2(5,28) = -Q(05)*VOL(I2,J2,J,I,K2)
      Q2(6,28) = Q(06)*VOL(I2,J2,J,I,K2)
      Q2(7,28) = Q(07)*VOL(I2,J2,J,I,K2)
      Q2(8,28) = Q(08)*VOL(I2,J2,J,I,K2)
      Q2(9,28) = -Q(09)*VOL(I2,J2,J,I,K2)
      Q2(10,28) = Q(10)*VOL(I2,J2,J,I,K2)
      Q2(11,28) = Q(11)*VOL(I2,J2,J,I,K2)
      Q2(12,28) = -Q(12)*VOL(I2,J2,J,I,K2)
      Q2(13,28) = Q(13)*VOL(I2,J2,J,I,K2)
      Q2(14,28) = Q(14)*VOL(I2,J2,J,I,K2)
      Q2(15,28) = -Q(15)*VOL(I2,J2,J,I,K2)
      Q2(16,28) = -Q(16)*VOL(I2,J2,J,I,K2)
      Q2(17,28) = -Q(17)*VOL(I2,J2,J,I,K2)
      Q2(18,28) = Q(18)*VOL(I2,J2,J,I,K2)
      Q2(19,28) = Q(19)*VOL(I2,J2,J,I,K2)
      Q2(20,28) = Q(20)*VOL(I2,J2,J,I,K2)
      Q2(21,28) = -Q(21)*VOL(I2,J2,J,I,K2)
      Q2(22,28) = -Q(22)*VOL(I2,J2,J,I,K2)
      Q2(23,28) = -Q(23)*VOL(I2,J2,J,I,K2)
      Q2(24,28) = Q(24)*VOL(I2,J2,J,I,K2)
      Q2(25,28) = -Q(25)*VOL(I2,J2,J,I,K2)
      Q2(26,28) = Q(26)*VOL(I2,J2,J,I,K2)
      Q2(27,28) = -Q(27)*VOL(I2,J2,J,I,K2)
      Q2(1,28) = Q2(1,28) - 2*XNK(1,1)*ABS(XI
     >   H) - 2*ABS(ETAH)*XNJ(1,1,K2) - 2*ABS(MUH)
     >   *XNI(1,1,J2,K2)
      Q2(2,28) = Q2(2,28) - 2*XNK(2,1)*ABS(XI
     >   H) - 2*ABS(ETAH)*XNJ(2,1,K2)
      Q2(3,28) = Q2(3,28) + 2*XNK(3,1)*ABS(XIH)
     >    + 2*ABS(ETAH)*XNJ(3,1,K2) + 2*5**(0.5D0)
     >   *ABS(MUH)*XNI(1,1,J2,K2)
      Q2(4,28) = Q2(4,28) - 2*XNK(1,2)*ABS(XI
     >   H) - 2*ABS(MUH)*XNI(2,1,J2,K2)
      Q2(5,28) = Q2(5,28) -2*XNK(2,2)*ABS(XIH)
      Q2(6,28) = Q2(6,28) + 2*XNK(3,2)*ABS(XIH)
     >    + 2*5**(0.5D0)*ABS(MUH)*XNI(2,1,J2,K2)
      Q2(7,28) = Q2(7,28) + 2*XNK(1,3)*ABS(XIH)
     >    + 2*ABS(MUH)*XNI(3,1,J2,K2) + 2*5**(0.5D
     >   0)*ABS(ETAH)*XNJ(1,1,K2)
      Q2(8,28) = Q2(8,28) + 2*XNK(2,3)*ABS(XIH)
     >    + 2*5**(0.5D0)*ABS(ETAH)*XNJ(2,1,K2)
      Q2(9,28) = Q2(9,28) - 2*XNK(3,3)*ABS(XI
     >   H) - 2*5**(0.5D0)*ABS(ETAH)*XNJ(3,1,K2) -
     >    2*5**(0.5D0)*ABS(MUH)*XNI(3,1,J2,K2)
      Q2(10,28) = Q2(10,28) + 2*ABS(ETAH)*XNJ(1
     >   ,2,K2) + 2*ABS(MUH)*XNI(1,2,J2,K2)
      Q2(11,28) = Q2(11,28) + 2*ABS(ETAH)*XNJ(2
     >   ,2,K2)
      Q2(12,28) = Q2(12,28) - 2*ABS(ETAH)*XNJ
     >   (3,2,K2) - 2*5**(0.5D0)*ABS(MUH)*XNI(1,2,
     >   J2,K2)
      Q2(13,28) = Q2(13,28) + 2*ABS(MUH)*XNI(2,
     >   2,J2,K2)
      Q2(14,28) = Q2(14,28) + 0
      Q2(15,28) = Q2(15,28) -2*5**(0.5D0)*ABS
     >   (MUH)*XNI(2,2,J2,K2)
      Q2(16,28) = Q2(16,28) - 2*ABS(MUH)*XNI(
     >   3,2,J2,K2) - 2*5**(0.5D0)*ABS(ETAH)*XNJ(1
     >   ,2,K2)
      Q2(17,28) = Q2(17,28) -2*5**(0.5D0)*ABS
     >   (ETAH)*XNJ(2,2,K2)
      Q2(18,28) = Q2(18,28) + 2*5**(0.5D0)*ABS(
     >   ETAH)*XNJ(3,2,K2) + 2*5**(0.5D0)*ABS(MUH)
     >   *XNI(3,2,J2,K2)
      Q2(19,28) = Q2(19,28) + 2*ABS(ETAH)*XNJ(1
     >   ,3,K2) + 2*ABS(MUH)*XNI(1,3,J2,K2) + 2*5*
     >   *(0.5D0)*XNK(1,1)*ABS(XIH)
      Q2(20,28) = Q2(20,28) + 2*ABS(ETAH)*XNJ(2
     >   ,3,K2) + 2*5**(0.5D0)*XNK(2,1)*ABS(XIH)
      Q2(21,28) = Q2(21,28) - 2*ABS(ETAH)*XNJ
     >   (3,3,K2) - 2*5**(0.5D0)*ABS(MUH)*XNI(1,3,
     >   J2,K2) - 2*5**(0.5D0)*XNK(3,1)*ABS(XIH)
      Q2(22,28) = Q2(22,28) - 2*ABS(MUH)*XNI(
     >   2,3,J2,K2) - 2*5**(0.5D0)*XNK(1,2)*ABS(XI
     >   H)
      Q2(23,28) = Q2(23,28) -2*5**(0.5D0)*XNK
     >   (2,2)*ABS(XIH)
      Q2(24,28) = Q2(24,28) + 2*5**(0.5D0)*ABS(
     >   MUH)*XNI(2,3,J2,K2) + 2*5**(0.5D0)*XNK(3,
     >   2)*ABS(XIH)
      Q2(25,28) = Q2(25,28) - 2*ABS(MUH)*XNI(
     >   3,3,J2,K2) - 2*5**(0.5D0)*ABS(ETAH)*XNJ(1
     >   ,3,K2) - 2*5**(0.5D0)*XNK(1,3)*ABS(XIH)
      Q2(26,28) = Q2(26,28) + 2*5**(0.5D0)*ABS(
     >   ETAH)*XNJ(2,3,K2) + 2*5**(0.5D0)*XNK(2,3)
     >   *ABS(XIH)
      Q2(27,28) = Q2(27,28) - 2*5**(0.5D0)*AB
     >   S(ETAH)*XNJ(3,3,K2) - 2*5**(0.5D0)*ABS(MU
     >   H)*XNI(3,3,J2,K2) - 2*5**(0.5D0)*XNK(3,3)
     >   *ABS(XIH)
      ENDIF
*
      CALL ALSBD(IELEM**3,1,Q2,IER,IELEM**3)
      IF(IER.NE.0) CALL XABORT('SNFTH3: SINGULAR MATRIX.')
*
      IF(IELEM.EQ.1) THEN
      XNK(1,1)=2.0D0*Q2(1,2)-XNK(1,1)
      ELSE IF(IELEM.EQ.2) THEN
      XNK(1,1) = XNK(1,1) + SIGN(1.0,XIH)*2.0D0*3.0D0**(0.5D0)*Q2(5,9)
      XNK(2,1) = XNK(2,1) + SIGN(1.0,XIH)*2.0D0*3.0D0**(0.5D0)*Q2(6,9)
      XNK(1,2) = XNK(1,2) + SIGN(1.0,XIH)*2.0D0*3.0D0**(0.5D0)*Q2(7,9)
      XNK(2,2) = XNK(2,2) + SIGN(1.0,XIH)*2.0D0*3.0D0**(0.5D0)*Q2(8,9)
      ELSE IF(IELEM.EQ.3) THEN
      XNK(1,1) = 2*Q2(01,28) - XNK(1,1) + 2*5**
     >   (0.5D0)*Q2(19,28)
      XNK(2,1) = 2*Q2(02,28) - XNK(2,1) + 2*5**
     >   (0.5D0)*Q2(20,28)
      XNK(3,1) = 2*Q2(03,28) - XNK(3,1) + 2*5**
     >   (0.5D0)*Q2(21,28)
      XNK(1,2) = 2*Q2(04,28) - XNK(1,2) + 2*5**
     >   (0.5D0)*Q2(22,28)
      XNK(2,2) = 2*Q2(05,28) - XNK(2,2) + 2*5**
     >   (0.5D0)*Q2(23,28)
      XNK(3,2) = 2*Q2(06,28) - XNK(3,2) + 2*5**
     >   (0.5D0)*Q2(24,28)
      XNK(1,3) = 2*Q2(07,28) - XNK(1,3) + 2*5**
     >   (0.5D0)*Q2(25,28)
      XNK(2,3) = 2*Q2(08,28) - XNK(2,3) + 2*5**
     >   (0.5D0)*Q2(26,28)
      XNK(3,3) = 2*Q2(09,28) - XNK(3,3) + 2*5**
     >   (0.5D0)*Q2(27,28)
      ENDIF
*
      IF(JL.LT.ISPLH)THEN
      IF(IELEM.EQ.1) THEN
      XNJ(1,1,K2)=2.0D0*Q2(1,2)-XNJ(1,1,K2)
      ELSEIF(IELEM.EQ.2) THEN
      XNJ(1,1,K2) = XNJ(1,1,K2) + SIGN(1.0,ETAH)*
     >   2.0D0*3.0D0**(0.5D0)*Q2(3,9)
      XNJ(2,1,K2) = XNJ(2,1,K2) + SIGN(1.0,ETAH)*
     >   2.0D0*3.0D0**(0.5D0)*Q2(4,9)
      XNJ(1,2,K2) = XNJ(1,2,K2) + SIGN(1.0,ETAH)*
     >   2.0D0*3.0D0**(0.5D0)*Q2(7,9)
      XNJ(2,2,K2) = XNJ(2,2,K2) + SIGN(1.0,ETAH)*
     >   2.0D0*3.0D0**(0.5D0)*Q2(8,9)
      ELSEIF(IELEM.EQ.3) THEN
      XNJ(1,1,K2) = 2*Q2(01,28) - XNJ(1,1,K2) + 2
     >   *5**(0.5D0)*Q2(07,28)
      XNJ(2,1,K2) = 2*Q2(02,28) - XNJ(2,1,K2) + 2
     >   *5**(0.5D0)*Q2(08,28)
      XNJ(3,1,K2) = 2*Q2(03,28) - XNJ(3,1,K2) + 2
     >   *5**(0.5D0)*Q2(09,28)
      XNJ(1,2,K2) = 2*Q2(10,28) - XNJ(1,2,K2) + 2
     >   *5**(0.5D0)*Q2(16,28)
      XNJ(2,2,K2) = 2*Q2(11,28) - XNJ(2,2,K2) + 2
     >   *5**(0.5D0)*Q2(17,28)
      XNJ(3,2,K2) = 2*Q2(12,28) - XNJ(3,2,K2) + 2
     >   *5**(0.5D0)*Q2(18,28)
      XNJ(1,3,K2) = 2*Q2(19,28) - XNJ(1,3,K2) + 2
     >   *5**(0.5D0)*Q2(25,28)
      XNJ(2,3,K2) = 2*Q2(20,28) - XNJ(2,3,K2) + 2
     >   *5**(0.5D0)*Q2(26,28)
      XNJ(3,3,K2) = 2*Q2(21,28) - XNJ(3,3,K2) + 2
     >   *5**(0.5D0)*Q2(27,28)
      ENDIF
      ELSEIF((JL.EQ.ISPLH).AND.(IHEXJ.LE.NHEX))THEN
         I3=I2
         C1=1.0D0
         IF((J.EQ.1).AND.(ILOZJ.EQ.3)) THEN
            I3=ISPLH+1 -I2
            C1=-1.0D0
         ENDIF
      IF(IELEM.EQ.1) THEN
      BFLUX(ISIDEJ,INDEXJ,I3,1,1,K2) = 2.0D0*Q2(1,2)-XNJ(1,1,K2)
      ELSEIF(IELEM.EQ.2) THEN
      BFLUX(ISIDEJ,INDEXJ,I3,1,1,K2) = (XNJ(1,1,K2) + SIGN(1.0,ETAH)*
     >   2.0D0*3.0D0**(0.5D0)*Q2(3,9))
      BFLUX(ISIDEJ,INDEXJ,I3,2,1,K2) = (XNJ(2,1,K2) + SIGN(1.0,ETAH)*
     >   2.0D0*3.0D0**(0.5D0)*Q2(4,9))*C1
      BFLUX(ISIDEJ,INDEXJ,I3,1,2,K2) = (XNJ(1,2,K2) + SIGN(1.0,ETAH)*
     >   2.0D0*3.0D0**(0.5D0)*Q2(7,9))*1
      BFLUX(ISIDEJ,INDEXJ,I3,2,2,K2) = (XNJ(2,2,K2) + SIGN(1.0,ETAH)*
     >   2.0D0*3.0D0**(0.5D0)*Q2(8,9))*C1
      ELSEIF(IELEM.EQ.3) THEN
      BFLUX(ISIDEJ,INDEXJ,I3,1,1,K2) = 2*Q2(01,28) - XNJ(1,1,K2) + 2
     >   *5**(0.5D0)*Q2(07,28)
      BFLUX(ISIDEJ,INDEXJ,I3,2,1,K2) = (2*Q2(02,28) - XNJ(2,1,K2) + 2
     >   *5**(0.5D0)*Q2(08,28))*C1
      BFLUX(ISIDEJ,INDEXJ,I3,3,1,K2) = 2*Q2(03,28) - XNJ(3,1,K2) + 2
     >   *5**(0.5D0)*Q2(09,28)
      BFLUX(ISIDEJ,INDEXJ,I3,1,2,K2) = 2*Q2(10,28) - XNJ(1,2,K2) + 2
     >   *5**(0.5D0)*Q2(16,28)
      BFLUX(ISIDEJ,INDEXJ,I3,2,2,K2) = (2*Q2(11,28) - XNJ(2,2,K2) + 2
     >   *5**(0.5D0)*Q2(17,28))*C1
      BFLUX(ISIDEJ,INDEXJ,I3,3,2,K2) = 2*Q2(12,28) - XNJ(3,2,K2) + 2
     >   *5**(0.5D0)*Q2(18,28)
      BFLUX(ISIDEJ,INDEXJ,I3,1,3,K2) = 2*Q2(19,28) - XNJ(1,3,K2) + 2
     >   *5**(0.5D0)*Q2(25,28)
      BFLUX(ISIDEJ,INDEXJ,I3,2,3,K2) = (2*Q2(20,28) - XNJ(2,3,K2) + 2
     >   *5**(0.5D0)*Q2(26,28))*C1
      BFLUX(ISIDEJ,INDEXJ,I3,3,3,K2) = 2*Q2(21,28) - XNJ(3,3,K2) + 2
     >   *5**(0.5D0)*Q2(27,28)
      ENDIF
      ENDIF
*
      IF(IL.LT.ISPLH)THEN
      IF(IELEM.EQ.1) THEN
      XNI(1,1,J2,K2) = 2.0D0*Q2(1,2)-XNI(1,1,J2,K2)
      ELSEIF(IELEM.EQ.2) THEN
      XNI(1,1,J2,K2) = XNI(1,1,J2,K2) + SIGN(1.0,MUH)*
     >   2.0D0*3.0D0**(0.5D0)*Q2(2,9)
      XNI(2,1,J2,K2) = XNI(2,1,J2,K2) + SIGN(1.0,MUH)*
     >   2.0D0*3.0D0**(0.5D0)*Q2(4,9)
      XNI(1,2,J2,K2) = XNI(1,2,J2,K2) + SIGN(1.0,MUH)*
     >   2.0D0*3.0D0**(0.5D0)*Q2(6,9)
      XNI(2,2,J2,K2) = XNI(2,2,J2,K2) + SIGN(1.0,MUH)*
     >   2.0D0*3.0D0**(0.5D0)*Q2(8,9)
      ELSEIF(IELEM.EQ.3) THEN
      XNI(1,1,J2,K2) = 2*Q2(01,28) - XNI(1,1,J2,K2)
     >    + 2*5**(0.5D0)*Q2(03,28)
      XNI(2,1,J2,K2) = 2*Q2(04,28) - XNI(2,1,J2,K2)
     >    + 2*5**(0.5D0)*Q2(06,28)
      XNI(3,1,J2,K2) = 2*Q2(07,28) - XNI(3,1,J2,K2)
     >    + 2*5**(0.5D0)*Q2(09,28)
      XNI(1,2,J2,K2) = 2*Q2(10,28) - XNI(1,2,J2,K2)
     >    + 2*5**(0.5D0)*Q2(12,28)
      XNI(2,2,J2,K2) = 2*Q2(13,28) - XNI(2,2,J2,K2)
     >    + 2*5**(0.5D0)*Q2(15,28)
      XNI(3,2,J2,K2) = 2*Q2(16,28) - XNI(3,2,J2,K2)
     >    + 2*5**(0.5D0)*Q2(18,28)
      XNI(1,3,J2,K2) = 2*Q2(19,28) - XNI(1,3,J2,K2)
     >    + 2*5**(0.5D0)*Q2(21,28)
      XNI(2,3,J2,K2) = 2*Q2(22,28) - XNI(2,3,J2,K2)
     >    + 2*5**(0.5D0)*Q2(24,28)
      XNI(3,3,J2,K2) = 2*Q2(25,28) - XNI(3,3,J2,K2)
     >    + 2*5**(0.5D0)*Q2(27,28)
      ENDIF
      ELSEIF((IL.EQ.ISPLH).AND.(IHEXI.LE.NHEX))THEN
         J3=J2
         C1=1.0D0
         IF((J.EQ.3).AND.(ILOZI.EQ.1)) THEN
            J3=ISPLH+1-J2
            C1=-1.0D0
         ENDIF
      IF(IELEM.EQ.1) THEN
      BFLUX(ISIDEI,INDEXI,J3,1,1,K2)=2.0D0*Q2(1,2)-XNI(1,1,J2,K2)
      ELSEIF(IELEM.EQ.2) THEN
      BFLUX(ISIDEI,INDEXI,J3,1,1,K2) = (XNI(1,1,J2,K2) +
     >   SIGN(1.0,MUH)*2.0D0*3.0D0**(0.5D0)*Q2(2,9))
      BFLUX(ISIDEI,INDEXI,J3,2,1,K2) = (XNI(2,1,J2,K2) +
     >   SIGN(1.0,MUH)*2.0D0*3.0D0**(0.5D0)*Q2(4,9))*C1
      BFLUX(ISIDEI,INDEXI,J3,1,2,K2) = (XNI(1,2,J2,K2) +
     >   SIGN(1.0,MUH)*2.0D0*3.0D0**(0.5D0)*Q2(6,9))*1
      BFLUX(ISIDEI,INDEXI,J3,2,2,K2) = (XNI(2,2,J2,K2) +
     >   SIGN(1.0,MUH)*2.0D0*3.0D0**(0.5D0)*Q2(8,9))*C1
      ELSEIF(IELEM.EQ.3) THEN
      BFLUX(ISIDEI,INDEXI,J3,1,1,K2) = 2*Q2(01,28) - XNI(1,1,J2,K2)
     >    + 2*5**(0.5D0)*Q2(03,28)
      BFLUX(ISIDEI,INDEXI,J3,2,1,K2) = (2*Q2(04,28) - XNI(2,1,J2,K2)
     >    + 2*5**(0.5D0)*Q2(06,28))*C1
      BFLUX(ISIDEI,INDEXI,J3,3,1,K2) = 2*Q2(07,28) - XNI(3,1,J2,K2)
     >    + 2*5**(0.5D0)*Q2(09,28)
      BFLUX(ISIDEI,INDEXI,J3,1,2,K2) = 2*Q2(10,28) - XNI(1,2,J2,K2)
     >    + 2*5**(0.5D0)*Q2(12,28)
      BFLUX(ISIDEI,INDEXI,J3,2,2,K2) = (2*Q2(13,28) - XNI(2,2,J2,K2)
     >    + 2*5**(0.5D0)*Q2(15,28))*C1
      BFLUX(ISIDEI,INDEXI,J3,3,2,K2) = 2*Q2(16,28) - XNI(3,2,J2,K2)
     >    + 2*5**(0.5D0)*Q2(18,28)
      BFLUX(ISIDEI,INDEXI,J3,1,3,K2) = 2*Q2(19,28) - XNI(1,3,J2,K2)
     >    + 2*5**(0.5D0)*Q2(21,28)
      BFLUX(ISIDEI,INDEXI,J3,2,3,K2) = (2*Q2(22,28) - XNI(2,3,J2,K2)
     >    + 2*5**(0.5D0)*Q2(24,28))*C1
      BFLUX(ISIDEI,INDEXI,J3,3,3,K2) = 2*Q2(25,28) - XNI(3,3,J2,K2)
     >    + 2*5**(0.5D0)*Q2(27,28)
      ENDIF
      ENDIF
*
      ILOZ=((J-1)*ISPLH+(J2-1))*ISPLH+I2
      DO 210 P=1,NSCT
      DO 200 IEL=1,IELEM**3
      FLUX(IEL,P,ILOZ,I,K2) = FLUX(IEL,P,ILOZ,I,K2)+Q2(IEL,IELEM**3+1)
     1 *DN(P,M)
  200 CONTINUE
  210 CONTINUE
*
  220 CONTINUE
*
      IF((NCODE(5).NE.1).or.(NCODE(6).NE.1))THEN
      DO 240 IEL=1,IELEM
      DO 230 JEL=1,IELEM
      IOF=((((((M-1)*NHEX+(I-1))*3+(J-1))*ISPLH+(J2-1))*ISPLH+
     1    (I2-1))*IELEM+(JEL-1))*IELEM+IEL
      FUNKNO(L4+IOF,IG)=REAL(XNK(IEL,JEL))
  230 CONTINUE
  240 CONTINUE
      ENDIF
*
  250 CONTINUE
  260 CONTINUE
  270 CONTINUE
  280 CONTINUE
      FLUX_G(:,:,:,:,:,IG)=FLUX_G(:,:,:,:,:,IG)+FLUX(:,:,:,:,:)
  290 CONTINUE
  300 CONTINUE
*$OMP END PARALLEL DO
  310 CONTINUE
      DO 320 IG=1,NGEFF
        IF(.NOT.INCONV(IG)) GO TO 320
        FUNKNO(:L4,IG)=
     1  RESHAPE(REAL(FLUX_G(:IELEM**3,:NSCT,:3*ISPLH**2,:NHEX,:LZ,IG)),
     2  (/L4/))
  320 CONTINUE
*----
*  SCRATCH STORAGE DEALLOCATION
*----
      DEALLOCATE(FLUX_G,FLUX,BFLUX,INDANG)
      RETURN
  500 FORMAT(16H SNFTH3: thread=,I8,12H --->(group=,I4,7H angle=,I4,1H))
      END
