*** Gd3x3C case with CP/IC/MOC solution : 
* BWR 3x3 array based on ATRIUM-10 geometry, 
* All 24UOX, 45Gd in RC = Right Center position
* import tdt_data generated by glow.
* Solve 295 group CP/IC/MOC problem.
* Mix definition from 24UOX and 45Gd AT10 fuels.
* nuclear data evaluation : endfb8r1


MODULE GEO: LIB: G2S: SALT: USS: MCCGT: ASM: FLU: GREP: DELETE: END: ;
PROCEDURE assertS ;
LINKED_LIST GEOISO GEOSPC MICLIB MICLIBG TRACKIC TRACKCP TRACKMOC PIJ SYS FLUX
                        MICLIB2  MICLIBG2 ;

SEQ_BINARY TRACKFILIC TRACKFILCP TRACKFILMOC ;

SEQ_ASCII tdtMACRO :: FILE '3x3_Gd_RC_TISO_MACRO.dat' ;
SEQ_ASCII glowMACRO.ps :: FILE '3x3_Gd_RC_GLOW_TISO_MACRO.ps' ;

SEQ_ASCII g2s_tdt g2s_tdt_IC ;
SEQ_ASCII geo_tiso.ps glow_tiso.ps ;
SEQ_ASCII geoTisoIC.ps ;

REAL keffGEOIC keffGEOPIJ keffGEOCP  keffGEOMOC ;
REAL keffGLOWCP keffGLOWPIJ keffGLOWIC keffGLOWMOC ;
REAL keffGMCP keffGMPIJ keffGMIC keffGMMOC ; 
REAL delCPIC delMOCIC deltak ;

* ---
* TRACKING PARAMTERES
* ---
INTEGER an2d ;
:: >>an2d<< ;
REAL densur ; 
:: >>densur<< ;
INTEGER an2dIC ;
:: >>an2dIC<< ;
REAL densurIC ;
:: >>densurIC<< ;

REAL Rgap Rclad pitch := 0.4520 0.5140 1.295 ;
REAL Rcomb1 Rcomb2 Rcomb3 Rcomb4 := 0.313602 0.396678 0.43227 0.4435 ;
REAL RcombGd1 RcombGd2 RcombGd3 RcombGd4 RcombGd5 RcombGd6 
         := 0.19834 0.28049 0.34353 0.39668 0.43227 0.4435 ;

REAL 2_pitch 3_pitch ;
EVALUATE 2_pitch := 2.0 pitch * ;
EVALUATE 3_pitch := 3.0 pitch * ;

GEOISO := GEO: :: CAR2D 3 3
    X- ALBE 1.0 X+ ALBE 1.0 
    Y- ALBE 1.0 Y+ ALBE 1.0 
    CELL
    C1 C1 C1
    C1 C1 C7
    C1 C1 C1
    MESHX 0.0 <<pitch>> <<2_pitch>> <<3_pitch>>
    MESHX 0.0 <<pitch>> <<2_pitch>> <<3_pitch>>
    ::: C1 :=  GEO: CARCEL 6 
                RADIUS 0.0 <<Rcomb1>> <<Rcomb2>>
                <<Rcomb3>> <<Rcomb4>> <<Rgap>> <<Rclad>>
                MIX 1 1 1 1 3 4 5
                MESHX 0.0 <<pitch>> MESHY 0.0 <<pitch>>
    ;
    ::: C7 := GEO: CARCEL 8
              RADIUS 0.0 <<RcombGd1>> <<RcombGd2>> <<RcombGd3>> <<RcombGd4>>
                          <<RcombGd5>> <<RcombGd6>> <<Rgap>> <<Rclad>> 
              MIX 2 2 2 2 2 2 3 4 5
              MESHX 0.0 <<pitch>> MESHY 0.0 <<pitch>>
    ;
    
;

MICLIB := LIB: ::
    EDIT 0
    NMIX 5    ! MAXIMUM OF MATERIAL MIXTURES
    PT CALENDF 4
    ANIS 1
  MIXS LIB: DRAGON FIL: endfb8r1_295

  MIX 1 (*24UOX*) 900.0
    O16     = O16    4.66705E-02
    U234    = U234   5.15910E-06 1
    U235    = U235   5.67035E-04 1 
    U238    = U238   2.27631E-02 1 
  MIX 2 (*45Gd*) 900.0
    O16     = O16    4.621410E-02
    U234    = U234   9.451580E-06 
    U235    = U235   9.945290E-04 1 
    U238    = U238   2.107540E-02 1
    Gd154    = Gd154   2.986510E-05 1
    Gd155    = Gd155   2.027540E-04 1
    Gd156    = Gd156   2.804310E-04 1
    Gd157    = Gd157   2.143990E-04 1
    Gd158    = Gd158   3.403000E-04 1
    Gd160    = Gd160   2.994740E-04 1  
  
  MIX 3 (*GAP*) 900.0 NOEV
    He4      = He4 1.50456E-04

  MIX 4 (*CLAD*) 559.0 NOEV
    Ni60  = Ni60   9.69262E-06
    Zr90  = Zr90   2.18317E-02 2
    Zr91  = Zr91   4.76096E-03 2
    Zr92  = Zr92   7.27723E-03 2
    Zr94  = Zr94   7.37482E-03 2
    Zr96  = Zr96   1.18812E-03 2
    Fe58  = Fe58   2.68895E-07
    Sn112  = Sn112   4.67343E-06
    Sn116  = Sn116   7.00535E-05
    Cr50  = Cr50   3.29613E-06
    Sn114  = Sn114   3.17985E-06
    Sn117  = Sn117   3.70020E-05
    Sn122  = Sn122   2.23073E-05
    O17  = O17   1.17376E-07
    Sn124  = Sn124   2.78961E-05
    Cr54  = Cr54   1.79408E-06
    Ni62  = Ni62   1.34344E-06
    Cr53  = Cr53   7.20746E-06
    Sn115  = Sn115   1.63812E-06
    Ni61  = Ni61   4.21331E-07
    Ni64  = Ni64   3.42085E-07
    Cr52  = Cr52   6.35631E-05
    O16  = O16   3.08132E-04
    Sn120  = Sn120   1.56970E-04
    Ni58  = Ni58   2.51627E-05
    Fe54  = Fe54   5.57337E-06
    Fe57  = Fe57   2.02053E-06
    Fe56  = Fe56   8.74901E-05
    Sn118  = Sn118   1.16691E-04
    Sn119  = Sn119   4.13864E-05

  MIX 5 (*MODERATOR*) 559.0 NOEV
    H1      = H1_H2O 4.94546E-02  
    O16     = O16    2.47298E-02 
  ;

MICLIBG := LIB: ::
    EDIT 0
    NMIX 5    ! MAXIMUM OF MATERIAL MIXTURES
    PT CALENDF 4
    ANIS 1
  MIXS LIB: DRAGON FIL: endfb8r1_295

  MIX 4 (*24UOX*) 900.0
    O16     = O16    4.66705E-02
    U234    = U234   5.15910E-06 1
    U235    = U235   5.67035E-04 1 
    U238    = U238   2.27631E-02 1 
  MIX 5 (*45Gd*) 900.0
    O16     = O16    4.621410E-02
    U234    = U234   9.451580E-06 
    U235    = U235   9.945290E-04 1 
    U238    = U238   2.107540E-02 1
    Gd154    = Gd154   2.986510E-05 1
    Gd155    = Gd155   2.027540E-04 1
    Gd156    = Gd156   2.804310E-04 1
    Gd157    = Gd157   2.143990E-04 1
    Gd158    = Gd158   3.403000E-04 1
    Gd160    = Gd160   2.994740E-04 1  
  MIX 2 (*CLAD*) 559.0 NOEV
    Ni60  = Ni60   9.69262E-06
    Zr90  = Zr90   2.18317E-02 2
    Zr91  = Zr91   4.76096E-03 2
    Zr92  = Zr92   7.27723E-03 2
    Zr94  = Zr94   7.37482E-03 2
    Zr96  = Zr96   1.18812E-03 2
    Fe58  = Fe58   2.68895E-07
    Sn112  = Sn112   4.67343E-06
    Sn116  = Sn116   7.00535E-05
    Cr50  = Cr50   3.29613E-06
    Sn114  = Sn114   3.17985E-06
    Sn117  = Sn117   3.70020E-05
    Sn122  = Sn122   2.23073E-05
    O17  = O17   1.17376E-07
    Sn124  = Sn124   2.78961E-05
    Cr54  = Cr54   1.79408E-06
    Ni62  = Ni62   1.34344E-06
    Cr53  = Cr53   7.20746E-06
    Sn115  = Sn115   1.63812E-06
    Ni61  = Ni61   4.21331E-07
    Ni64  = Ni64   3.42085E-07
    Cr52  = Cr52   6.35631E-05
    O16  = O16   3.08132E-04
    Sn120  = Sn120   1.56970E-04
    Ni58  = Ni58   2.51627E-05
    Fe54  = Fe54   5.57337E-06
    Fe57  = Fe57   2.02053E-06
    Fe56  = Fe56   8.74901E-05
    Sn118  = Sn118   1.16691E-04
    Sn119  = Sn119   4.13864E-05
  MIX 3 (*GAP*) 900.0 NOEV
    He4      = He4 1.50456E-04
  MIX 1 (*MODERATOR*) 559.0 NOEV
    H1      = H1_H2O 4.94546E-02  
    O16     = O16    2.47298E-02 
  ;

*** TISO case treatment ***


g2s_tdt geo_tiso.ps := G2S: GEOISO :: ;
g2s_tdt_IC geoTisoIC.ps := G2S: GEOISO :: MACRO ;

glow_tiso.ps := G2S: tdtMACRO :: ;

*** TREAT ALL GEO: CASES FIRST ***

TRACKIC TRACKFILIC := SALT: g2s_tdt_IC ::
     EDIT 2
     TITLE "MULTICELL SURFACIC 3x3_Gd_RC GEO CASE"
     TISO <<an2dIC>> <<densurIC>>
     IC EPSJ 1.0E-6
     ;

TRACKCP TRACKFILCP := SALT: g2s_tdt ::
     EDIT 2
     TITLE "TISO CP SURFACIC 3x3_Gd_RC GEO CASE"
     TISO <<an2d>> <<densur>>
     ;

TRACKMOC TRACKFILMOC := SALT: g2s_tdt ::
     EDIT 2
     TITLE "TISO MOC SURFACIC 3x3_Gd_RC GEO CASE"
     TISO EQW <<an2d>> <<densur>>
     ;

* MOC tracking
TRACKMOC := MCCGT: TRACKMOC TRACKFILMOC ::
  EDIT 0
;

* --- 
* Self-shielding using PIJ tracking
* ---
MICLIB2 := USS: MICLIB TRACKCP TRACKFILCP ::
    EDIT 1 PASS 3 GRMIN 52
;

* ---
* Flux calculation
* ---

* FULL PIJ MATRIX RECONSTRUCTION
PIJ := ASM: MICLIB2 TRACKIC TRACKFILIC :: PIJ EDIT 2 ;
FLUX := FLU: PIJ MICLIB2 TRACKIC :: 
    EDIT 1 TYPE K
;
GREP: FLUX :: GETVAL 'K-EFFECTIVE' 1 1 1 >>keffGEOPIJ<< ;
PIJ FLUX := DELETE: PIJ FLUX ;
ECHO "test multicell 3x3_Gd_RC FULL PIJ reconstruction completed, keff=" keffGEOPIJ ;

* FLUX-CURRENT ITERATION WITHOUT FULL PIJ MATRIX RECONSTRUCTION
PIJ := ASM: MICLIB2 TRACKIC TRACKFILIC :: ARM EDIT 2 ;
FLUX := FLU: PIJ MICLIB2 TRACKIC ::
    EDIT 4 TYPE K
;
GREP: FLUX :: GETVAL 'K-EFFECTIVE' 1 1 1 >>keffGEOIC<< ;
assertS FLUX :: 'K-EFFECTIVE' 1 <<keffGEOPIJ>> ;
PIJ FLUX := DELETE: PIJ FLUX ;
ECHO "test multicell 3x3_Gd_RC FLUX-CURRENT iteration completed, keff=" keffGEOIC ;

PIJ := ASM: MICLIB2 TRACKCP TRACKFILCP :: PIJ EDIT 2 ;
FLUX := FLU: PIJ MICLIB2 TRACKCP :: 
    EDIT 1 TYPE K 
;
GREP: FLUX :: GETVAL 'K-EFFECTIVE' 1 1 1 >>keffGEOCP<< ;
PIJ FLUX := DELETE: PIJ FLUX ;
ECHO "test multicell 3x3_Gd_RC CP completed, keff=" keffGEOCP ;

EVALUATE delCPIC := keffGEOCP keffGEOIC - 1E+5 * ;
ECHO "With GEO: difference between CP and IC keff = " delCPIC ;

SYS := ASM: MICLIB2 TRACKMOC TRACKFILMOC :: ARM EDIT 2 ; 
FLUX := FLU: SYS MICLIB2 TRACKMOC TRACKFILMOC :: 
    EDIT 1 TYPE K
;
GREP: FLUX :: GETVAL 'K-EFFECTIVE' 1 1 1 >>keffGEOMOC<< ;
ECHO "test multicell 3x3_Gd_RC MOC completed, keff=" keffGEOMOC ;

EVALUATE delMOCIC := keffGEOMOC keffGEOIC - 1E+5 * ;
ECHO "With GEO: difference between MOC and IC keff = " delMOCIC ;


FLUX SYS := DELETE: FLUX SYS ;
TRACKIC TRACKFILIC := DELETE: TRACKIC TRACKFILIC ;
TRACKCP TRACKFILCP := DELETE: TRACKCP TRACKFILCP ;
TRACKMOC TRACKFILMOC := DELETE: TRACKMOC TRACKFILMOC ;

*----
ECHO " --- GLOW CASES TREATMENT --- " ;
*** TREAT ALL GLOW CASES SECOND ***

TRACKIC TRACKFILIC := SALT: tdtMACRO ::
     EDIT 2
     TITLE "MULTICELL SURFACIC 3x3_Gd_RC GLOW CASE"
     TISO <<an2dIC>> <<densurIC>>
     IC EPSJ 1.0E-6
     ;

TRACKCP TRACKFILCP := SALT: tdtMACRO ::
     EDIT 2
     TITLE "TISO CP SURFACIC 3x3_Gd_RC GLOW CASE"
     TISO <<an2d>> <<densur>>
     NOIC
     ;

TRACKMOC TRACKFILMOC := SALT: tdtMACRO ::
     EDIT 2
     TITLE "TISO MOC SURFACIC 3x3_Gd_RC GLOW CASE"
     TISO EQW <<an2d>> <<densur>>
     NOIC
     ;

* MOC tracking
TRACKMOC := MCCGT: TRACKMOC TRACKFILMOC ::
  EDIT 0
;

* --- 
* Self-shielding using PIJ tracking
* ---
MICLIBG2 := USS: MICLIBG TRACKCP TRACKFILCP ::
    EDIT 1 PASS 3 GRMIN 52
;

* ---
* Flux calculation
* ---

* FULL PIJ MATRIX RECONSTRUCTION
PIJ := ASM: MICLIBG2 TRACKIC TRACKFILIC :: PIJ EDIT 2 ;
FLUX := FLU: PIJ MICLIBG2 TRACKIC :: 
    EDIT 1 TYPE K 
;
GREP: FLUX :: GETVAL 'K-EFFECTIVE' 1 1 1 >>keffGLOWPIJ<< ;
PIJ FLUX := DELETE: PIJ FLUX ;
ECHO "test multicell 3x3_Gd_RC (GLOW) FULL PIJ reconstruction completed, keff=" keffGLOWPIJ ;

* FLUX-CURRENT ITERATION WITHOUT FULL PIJ MATRIX RECONSTRUCTION
PIJ := ASM: MICLIBG2 TRACKIC TRACKFILIC :: ARM EDIT 2 ;
FLUX := FLU: PIJ MICLIBG2 TRACKIC :: 
    EDIT 4 TYPE K
;
GREP: FLUX :: GETVAL 'K-EFFECTIVE' 1 1 1 >>keffGLOWIC<< ;
assertS FLUX :: 'K-EFFECTIVE' 1 <<keffGLOWPIJ>> ;
PIJ FLUX := DELETE: PIJ FLUX ;
ECHO "test multicell 3x3_Gd_RC (GLOW) FLUX-CURRENT iteration completed, keff=" keffGLOWIC ;

PIJ := ASM: MICLIBG2 TRACKCP TRACKFILCP :: PIJ EDIT 2 ;
FLUX := FLU: PIJ MICLIBG2 TRACKCP :: 
    EDIT 1 TYPE K 
;
GREP: FLUX :: GETVAL 'K-EFFECTIVE' 1 1 1 >>keffGLOWCP<< ;
PIJ FLUX := DELETE: PIJ FLUX ;
ECHO "test multicell 3x3_Gd_RC (GLOW) CP completed, keff=" keffGLOWCP ;

EVALUATE delCPIC := keffGLOWCP keffGLOWIC - 1E+5 * ;
ECHO "With GLOW: difference between CP and IC keff = " delCPIC ;

SYS := ASM: MICLIBG2 TRACKMOC TRACKFILMOC :: ARM EDIT 2 ; 
FLUX := FLU: SYS MICLIBG2 TRACKMOC TRACKFILMOC :: 
    EDIT 1 TYPE K 
;
GREP: FLUX :: GETVAL 'K-EFFECTIVE' 1 1 1 >>keffGLOWMOC<< ;
ECHO "test multicell 3x3_Gd_RC (GLOW) MOC completed, keff=" keffGLOWMOC ;

EVALUATE delMOCIC := keffGLOWMOC keffGLOWIC - 1E+5 * ;
ECHO "With GLOW: difference between MOC and IC keff = " delMOCIC ;

FLUX SYS := DELETE: FLUX SYS ;
TRACKIC TRACKFILIC := DELETE: TRACKIC TRACKFILIC ;
TRACKCP TRACKFILCP := DELETE: TRACKCP TRACKFILCP ;
TRACKMOC TRACKFILMOC := DELETE: TRACKMOC TRACKFILMOC ;

! Assert differences with GEO results 
ECHO "Test IC from IC tracking : GLOW - GEO" ;
ECHO "keffGLOWIC = " keffGLOWIC ;
ECHO "keffGEOIC = " keffGEOIC ;
EVALUATE deltak := keffGLOWIC keffGEOIC - 1E+5 * ;
ECHO "delta k IC (GLOW - GEO) = " deltak "pcm" ;

ECHO "Test PIJ from IC tracking : GLOW - GEO" ;
ECHO "keffGLOWPIJ = " keffGLOWPIJ ;
ECHO "keffGEOPIJ = " keffGEOPIJ ;
EVALUATE deltak := keffGLOWPIJ keffGEOPIJ - 1E+5 * ;
ECHO "delta k PIJ (GLOW - GEO) = " deltak "pcm" ;

ECHO "Test CP from NOIC tracking : GLOW - GEO" ;
ECHO "keffGLOWCP = " keffGLOWCP ;
ECHO "keffGEOCP = " keffGEOCP ;
EVALUATE deltak := keffGLOWCP keffGEOCP - 1E+5 * ;
ECHO "delta k CP (GLOW - GEO) = " deltak "pcm" ;

ECHO "Test MOC from NOIC tracking : GLOW - GEO" ;
ECHO "keffGLOWMOC = " keffGLOWMOC ;
ECHO "keffGEOMOC = " keffGEOMOC ;
EVALUATE deltak := keffGLOWMOC keffGEOMOC - 1E+5 * ;
ECHO "delta k MOC (GLOW - GEO) = " deltak "pcm" ;

END: ;

