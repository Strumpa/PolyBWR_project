* -------------------------------
*    STRUCTURES AND MODULES
* -------------------------------
MODULE  GEO: END: G2S: ;

LINKED_LIST GEOMSS ;
SEQ_ASCII Geo_G2S_SS ;
SEQ_ASCII FIG_SS.ps :: FILE './GEOM_FIG_CTRL_TEST.ps' ;
!Assembly scale data

REAL Pitch_A W_gap := 15.24 0.75 ; ! Assembly pitch and water gap thickness
REAL Box_out Box_in Box_thi := 13.74 13.4 0.17 ; ! Outer box outer, inner sides and thickness
REAL Chan_out Chan_in Chan_thi := 3.5 3.34 0.08 ; ! Channel box outer, inner sides and thickness

! UOX Pincell scale data

REAL Pitch_C := 1.29500 ;
REAL Rgap Rclad pitch := 0.4520 0.5140 1.29500 ;
REAL Rcomb1 Rcomb2 Rcomb3 Rcomb4 ;
EVALUATE Rcomb4 := 0.4435 ;
EVALUATE Rcomb1 := 0.313602 ;
EVALUATE Rcomb2 := 0.396678 ; 
EVALUATE Rcomb3 := 0.43227 ;

!Mix numbers for C1 fuel 
INTEGER COMB0101 COMB0102 COMB0103 COMB0104 ;
EVALUATE COMB0101 COMB0102 COMB0103 COMB0104 := 1 2 3 4 ;

!Mix numbers for C2 fuel 
INTEGER COMB0201 COMB0202 COMB0203 COMB0204 ;
EVALUATE COMB0201 COMB0202 COMB0203 COMB0204 := 21 22 23 24 ;

!Mix numbers for C3 fuel 
INTEGER COMB0301 COMB0302 COMB0303 COMB0304 ;
EVALUATE COMB0301 COMB0302 COMB0303 COMB0304 := 31 32 33 34 ;

!Mix numbers for C4 fuel 
INTEGER COMB0401 COMB0402 COMB0403 COMB0404 ;
EVALUATE COMB0401 COMB0402 COMB0403 COMB0404 := 41 42 43 44 ;

!Mix numbers for C5 fuel 
INTEGER COMB0501 COMB0502 COMB0503 COMB0504 ;
EVALUATE COMB0501 COMB0502 COMB0503 COMB0504 := 51 52 53 54 ;

!Mix numbers for C6 fuel 
INTEGER COMB0601 COMB0602 COMB0603 COMB0604 ;
EVALUATE COMB0601 COMB0602 COMB0603 COMB0604 := 61 62 63 64 ;

! UOX + Gd Pincell scale data

REAL RcombGd1 RcombGd2 RcombGd3 RcombGd4 RcombGd5 RcombGd6 ;
EVALUATE RcombGd1 RcombGd2 RcombGd3 RcombGd4 RcombGd5 RcombGd6 
         := 0.19834 0.28049 0.34353 0.39668 0.43227 0.4435 ;

! Mix numbers C7 fuel
INTEGER COMB0701 COMB0702 COMB0703 COMB0704 COMB0705 COMB0706 ;
EVALUATE COMB0701 COMB0702 COMB0703 COMB0704 COMB0705 COMB0706 := 71 72 73 74 75 76 ;
! Mix numbers C7 fuel
INTEGER COMB0801 COMB0802 COMB0803 COMB0804 COMB0805 COMB0806 ;
EVALUATE COMB0801 COMB0802 COMB0803 COMB0804 COMB0805 COMB0806 := 81 82 83 84 85 86 ;

! Pin level common mixes : Clad, Gap, Coolant (intra-pin water) :

INTEGER GAP CLAD COOL ;
EVALUATE GAP CLAD COOL := 5 6 7 ;

! Assembly level common mixes water, channel and box
! Careful : some intra assembly water is treated at the 2nd geometry level
! MODE is associated with H20 OUTSIDE assembly box and INSIDE channel box, the rest is COOL
INTEGER BOX MODE ;
EVALUATE BOX MODE := 60 70 ;

!--> check compositions for box and channel, the same --> Yes !
! Treating Cladding and Boxes with different mixes but outer box and channel are the same

! Control cross/rod mix numbers :
INTEGER CTRL_CRS CTRL_ROD ;
EVALUATE CTRL_CRS CTRL_ROD := 111 112 ;

! Control cross geometrical data :
! for a wing : Total wing length (cross' half length) and seath width (cross' half width) 
REAL CRS_L CRS_W := 12.06438 0.35216 ; 
REAL CX1 CX2 := 0.8616 11.27154 ; ! Sub-divisions for cross geometry.

! Control rod geometrical data :
REAL Rrod PitchRod := 0.21082 0.57833 ;
REAL 1_PR 2_PR 3_PR 4_PR 5_PR 6_PR 7_PR 8_PR 9_PR 10_PR ;
REAL 11_PR 12_PR 13_PR 14_PR 15_PR 16_PR 17_PR  ;
EVALUATE 1_PR := PitchRod CX1 + ;
EVALUATE 2_PR := 2.0 PitchRod * CX1 + ;
EVALUATE 3_PR := 3.0 PitchRod * CX1 + ;
EVALUATE 4_PR := 4.0 PitchRod * CX1 + ;
EVALUATE 5_PR := 5.0 PitchRod * CX1 + ;
EVALUATE 6_PR := 6.0 PitchRod * CX1 + ;
EVALUATE 7_PR := 7.0 PitchRod * CX1 + ;
EVALUATE 8_PR := 8.0 PitchRod * CX1 + ;
EVALUATE 9_PR := 9.0 PitchRod * CX1 + ;
EVALUATE 10_PR := 10.0 PitchRod * CX1 + ;
EVALUATE 11_PR := 11.0 PitchRod * CX1 + ;
EVALUATE 12_PR := 12.0 PitchRod * CX1 + ;
EVALUATE 13_PR := 13.0 PitchRod * CX1 + ;
EVALUATE 14_PR := 14.0 PitchRod * CX1 + ;
EVALUATE 15_PR := 15.0 PitchRod * CX1 + ;
EVALUATE 16_PR := 16.0 PitchRod * CX1 + ;
EVALUATE 17_PR := 17.0 PitchRod * CX1 + ;

REAL N_CtrlR := 18.0 ;
!EVALUATE Rrod PitchRod

! Evaluating dependencies


! Multiples of cell pitch : used to define 2nd level geometry mesh 
REAL 2_PC 3_PC 4_PC 5_PC 6_PC 7_PC 8_PC 9_PC 10_PC ;
EVALUATE 2_PC := Pitch_C 2.0 * ;
EVALUATE 3_PC := Pitch_C 3.0 * ;
EVALUATE 4_PC := Pitch_C 4.0 * ;
EVALUATE 5_PC := Pitch_C 5.0 * ;
EVALUATE 6_PC := Pitch_C 6.0 * ;
EVALUATE 7_PC := Pitch_C 7.0 * ;
EVALUATE 8_PC := Pitch_C 8.0 * ;
EVALUATE 9_PC := Pitch_C 9.0 * ;
EVALUATE 10_PC := Pitch_C 10.0 * ;

! X1 = Water gap + outer box thickness +extra water : in box water not a multiple of Pitch_C
REAL XtraMode ;
EVALUATE XtraMode := Box_in 10.0 Pitch_C * - 2.0 / ;

!Meshing points for macro geometry :
REAL X1 X2 X3 X4  ;
! X2 = X1 + 4*Pitch_C
! X3 = X2 + 3*Pitch_C
! X4 = X3 + 3*Pitch_C
EVALUATE X1 := W_gap Box_thi + XtraMode + ;
EVALUATE X2 := 4.0 Pitch_C * X1 + ;
EVALUATE X3 := 3.0 Pitch_C * X2 + ;
EVALUATE X4 := 3.0 Pitch_C * X3 + ;  

REAL Y1 Y2 Y3 Y4 ;
EVALUATE Y1 := X1 ;
EVALUATE Y2 := 4.0 Pitch_C * Y1 + ;
EVALUATE Y3 := 3.0 Pitch_C * Y2 + ;
EVALUATE Y4 := 3.0 Pitch_C * Y3 + ;

REAL L1 L2 L3 L ;
EVALUATE L1 := X2 X1 - ;
EVALUATE L2 := X3 X2 - ;
EVALUATE L3 := X4 X3 - ;
EVALUATE L := X4 X1 - ;



REAL x_step y_step ; ! allows for submeshing of in-assembly box water
! in-assembly box water included in geom level with outer assembly water and box
EVALUATE x_step := W_gap Box_thi + ;
EVALUATE y_step := XtraMode Box_thi + ;

! inner CHaNneL box related parameters, used to define 2nd level CHNL Geometry
! CHNL = channel box + surroudning water to fit with pin CARCEL edges 
REAL XCHNL1 XCHNL2 XCHNL3 XCHNL4 XCHNL5 ;
EVALUATE XCHNL1 := L2 Chan_out - 2.0 / ;
EVALUATE XCHNL2 := XCHNL1 Chan_thi + ;
EVALUATE XCHNL3 := pitch XCHNL2 - ;
EVALUATE XCHNL4 := XCHNL3 Chan_thi + ;

ECHO XCHNL1 XCHNL2 XCHNL3 XCHNL4 ;


GEOMSS := GEO: :: CAR2D 3 3 
EDIT 10
X- DIAG X+ REFL
Y- REFL Y+ DIAG
CELL 
WBC1 WB WBC2 
      LAT WR 
         WTC 

MESHX 0.0 <<X1>> <<X4>> <<Pitch_A>>
MESHY 0.0 <<Y1>> <<Y4>> <<Pitch_A>>
::: WTC := GEO: CAR2D 3 3
    MESHX 0.0 <<XtraMode>> <<y_step>> <<X1>>
    MESHY 0.0 <<XtraMode>> <<y_step>> <<Y1>>
    MIX <<COOL>> <<BOX>> <<MODE>> 
        <<BOX>> <<BOX>> <<MODE>> 
        <<MODE>> <<MODE>> <<MODE>>
    ;
::: WBC1 := GEO: CAR2D 4 4 
    MESHX 0.0 <<CRS_W>> <<W_gap>> <<x_step>> <<X1>>
    MESHY 0.0 <<CRS_W>> <<W_gap>> <<x_step>> <<Y1>>
    MIX <<CTRL_CRS>> <<CTRL_CRS>> <<CTRL_CRS>> <<CTRL_CRS>>
        <<CTRL_CRS>> <<MODE>> <<MODE>> <<MODE>> 
        <<CTRL_CRS>> <<MODE>> <<BOX>> <<BOX>>
        <<CTRL_CRS>> <<MODE>> <<BOX>> <<COOL>>
    ;
::: WR := GEO: CAR2D 3 1
    MESHX 0.0 <<XtraMode>> <<y_step>> <<X1>>
    MESHY 0.0 <<L>>
    MIX <<COOL>> <<BOX>> <<MODE>>
    ;
::: WB := GEO: CAR2D 20 4
    EDIT 10
    MESHX 0.0 <<CX1>> 
    <<1_PR>> <<2_PR>> <<3_PR>> <<4_PR>> <<5_PR>> <<6_PR>> <<7_PR>> <<8_PR>> <<9_PR>>
    <<10_PR>> <<11_PR>> <<12_PR>> <<13_PR>> <<14_PR>> <<15_PR>> <<16_PR>> <<17_PR>>
    <<CX2>> <<L>>
    MESHY 0.0 <<CRS_W>> <<W_gap>> <<x_step>> <<Y1>>
    MIX <<CTRL_CRS>> CTRL_ROD CTRL_ROD CTRL_ROD CTRL_ROD 
        CTRL_ROD CTRL_ROD CTRL_ROD CTRL_ROD 
        CTRL_ROD CTRL_ROD CTRL_ROD CTRL_ROD
        CTRL_ROD CTRL_ROD CTRL_ROD CTRL_ROD
        CTRL_ROD CTRL_ROD <<MODE>>

        <<MODE>> <<MODE>> <<MODE>> <<MODE>> <<MODE>> 
        <<MODE>> <<MODE>> <<MODE>> <<MODE>> <<MODE>>
        <<MODE>> <<MODE>> <<MODE>> <<MODE>> <<MODE>>
        <<MODE>> <<MODE>> <<MODE>> <<MODE>> <<MODE>>

        <<BOX>> <<BOX>> <<BOX>> <<BOX>> <<BOX>> 
        <<BOX>> <<BOX>> <<BOX>> <<BOX>> <<BOX>> 
        <<BOX>> <<BOX>> <<BOX>> <<BOX>> <<BOX>>
        <<BOX>> <<BOX>> <<BOX>> <<BOX>> <<BOX>> 
        
        <<COOL>> <<COOL>> <<COOL>>
    ::: CTRL_ROD := GEO: CARCEL 1
                    RADIUS 0.0 0.01
                    MESHX 0.0 PitchRod MESHY 0.0 PitchRod
                    MIX CTRL_ROD CTRL_CRS
                ;
    ;

::: WBC2 := GEO: CAR2D 3 3
    MESHX 0.0 <<XtraMode>> <<y_step>> <<X1>>
    MESHY 0.0 <<W_gap>> <<x_step>> <<X1>>
    MIX <<MODE>> <<MODE>> <<MODE>> 
        <<BOX>>  <<BOX>> <<MODE>> 
        <<COOL>> <<BOX>> <<MODE>> 
    ;
::: LAT := GEO: CAR2D 10 10

    MESHX 0.0 <<Pitch_C>> <<2_PC>> <<3_PC>> <<4_PC>> <<5_PC>> <<6_PC>> <<7_PC>> <<8_PC>> <<9_PC>> <<10_PC>>
    MESHY 0.0 <<Pitch_C>> <<2_PC>> <<3_PC>> <<4_PC>> <<5_PC>> <<6_PC>> <<7_PC>> <<8_PC>> <<9_PC>> <<10_PC>>
    CELL
     C1 C2 C3 C5 C6 C5 C4 C3 C2 C1
     C2 C4 C7 C6 C7 C6 C6 C7 C4 C2
     C3 C7 C6 C6 C6 C7 C6 C6 C7 C3
     C5 C6 C6 C6 C6 C6 C7 C6 C5 C4
     C6 C7 C6 C6 C91 C92 C93 C4 C6 C4
     C5 C6 C7 C6 C94 C95 C96 C3 C7 C4
     C4 C6 C6 C7 C97 C98 C99 C4 C4 C4
     C3 C7 C6 C6 C4 C3 C4 C4 C8 C3
     C2 C4 C7 C5 C6 C7 C4 C8 C4 C2
     C1 C2 C3 C4 C4 C4 C4 C3 C2 C1
    ::: C1 :=  GEO: CARCEL 6 
                SECT 4 6
                RADIUS 0.0 <<Rcomb1>> <<Rcomb2>>
                <<Rcomb3>> <<Rcomb4>> <<Rgap>> <<Rclad>>
                MIX <<COMB0101>> <<COMB0102>> <<COMB0103>> <<COMB0104>>
                    <<GAP>> <<CLAD>>
                    <<COOL>> <<COOL>> <<COOL>> <<COOL>>
                    <<COOL>> <<COOL>> <<COOL>> <<COOL>>
                    <<COOL>> <<COOL>> <<COOL>> <<COOL>> 
                    MESHX 0.0 <<pitch>> MESHY 0.0 <<pitch>>
            ;
    ::: C2 := GEO: C1 MIX <<COMB0201>> <<COMB0202>> <<COMB0203>> <<COMB0204>>
                            <<GAP>> <<CLAD>>
                            <<COOL>> <<COOL>> <<COOL>> <<COOL>>
                            <<COOL>> <<COOL>> <<COOL>> <<COOL>>
                            <<COOL>> <<COOL>> <<COOL>> <<COOL>> 
            ;
    ::: C3 := GEO: C1 MIX <<COMB0301>> <<COMB0302>> <<COMB0303>> <<COMB0304>>
                            <<GAP>> <<CLAD>>
                            <<COOL>> <<COOL>> <<COOL>> <<COOL>>
                            <<COOL>> <<COOL>> <<COOL>> <<COOL>>
                            <<COOL>> <<COOL>> <<COOL>> <<COOL>> 
            ;
    ::: C4 := GEO: C1 MIX <<COMB0401>> <<COMB0402>> <<COMB0403>> <<COMB0404>>
                            <<GAP>> <<CLAD>>
                            <<COOL>> <<COOL>> <<COOL>> <<COOL>>
                            <<COOL>> <<COOL>> <<COOL>> <<COOL>>
                            <<COOL>> <<COOL>> <<COOL>> <<COOL>> 
            ;
    ::: C5 := GEO: C1 MIX <<COMB0501>> <<COMB0502>> <<COMB0503>> <<COMB0504>>
                            <<GAP>> <<CLAD>>
                            <<COOL>> <<COOL>> <<COOL>> <<COOL>>
                            <<COOL>> <<COOL>> <<COOL>> <<COOL>>
                            <<COOL>> <<COOL>> <<COOL>> <<COOL>> 
            ; 
    ::: C6 := GEO: C1 MIX <<COMB0601>> <<COMB0602>> <<COMB0603>> <<COMB0604>>
                            <<GAP>> <<CLAD>>
                            <<COOL>> <<COOL>> <<COOL>> <<COOL>>
                            <<COOL>> <<COOL>> <<COOL>> <<COOL>>
                            <<COOL>> <<COOL>> <<COOL>> <<COOL>> 
            ;
    ::: C7 := GEO: CARCEL 8
                SECT 4 8 
                RADIUS 0.0 <<RcombGd1>> <<RcombGd2>> <<RcombGd3>> <<RcombGd4>>
                            <<RcombGd5>> <<RcombGd6>> <<Rgap>> <<Rclad>> 
                MIX <<COMB0701>> <<COMB0702>> <<COMB0703>> <<COMB0704>> <<COMB0705>> <<COMB0706>>
                    <<GAP>> <<CLAD>> 
                    <<COOL>> <<COOL>> <<COOL>> <<COOL>> <<COOL>> <<COOL>>
                    <<COOL>> <<COOL>> <<COOL>> <<COOL>> <<COOL>> <<COOL>>
                MESHX 0.0 <<pitch>> MESHY 0.0 <<pitch>>
            ; 
    ::: C8 := GEO: C7 MIX <<COMB0801>> <<COMB0802>> <<COMB0803>> <<COMB0804>> <<COMB0805>> <<COMB0806>>
                    <<GAP>> <<CLAD>> 
                    <<COOL>> <<COOL>> <<COOL>> <<COOL>> <<COOL>> <<COOL>>
                    <<COOL>> <<COOL>> <<COOL>> <<COOL>> <<COOL>> <<COOL>> 
            ;
    ::: C91 := GEO: CAR2D 3 3 
            MESHX 0.0 <<XCHNL1>> <<XCHNL2>> <<pitch>> MESHY 0.0 <<XCHNL1>> <<XCHNL2>> <<pitch>>
            MIX <<COOL>> <<COOL>> <<COOL>>
                <<COOL>> <<BOX>> <<BOX>>
                <<COOL>> <<BOX>> <<MODE>>
            ;
    ::: C92 := GEO: CAR2D 1 3 
            MESHX 0.0 <<pitch>> MESHY 0.0 <<XCHNL1>> <<XCHNL2>> <<pitch>>
            MIX <<COOL>> <<BOX>> <<MODE>>
            ;
    ::: C93 := GEO: CAR2D 3 3 
            MESHX 0.0 <<XCHNL3>> <<XCHNL4>> <<pitch>> MESHY 0.0 <<XCHNL1>> <<XCHNL2>> <<pitch>>
            MIX <<COOL>> <<COOL>> <<COOL>>
                <<BOX>> <<BOX>> <<COOL>>
                <<MODE>> <<BOX>> <<COOL>>
            ;
    ::: C94 := GEO: CAR2D 3 1 
            MESHX 0.0 <<XCHNL1>> <<XCHNL2>> <<pitch>> MESHY 0.0 <<pitch>>
            MIX <<COOL>> <<BOX>> <<MODE>>
            ;
    ::: C95 := GEO: CAR2D 1 1 
            MESHX 0.0 <<pitch>> MESHY 0.0 <<pitch>>
            MIX <<MODE>>
            ;
    ::: C96 := GEO: CAR2D 3 1 
            MESHX 0.0 <<XCHNL3>> <<XCHNL4>> <<pitch>> MESHY 0.0 <<pitch>>
            MIX <<MODE>> <<BOX>> <<COOL>>
            ;
    ::: C97 := GEO: CAR2D 3 3 
            MESHX 0.0 <<XCHNL1>> <<XCHNL2>> <<pitch>> MESHY 0.0 <<XCHNL3>> <<XCHNL4>> <<pitch>>
            MIX <<COOL>> <<BOX>> <<MODE>>
                <<COOL>> <<BOX>> <<BOX>>
                <<COOL>> <<COOL>> <<COOL>>
            ;
    ::: C98 := GEO: CAR2D 1 3 
            MESHX 0.0 <<pitch>> MESHY 0.0 <<XCHNL3>> <<XCHNL4>> <<pitch>>
            MIX <<MODE>> <<BOX>> <<COOL>>
            ;
    ::: C99 := GEO: CAR2D 3 3 
            MESHX 0.0 <<XCHNL3>> <<XCHNL4>> <<pitch>> MESHY 0.0 <<XCHNL3>> <<XCHNL4>> <<pitch>>
            MIX <<MODE>> <<BOX>> <<COOL>>
                <<BOX>> <<BOX>> <<COOL>>
                <<COOL>> <<COOL>> <<COOL>>
            ;
    ;


;
    
Geo_G2S_SS FIG_SS.ps := G2S: GEOMSS ::  ;

ECHO "Test GEOMSS done" ;

END: ;