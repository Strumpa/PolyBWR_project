*----
*  TEST CASE TDCM67
*  MACROSCOPIC CROSS SECTIONS
*  SIMPLE S30 CASE WITH MULTICELL SURFACIC APPROXIMATION
*----
*  Define STRUCTURES and MODULES used
*----
LINKED_LIST MACRO GEOM TRACK PIJ FLUX OUT ;
MODULE MAC: G2S: SALT: ASM: FLU: EDI: GREP: DELETE: END: ;
SEQ_BINARY TRACK_FIL ;
SEQ_ASCII Fic_SAL :: FILE 'lattice_hex_tiso_S30_7cells_macro.dat' ;
SEQ_ASCII Fic_SAL.eps :: FILE 'Fic_SAL_TDCM67.eps' ;
REAL keff ;
PROCEDURE assertS ;
*
MACRO := MAC: ::
     EDIT 2 NGRO 1 NMIX 5 NIFI 1
     READ INPUT
     MIX 1 (*COMBUSTIBLE*)
        TOTAL 0.3652    SCAT 1 1 0.3234
        NUSIGF 0.05564      CHI 1.0
     MIX 2 (*GAINE*)
        TOTAL 0.4029        SCAT 1 1 0.4000
     MIX 3 (*EAU*)
        TOTAL 0.3683        SCAT 1 1 0.3661
;
Fic_SAL.eps := G2S: Fic_SAL :: DRAWNOD MACRO ;

TRACK TRACK_FIL := SALT: Fic_SAL ::
     EDIT 2
     TITLE "MULTICELL SURFACIC S30 CASE"
     TISO 8 40.0
     IC EPSJ 1.0E-4
     ;

* FULL PIJ MATRIX RECONSTRUCTION
PIJ := ASM: MACRO TRACK TRACK_FIL :: PIJ EDIT 2 ;
FLUX := FLU: PIJ MACRO TRACK :: TYPE K ;
GREP: FLUX :: GETVAL 'K-EFFECTIVE' 1 1 1 >>keff<< ;
PIJ FLUX := DELETE: PIJ FLUX ;

* FLUX-CURRENT ITERATION WITHOUT FULL PIJ MATRIX RECONSTRUCTION
PIJ := ASM: MACRO TRACK TRACK_FIL :: ARM EDIT 2 ;
FLUX := FLU: PIJ MACRO TRACK :: EDIT 4 TYPE K ;

* PIN-BY-PIN EDITION
OUT := EDI: FLUX MACRO TRACK Fic_SAL :: EDIT 2
     MERG G2S 2
     TRIA 0.0 0.0 0.68002 0.0 0.068002 0.39261
     QUAD 0.68002 0.0 2.382203 0.0 1.786652 1.031524 0.068002 0.39261
     ;

* ASSERTS
assertS FLUX :: 'K-EFFECTIVE' 1 <<keff>> ;
assertS FLUX :: 'K-EFFECTIVE' 1 0.5548652 ;
PIJ FLUX := DELETE: PIJ FLUX ;
ECHO "keff=" keff ;
ECHO "test TDCM67 completed" ;
END: ;
