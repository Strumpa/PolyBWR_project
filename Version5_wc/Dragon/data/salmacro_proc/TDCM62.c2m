*----
*  TEST CASE TDCM62
*  3-REGION PIJ AND SN WITH DOUBLE HETEROGENEITY (BIHET) MIXTURES
*  TYPE K PROBLEM WITH SAL
*  ONE-GROUP MACROLIB-DEFINED CROSS SECTIONS
*----
*  Define STRUCTURES and MODULES used
*----
LINKED_LIST MACRO ASSMB TRACK SYS FLUX EDIT ;
MODULE GEO: G2S: SALT: MAC: ASM: FLU: UTL: DELETE: GREP: END: ;
REAL keff ;
SEQ_ASCII Fil_SAL Fil_SAL61.ps ;
SEQ_BINARY TRACK_FIL ;
PROCEDURE assertS ;
*
MACRO := MAC: ::
 EDIT 2 NGRO 1 NMIX 4 NIFI 1
     READ INPUT
     MIX 1
        TOTAL 0.41       SCAT 1 1 0.3
        NUSIGF 0.1       CHI 1.0
     MIX 2
        TOTAL 0.45       SCAT 1 1 0.347
        NUSIGF 0.17      CHI 1.0
     MIX 3
        TOTAL 0.53       SCAT 1 1 0.5
     MIX 4
        TOTAL 0.3        SCAT 1 1 0.2
        NUSIGF 0.1       CHI 1.0
     ;
ASSMB := GEO: :: CARCEL 2
          X- ALBE 1.0 X+ ALBE 1.0
          Y- ALBE 1.0 Y+ ALBE 1.0
          MIX 5 4 6
          RADIUS 0.0 1.0 3.0
          MESHX 0.0 8.8623
          MESHY 0.0 8.8623
     BIHET SPHE (*NG=*) 2 (* NMILG= *) 2 (* SPHERICAL MICRO-STRUCTURE *)
     (* NS= *) 1 1
     (* M-S-1 *) 0.0 0.3 (* M-S 2 *) 0.0 0.5
     (* COMPOSITE MIXTURES *)  5 6
     (* MIXTURES SURROUNDING M-S *) 3 1
     (* COMPOSITE MIXTURE 5 FRACT *) 0.4 0.0
     (* REAL MIXTURE CONTENT M-S-1 *) 3
     (* COMPOSITE MIXTURE 6 FRACT *) 0.0 0.3
     (* REAL MIXTURE CONTENT M-S-1 *) 2
          ;
*----
*  Collision probability method
*----
Fil_SAL Fil_SAL61.ps := G2S: ASSMB ;
TRACK TRACK_FIL := SALT: Fil_SAL ASSMB ::
     EDIT 1
     TITLE 'DOUBLE-HETEROGENEITY TEST'
     ALLG TISO 12 12.0
     HEBE QUAB 7
     ;
*
SYS := ASM: MACRO TRACK TRACK_FIL :: EDIT 1 PIJ ;
FLUX := FLU: MACRO TRACK SYS :: TYPE K ;
GREP: FLUX :: GETVAL 'K-EFFECTIVE' 1 1 1 >>keff<< ;
ECHO "HEBE K-EFFECTIVE=" keff ;
assertS FLUX :: 'K-EFFECTIVE' 1 1.074218 ;
TRACK TRACK_FIL SYS FLUX := DELETE: TRACK TRACK_FIL SYS FLUX ;
*
*----
*  Collision probability method SAPO
*----
TRACK TRACK_FIL := SALT: Fil_SAL ASSMB ::
     EDIT 1
     TITLE 'DOUBLE-HETEROGENEITY TEST'
     ALLG TISO 12 12.0
     SAPO QUAB 7
     ;
*
SYS := ASM: MACRO TRACK TRACK_FIL :: EDIT 1 PIJ ;
FLUX := FLU: MACRO TRACK SYS :: TYPE K ;
GREP: FLUX :: GETVAL 'K-EFFECTIVE' 1 1 1 >>keff<< ;
ECHO "SAPO K-EFFECTIVE=" keff ;
assertS FLUX :: 'K-EFFECTIVE' 1 1.074116 ;
TRACK TRACK_FIL SYS FLUX := DELETE: TRACK TRACK_FIL SYS FLUX ;
*
*----
*  Collision probability method SLSI
*----
TRACK TRACK_FIL := SALT: Fil_SAL ASSMB ::
     EDIT 1
     TITLE 'DOUBLE-HETEROGENEITY TEST'
     ALLG TISO 12 12.0
     SLSI 0.65 QUAB -100
     ;
*
SYS := ASM: MACRO TRACK TRACK_FIL :: EDIT 1 PIJ ;
FLUX := FLU: MACRO TRACK SYS :: TYPE K ;
GREP: FLUX :: GETVAL 'K-EFFECTIVE' 1 1 1 >>keff<< ;
ECHO "SLSI K-EFFECTIVE=" keff ;
assertS FLUX :: 'K-EFFECTIVE' 1 1.068913 ;
TRACK TRACK_FIL SYS FLUX := DELETE: TRACK TRACK_FIL SYS FLUX ;
ECHO "test TDCM62 completed" ;
END: ;
QUIT "LIST" .
