*----
*  TEST CASE TDCM65
*  MACROSCOPIC CROSS SECTIONS
*  SIMPLE BWR WITH MULTICELL SURFACIC APPROXIMATION
*----
*  Define STRUCTURES and MODULES used
*----
LINKED_LIST MACRO GEOM TRACK PIJ FLUX ;
MODULE MAC: GEO: G2S: SALT: ASM: FLU: GREP: DELETE: END: ;
SEQ_BINARY TRACK_FIL ;
SEQ_ASCII Fic_SAL :: FILE 'Fic_SAL_TDCM65.tdt' ;
SEQ_ASCII Fic_SAL.eps :: FILE 'Fic_SAL_TDCM65.eps' ;
REAL keff ;
PROCEDURE assertS ;
*
MACRO := MAC: ::
     EDIT 2 NGRO 1 NMIX 5 NIFI 1
     READ INPUT
     MIX 1 (*COMBUSTIBLE*)
        TOTAL 0.3652    SCAT 1 1 0.3234
        NUSIGF 0.05564      CHI 1.0
     MIX 2 (*GAINE*)
        TOTAL 0.4029        SCAT 1 1 0.4000
     MIX 3 (*EAU*)
        TOTAL 0.3683        SCAT 1 1 0.3661
;
GEOM := GEO: ::
     CAR2D 2 2
     X- ALBE 1.0  X+ ALBE 1.0
     Y- ALBE 1.0  Y+ ALBE 1.0
     MIX  C1 C2
          C2 C4
     TURN  A  A
           F  A          
     ::: C1 := GEO: CAR2D 4 4
          MIX  SC1 SC2 SC2 SC2
               SC2 SC3 SC3 SC3
               SC2 SC3 SC3 SC3
               SC2 SC3 SC3 SC3
          TURN   A   A   A   A
                 F   A   A   A
                 F   A   A   A
                 F   A   A   A
          ::: SC1 := GEO: CARCEL 1
               MESHX  0.0 2.25
               MESHY  0.0 2.25
               RADIUS 0.0 1.75
               OFFCENTER -1.125 -1.125
               MIX 2 3
          ;
          ::: SC2 := GEO: CAR2D 1 2
               MIX SSC1 SC3
               ::: SSC1 := GEO: CARCEL 2
                    MESHX  0.0 1.5
                    MESHY  0.0 0.75
                    RADIUS 0.0 0.4 0.6
                    OFFCENTER 0.0 -0.375
                    MIX 1 2 3
               ;
          ;
          ::: SC3 := GEO: CARCEL 2
               MESHX  0.0 1.5
               MESHY  0.0 1.5
               RADIUS 0.0 0.4 0.6
               MIX 1 2 3
          ;
     ;
     ::: C2 := GEO: CAR2D 1 6
          MIX  SC4 C3 C3 C3 C3 C3
          ::: SC4 := GEO: CAR2D 1 1
               MESHX  0.0 0.5
               MESHY  0.0 1.75
               MIX 3
          ;
          ::: C3 := GEO: CARCEL 1
               MESHX  0.0 0.5
               MESHY  0.0 1.0
               RADIUS 0.0 0.3
               OFFCENTER 0.25 0.0
               MIX 2 3
          ;
     ;
     ::: C4 := GEO: CARCEL 1
          MESHX  0.0 0.5
          MESHY  0.0 0.5
          RADIUS 0.0 0.3
          OFFCENTER 0.25 0.25
          MIX 2 3
     ;
;
Fic_SAL Fic_SAL.eps := G2S: GEOM :: DRAWNOD MACRO ;

TRACK TRACK_FIL := SALT: Fic_SAL ::
     EDIT 2
     TITLE "MULTICELL SURFACIC SIMPLE CASE"
     TISO 8 40.0
     IC EPSJ 1.0E-4
     ;

* FULL PIJ MATRIX RECONSTRUCTION
PIJ := ASM: MACRO TRACK TRACK_FIL :: PIJ EDIT 2 ;
FLUX := FLU: PIJ MACRO TRACK :: TYPE K ;
GREP: FLUX :: GETVAL 'K-EFFECTIVE' 1 1 1 >>keff<< ;
PIJ FLUX := DELETE: PIJ FLUX ;

* FLUX-CURRENT ITERATION WITHOUT FULL PIJ MATRIX RECONSTRUCTION
PIJ := ASM: MACRO TRACK TRACK_FIL :: ARM EDIT 2 ;
FLUX := FLU: PIJ MACRO TRACK :: EDIT 4 TYPE K ;
assertS FLUX :: 'K-EFFECTIVE' 1 <<keff>> ;
assertS FLUX :: 'K-EFFECTIVE' 1 1.040232 ;
PIJ FLUX := DELETE: PIJ FLUX ;
ECHO "test TDCM65 completed" ;
END: ;
