*----
*  TEST CASE TDCM64
*  MACROSCOPIC CROSS SECTIONS
*  SIMPLE MULTICELL SURFACIC CASE WITH MACRO-GEOMETRY UNFOLDING
*----
*  Define STRUCTURES and MODULES used
*----
LINKED_LIST MACRO GEOM TRACK TRACK2 PIJ FLUX ;
MODULE MAC: GEO: G2S: SALT: ASM: FLU: DELETE: END: ;
SEQ_BINARY TRACK_FIL TRACK_FIL2 ;
SEQ_ASCII Fic_SAL :: FILE 'Fic_SAL_TDCM64.tdt' ;
SEQ_ASCII Fic_SAL.eps :: FILE 'Fic_SAL_TDCM64.eps' ;
PROCEDURE assertS ;
*
MACRO := MAC: ::
     EDIT 2 NGRO 1 NMIX 5 NIFI 1
     READ INPUT
     MIX 1 (*COMBUSTIBLE*)
        TOTAL 0.3652    SCAT 1 1 0.3234
        NUSIGF 0.04564      CHI 1.0
     MIX 2 (*GAINE*)
        TOTAL 0.4029        SCAT 1 1 0.4000
     MIX 3 (*EAU*)
        TOTAL 0.3683        SCAT 1 1 0.3661
;
GEOM := GEO: ::
     CAR2D 2 2
     X- ALBE 1.0  X+ ALBE 1.0
     Y- ALBE 1.0  Y+ ALBE 1.0
     MIX  C1 C2
          C2 C4
     TURN  A  A
           F  A          
     ::: C1  := GEO: CAR2D 1 1
               MESHX  0.0 1.75
               MESHY  0.0 1.75
               SPLITX 3 SPLITY 3
               MIX 3
     ;
     ::: C2  := GEO: CAR2D 1 1
               MESHX  0.0 1.5
               MESHY  0.0 1.75
               MIX 1
     ;
     ::: C4  := GEO: CARCEL 2
               MESHX  0.0 1.5
               MESHY  0.0 1.5
               RADIUS 0.0 0.3 0.6
               OFFCENTER 0.75 0.75
               MIX 1 1 2
     ;
;
Fic_SAL Fic_SAL.eps := G2S: GEOM :: DRAWNOD MACRO ;

TRACK2 TRACK_FIL2 := SALT: Fic_SAL ::
     EDIT 3
     TITLE "MULTICELL SURFACIC SIMPLE CASE"
     TISO 8 40.0
     EPSJ 1.0E-4
     ;

* FULL PIJ MATRIX RECONSTRUCTION
PIJ := ASM: MACRO TRACK2 TRACK_FIL2 :: PIJ EDIT 99 ;
FLUX := FLU: PIJ MACRO TRACK2 :: TYPE K ;
assertS FLUX :: 'K-EFFECTIVE' 1 1.036331 ;
PIJ FLUX := DELETE: PIJ FLUX ;

* FLUX-CURRENT ITERATION WITHOUT FULL PIJ MATRIX RECONSTRUCTION
PIJ := ASM: MACRO TRACK2 TRACK_FIL2 :: ARM EDIT 2 ;
FLUX := FLU: PIJ MACRO TRACK2 :: EDIT 4 TYPE K ;
assertS FLUX :: 'K-EFFECTIVE' 1 1.036331 ;
PIJ FLUX := DELETE: PIJ FLUX ;
ECHO "test TDCM64 completed" ;
END: ;
