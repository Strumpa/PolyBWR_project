*** AT10_3x3 case with CP solution : 
* BWR 3x3 array based on ATRIUM-10 geometry, 
* Generate geometry with GEO:, import tdt_data generated by glow.
* Solve 1 group CP problem to ensure proper material numbering treatment
* issue : GLOW/DRAGON material ordering match, but SALOME re-writes material ids using its internal system.
* Macroscpic cross sections from 24UOX, 32UOX, 42UOX, 45UOX, 50UOX and 45Gd AT10 pins, 
* self-shielded and solved in DRAGON : endfb8r1 - SHEM295
* Homogenized in fuel and condensed to 1 group.



MODULE GEO: MAC: G2S: SALT: ASM: FLU: GREP: DELETE: END: ;
PROCEDURE assertS ;
LINKED_LIST GEOISO GEOSPC MACRO MACROG TRKGEO TRKGLOW PIJ SYS FLUX ;

SEQ_BINARY TRKFILGEO TRKFILGLOW ;
SEQ_ASCII tdt_spc :: FILE 'AT10_3x3_cells_TSPC.dat' ;
SEQ_ASCII tdt_iso :: FILE 'AT10_3x3_cells_TISO.dat' ;
SEQ_ASCII glow_tiso.ps :: FILE 'AT10_3x3_TISO_glow.ps' ;
SEQ_ASCII glow_tspc.ps :: FILE 'AT10_3x3_TSPC_glow.ps' ;
SEQ_ASCII g2s_tdt ;
SEQ_ASCII geo_tiso.ps :: FILE 'AT10_3x3_TISO.ps' ;
SEQ_ASCII geo_tspc.ps :: FILE 'AT10_3x3_TSPC.ps' ;

REAL keffGEO keffGLOW ;
* ---
* TRACKING PARAMTERES
* ---

INTEGER an2d := 24 ;  ! ANGULAR QUADRATURE PARAMETER
REAL densur := 50.0 ; ! DENSITY OF INTEGRATION LINES CM^-1


REAL Rgap Rclad pitch := 0.4520 0.5140 1.295 ;
REAL Rcomb1 Rcomb2 Rcomb3 Rcomb4 := 0.313602 0.396678 0.43227 0.4435 ;
REAL RcombGd1 RcombGd2 RcombGd3 RcombGd4 RcombGd5 RcombGd6 
         := 0.19834 0.28049 0.34353 0.39668 0.43227 0.4435 ;
         
REAL 2_pitch 3_pitch ;
EVALUATE 2_pitch := 2.0 pitch * ;
EVALUATE 3_pitch := 3.0 pitch * ;

GEOSPC := GEO: :: CAR2D 3 3
    X- REFL X+ REFL 
    Y- REFL Y+ REFL
    CELL
    C1 C2 C3
    C2 C4 C7
    C3 C7 C6
    MESHX 0.0 <<pitch>> <<2_pitch>> <<3_pitch>>
    MESHX 0.0 <<pitch>> <<2_pitch>> <<3_pitch>>
    ::: C1 :=  GEO: CARCEL 6 
                RADIUS 0.0 <<Rcomb1>> <<Rcomb2>>
                <<Rcomb3>> <<Rcomb4>> <<Rgap>> <<Rclad>>
                MIX 1 1 1 1 7 8 9
                MESHX 0.0 <<pitch>> MESHY 0.0 <<pitch>>
    ;
    ::: C2 := GEO: C1 MIX 2 2 2 2 7 8 9
    ;
    ::: C3 := GEO: C1 MIX 3 3 3 3 7 8 9
    ;
    ::: C4 := GEO: C1 MIX 4 4 4 4 7 8 9
    ;
    ::: C6 := GEO: C1 MIX 5 5 5 5 7 8 9
    ;
    ::: C7 := GEO: CARCEL 8
              RADIUS 0.0 <<RcombGd1>> <<RcombGd2>> <<RcombGd3>> <<RcombGd4>>
                          <<RcombGd5>> <<RcombGd6>> <<Rgap>> <<Rclad>> 
              MIX 6 6 6 6 6 6 7 8 9
              MESHX 0.0 <<pitch>> MESHY 0.0 <<pitch>>
    ;
    
;

GEOISO := GEO: :: CAR2D 3 3
    X- ALBE 1.0 X+ ALBE 1.0 
    Y- ALBE 1.0 Y+ ALBE 1.0 
    CELL
    C1 C2 C3
    C2 C4 C7
    C3 C7 C6
    MESHX 0.0 <<pitch>> <<2_pitch>> <<3_pitch>>
    MESHX 0.0 <<pitch>> <<2_pitch>> <<3_pitch>>
    ::: C1 :=  GEO: CARCEL 6 
                RADIUS 0.0 <<Rcomb1>> <<Rcomb2>>
                <<Rcomb3>> <<Rcomb4>> <<Rgap>> <<Rclad>>
                MIX 1 1 1 1 7 8 9
                MESHX 0.0 <<pitch>> MESHY 0.0 <<pitch>>
    ;
    ::: C2 := GEO: C1 MIX 2 2 2 2 7 8 9
    ;
    ::: C3 := GEO: C1 MIX 3 3 3 3 7 8 9
    ;
    ::: C4 := GEO: C1 MIX 4 4 4 4 7 8 9
    ;
    ::: C6 := GEO: C1 MIX 5 5 5 5 7 8 9
    ;
    ::: C7 := GEO: CARCEL 8
              RADIUS 0.0 <<RcombGd1>> <<RcombGd2>> <<RcombGd3>> <<RcombGd4>>
                          <<RcombGd5>> <<RcombGd6>> <<Rgap>> <<Rclad>> 
              MIX 6 6 6 6 6 6 7 8 9
              MESHX 0.0 <<pitch>> MESHY 0.0 <<pitch>>
    ;
    
;

MACRO := MAC: ::
     EDIT 2 NGRO 1 NMIX 9 NIFI 1
     READ INPUT

     MIX 1 (*24UOX*)
        TOTAL 4.41963434E-01    SCAT 1 1 3.98291796E-01
        NUSIGF 5.10632172E-02      CHI 1.0

      MIX 2 (*32UOX*)
        TOTAL 4.45725083E-01    SCAT 1 1 3.97643209E-01
        NUSIGF 6.02531061E-02      CHI 1.0

      MIX 3 (*42UOX*)
        TOTAL 4.48863983E-01    SCAT 1 1 3.97252828E-01
        NUSIGF 6.74651340E-02      CHI 1.0

      MIX 4 (*45UOX*)
        TOTAL 4.50370818E-01    SCAT 1 1 3.97226244E-01
        NUSIGF 7.04888850E-02      CHI 1.0

      MIX 5 (*50UOX*)
        TOTAL 4.48411316E-01    SCAT 1 1 3.97542506E-01
        NUSIGF 6.58325031E-02      CHI 1.0
      
      MIX 6 (*45Gd*)
        TOTAL 5.11014581E-01    SCAT 1 1 3.98968190E-01
        NUSIGF 3.49357314E-02      CHI 1.0
    
      MIX 7 (*GAP*)
        TOTAL 3.00460903E-04  SCAT 1 1 3.00460844E-04
      MIX 8 (*CLAD*)
        TOTAL 0.4029        SCAT 1 1 0.4000
      MIX 9 (*WATER*)
        TOTAL 0.3683        SCAT 1 1 0.3661
;

MACROG := MAC: ::
     EDIT 2 NGRO 1 NMIX 9 NIFI 1
     READ INPUT
     MIX 9 (*24UOX*)
        TOTAL 4.41963434E-01    SCAT 1 1 3.98291796E-01
        NUSIGF 5.10632172E-02      CHI 1.0

      MIX 8 (*32UOX*)
        TOTAL 4.45725083E-01    SCAT 1 1 3.97643209E-01
        NUSIGF 6.02531061E-02      CHI 1.0

      MIX 6 (*42UOX*)
        TOTAL 4.48863983E-01    SCAT 1 1 3.97252828E-01
        NUSIGF 6.74651340E-02      CHI 1.0

      MIX 7 (*45UOX*)
        TOTAL 4.50370818E-01    SCAT 1 1 3.97226244E-01
        NUSIGF 7.04888850E-02      CHI 1.0

      MIX 4 (*50UOX*)
        TOTAL 4.48411316E-01    SCAT 1 1 3.97542506E-01
        NUSIGF 6.58325031E-02      CHI 1.0
      
      MIX 5 (*45Gd*)
        TOTAL 5.11014581E-01    SCAT 1 1 3.98968190E-01
        NUSIGF 3.49357314E-02      CHI 1.0
    
      MIX 3 (*GAP*)
        TOTAL 3.00460903E-04  SCAT 1 1 3.00460844E-04
      MIX 2 (*CLAD*)
        TOTAL 0.4029        SCAT 1 1 0.4000
      MIX 1 (*WATER*)
        TOTAL 0.3683        SCAT 1 1 0.3661
;

*** TISO case treatment ***


g2s_tdt geo_tiso.ps := G2S: GEOISO :: ;

glow_tiso.ps := G2S: tdt_iso :: ;

TRKGEO TRKFILGEO := SALT: g2s_tdt ::
     EDIT 2
     TITLE "TISO CP SURFACIC AT10_3x3 CASE - GEO:"
     TISO EQW <<an2d>> <<densur>>
     NOIC
     ;

TRKGLOW TRKFILGLOW := SALT: tdt_iso ::
     EDIT 2
     TITLE "TISO CP SURFACIC AT10_3x3 CASE - GLOW"
     TISO EQW <<an2d>> <<densur>>
     NOIC
     ;

* FULL PIJ MATRIX RECONSTRUCTION
* GEO reference
PIJ := ASM: MACRO TRKGEO TRKFILGEO :: PIJ EDIT 2 ;
FLUX := FLU: PIJ MACRO TRKGEO :: TYPE K ;
GREP: FLUX :: GETVAL 'K-EFFECTIVE' 1 1 1 >>keffGEO<< ;
PIJ FLUX := DELETE: PIJ FLUX ;
ECHO "test AT10_3x3 GEO: / TISO with FULL PIJ reconstruction completed, keff=" keffGEO ;

* GLOW test
PIJ := ASM: MACROG TRKGLOW TRKFILGLOW :: PIJ EDIT 2 ;
FLUX := FLU: PIJ MACROG TRKGLOW :: TYPE K ;
GREP: FLUX :: GETVAL 'K-EFFECTIVE' 1 1 1 >>keffGLOW<< ;
ECHO "test AT10_3x3 GLOW / TISO with FULL PIJ reconstruction completed, keff=" keffGLOW ;
assertS FLUX :: 'K-EFFECTIVE' 1 <<keffGEO>> ;
PIJ FLUX := DELETE: PIJ FLUX ;

g2s_tdt TRKGEO TRKFILGEO TRKGLOW TRKFILGLOW 
    := DELETE: g2s_tdt TRKGEO TRKFILGEO TRKGLOW TRKFILGLOW ;


*** TSPC case treatment ***

g2s_tdt geo_tspc.ps := G2S: GEOSPC :: ;

glow_tspc.ps := G2S: tdt_spc :: ;

TRKGEO TRKFILGEO := SALT: g2s_tdt ::
     EDIT 2
     TITLE "TSPC CP SURFACIC AT10_3x3 CASE - GEO:"
     TSPC EQW2 <<an2d>> <<densur>>
     ;

TRKGLOW TRKFILGLOW := SALT: tdt_spc ::
     EDIT 2
     TITLE "TSPC CP SURFACIC AT10_3x3 CASE - GLOW"
     TSPC EQW2 <<an2d>> <<densur>>
     ;

* FULL PIJ MATRIX RECONSTRUCTION
* GEO reference
PIJ := ASM: MACRO TRKGEO TRKFILGEO :: PIJ EDIT 2 ;
FLUX := FLU: PIJ MACRO TRKGEO :: TYPE K ;
GREP: FLUX :: GETVAL 'K-EFFECTIVE' 1 1 1 >>keffGEO<< ;
PIJ FLUX := DELETE: PIJ FLUX ;
ECHO "test AT10_3x3 GEO: / TSPC with FULL PIJ reconstruction completed, keff=" keffGEO ;

* GLOW test
PIJ := ASM: MACROG TRKGLOW TRKFILGLOW :: PIJ EDIT 2 ;
FLUX := FLU: PIJ MACROG TRKGLOW :: TYPE K ;
GREP: FLUX :: GETVAL 'K-EFFECTIVE' 1 1 1 >>keffGLOW<< ;
ECHO "test AT10_3x3 GLOW / TSPC with FULL PIJ reconstruction completed, keff=" keffGLOW ;
assertS FLUX :: 'K-EFFECTIVE' 1 <<keffGEO>> ;
PIJ FLUX := DELETE: PIJ FLUX ;



g2s_tdt TRKGEO TRKFILGEO TRKGLOW TRKFILGLOW 
    := DELETE: g2s_tdt TRKGEO TRKFILGEO TRKGLOW TRKFILGLOW ;
