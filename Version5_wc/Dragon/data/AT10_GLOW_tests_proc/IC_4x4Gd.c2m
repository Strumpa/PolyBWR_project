*** AT10_4x4Gd case with CP/IC/MOC solution : 
* BWR 4x4 array based on ATRIUM-10 geometry, 
* import tdt_data generated by glow.
* Solve 1 group CP/IC/MOC problem to ensure proper material numbering treatment
* issue : GLOW/DRAGON material ordering match, but SALOME re-writes material ids using its internal system.
* Macroscpic cross sections from 24UOX, 32UOX, 42UOX, 45UOX, 48UOX, 50UOX and 45Gd AT10 pins, 
* self-shielded and solved in DRAGON : endfb8r1 - SHEM295
* Homogenized in fuel and condensed to 1 group.


MODULE GEO: MAC: G2S: SALT: MCCGT: ASM: FLU: GREP: DELETE: END: ;
PROCEDURE assertS ;
LINKED_LIST GEOISO GEOSPC MACRO MACROG TRACKIC TRACKCP TRACKMOC PIJ SYS FLUX ;

SEQ_BINARY TRACKFILIC TRACKFILCP TRACKFILMOC ;
SEQ_ASCII tdt_iso :: FILE 'AT10_4x4_cells_TISO.dat' ;

SEQ_ASCII tdtMACRO :: FILE 'AT10_4x4_TISO_MACRO.dat' ;
SEQ_ASCII glowMACRO.ps :: FILE 'AT10_4x4_GLOW_TISO_MACRO.ps' ;

SEQ_ASCII g2s_tdt g2s_tdt_IC ;
SEQ_ASCII geo_tiso.ps ;
SEQ_ASCII geoTisoIC.ps ;

REAL keffGEOIC keffGEOPIJ keffGEOCP  keffGEOMOC ;
REAL keffGLOWCP keffGLOWPIJ keffGLOWIC keffGLOWMOC ;
REAL keffGMCP keffGMPIJ keffGMIC keffGMMOC ; 
REAL delCPIC delMOCIC deltak ;

* ---
* TRACKING PARAMTERES
* ---
ECHO "BEGIN AT10_4x4Gd multicell CASE" ;

INTEGER an2d := 24 ;  ! ANGULAR QUADRATURE PARAMETER
REAL densur := 80.0 ; ! DENSITY OF INTEGRATION LINES CM^-1


REAL Rgap Rclad pitch := 0.4520 0.5140 1.295 ;
REAL Rcomb1 Rcomb2 Rcomb3 Rcomb4 := 0.313602 0.396678 0.43227 0.4435 ;
REAL RcombGd1 RcombGd2 RcombGd3 RcombGd4 RcombGd5 RcombGd6 
         := 0.19834 0.28049 0.34353 0.39668 0.43227 0.4435 ;

REAL 2_pitch 3_pitch 4_pitch ;
EVALUATE 2_pitch := 2.0 pitch * ;
EVALUATE 3_pitch := 3.0 pitch * ;
EVALUATE 4_pitch := 4.0 pitch * ;

GEOISO := GEO: :: CAR2D 4 4
    X- ALBE 1.0 X+ ALBE 1.0
    Y- ALBE 1.0 Y+ ALBE 1.0
    CELL
    C1 C2 C3 C5
    C2 C4 C7 C6
    C3 C7 C6 C6
    C5 C6 C6 C6 
    MESHX 0.0 <<pitch>> <<2_pitch>> <<3_pitch>> <<4_pitch>>
    MESHX 0.0 <<pitch>> <<2_pitch>> <<3_pitch>> <<4_pitch>>
    ::: C1 :=  GEO: CARCEL 6 
                RADIUS 0.0 <<Rcomb1>> <<Rcomb2>>
                <<Rcomb3>> <<Rcomb4>> <<Rgap>> <<Rclad>>
                MIX 1 1 1 1 8 9 10
                    MESHX 0.0 <<pitch>> MESHY 0.0 <<pitch>>
    ;
    ::: C2 := GEO: C1 MIX 2 2 2 2 8 9 10
    ;
    ::: C3 := GEO: C1 MIX 3 3 3 3 8 9 10
    ;
    ::: C4 := GEO: C1 MIX 4 4 4 4 8 9 10
    ;
    ::: C5 := GEO: C1 MIX 5 5 5 5 8 9 10
    ;
    ::: C6 := GEO: C1 MIX 6 6 6 6 8 9 10
    ;
    ::: C7 := GEO: CARCEL 8
              RADIUS 0.0 <<RcombGd1>> <<RcombGd2>> <<RcombGd3>> <<RcombGd4>>
                          <<RcombGd5>> <<RcombGd6>> <<Rgap>> <<Rclad>> 
              MIX 7 7 7 7 7 7 8 9 10
              MESHX 0.0 <<pitch>> MESHY 0.0 <<pitch>>
    ; 
;

MACRO := MAC: ::
    EDIT 2 NGRO 1 NMIX 10 NIFI 1
    READ INPUT

    MIX 1 (*24UOX*)
        TOTAL 4.43890840E-01    SCAT 1 1 3.99300873E-01
        NUSIGF 5.23696020E-02      CHI 1.0

    MIX 2 (*32UOX*)
        TOTAL 4.47801262E-01    SCAT 1 1 3.98593932E-01
        NUSIGF 6.20270856E-02      CHI 1.0

    MIX 3 (*42UOX*)
        TOTAL 4.50964361E-01    SCAT 1 1 3.97878230E-01
        NUSIGF 7.01253936E-02      CHI 1.0

    MIX 4 (*45UOX*)
        TOTAL 4.52631325E-01    SCAT 1 1 3.98060858E-01
        NUSIGF 7.29189515E-02      CHI 1.0

    MIX 5 (*48UOX*)
        TOTAL 4.53984380E-01    SCAT 1 1 3.97542506E-01
        NUSIGF 7.79897496E-02      CHI 1.0

    MIX 6 (*50UOX*)
        TOTAL 4.53644007E-01    SCAT 1 1 3.96933615E-01
        NUSIGF 7.77277201E-02      CHI 1.0
      
    MIX 7 (*45Gd*)
        TOTAL 5.22946239E-01    SCAT 1 1 3.99194658E-01
        NUSIGF 3.58763784E-02      CHI 1.0
    
    MIX 8 (*GAP*)
        TOTAL 3.12748462E-01  SCAT 1 1 3.10926110E-01
    MIX 9 (*CLAD*)
        TOTAL 2.88040959E-04        SCAT 1 1 2.88040930E-04
    MIX 10 (*WATER*)
        TOTAL 7.66823292E-01        SCAT 1 1 7.65839636E-01
;


MACROG := MAC: ::
    EDIT 2 NGRO 1 NMIX 10 NIFI 1
    READ INPUT
    MIX 10 (*24UOX*)
        TOTAL 4.43890840E-01    SCAT 1 1 3.99300873E-01
        NUSIGF 5.23696020E-02      CHI 1.0

    MIX 9 (*32UOX*)
        TOTAL 4.47801262E-01    SCAT 1 1 3.98593932E-01
        NUSIGF 6.20270856E-02      CHI 1.0

    MIX 7 (*42UOX*)
        TOTAL 4.50964361E-01    SCAT 1 1 3.97878230E-01
        NUSIGF 7.01253936E-02      CHI 1.0

    MIX 8 (*45UOX*)
        TOTAL 4.52631325E-01    SCAT 1 1 3.98060858E-01
        NUSIGF 7.29189515E-02      CHI 1.0

    MIX 5 (*48UOX*)
        TOTAL 4.53984380E-01    SCAT 1 1 3.97542506E-01
        NUSIGF 7.79897496E-02      CHI 1.0

    MIX 4 (*50UOX*)
        TOTAL 4.53644007E-01    SCAT 1 1 3.96933615E-01
        NUSIGF 7.77277201E-02      CHI 1.0

    MIX 6 (*45Gd*)
        TOTAL 5.22946239E-01    SCAT 1 1 3.99194658E-01
        NUSIGF 3.58763784E-02      CHI 1.0
    
    MIX 3 (*GAP*)
        TOTAL 3.12748462E-01  SCAT 1 1 3.10926110E-01
    MIX 2 (*CLAD*)
        TOTAL 2.88040959E-04        SCAT 1 1 2.88040930E-04
    MIX 1 (*WATER*)
        TOTAL 7.66823292E-01        SCAT 1 1 7.65839636E-01
;


*** TISO case treatment ***


g2s_tdt geo_tiso.ps := G2S: GEOISO :: ;
g2s_tdt_IC geoTisoIC.ps := G2S: GEOISO :: MACRO ;

!glow_tiso.ps := G2S: tdt_iso :: ;

*** TREAT ALL GEO: CASES FIRST ***

TRACKIC TRACKFILIC := SALT: g2s_tdt_IC ::
     EDIT 2
     TITLE "MULTICELL SURFACIC AT10_4x4Gd GEO CASE"
     TISO <<an2d>> <<densur>>
     IC EPSJ 1.0E-6
     ;

TRACKCP TRACKFILCP := SALT: g2s_tdt ::
     EDIT 2
     TITLE "TISO CP SURFACIC AT10_4x4Gd GEO CASE"
     TISO <<an2d>> <<densur>>
     ;

TRACKMOC TRACKFILMOC := SALT: g2s_tdt ::
     EDIT 2
     TITLE "TISO MOC SURFACIC AT10_4x4Gd GEO CASE"
     TISO EQW <<an2d>> <<densur>>
     ;

* MOC tracking
TRACKMOC := MCCGT: TRACKMOC TRACKFILMOC ::
  EDIT 0
;


* ---
* Flux calculation
* ---

* FULL PIJ MATRIX RECONSTRUCTION
PIJ := ASM: MACRO TRACKIC TRACKFILIC :: PIJ EDIT 2 ;
FLUX := FLU: PIJ MACRO TRACKIC :: TYPE K ;
GREP: FLUX :: GETVAL 'K-EFFECTIVE' 1 1 1 >>keffGEOPIJ<< ;
PIJ FLUX := DELETE: PIJ FLUX ;
ECHO "test multicell AT10_4x4Gd FULL PIJ reconstruction completed, keff=" keffGEOPIJ ;

* FLUX-CURRENT ITERATION WITHOUT FULL PIJ MATRIX RECONSTRUCTION
PIJ := ASM: MACRO TRACKIC TRACKFILIC :: ARM EDIT 2 ;
FLUX := FLU: PIJ MACRO TRACKIC :: EDIT 4 TYPE K ;
GREP: FLUX :: GETVAL 'K-EFFECTIVE' 1 1 1 >>keffGEOIC<< ;
assertS FLUX :: 'K-EFFECTIVE' 1 <<keffGEOPIJ>> ;
PIJ FLUX := DELETE: PIJ FLUX ;
ECHO "test multicell AT10_4x4Gd FLUX-CURRENT iteration completed, keff=" keffGEOIC ;

PIJ := ASM: MACRO TRACKCP TRACKFILCP :: PIJ EDIT 2 ;
FLUX := FLU: PIJ MACRO TRACKCP :: TYPE K ;
GREP: FLUX :: GETVAL 'K-EFFECTIVE' 1 1 1 >>keffGEOCP<< ;
PIJ FLUX := DELETE: PIJ FLUX ;
ECHO "test multicell AT10_4x4Gd CP completed, keff=" keffGEOCP ;

EVALUATE delCPIC := keffGEOCP keffGEOIC - 1E+5 * ;
ECHO "With GEO: difference between CP and IC keff = " delCPIC ;

SYS := ASM: MACRO TRACKMOC TRACKFILMOC :: ARM EDIT 2 ; 
FLUX := FLU: SYS MACRO TRACKMOC TRACKFILMOC :: EDIT 1 TYPE K ;
GREP: FLUX :: GETVAL 'K-EFFECTIVE' 1 1 1 >>keffGEOMOC<< ;
ECHO "test multicell AT10_4x4Gd MOC completed, keff=" keffGEOMOC ;

EVALUATE delMOCIC := keffGEOMOC keffGEOIC - 1E+5 * ;
ECHO "With GEO: difference between MOC and IC keff = " delMOCIC ;


FLUX SYS := DELETE: FLUX SYS ;
TRACKIC TRACKFILIC := DELETE: TRACKIC TRACKFILIC ;
TRACKCP TRACKFILCP := DELETE: TRACKCP TRACKFILCP ;
TRACKMOC TRACKFILMOC := DELETE: TRACKMOC TRACKFILMOC ;

*----
* Consider cases with MACRO defined in tdt for IC tracking
*---


TRACKIC TRACKFILIC := SALT: tdtMACRO ::
     EDIT 2
     TITLE "MULTICELL SURFACIC AT10_4x4 GLOW+MACRO"
     TISO <<an2d>> <<densur>>
     IC EPSJ 1.0E-6
     ;

TRACKCP TRACKFILCP := SALT: tdtMACRO ::
     EDIT 2
     TITLE "CP AT10_4x4 GLOW+MACRO"
     TISO <<an2d>> <<densur>>
     NOIC
     ;
TRACKMOC TRACKFILMOC := SALT: tdtMACRO ::
     EDIT 2
     TITLE "MOC AT10_4x4 GLOW+MACRO"
     TISO EQW <<an2d>> <<densur>>
     NOIC
     ;

* MOC tracking
TRACKMOC := MCCGT: TRACKMOC TRACKFILMOC ::
  EDIT 0
;    
* ---
* Flux calculation
* ---

* FULL PIJ MATRIX RECONSTRUCTION
PIJ := ASM: MACROG TRACKIC TRACKFILIC :: PIJ EDIT 2 ;
FLUX := FLU: PIJ MACROG TRACKIC :: TYPE K ;
GREP: FLUX :: GETVAL 'K-EFFECTIVE' 1 1 1 >>keffGMPIJ<< ;
PIJ FLUX := DELETE: PIJ FLUX ;
ECHO "test multicell AT10_4x4Gd (GLOW+MACRO) FULL PIJ reconstruction completed, keff=" keffGMPIJ ;

* FLUX-CURRENT ITERATION WITHOUT FULL PIJ MATRIX RECONSTRUCTION
PIJ := ASM: MACROG TRACKIC TRACKFILIC :: ARM EDIT 2 ;
FLUX := FLU: PIJ MACROG TRACKIC :: EDIT 4 TYPE K ;
GREP: FLUX :: GETVAL 'K-EFFECTIVE' 1 1 1 >>keffGMIC<< ;
assertS FLUX :: 'K-EFFECTIVE' 1 <<keffGMPIJ>> ;
PIJ FLUX := DELETE: PIJ FLUX ;
ECHO "test multicell AT10_4x4Gd (GLOW+MACRO) FLUX-CURRENT iteration completed, keff=" keffGMIC ;

* CP MATRIX RECONSTRUCTION FROM NOIC TRACKING
PIJ := ASM: MACROG TRACKCP TRACKFILCP :: PIJ EDIT 2 ;
FLUX := FLU: PIJ MACROG TRACKCP :: EDIT 4 TYPE K ;
GREP: FLUX :: GETVAL 'K-EFFECTIVE' 1 1 1 >>keffGMCP<< ;
PIJ FLUX := DELETE: PIJ FLUX ;
ECHO "test multicell AT10_4x4Gd (GLOW+MACRO) CP completed, keff=" keffGMCP ;

EVALUATE delCPIC := keffGMCP keffGMIC - 1E+5 * ;
ECHO "With GLOW+MACRO: difference between CP and IC keff = " delCPIC ;

* MOC FROM NOIC TRACKING
PIJ := ASM: MACROG TRACKMOC TRACKFILMOC :: ARM EDIT 2 ;
FLUX := FLU: PIJ MACROG TRACKMOC TRACKFILMOC :: EDIT 4 TYPE K ;
GREP: FLUX :: GETVAL 'K-EFFECTIVE' 1 1 1 >>keffGMMOC<< ;
PIJ FLUX := DELETE: PIJ FLUX ;
ECHO "test multicell AT10_4x4Gd (GLOW+MACRO) MOC completed, keff=" keffGMMOC ;

EVALUATE delCPIC := keffGMMOC keffGMIC - 1E+5 * ;
ECHO "With GLOW+MACRO: difference between MOC and IC keff = " delCPIC ;

TRACKIC TRACKFILIC := DELETE: TRACKIC TRACKFILIC ;
TRACKCP TRACKFILCP := DELETE: TRACKCP TRACKFILCP ;
TRACKMOC TRACKFILMOC := DELETE: TRACKMOC TRACKFILMOC ;


! Assert differences with GEO results 
ECHO "Test IC from IC tracking : GLOW+MACRO - GEO" ;
ECHO "keffGMIC = " keffGMIC ;
ECHO "keffGEOIC = " keffGEOIC ;
EVALUATE deltak := keffGMIC keffGEOIC - 1E+5 * ;
ECHO "delta k IC (GLOW+MACRO - GEO) = " deltak " pcm" ;

ECHO "Test PIJ from IC tracking : GLOW+MACRO - GEO" ;
ECHO "keffGMPIJ = " keffGMPIJ ;
ECHO "keffGEOPIJ = " keffGEOPIJ ;
EVALUATE deltak := keffGMPIJ keffGEOPIJ - 1E+5 * ;
ECHO "delta k PIJ (GLOW+MACRO - GEO) = " deltak " pcm" ;

ECHO "Test CP from NOIC tracking : GLOW+MACRO - GEO" ;
ECHO "keffGMCP = " keffGMCP ;
ECHO "keffGEOCP = " keffGEOCP ;
EVALUATE deltak := keffGMCP keffGEOCP - 1E+5 * ;
ECHO "delta k CP (GLOW+MACRO - GEO) = " deltak " pcm" ;

ECHO "Test MOC from NOIC tracking : GLOW+MACRO - GEO" ;
ECHO "keffGMMOC = " keffGMMOC ;
ECHO "keffGEOMOC = " keffGEOMOC ;
EVALUATE deltak := keffGMMOC keffGEOMOC - 1E+5 * ;
ECHO "delta k MOC (GLOW+MACRO - GEO) = " deltak " pcm" ;



END: ;
