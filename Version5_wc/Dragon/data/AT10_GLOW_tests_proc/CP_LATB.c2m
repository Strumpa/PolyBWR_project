*** AT10_LAT_MODEBOX case with CP solution : 
* BWR AT10_LAT_MODEBOX array based on ATRIUM-10 geometry, 
* Generate geometry with GEO:, import tdt_data generated by glow.
* Solve 1 group CP problem to ensure proper material numbering treatment
* issue : GLOW/DRAGON material ordering match, but SALOME re-writes material ids using its internal system.
* Macroscpic cross sections from AT10 assembly pins, 
* self-shielded and solved in DRAGON : endfb8r1 - SHEM295
* Homogenized in fuel and condensed to 1 group.



MODULE GEO: MAC: G2S: SALT: ASM: FLU: GREP: DELETE: END: ;
PROCEDURE assertS ;
LINKED_LIST GEOISO GEOSPC MACRO MACROG TRKGEO TRKGLOW PIJ SYS FLUX ;

SEQ_BINARY TRKFILGEO TRKFILGLOW ;
SEQ_ASCII tdt_spc :: FILE 'AT10_lattice_MODEBOX_TSPC.dat' ;
SEQ_ASCII tdt_iso :: FILE 'AT10_lattice_MODEBOX_TISO.dat' ;
SEQ_ASCII glow_tiso.ps :: FILE 'AT10_LAT_MODEBOX_TISO_glow.ps' ;
SEQ_ASCII glow_tspc.ps :: FILE 'AT10_LAT_MODEBOX_TSPC_glow.ps' ;
SEQ_ASCII g2s_tdt ;
SEQ_ASCII geo_tiso.ps :: FILE 'AT10_LAT_MODEBOX_TISO.ps' ;
SEQ_ASCII geo_tspc.ps :: FILE 'AT10_LAT_MODEBOX_TSPC.ps' ;

REAL keffGEO keffGLOW ;
* ---
* TRACKING PARAMTERES
* ---

ECHO "BEGIN AT10_LAT_MODEBOX CP CASE" ;
INTEGER an2d := 24 ;  ! ANGULAR QUADRATURE PARAMETER, (24, 100.0) caused error in SALT: + TSPC (SALTLC)   
REAL densur := 100.0 ; ! DENSITY OF INTEGRATION LINES CM^-1


* --------------------------------
*    DIMENSIONS DEFINITION
* --------------------------------
!Assembly scale data

REAL Pitch_A W_gap := 15.24 0.75 ; ! Assembly pitch and water gap thickness
REAL Box_out Box_in Box_thi := 13.74 13.4 0.17 ; ! Outer box outer, inner sides and thickness
REAL Chan_out Chan_in Chan_thi := 3.5 3.34 0.08 ; ! Channel box outer, inner sides and thickness


! UOX Pincell scale data

REAL Rgap Rclad pitch := 0.4520 0.5140 1.29500 ;
REAL Rcomb1 Rcomb2 Rcomb3 Rcomb4 ;
EVALUATE Rcomb4 := 0.4435 ;
EVALUATE Rcomb1 := 0.313602 ;
EVALUATE Rcomb2 := 0.396678 ; 
EVALUATE Rcomb3 := 0.43227 ;
REAL RCool := 0.58075 ; ! radius half way between outer clad and pincell side.

REAL RcombGd1 RcombGd2 RcombGd3 RcombGd4 RcombGd5 RcombGd6 ;
EVALUATE RcombGd1 RcombGd2 RcombGd3 RcombGd4 RcombGd5 RcombGd6 
        := 0.19834 0.28049 0.34353 0.39668 0.43227 0.4435 ;

! Evaluating dependencies

! Multiples of cell pitch : used to define 2nd level geometry mesh 
REAL 2_pitch 3_pitch 4_pitch 5_pitch 6_pitch 7_pitch 8_pitch 9_pitch 10_pitch ;
EVALUATE 2_pitch := pitch 2.0 * ;
EVALUATE 3_pitch := pitch 3.0 * ;
EVALUATE 4_pitch := pitch 4.0 * ;
EVALUATE 5_pitch := pitch 5.0 * ;
EVALUATE 6_pitch := pitch 6.0 * ;
EVALUATE 7_pitch := pitch 7.0 * ;
EVALUATE 8_pitch := pitch 8.0 * ;
EVALUATE 9_pitch := pitch 9.0 * ;
EVALUATE 10_pitch := pitch 10.0 * ;

! LLat = total lattice length
REAL LLat := pitch 10.0 * ;


! X1 = Water gap + outer box thickness +extra water : in box water not a multiple of pitch
REAL XtraCool ;
EVALUATE XtraCool := Box_in 10.0 pitch * - 2.0 / ;

!Meshing points for macro geometry :
REAL X1 X2 X3 X4  ;
! X2 = X1 + 4*pitch
! X3 = X2 + 3*pitch
! X4 = X3 + 3*pitch
EVALUATE X1 := W_gap Box_thi + XtraCool + ;
EVALUATE X2 := 4.0 pitch * X1 + ;
EVALUATE X3 := 3.0 pitch * X2 + ;
EVALUATE X4 := 3.0 pitch * X3 + ;  

REAL Y1 Y2 Y3 Y4 ;
EVALUATE Y1 := X1 ;
EVALUATE Y2 := 4.0 pitch * Y1 + ;
EVALUATE Y3 := 3.0 pitch * Y2 + ;
EVALUATE Y4 := 3.0 pitch * Y3 + ;

REAL L1 L2 L3 ;
EVALUATE L1 := X2 X1 - ;
EVALUATE L2 := X3 X2 - ;
EVALUATE L3 := X4 X3 - ;

REAL H1 H2 H3 ;
EVALUATE H1 := Y2 Y1 - ;
EVALUATE H2 := Y3 Y2 - ;
EVALUATE H3 := Y4 Y3 - ;

REAL x_step y_step ; ! allows for submeshing of in-assembly box water
! in-assembly box water included in geom level with outer assembly water and box
EVALUATE x_step := W_gap Box_thi + ;
EVALUATE y_step := XtraCool Box_thi + ;

! inner CHaNneL box related parameters, used to define 2nd level CHNL Geometry
! CHNL = channel box + surroudning water to fit with pin CARCEL edges 
REAL XCHNL1 XCHNL2 XCHNL3 XCHNL4 XCHNL5 ;
EVALUATE XCHNL1 := L2 Chan_out - 2.0 / ;
EVALUATE XCHNL2 := XCHNL1 Chan_thi + ;
EVALUATE XCHNL3 := XCHNL2 Chan_in + ;
EVALUATE XCHNL4 := XCHNL3 Chan_thi + ;
EVALUATE XCHNL5 := L2 ;

REAL X1sym := pitch XCHNL2 - ;
REAL X2sym := pitch XCHNL1 - ;

GEOSPC := GEO: :: CAR2D 10 10 
    X- REFL X+ REFL
    Y- REFL Y+ REFL
    CELL
    C1 C2 C3 C5 C6 C5 C4 C3 C2 C1
    C2 C4 C7 C6 C7 C6 C6 C7 C4 C2
    C3 C7 C6 C6 C6 C7 C6 C6 C7 C3
    C5 C6 C6 C6 C6 C6 C7 C6 C5 C4
    C6 C7 C6 C6 W1 WB W2 C4 C6 C4
    C5 C6 C7 C6 WL W0 WR C3 C7 C4
    C4 C6 C6 C7 W3 WT W4 C4 C4 C4
    C3 C7 C6 C6 C4 C3 C4 C4 C8 C3
    C2 C4 C7 C5 C6 C7 C4 C8 C4 C2
    C1 C2 C3 C4 C4 C4 C4 C3 C2 C1
    MESHX 0.0 <<pitch>> <<2_pitch>> <<3_pitch>> <<4_pitch>> <<5_pitch>> 
    <<6_pitch>> <<7_pitch>> <<8_pitch>> <<9_pitch>> <<10_pitch>>
    
    MESHY 0.0 <<pitch>> <<2_pitch>> <<3_pitch>> <<4_pitch>> <<5_pitch>> 
    <<6_pitch>> <<7_pitch>> <<8_pitch>> <<9_pitch>> <<10_pitch>>
    ::: C1 :=  GEO: CARCEL 6 
                RADIUS 0.0 <<Rcomb1>> <<Rcomb2>>
                <<Rcomb3>> <<Rcomb4>> <<Rgap>> <<Rclad>>
                MIX 1 1 1 1 9 10 11
                    MESHX 0.0 <<pitch>> MESHY 0.0 <<pitch>>
    ;
    ::: C2 := GEO: C1 MIX 2 2 2 2 9 10 11
    ;
    ::: C3 := GEO: C1 MIX 3 3 3 3 9 10 11
    ;
    ::: C4 := GEO: C1 MIX 4 4 4 4 9 10 11
    ;
    ::: C5 := GEO: C1 MIX 5 5 5 5 9 10 11
    ;
    ::: C6 := GEO: C1 MIX 6 6 6 6 9 10 11
    ;
    ::: C7 := GEO: CARCEL 8
              RADIUS 0.0 <<RcombGd1>> <<RcombGd2>> <<RcombGd3>> <<RcombGd4>>
                          <<RcombGd5>> <<RcombGd6>> <<Rgap>> <<Rclad>> 
              MIX 7 7 7 7 7 7 9 10 11
              MESHX 0.0 <<pitch>> MESHY 0.0 <<pitch>>
    ;
    ::: C8 := GEO: C7 MIX 8 8 8 8 8 8 9 10 11
    ;
    ::: W1 := GEO: CAR2D 3 3
       MESHX 0.0 <<XCHNL1>> <<XCHNL2>> <<pitch>>
       MESHY 0.0 <<XCHNL1>> <<XCHNL2>> <<pitch>>
       MIX 11 11 11
           11 12 12
           11 12 13 ;
    ::: WB := GEO: CAR2D 1 3
       MESHX 0.0 <<pitch>>
       MESHY 0.0 <<XCHNL1>> <<XCHNL2>> <<pitch>>
       MIX 11 12 13 ;
   ::: W2 := GEO: CAR2D 3 3
       MESHX 0.0 <<X1sym>> <<X2sym>> <<pitch>>
       MESHY 0.0 <<XCHNL1>> <<XCHNL2>> <<pitch>>
       MIX 11 11 11
           12 12 11
           13 12 11 ;
   ::: WL := GEO: CAR2D 3 1
       MESHX 0.0 <<XCHNL1>> <<XCHNL2>> <<pitch>>
       MESHY 0.0 <<pitch>>
       MIX 11 12 13 ;
   ::: W0 := GEO: CAR2D 1 1
       MESHX 0.0 <<pitch>>
       MESHY 0.0 <<pitch>>
        MIX 13 ;
   ::: WR := GEO: CAR2D 3 1
       MESHX 0.0 <<X1sym>> <<X2sym>> <<pitch>>
       MESHY 0.0 <<pitch>>
       MIX 13 12 11 ;
   ::: W3 := GEO: CAR2D 3 3
       MESHX 0.0 <<XCHNL1>> <<XCHNL2>> <<pitch>>
       MESHY 0.0 <<X1sym>> <<X2sym>> <<pitch>>
       MIX 11 12 13
           11 12 12
           11 11 11 ;
   ::: WT := GEO: CAR2D 1 3
       MESHX 0.0 <<pitch>>
       MESHY 0.0 <<X1sym>> <<X2sym>> <<pitch>>
       MIX 13 12 11 ;
   ::: W4 := GEO: CAR2D 3 3
        MESHX 0.0 <<X1sym>> <<X2sym>> <<pitch>>
        MESHY 0.0 <<X1sym>> <<X2sym>> <<pitch>>
        MIX 13 12 11
            12 12 11
            11 11 11 ;

;

GEOISO := GEO: :: CAR2D 10 10 
    X- ALBE 1.0 X+ ALBE 1.0
    Y- ALBE 1.0 Y+ ALBE 1.0
    CELL
    C1 C2 C3 C5 C6 C5 C4 C3 C2 C1
    C2 C4 C7 C6 C7 C6 C6 C7 C4 C2
    C3 C7 C6 C6 C6 C7 C6 C6 C7 C3
    C5 C6 C6 C6 C6 C6 C7 C6 C5 C4
    C6 C7 C6 C6 W1 WB W2 C4 C6 C4
    C5 C6 C7 C6 WL W0 WR C3 C7 C4
    C4 C6 C6 C7 W3 WT W4 C4 C4 C4
    C3 C7 C6 C6 C4 C3 C4 C4 C8 C3
    C2 C4 C7 C5 C6 C7 C4 C8 C4 C2
    C1 C2 C3 C4 C4 C4 C4 C3 C2 C1
    MESHX 0.0 <<pitch>> <<2_pitch>> <<3_pitch>> <<4_pitch>> <<5_pitch>> 
    <<6_pitch>> <<7_pitch>> <<8_pitch>> <<9_pitch>> <<10_pitch>>
    
    MESHY 0.0 <<pitch>> <<2_pitch>> <<3_pitch>> <<4_pitch>> <<5_pitch>> 
    <<6_pitch>> <<7_pitch>> <<8_pitch>> <<9_pitch>> <<10_pitch>>
    ::: C1 :=  GEO: CARCEL 6 
                RADIUS 0.0 <<Rcomb1>> <<Rcomb2>>
                <<Rcomb3>> <<Rcomb4>> <<Rgap>> <<Rclad>>
                MIX 1 1 1 1 9 10 11
                    MESHX 0.0 <<pitch>> MESHY 0.0 <<pitch>>
    ;
    ::: C2 := GEO: C1 MIX 2 2 2 2 9 10 11
    ;
    ::: C3 := GEO: C1 MIX 3 3 3 3 9 10 11
    ;
    ::: C4 := GEO: C1 MIX 4 4 4 4 9 10 11
    ;
    ::: C5 := GEO: C1 MIX 5 5 5 5 9 10 11
    ;
    ::: C6 := GEO: C1 MIX 6 6 6 6 9 10 11
    ;
    ::: C7 := GEO: CARCEL 8
              RADIUS 0.0 <<RcombGd1>> <<RcombGd2>> <<RcombGd3>> <<RcombGd4>>
                          <<RcombGd5>> <<RcombGd6>> <<Rgap>> <<Rclad>> 
              MIX 7 7 7 7 7 7 9 10 11
              MESHX 0.0 <<pitch>> MESHY 0.0 <<pitch>>
    ;
    ::: C8 := GEO: C7 MIX 8 8 8 8 8 8 9 10 11
    ;
    ::: W1 := GEO: CAR2D 3 3
       MESHX 0.0 <<XCHNL1>> <<XCHNL2>> <<pitch>>
       MESHY 0.0 <<XCHNL1>> <<XCHNL2>> <<pitch>>
       MIX 11 11 11
           11 12 12
           11 12 13 ;
    ::: WB := GEO: CAR2D 1 3
       MESHX 0.0 <<pitch>>
       MESHY 0.0 <<XCHNL1>> <<XCHNL2>> <<pitch>>
       MIX 11 12 13 ;
   ::: W2 := GEO: CAR2D 3 3
       MESHX 0.0 <<X1sym>> <<X2sym>> <<pitch>>
       MESHY 0.0 <<XCHNL1>> <<XCHNL2>> <<pitch>>
       MIX 11 11 11
           12 12 11
           13 12 11 ;
   ::: WL := GEO: CAR2D 3 1
       MESHX 0.0 <<XCHNL1>> <<XCHNL2>> <<pitch>>
       MESHY 0.0 <<pitch>>
       MIX 11 12 13 ;
   ::: W0 := GEO: CAR2D 1 1
       MESHX 0.0 <<pitch>>
       MESHY 0.0 <<pitch>>
        MIX 13 ;
   ::: WR := GEO: CAR2D 3 1
       MESHX 0.0 <<X1sym>> <<X2sym>> <<pitch>>
       MESHY 0.0 <<pitch>>
       MIX 13 12 11 ;
   ::: W3 := GEO: CAR2D 3 3
       MESHX 0.0 <<XCHNL1>> <<XCHNL2>> <<pitch>>
       MESHY 0.0 <<X1sym>> <<X2sym>> <<pitch>>
       MIX 11 12 13
           11 12 12
           11 11 11 ;
   ::: WT := GEO: CAR2D 1 3
       MESHX 0.0 <<pitch>>
       MESHY 0.0 <<X1sym>> <<X2sym>> <<pitch>>
       MIX 13 12 11 ;
   ::: W4 := GEO: CAR2D 3 3
        MESHX 0.0 <<X1sym>> <<X2sym>> <<pitch>>
        MESHY 0.0 <<X1sym>> <<X2sym>> <<pitch>>
        MIX 13 12 11
            12 12 11
            11 11 11 ;

;

MACRO := MAC: ::
     EDIT 2 NGRO 1 NMIX 13 NIFI 1
     READ INPUT

     MIX 1 (*24UOX*)
        TOTAL 4.43890840E-01    SCAT 1 1 3.99300873E-01
        NUSIGF 5.23696020E-02      CHI 1.0

      MIX 2 (*32UOX*)
        TOTAL 4.47801262E-01    SCAT 1 1 3.98593932E-01
        NUSIGF 6.20270856E-02      CHI 1.0

      MIX 3 (*42UOX*)
        TOTAL 4.50964361E-01    SCAT 1 1 3.97878230E-01
        NUSIGF 7.01253936E-02      CHI 1.0

      MIX 4 (*45UOX*)
        TOTAL 4.52631325E-01    SCAT 1 1 3.98060858E-01
        NUSIGF 7.29189515E-02      CHI 1.0

      MIX 5 (*48UOX*)
        TOTAL 4.53984380E-01    SCAT 1 1 3.97542506E-01
        NUSIGF 7.79897496E-02      CHI 1.0

      MIX 6 (*50UOX*)
        TOTAL 4.53644007E-01    SCAT 1 1 3.96933615E-01
        NUSIGF 7.77277201E-02      CHI 1.0
      
      MIX 7 (*45Gd*)
        TOTAL 5.22946239E-01    SCAT 1 1 3.99194658E-01
        NUSIGF 3.58763784E-02      CHI 1.0

      MIX 8 (*42Gd*)
        TOTAL 5.12946239E-01    SCAT 1 1 3.99194658E-01
        NUSIGF 3.48763784E-02      CHI 1.0
    
      MIX 9 (*GAP*)
        TOTAL 3.12748462E-01  SCAT 1 1 3.10926110E-01
      MIX 10 (*CLAD*)
        TOTAL 2.88040959E-04        SCAT 1 1 2.88040930E-04
      MIX 11 (*COOLANT*)
        TOTAL 7.66823292E-01        SCAT 1 1 7.65839636E-01
      MIX 12 (*MODERATOR*)
        TOTAL 7.66823292E-01        SCAT 1 1 7.65839636E-01
      MIX 13 (*MODERATOR BOX*)
        TOTAL 2.88040959E-04        SCAT 1 1 2.88040930E-04
;

!#  1 - MODERATOR
!#  2 - MODERATOR_BOX
!#  3 - COOLANT
!#  4 - CLAD
!#  5 - GAP
!#  6 - 24UOX
!#  7 - 32UOX
!#  8 - 42UOX
!#  9 - 45UOX
!# 10 - 42Gd
!# 11 - 45Gd
!# 12 - 50UOX
!# 13 - 48UOX

MACROG := MAC: ::
     EDIT 2 NGRO 1 NMIX 13 NIFI 1
     READ INPUT
     MIX 6 (*24UOX*)
        TOTAL 4.43890840E-01    SCAT 1 1 3.99300873E-01
        NUSIGF 5.23696020E-02      CHI 1.0

      MIX 7 (*32UOX*)
        TOTAL 4.47801262E-01    SCAT 1 1 3.98593932E-01
        NUSIGF 6.20270856E-02      CHI 1.0

      MIX 8 (*42UOX*)
        TOTAL 4.50964361E-01    SCAT 1 1 3.97878230E-01
        NUSIGF 7.01253936E-02      CHI 1.0

      MIX 9 (*45UOX*)
        TOTAL 4.52631325E-01    SCAT 1 1 3.98060858E-01
        NUSIGF 7.29189515E-02      CHI 1.0

      MIX 13 (*48UOX*)
        TOTAL 4.53984380E-01    SCAT 1 1 3.97542506E-01
        NUSIGF 7.79897496E-02      CHI 1.0

      MIX 12 (*50UOX*)
        TOTAL 4.53644007E-01    SCAT 1 1 3.96933615E-01
        NUSIGF 7.77277201E-02      CHI 1.0
      
      MIX 11 (*45Gd*)
        TOTAL 5.22946239E-01    SCAT 1 1 3.99194658E-01
        NUSIGF 3.58763784E-02      CHI 1.0
      
      MIX 10 (*42Gd*)
        TOTAL 5.12946239E-01    SCAT 1 1 3.99194658E-01
        NUSIGF 3.48763784E-02      CHI 1.0
    
      MIX 5 (*GAP*)
        TOTAL 3.12748462E-01  SCAT 1 1 3.10926110E-01
      MIX 4 (*CLAD*)
        TOTAL 2.88040959E-04        SCAT 1 1 2.88040930E-04
      MIX 3 (*COOLANT*)
        TOTAL 7.66823292E-01        SCAT 1 1 7.65839636E-01
      MIX 1 (*MODERATOR*)
        TOTAL 7.66823292E-01        SCAT 1 1 7.65839636E-01
      MIX 2 (*MODERATOR BOX*)
        TOTAL 2.88040959E-04        SCAT 1 1 2.88040930E-04
;

*** TISO case treatment ***


g2s_tdt geo_tiso.ps := G2S: GEOISO :: ;

glow_tiso.ps := G2S: tdt_iso :: ;

TRKGEO TRKFILGEO := SALT: g2s_tdt ::
     EDIT 2
     TITLE "TISO CP SURFACIC AT10_LAT_MODEBOX CASE - GEO:"
     TISO EQW <<an2d>> <<densur>>
     NOIC
     ;

TRKGLOW TRKFILGLOW := SALT: tdt_iso ::
     EDIT 2
     TITLE "TISO CP SURFACIC AT10_LAT_MODEBOX CASE - GLOW"
     TISO EQW <<an2d>> <<densur>>
     NOIC
     ;

* FULL PIJ MATRIX RECONSTRUCTION
* GEO reference
PIJ := ASM: MACRO TRKGEO TRKFILGEO :: PIJ EDIT 2 ;
FLUX := FLU: PIJ MACRO TRKGEO :: TYPE K ;
GREP: FLUX :: GETVAL 'K-EFFECTIVE' 1 1 1 >>keffGEO<< ;
PIJ FLUX := DELETE: PIJ FLUX ;
ECHO "test AT10_LAT_MODEBOX GEO: / TISO with FULL PIJ reconstruction completed, keff=" keffGEO ;

* GLOW test
PIJ := ASM: MACROG TRKGLOW TRKFILGLOW :: PIJ EDIT 2 ;
FLUX := FLU: PIJ MACROG TRKGLOW :: TYPE K ;
GREP: FLUX :: GETVAL 'K-EFFECTIVE' 1 1 1 >>keffGLOW<< ;
ECHO "test AT10_LAT_MODEBOX GLOW / TISO with FULL PIJ reconstruction completed, keff=" keffGLOW ;
assertS FLUX :: 'K-EFFECTIVE' 1 <<keffGEO>> ;
PIJ FLUX := DELETE: PIJ FLUX ;

g2s_tdt TRKGEO TRKFILGEO TRKGLOW TRKFILGLOW 
    := DELETE: g2s_tdt TRKGEO TRKFILGEO TRKGLOW TRKFILGLOW ;


*** TSPC case treatment ***

g2s_tdt geo_tspc.ps := G2S: GEOSPC :: ;

glow_tspc.ps := G2S: tdt_spc :: ;

TRKGEO TRKFILGEO := SALT: g2s_tdt ::
     EDIT 2
     TITLE "TSPC CP SURFACIC AT10_LAT_MODEBOX CASE - GEO:"
     TSPC EQW2 <<an2d>> <<densur>>
     ;

TRKGLOW TRKFILGLOW := SALT: tdt_spc ::
     EDIT 2
     TITLE "TSPC CP SURFACIC AT10_LAT_MODEBOX CASE - GLOW"
     TSPC EQW2 <<an2d>> <<densur>>
     ;

* FULL PIJ MATRIX RECONSTRUCTION
* GEO reference
PIJ := ASM: MACRO TRKGEO TRKFILGEO :: PIJ EDIT 2 ;
FLUX := FLU: PIJ MACRO TRKGEO :: TYPE K ;
GREP: FLUX :: GETVAL 'K-EFFECTIVE' 1 1 1 >>keffGEO<< ;
PIJ FLUX := DELETE: PIJ FLUX ;
ECHO "test AT10_LAT_MODEBOX GEO: / TSPC with FULL PIJ reconstruction completed, keff=" keffGEO ;

* GLOW test
PIJ := ASM: MACROG TRKGLOW TRKFILGLOW :: PIJ EDIT 2 ;
FLUX := FLU: PIJ MACROG TRKGLOW :: TYPE K ;
GREP: FLUX :: GETVAL 'K-EFFECTIVE' 1 1 1 >>keffGLOW<< ;
ECHO "test AT10_LAT_MODEBOX GLOW / TSPC with FULL PIJ reconstruction completed, keff=" keffGLOW ;
assertS FLUX :: 'K-EFFECTIVE' 1 <<keffGEO>> ;
PIJ FLUX := DELETE: PIJ FLUX ;



g2s_tdt TRKGEO TRKFILGEO TRKGLOW TRKFILGLOW 
    := DELETE: g2s_tdt TRKGEO TRKFILGEO TRKGLOW TRKFILGLOW ;
