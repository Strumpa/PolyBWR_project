*** AT10_ASSEMBLY case with CP/IC/MOC solution : 
* BWR LATTICE array based on ATRIUM-10 geometry, 
* import tdt_data generated by glow.
* Solve 1 group CP/IC/MOC problem to ensure proper material numbering treatment
* issue : GLOW/DRAGON material ordering match, but SALOME re-writes material ids using its internal system.
* Macroscpic cross sections from ATRIUM-10 assembly pins. 
* self-shielded and solved in DRAGON : endfb8r1 - SHEM295
* Homogenized in fuel and condensed to 1 group.


MODULE GEO: MAC: G2S: SALT: MCCGT: ASM: FLU: GREP: DELETE: END: ;
PROCEDURE assertS ;
LINKED_LIST GEOISO GEOSPC MACRO MACROG TRACKIC TRACKCP TRACKMOC PIJ SYS FLUX ;

SEQ_BINARY TRACKFILIC TRACKFILCP TRACKFILMOC ;
SEQ_ASCII tdt_iso :: FILE 'AT10_assembly_TISO_MACRO.dat' ;
SEQ_ASCII glow_tiso.ps :: FILE 'AT10_assembly_GLOW_TISO.ps' ;

SEQ_ASCII g2s_tdt g2s_tdt_IC ;
SEQ_ASCII geo_tiso.ps :: FILE 'AT10_assembly_GEO_TISO.ps' ;
SEQ_ASCII geoTisoIC.ps :: FILE 'AT10_assembly_GEO_TISO_IC.ps' ;

REAL keffGEOCP keffGEOPIJ keffGEOIC keffGEOMOC ;
REAL keffGLOWCP keffGLOPIJ keffGLOWIC keffGLOWMOC ;
REAL delCPIC delMOCIC deltak ;

* ---
* TRACKING PARAMTERES
* ---
ECHO "BEGIN AT10_ASSEMBLY multicell NOGD CASE" ;

INTEGER an2d := 24 ;  ! ANGULAR QUADRATURE PARAMETER
REAL densur := 100.0 ; ! DENSITY OF INTEGRATION LINES CM^-1

* --------------------------------
*    DIMENSIONS DEFINITION
* --------------------------------
!Assembly scale data

REAL Pitch_A W_gap := 15.24 0.75 ; ! Assembly pitch and water gap thickness
REAL Box_out Box_in Box_thi := 13.74 13.4 0.17 ; ! Outer box outer, inner sides and thickness
REAL Chan_out Chan_in Chan_thi := 3.5 3.34 0.08 ; ! Channel box outer, inner sides and thickness


! UOX Pincell scale data

REAL Rgap Rclad pitch := 0.4520 0.5140 1.29500 ;
REAL Rcomb1 Rcomb2 Rcomb3 Rcomb4 ;
EVALUATE Rcomb4 := 0.4435 ;
EVALUATE Rcomb1 := 0.313602 ;
EVALUATE Rcomb2 := 0.396678 ; 
EVALUATE Rcomb3 := 0.43227 ;
REAL RCool := 0.58075 ; ! radius half way between outer clad and pincell side.

REAL RcombGd1 RcombGd2 RcombGd3 RcombGd4 RcombGd5 RcombGd6 ;
EVALUATE RcombGd1 RcombGd2 RcombGd3 RcombGd4 RcombGd5 RcombGd6 
        := 0.19834 0.28049 0.34353 0.39668 0.43227 0.4435 ;

! Evaluating dependencies

! Multiples of cell pitch : used to define 2nd level geometry mesh 
REAL 2_pitch 3_pitch 4_pitch 5_pitch 6_pitch 7_pitch 8_pitch 9_pitch ;
EVALUATE 2_pitch := pitch 2.0 * ;
EVALUATE 3_pitch := pitch 3.0 * ;
EVALUATE 4_pitch := pitch 4.0 * ;
EVALUATE 5_pitch := pitch 5.0 * ;
EVALUATE 6_pitch := pitch 6.0 * ;
EVALUATE 7_pitch := pitch 7.0 * ;
EVALUATE 8_pitch := pitch 8.0 * ;
EVALUATE 9_pitch := pitch 9.0 * ;
REAL LLat := pitch 10.0 * ;


! X1 = Water gap + outer box thickness +extra water : in box water not a multiple of pitch
REAL XtraCool ;
EVALUATE XtraCool := Box_in 10.0 pitch * - 2.0 / ;

!Meshing points for macro geometry :
REAL X1 X2 X3 X4  ;
! X2 = X1 + 4*pitch
! X3 = X2 + 3*pitch
! X4 = X3 + 3*pitch
EVALUATE X1 := W_gap Box_thi + XtraCool + ;
EVALUATE X2 := 4.0 pitch * X1 + ;
EVALUATE X3 := 3.0 pitch * X2 + ;
EVALUATE X4 := 3.0 pitch * X3 + ;  

REAL Y1 Y2 Y3 Y4 ;
EVALUATE Y1 := X1 ;
EVALUATE Y2 := 4.0 pitch * Y1 + ;
EVALUATE Y3 := 3.0 pitch * Y2 + ;
EVALUATE Y4 := 3.0 pitch * Y3 + ;

REAL L1 L2 L3 ;
EVALUATE L1 := X2 X1 - ;
EVALUATE L2 := X3 X2 - ;
EVALUATE L3 := X4 X3 - ;

REAL H1 H2 H3 ;
EVALUATE H1 := Y2 Y1 - ;
EVALUATE H2 := Y3 Y2 - ;
EVALUATE H3 := Y4 Y3 - ;

REAL x_step y_step ; ! allows for submeshing of in-assembly box water
! in-assembly box water included in geom level with outer assembly water and box
EVALUATE x_step := W_gap Box_thi + ;
EVALUATE y_step := XtraCool Box_thi + ;

! inner CHaNneL box related parameters, used to define 2nd level CHNL Geometry
! CHNL = channel box + surroudning water to fit with pin CARCEL edges 
REAL XCHNL1 XCHNL2 XCHNL3 XCHNL4 XCHNL5 ;
EVALUATE XCHNL1 := L2 Chan_out - 2.0 / ;
EVALUATE XCHNL2 := XCHNL1 Chan_thi + ;
EVALUATE XCHNL3 := XCHNL2 Chan_in + ;
EVALUATE XCHNL4 := XCHNL3 Chan_thi + ;
EVALUATE XCHNL5 := L2 ;

REAL X1sym := pitch XCHNL2 - ;
REAL X2sym := pitch XCHNL1 - ;

GEOISO := GEO: :: CAR2D 3 3
   EDIT 1
   X- DIAG X+ ALBE 1.0
   Y- ALBE 1.0 Y+ DIAG
   CELL
   BotCL BMidW BotCR
           LAT RMidW
               TopCR
   MESHX 0.0 <<X1>> <<X4>> <<Pitch_A>>
   MESHY 0.0 <<Y1>> <<Y4>> <<Pitch_A>>
   ::: BotCL :=  GEO: CAR2D 3 3
       MESHX 0.0 <<W_gap>> <<x_step>> <<X1>>
       MESHY 0.0 <<W_gap>> <<x_step>> <<Y1>>
       MIX
           13 13 13 
           13 14 14 
           13 14 11 ;
   ::: BotCR := GEO: CAR2D 3 3
       MESHX 0.0 <<XtraCool>> <<y_step>> <<X1>>
       MESHY 0.0 <<W_gap>> <<x_step>> <<X1>>
       MIX
           13 13 13
           14 14 13 
           11 14 13 ;
   ::: TopCR := GEO: CAR2D 3 3
       MESHX 0.0 <<XtraCool>> <<y_step>> <<X1>>
       MESHY 0.0 <<XtraCool>> <<y_step>> <<Y1>>
       MIX
           11 14 13  
           14 14 13  
           13 13 13 ;
   ::: RMidW := GEO: CAR2D 3 1
       MESHX 0.0 <<XtraCool>> <<y_step>> <<X1>>
       MESHY 0.0 <<LLat>>
       SPLITY 10
       MIX 11 14 13 ;
   ::: BMidW := GEO:  CAR2D 1 3
       MESHX 0.0 <<LLat>>
       MESHY 0.0 <<W_gap>> <<x_step>> <<Y1>>
       SPLITX 10
       MIX 13 14 11 ;
   ::: LAT :=  GEO: CAR2D 10 10
       MESHX 0.0 <<pitch>> <<2_pitch>> <<3_pitch>> <<4_pitch>> <<5_pitch>>
                 <<6_pitch>> <<7_pitch>> <<8_pitch>> <<9_pitch>> <<LLat>>
       MESHY 0.0 <<pitch>> <<2_pitch>> <<3_pitch>> <<4_pitch>> <<5_pitch>>
                 <<6_pitch>> <<7_pitch>> <<8_pitch>> <<9_pitch>> <<LLat>>
       CELL
    C1 C2 C3 C5 C6 C5 C4 C3 C2 C1
    C2 C4 C7 C6 C7 C6 C6 C7 C4 C2
    C3 C7 C6 C6 C6 C7 C6 C6 C7 C3
    C5 C6 C6 C6 C6 C6 C7 C6 C5 C4
    C6 C7 C6 C6 W1 WB W2 C4 C6 C4
    C5 C6 C7 C6 WL W0 WR C3 C7 C4
    C4 C6 C6 C7 W3 WT W4 C4 C4 C4
    C3 C7 C6 C6 C4 C3 C4 C4 C8 C3
    C2 C4 C7 C5 C6 C7 C4 C8 C4 C2
    C1 C2 C3 C4 C4 C4 C4 C3 C2 C1
    ::: C1  :=  GEO: CARCEL 6
      RADIUS 0.0 <<Rcomb1>> <<Rcomb2>> <<Rcomb3>> <<Rcomb4>> <<Rgap>> <<Rclad>>
      MIX 1 1 1 1 9 10 11
    MESHX 0.0 <<pitch>>
    MESHY 0.0 <<pitch>> ;
    ::: C2  :=  GEO: C1
      MIX 2 2 2 2 9 10 11 ;
    ::: C3  :=  GEO: C1
      MIX 3 3 3 3 9 10 11 ;
    ::: C4  :=  GEO: C1
      MIX 4 4 4 4 9 10 11 ;
    ::: C5  :=  GEO: C1
      MIX 5 5 5 5 9 10 11 ;
    ::: C6 :=  GEO: C1
      MIX 6 6 6 6 9 10 11 ;

    ::: C7  :=  GEO: CARCEL 8
        RADIUS 0.0 <<RcombGd1>> <<RcombGd2>> <<RcombGd3>> <<RcombGd4>> <<RcombGd5>> <<RcombGd6>> <<Rgap>> <<Rclad>>
        MIX 7 7 7 7 7 7 9 10 11
      MESHX 0.0 <<pitch>>
      MESHY 0.0 <<pitch>> ;

    ::: C8  :=  GEO: C7
      MIX 8 8 8 8 8 8 9 10 11 ;
   ::: W1 := GEO: CAR2D 3 3
       MESHX 0.0 <<XCHNL1>> <<XCHNL2>> <<pitch>>
       MESHY 0.0 <<XCHNL1>> <<XCHNL2>> <<pitch>>
       MIX 11 11 11
           11 12 12
           11 12 13 ;
   ::: WB := GEO: CAR2D 1 3
       MESHX 0.0 <<pitch>>
       MESHY 0.0 <<XCHNL1>> <<XCHNL2>> <<pitch>>
       MIX 11 12 13 ;
   ::: W2 := GEO: CAR2D 3 3
       MESHX 0.0 <<X1sym>> <<X2sym>> <<pitch>>
       MESHY 0.0 <<XCHNL1>> <<XCHNL2>> <<pitch>>
       MIX 11 11 11
           12 12 11
           13 12 11 ;
   ::: WL := GEO: CAR2D 3 1
       MESHX 0.0 <<XCHNL1>> <<XCHNL2>> <<pitch>>
       MESHY 0.0 <<pitch>>
       MIX 11 12 13 ;
   ::: W0 := GEO: CAR2D 1 1
       MESHX 0.0 <<pitch>>
       MESHY 0.0 <<pitch>>
        MIX 13 ;
   ::: WR := GEO: CAR2D 3 1
       MESHX 0.0 <<X1sym>> <<X2sym>> <<pitch>>
       MESHY 0.0 <<pitch>>
       MIX 13 12 11 ;
   ::: W3 := GEO: CAR2D 3 3
       MESHX 0.0 <<XCHNL1>> <<XCHNL2>> <<pitch>>
       MESHY 0.0 <<X1sym>> <<X2sym>> <<pitch>>
       MIX 11 12 13
           11 12 12
           11 11 11 ;
   ::: WT := GEO: CAR2D 1 3
       MESHX 0.0 <<pitch>>
       MESHY 0.0 <<X1sym>> <<X2sym>> <<pitch>>
       MIX 13 12 11 ;
   ::: W4 := GEO: CAR2D 3 3
        MESHX 0.0 <<X1sym>> <<X2sym>> <<pitch>>
        MESHY 0.0 <<X1sym>> <<X2sym>> <<pitch>>
        MIX 13 12 11
            12 12 11
            11 11 11 ;
   ;
;

MACRO := MAC: ::
     EDIT 2 NGRO 1 NMIX 14 NIFI 1
     READ INPUT

     MIX 1 (*24UOX*)
        TOTAL 4.43890840E-01    SCAT 1 1 3.99300873E-01
        NUSIGF 5.23696020E-02      CHI 1.0

      MIX 2 (*32UOX*)
        TOTAL 4.47801262E-01    SCAT 1 1 3.98593932E-01
        NUSIGF 6.20270856E-02      CHI 1.0

      MIX 3 (*42UOX*)
        TOTAL 4.50964361E-01    SCAT 1 1 3.97878230E-01
        NUSIGF 7.01253936E-02      CHI 1.0

      MIX 4 (*45UOX*)
        TOTAL 4.52631325E-01    SCAT 1 1 3.98060858E-01
        NUSIGF 7.29189515E-02      CHI 1.0

      MIX 5 (*48UOX*)
        TOTAL 4.53984380E-01    SCAT 1 1 3.97542506E-01
        NUSIGF 7.79897496E-02      CHI 1.0

      MIX 6 (*50UOX*)
        TOTAL 4.53644007E-01    SCAT 1 1 3.96933615E-01
        NUSIGF 7.77277201E-02      CHI 1.0
      
      MIX 7 (*45Gd --> 50UOX*)
        TOTAL 4.53644007E-01    SCAT 1 1 3.96933615E-01
        NUSIGF 7.77277201E-02      CHI 1.0

      MIX 8 (*42Gd --> 50UOX*)
        TOTAL 4.53644007E-01    SCAT 1 1 3.96933615E-01
        NUSIGF 7.77277201E-02      CHI 1.0
    
      MIX 9 (*GAP*)
        TOTAL 3.12748462E-01  SCAT 1 1 3.10926110E-01
      MIX 10 (*CLAD*)
        TOTAL 2.88040959E-04        SCAT 1 1 2.88040930E-04
      MIX 11 (*COOLANT*)
        TOTAL 7.66823292E-01        SCAT 1 1 7.65839636E-01
      MIX 12 (*MODERATOR BOX*)
        TOTAL 2.88040959E-04        SCAT 1 1 2.88040930E-04
      MIX 13 (*MODERATOR*)
        TOTAL 7.66823292E-01        SCAT 1 1 7.65839636E-01
      MIX 14 (*CHANNEL BOX*)
        TOTAL 2.88040959E-04        SCAT 1 1 2.88040930E-04
;


MACROG := MAC: ::
     EDIT 2 NGRO 1 NMIX 14 NIFI 1
     READ INPUT
      MIX 1 (*MODERATOR*)
      TOTAL 7.66823292E-01        SCAT 1 1 7.65839636E-01

      MIX 2 (*CHANNEL_BOX*)
        TOTAL 2.88040959E-04        SCAT 1 1 2.88040930E-04

      MIX 3 (*COOLANT*)
        TOTAL 7.66823292E-01        SCAT 1 1 7.65839636E-01

      MIX 4 (*MODERATOR_BOX*)
        TOTAL 2.88040959E-04        SCAT 1 1 2.88040930E-04

      MIX 5 (*CLAD*)
        TOTAL 2.88040959E-04        SCAT 1 1 2.88040930E-04
      
      MIX 6 (*GAP*)
        TOTAL 3.12748462E-01  SCAT 1 1 3.10926110E-01
        
      MIX 7 (*24UOX*)
        TOTAL 4.43890840E-01    SCAT 1 1 3.99300873E-01
        NUSIGF 5.23696020E-02      CHI 1.0

      MIX 8 (*32UOX*)
        TOTAL 4.47801262E-01    SCAT 1 1 3.98593932E-01
        NUSIGF 6.20270856E-02      CHI 1.0

      MIX 9 (*42UOX*)
        TOTAL 4.50964361E-01    SCAT 1 1 3.97878230E-01
        NUSIGF 7.01253936E-02      CHI 1.0

      MIX 10 (*45UOX*)
        TOTAL 4.52631325E-01    SCAT 1 1 3.98060858E-01
        NUSIGF 7.29189515E-02      CHI 1.0

      MIX 11 (*42Gd --> 50UOX*)
        TOTAL 4.53644007E-01    SCAT 1 1 3.96933615E-01
        NUSIGF 7.77277201E-02      CHI 1.0

      MIX 12 (*45Gd --> 50UOX*)
        TOTAL 4.53644007E-01    SCAT 1 1 3.96933615E-01
        NUSIGF 7.77277201E-02      CHI 1.0 
      MIX 13 (*50UOX*)
        TOTAL 4.53644007E-01    SCAT 1 1 3.96933615E-01
        NUSIGF 7.77277201E-02      CHI 1.0

      MIX 14 (*48UOX*)
        TOTAL 4.53984380E-01    SCAT 1 1 3.97542506E-01
        NUSIGF 7.79897496E-02      CHI 1.0
;


*** TISO case treatment ***


g2s_tdt geo_tiso.ps := G2S: GEOISO :: ;
g2s_tdt_IC geoTisoIC.ps := G2S: GEOISO :: MACRO  ;

glow_tiso.ps := G2S: tdt_iso :: ;

*** TREAT ALL GEO: CASES FIRST ***

TRACKIC TRACKFILIC := SALT: g2s_tdt_IC ::
     EDIT 2
     TITLE "MULTICELL SURFACIC AT10_A GEO CASE"
     TISO <<an2d>> <<densur>>
     IC EPSJ 1.0E-5
     ;

TRACKCP TRACKFILCP := SALT: g2s_tdt ::
     EDIT 2
     TITLE "TISO CP SURFACIC AT10_A GEO CASE"
     TISO <<an2d>> <<densur>>
     ;

TRACKMOC TRACKFILMOC := SALT: g2s_tdt ::
     EDIT 2
     TITLE "TISO MOC SURFACIC AT10_A GEO CASE"
     TISO EQW <<an2d>> <<densur>>
     ;

* MOC tracking
TRACKMOC := MCCGT: TRACKMOC TRACKFILMOC ::
  EDIT 0
;


* ---
* Flux calculation
* ---

* FULL PIJ MATRIX RECONSTRUCTION
PIJ := ASM: MACRO TRACKIC TRACKFILIC :: PIJ EDIT 2 ;
FLUX := FLU: PIJ MACRO TRACKIC :: TYPE K ;
GREP: FLUX :: GETVAL 'K-EFFECTIVE' 1 1 1 >>keffGEOPIJ<< ;
PIJ FLUX := DELETE: PIJ FLUX ;
ECHO "test multicell AT10_A FULL PIJ reconstruction completed, keff=" keffGEOPIJ ;

* FLUX-CURRENT ITERATION WITHOUT FULL PIJ MATRIX RECONSTRUCTION
PIJ := ASM: MACRO TRACKIC TRACKFILIC :: ARM EDIT 2 ;
FLUX := FLU: PIJ MACRO TRACKIC :: EDIT 4 TYPE K ;
GREP: FLUX :: GETVAL 'K-EFFECTIVE' 1 1 1 >>keffGEOIC<< ;
assertS FLUX :: 'K-EFFECTIVE' 1 <<keffGEOPIJ>> ;
PIJ FLUX := DELETE: PIJ FLUX ;
ECHO "test multicell AT10_A FLUX-CURRENT iteration completed, keff=" keffGEOIC ;

PIJ := ASM: MACRO TRACKCP TRACKFILCP :: PIJ EDIT 2 ;
FLUX := FLU: PIJ MACRO TRACKCP :: TYPE K ;
GREP: FLUX :: GETVAL 'K-EFFECTIVE' 1 1 1 >>keffGEOCP<< ;
PIJ FLUX := DELETE: PIJ FLUX ;
ECHO "test multicell AT10_A CP completed, keff=" keffGEOCP ;

EVALUATE delCPIC := keffGEOCP keffGEOIC - 1E+5 * ;
ECHO "With GEO: difference between CP and IC keff = " delCPIC ;

SYS := ASM: MACRO TRACKMOC TRACKFILMOC :: ARM EDIT 2 ; 
FLUX := FLU: SYS MACRO TRACKMOC TRACKFILMOC :: EDIT 1 TYPE K ;
GREP: FLUX :: GETVAL 'K-EFFECTIVE' 1 1 1 >>keffGEOMOC<< ;
ECHO "test multicell AT10_A MOC completed, keff=" keffGEOMOC ;

EVALUATE delMOCIC := keffGEOMOC keffGEOIC - 1E+5 * ;
ECHO "With GEO: difference between MOC and IC keff = " delMOCIC ;


FLUX SYS := DELETE: FLUX SYS ;
TRACKIC TRACKFILIC := DELETE: TRACKIC TRACKFILIC ;
TRACKCP TRACKFILCP := DELETE: TRACKCP TRACKFILCP ;
TRACKMOC TRACKFILMOC := DELETE: TRACKMOC TRACKFILMOC ;

*----
ECHO " --- GLOW CASES TREATMENT --- " ;
*** TREAT ALL GLOW CASES SECOND ***

TRACKIC TRACKFILIC := SALT: tdt_iso ::
     EDIT 2
     TITLE "MULTICELL SURFACIC AT10_A GLOW+MACRO CASE"
     TISO <<an2d>> <<densur>>
     IC EPSJ 1.0E-5
     ;

TRACKCP TRACKFILCP := SALT: tdt_iso ::
     EDIT 2
     TITLE "TISO CP SURFACIC AT10_A GLOW+MACRO CASE"
     TISO <<an2d>> <<densur>>
     NOIC
     ;

TRACKMOC TRACKFILMOC := SALT: tdt_iso ::
     EDIT 2
     TITLE "TISO MOC SURFACIC AT10_A GLOW+MACRO CASE"
     TISO EQW <<an2d>> <<densur>>
     NOIC
     ;

* MOC tracking
TRACKMOC := MCCGT: TRACKMOC TRACKFILMOC ::
  EDIT 0
;

* ---
* Flux calculation
* ---

* FULL PIJ MATRIX RECONSTRUCTION
PIJ := ASM: MACROG TRACKIC TRACKFILIC :: PIJ EDIT 2 ;
FLUX := FLU: PIJ MACROG TRACKIC :: TYPE K ;
GREP: FLUX :: GETVAL 'K-EFFECTIVE' 1 1 1 >>keffGLOPIJ<< ;
PIJ FLUX := DELETE: PIJ FLUX ;
ECHO "test multicell AT10_A (GLOW+MACRO) FULL PIJ reconstruction completed, keff=" keffGLOPIJ ;

* FLUX-CURRENT ITERATION WITHOUT FULL PIJ MATRIX RECONSTRUCTION
PIJ := ASM: MACROG TRACKIC TRACKFILIC :: ARM EDIT 2 ;
FLUX := FLU: PIJ MACROG TRACKIC :: EDIT 4 TYPE K ;
GREP: FLUX :: GETVAL 'K-EFFECTIVE' 1 1 1 >>keffGLOWIC<< ;
assertS FLUX :: 'K-EFFECTIVE' 1 <<keffGLOPIJ>> ;
PIJ FLUX := DELETE: PIJ FLUX ;
ECHO "test multicell AT10_A (GLOW+MACRO) FLUX-CURRENT iteration completed, keff=" keffGLOWIC ;

PIJ := ASM: MACROG TRACKCP TRACKFILCP :: PIJ EDIT 2 ;
FLUX := FLU: PIJ MACROG TRACKCP :: TYPE K ;
GREP: FLUX :: GETVAL 'K-EFFECTIVE' 1 1 1 >>keffGLOWCP<< ;
PIJ FLUX := DELETE: PIJ FLUX ;
ECHO "test multicell AT10_A (GLOW+MACRO) CP completed, keff=" keffGLOWCP ;

EVALUATE delCPIC := keffGLOWCP keffGLOWIC - 1E+5 * ;
ECHO "With GLOW+MACRO: difference between CP and IC keff = " delCPIC ;

SYS := ASM: MACROG TRACKMOC TRACKFILMOC :: ARM EDIT 2 ; 
FLUX := FLU: SYS MACROG TRACKMOC TRACKFILMOC :: EDIT 1 TYPE K ;
GREP: FLUX :: GETVAL 'K-EFFECTIVE' 1 1 1 >>keffGLOWMOC<< ;
ECHO "test multicell AT10_A (GLOW+MACRO) MOC completed, keff=" keffGLOWMOC ;

EVALUATE delMOCIC := keffGLOWMOC keffGLOWIC - 1E+5 * ;
ECHO "With GLOW+MACRO: difference between MOC and IC keff = " delMOCIC ;

FLUX SYS := DELETE: FLUX SYS ;
TRACKIC TRACKFILIC := DELETE: TRACKIC TRACKFILIC ;
TRACKCP TRACKFILCP := DELETE: TRACKCP TRACKFILCP ;
TRACKMOC TRACKFILMOC := DELETE: TRACKMOC TRACKFILMOC ;


! Assert differences with GEO results 
ECHO "Test IC from IC tracking : GLOW+MACRO - GEO" ;
ECHO "keffGLOW+MACRO IC = " keffGLOWIC ;
ECHO "keffGEOIC = " keffGEOIC ;
EVALUATE deltak := keffGLOWIC keffGEOIC - 1E+5 * ;
ECHO "delta k IC (GLOW+MACRO - GEO) = " deltak " pcm" ;

ECHO "Test PIJ from IC tracking : GLOW+MACRO - GEO" ;
ECHO "keffGLOW+MACRO PIJ = " keffGLOPIJ ;
ECHO "keffGEOPIJ = " keffGEOPIJ ;
EVALUATE deltak := keffGLOPIJ keffGEOPIJ - 1E+5 * ;
ECHO "delta k PIJ (GLOW+MACRO - GEO) = " deltak " pcm" ;

ECHO "Test CP from NOIC tracking : GLOW+MACRO - GEO" ;
ECHO "keffGLOW+MACRO CP = " keffGLOWCP ;
ECHO "keffGEOCP = " keffGEOCP ;
EVALUATE deltak := keffGLOWCP keffGEOCP - 1E+5 * ;
ECHO "delta k CP (GLOW+MACRO - GEO) = " deltak " pcm" ;

ECHO "Test MOC from NOIC tracking : GLOW+MACRO - GEO" ;
ECHO "keffGLOW+MACRO MOC = " keffGLOWMOC ;
ECHO "keffGEOMOC = " keffGEOMOC ;
EVALUATE deltak := keffGLOWMOC keffGEOMOC - 1E+5 * ;
ECHO "delta k MOC (GLOW+MACRO - GEO) = " deltak " pcm" ;


END: ;