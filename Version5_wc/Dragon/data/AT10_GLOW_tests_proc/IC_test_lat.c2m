*** AT10_LAT case with CP/IC/MOC solution : 
* BWR LATTICE array based on ATRIUM-10 geometry, 
* import tdt_data generated by glow.
* Solve 1 group CP/IC/MOC problem to ensure proper material numbering treatment
* issue : GLOW/DRAGON material ordering match, but SALOME re-writes material ids using its internal system.
* Macroscpic cross sections from ATRIUM-10 assembly pins. 
* self-shielded and solved in DRAGON : endfb8r1 - SHEM295
* Homogenized in fuel and condensed to 1 group.


MODULE GEO: MAC: G2S: SALT: MCCGT: ASM: FLU: GREP: DELETE: END: ;
PROCEDURE assertS ;
LINKED_LIST GEOISO GEOSPC MACRO MACROG TRACKIC TRACKCP TRACKMOC PIJ SYS FLUX ;

SEQ_BINARY TRACKFILIC TRACKFILCP TRACKFILMOC ;
SEQ_ASCII tdt_iso :: FILE 'AT10_lattice_TISO.dat' ;
SEQ_ASCII tdtMACRO :: FILE 'AT10_lattice_TISO_MACRO.dat' ;
SEQ_ASCII glow_tiso.ps :: FILE 'AT10_lattice_TISO.ps' ;
SEQ_ASCII glowMACRO.ps :: FILE 'AT10_lattice_TISO_MACRO.ps' ;

SEQ_ASCII g2s_tdt :: FILE 'AT10_GEO_lattice_TISO.dat' ;
SEQ_ASCII g2s_tdt_IC :: FILE 'AT10_GEO_lattice_TISO_IC.dat' ;
SEQ_ASCII geo_tiso.ps ;
SEQ_ASCII geoTisoIC.ps ;

REAL keffGEOIC keffGEOPIJ keffGEOCP  keffGEOMOC ;
REAL keffGLOWCP keffGLOWPIJ keffGLOWIC keffGLOWMOC ;
REAL keffGMCP keffGMPIJ keffGMIC keffGMMOC ; 
REAL delCPIC delMOCIC deltak ;

* ---
* TRACKING PARAMTERES
* ---
ECHO "BEGIN AT10_LAT multicell CASE" ;

INTEGER an2d := 24 ;  ! ANGULAR QUADRATURE PARAMETER
REAL densur := 100.0 ; ! DENSITY OF INTEGRATION LINES CM^-1


REAL Rgap Rclad pitch := 0.4520 0.5140 1.295 ;
REAL Rcomb1 Rcomb2 Rcomb3 Rcomb4 := 0.313602 0.396678 0.43227 0.4435 ;
REAL RcombGd1 RcombGd2 RcombGd3 RcombGd4 RcombGd5 RcombGd6 
         := 0.19834 0.28049 0.34353 0.39668 0.43227 0.4435 ;

REAL 2_pitch 3_pitch 4_pitch 5_pitch 6_pitch 7_pitch 8_pitch 9_pitch 10_pitch ;
EVALUATE 2_pitch := 2.0 pitch * ;
EVALUATE 3_pitch := 3.0 pitch * ;
EVALUATE 4_pitch := 4.0 pitch * ;
EVALUATE 5_pitch := 5.0 pitch * ;
EVALUATE 6_pitch := 6.0 pitch * ;
EVALUATE 7_pitch := 7.0 pitch * ;
EVALUATE 8_pitch := 8.0 pitch * ;
EVALUATE 9_pitch := 9.0 pitch * ;
EVALUATE 10_pitch := 10.0 pitch * ;

GEOISO := GEO: :: CAR2D 10 10 
    X- ALBE 1.0 X+ ALBE 1.0
    Y- ALBE 1.0 Y+ ALBE 1.0
    CELL
    C1 C2 C3 C5 C6 C5 C4 C3 C2 C1
    C2 C4 C7 C6 C7 C6 C6 C7 C4 C2
    C3 C7 C6 C6 C6 C7 C6 C6 C7 C3
    C5 C6 C6 C6 C6 C6 C7 C6 C5 C4
    C6 C7 C6 C6 W0 W0 W0 C4 C6 C4
    C5 C6 C7 C6 W0 W0 W0 C3 C7 C4
    C4 C6 C6 C7 W0 W0 W0 C4 C4 C4
    C3 C7 C6 C6 C4 C3 C4 C4 C8 C3
    C2 C4 C7 C5 C6 C7 C4 C8 C4 C2
    C1 C2 C3 C4 C4 C4 C4 C3 C2 C1
    MESHX 0.0 <<pitch>> <<2_pitch>> <<3_pitch>> <<4_pitch>> <<5_pitch>> 
    <<6_pitch>> <<7_pitch>> <<8_pitch>> <<9_pitch>> <<10_pitch>>
    
    MESHY 0.0 <<pitch>> <<2_pitch>> <<3_pitch>> <<4_pitch>> <<5_pitch>> 
    <<6_pitch>> <<7_pitch>> <<8_pitch>> <<9_pitch>> <<10_pitch>>
    ::: C1 :=  GEO: CARCEL 6 
                RADIUS 0.0 <<Rcomb1>> <<Rcomb2>>
                <<Rcomb3>> <<Rcomb4>> <<Rgap>> <<Rclad>>
                MIX 1 1 1 1 9 10 11
                    MESHX 0.0 <<pitch>> MESHY 0.0 <<pitch>>
    ;
    ::: C2 := GEO: C1 MIX 2 2 2 2 9 10 11
    ;
    ::: C3 := GEO: C1 MIX 3 3 3 3 9 10 11
    ;
    ::: C4 := GEO: C1 MIX 4 4 4 4 9 10 11
    ;
    ::: C5 := GEO: C1 MIX 5 5 5 5 9 10 11
    ;
    ::: C6 := GEO: C1 MIX 6 6 6 6 9 10 11
    ;
    ::: C7 := GEO: CARCEL 8
              RADIUS 0.0 <<RcombGd1>> <<RcombGd2>> <<RcombGd3>> <<RcombGd4>>
                          <<RcombGd5>> <<RcombGd6>> <<Rgap>> <<Rclad>> 
              MIX 7 7 7 7 7 7 9 10 11
              MESHX 0.0 <<pitch>> MESHY 0.0 <<pitch>>
    ;
    ::: C8 := GEO: C7 MIX 8 8 8 8 8 8 9 10 11
    ;
    ::: W0 := GEO: CAR2D 1 1
      MESHX 0.0 <<pitch>>
      MESHX 0.0 <<pitch>>
      MIX 12
    ;

;

MACRO := MAC: ::
     EDIT 2 NGRO 1 NMIX 12 NIFI 1
     READ INPUT

     MIX 1 (*24UOX*)
        TOTAL 4.43890840E-01    SCAT 1 1 3.99300873E-01
        NUSIGF 5.23696020E-02      CHI 1.0

      MIX 2 (*32UOX*)
        TOTAL 4.47801262E-01    SCAT 1 1 3.98593932E-01
        NUSIGF 6.20270856E-02      CHI 1.0

      MIX 3 (*42UOX*)
        TOTAL 4.50964361E-01    SCAT 1 1 3.97878230E-01
        NUSIGF 7.01253936E-02      CHI 1.0

      MIX 4 (*45UOX*)
        TOTAL 4.52631325E-01    SCAT 1 1 3.98060858E-01
        NUSIGF 7.29189515E-02      CHI 1.0

      MIX 5 (*48UOX*)
        TOTAL 4.53984380E-01    SCAT 1 1 3.97542506E-01
        NUSIGF 7.79897496E-02      CHI 1.0

      MIX 6 (*50UOX*)
        TOTAL 4.53644007E-01    SCAT 1 1 3.96933615E-01
        NUSIGF 7.77277201E-02      CHI 1.0
      
      !MIX 7 (*45Gd*)
      !  TOTAL 5.22946239E-01    SCAT 1 1 3.99194658E-01
      !  NUSIGF 3.58763784E-02      CHI 1.0
      MIX 7 (*50UOX*)
        TOTAL 4.53644007E-01    SCAT 1 1 3.96933615E-01
        NUSIGF 7.77277201E-02      CHI 1.0

      !MIX 8 (*42Gd*)
      !  TOTAL 5.12946239E-01    SCAT 1 1 3.99194658E-01
      !  NUSIGF 3.48763784E-02      CHI 1.0

      MIX 8 (*50UOX*)
        TOTAL 4.53644007E-01    SCAT 1 1 3.96933615E-01
        NUSIGF 7.77277201E-02      CHI 1.0
    
      MIX 9 (*GAP*)
        TOTAL 3.12748462E-01  SCAT 1 1 3.10926110E-01
      MIX 10 (*CLAD*)
        TOTAL 2.88040959E-04        SCAT 1 1 2.88040930E-04
      MIX 11 (*COOLANT*)
        TOTAL 7.66823292E-01        SCAT 1 1 7.65839636E-01
      MIX 12 (*MODERATOR*)
        TOTAL 7.66823292E-01        SCAT 1 1 7.65839636E-01
;

!#  1 - COOLANT
!#  2 - CLAD
!#  3 - GAP
!#  4 - 24UOX
!#  5 - 32UOX
!#  6 - 42UOX
!#  7 - 45UOX
!#  8 - 42Gd
!#  9 - 45Gd
!# 10 - 50UOX
!# 11 - 48UOX
!# 12 - MODERATOR

MACROG := MAC: ::
     EDIT 2 NGRO 1 NMIX 12 NIFI 1
     READ INPUT
     MIX 4 (*24UOX*)
        TOTAL 4.43890840E-01    SCAT 1 1 3.99300873E-01
        NUSIGF 5.23696020E-02      CHI 1.0

      MIX 5 (*32UOX*)
        TOTAL 4.47801262E-01    SCAT 1 1 3.98593932E-01
        NUSIGF 6.20270856E-02      CHI 1.0

      MIX 6 (*42UOX*)
        TOTAL 4.50964361E-01    SCAT 1 1 3.97878230E-01
        NUSIGF 7.01253936E-02      CHI 1.0

      MIX 7 (*45UOX*)
        TOTAL 4.52631325E-01    SCAT 1 1 3.98060858E-01
        NUSIGF 7.29189515E-02      CHI 1.0

      MIX 11 (*48UOX*)
        TOTAL 4.53984380E-01    SCAT 1 1 3.97542506E-01
        NUSIGF 7.79897496E-02      CHI 1.0

      MIX 10 (*50UOX*)
        TOTAL 4.53644007E-01    SCAT 1 1 3.96933615E-01
        NUSIGF 7.77277201E-02      CHI 1.0
      
      !MIX 9 (*45Gd*)
      !  TOTAL 5.22946239E-01    SCAT 1 1 3.99194658E-01
      !  NUSIGF 3.58763784E-02      CHI 1.0

      MIX 9 (*50UOX*)
        TOTAL 4.53644007E-01    SCAT 1 1 3.96933615E-01
        NUSIGF 7.77277201E-02      CHI 1.0
      
      !MIX 8 (*42Gd*)
      !  TOTAL 5.12946239E-01    SCAT 1 1 3.99194658E-01
      !  NUSIGF 3.48763784E-02      CHI 1.0
      MIX 8 (*50UOX*)
        TOTAL 4.53644007E-01    SCAT 1 1 3.96933615E-01
        NUSIGF 7.77277201E-02      CHI 1.0
    
      MIX 3 (*GAP*)
        TOTAL 3.12748462E-01  SCAT 1 1 3.10926110E-01
      MIX 2 (*CLAD*)
        TOTAL 2.88040959E-04        SCAT 1 1 2.88040930E-04
      MIX 1 (*COOLANT*)
        TOTAL 7.66823292E-01        SCAT 1 1 7.65839636E-01
      MIX 12 (*MODERATOR*)
        TOTAL 7.66823292E-01        SCAT 1 1 7.65839636E-01
;


*** TISO case treatment ***


g2s_tdt geo_tiso.ps := G2S: GEOISO :: ;
g2s_tdt_IC geoTisoIC.ps := G2S: GEOISO :: MACRO ;

glow_tiso.ps := G2S: tdt_iso :: ;
glowMACRO.ps := G2S: tdtMACRO :: ;

ECHO " --- GEO: CASES TREATMENT --- " ;

*** TREAT ALL GEO: CASES FIRST ***

TRACKIC TRACKFILIC := SALT: g2s_tdt_IC ::
     EDIT 2
     TITLE "MULTICELL SURFACIC AT10_LAT CASE"
     TISO <<an2d>> <<densur>>
     IC EPSJ 1.0E-6
     ;

TRACKCP TRACKFILCP := SALT: g2s_tdt ::
     EDIT 2
     TITLE "TISO CP SURFACIC AT10_LAT CASE"
     TISO <<an2d>> <<densur>>
     ;

TRACKMOC TRACKFILMOC := SALT: g2s_tdt ::
     EDIT 2
     TITLE "TISO MOC SURFACIC AT10_LAT CASE"
     TISO EQW <<an2d>> <<densur>>
     ;

* MOC tracking
TRACKMOC := MCCGT: TRACKMOC TRACKFILMOC ::
  EDIT 0
;


* ---
* Flux calculation
* ---

* FULL PIJ MATRIX RECONSTRUCTION
PIJ := ASM: MACRO TRACKIC TRACKFILIC :: PIJ EDIT 2 ;
FLUX := FLU: PIJ MACRO TRACKIC :: TYPE K ;
GREP: FLUX :: GETVAL 'K-EFFECTIVE' 1 1 1 >>keffGEOPIJ<< ;
PIJ FLUX := DELETE: PIJ FLUX ;
ECHO "test multicell AT10_LAT (GEO:) FULL PIJ reconstruction completed, keff=" keffGEOPIJ ;

* FLUX-CURRENT ITERATION WITHOUT FULL PIJ MATRIX RECONSTRUCTION
PIJ := ASM: MACRO TRACKIC TRACKFILIC :: ARM EDIT 2 ;
FLUX := FLU: PIJ MACRO TRACKIC :: EDIT 4 TYPE K ;
GREP: FLUX :: GETVAL 'K-EFFECTIVE' 1 1 1 >>keffGEOIC<< ;
assertS FLUX :: 'K-EFFECTIVE' 1 <<keffGEOPIJ>> ;
PIJ FLUX := DELETE: PIJ FLUX ;
ECHO "test multicell AT10_LAT (GEO:) FLUX-CURRENT iteration completed, keff=" keffGEOIC ;

PIJ := ASM: MACRO TRACKCP TRACKFILCP :: PIJ EDIT 2 ;
FLUX := FLU: PIJ MACRO TRACKCP :: TYPE K ;
GREP: FLUX :: GETVAL 'K-EFFECTIVE' 1 1 1 >>keffGEOCP<< ;
PIJ FLUX := DELETE: PIJ FLUX ;
ECHO "test multicell AT10_LAT (GEO:) CP completed, keff=" keffGEOCP ;

EVALUATE delCPIC := keffGEOCP keffGEOIC - 1E+5 * ;
ECHO "With GEO: difference between CP and IC keff = " delCPIC ;

SYS := ASM: MACRO TRACKMOC TRACKFILMOC :: ARM EDIT 2 ; 
FLUX := FLU: SYS MACRO TRACKMOC TRACKFILMOC :: EDIT 1 TYPE K ;
GREP: FLUX :: GETVAL 'K-EFFECTIVE' 1 1 1 >>keffGEOMOC<< ;
ECHO "test multicell AT10_LAT (GEO:) MOC completed, keff=" keffGEOMOC ;

EVALUATE delMOCIC := keffGEOMOC keffGEOIC - 1E+5 * ;
ECHO "With GEO: difference between MOC and IC keff = " delMOCIC ;


FLUX SYS := DELETE: FLUX SYS ;
TRACKIC TRACKFILIC := DELETE: TRACKIC TRACKFILIC ;
TRACKCP TRACKFILCP := DELETE: TRACKCP TRACKFILCP ;
TRACKMOC TRACKFILMOC := DELETE: TRACKMOC TRACKFILMOC ;

*----
ECHO " --- GLOW CASES TREATMENT --- " ;
*** TREAT ALL GLOW CASES SECOND ***

TRACKIC TRACKFILIC := SALT: tdt_iso ::
     EDIT 2
     TITLE "MULTICELL SURFACIC AT10_LAT CASE"
     TISO <<an2d>> <<densur>>
     IC EPSJ 1.0E-6
     ;

TRACKCP TRACKFILCP := SALT: tdt_iso ::
     EDIT 2
     TITLE "TISO CP SURFACIC AT10_LAT CASE"
     TISO <<an2d>> <<densur>>
     NOIC
     ;

TRACKMOC TRACKFILMOC := SALT: tdt_iso ::
     EDIT 2
     TITLE "TISO MOC SURFACIC AT10_LAT CASE"
     TISO EQW <<an2d>> <<densur>>
     NOIC
     ;

* MOC tracking
TRACKMOC := MCCGT: TRACKMOC TRACKFILMOC ::
  EDIT 0
;

* ---
* Flux calculation
* ---

* FULL PIJ MATRIX RECONSTRUCTION
PIJ := ASM: MACROG TRACKIC TRACKFILIC :: PIJ EDIT 2 ;
FLUX := FLU: PIJ MACROG TRACKIC :: TYPE K ;
GREP: FLUX :: GETVAL 'K-EFFECTIVE' 1 1 1 >>keffGLOWPIJ<< ;
PIJ FLUX := DELETE: PIJ FLUX ;
ECHO "test multicell AT10_LAT (GLOW) FULL PIJ reconstruction completed, keff=" keffGLOWPIJ ;

* FLUX-CURRENT ITERATION WITHOUT FULL PIJ MATRIX RECONSTRUCTION
PIJ := ASM: MACROG TRACKIC TRACKFILIC :: ARM EDIT 2 ;
FLUX := FLU: PIJ MACROG TRACKIC :: EDIT 4 TYPE K ;
GREP: FLUX :: GETVAL 'K-EFFECTIVE' 1 1 1 >>keffGLOWIC<< ;
assertS FLUX :: 'K-EFFECTIVE' 1 <<keffGLOWPIJ>> ;
PIJ FLUX := DELETE: PIJ FLUX ;
ECHO "test multicell AT10_LAT (GLOW) FLUX-CURRENT iteration completed, keff=" keffGLOWIC ;

PIJ := ASM: MACROG TRACKCP TRACKFILCP :: PIJ EDIT 2 ;
FLUX := FLU: PIJ MACROG TRACKCP :: TYPE K ;
GREP: FLUX :: GETVAL 'K-EFFECTIVE' 1 1 1 >>keffGLOWCP<< ;
PIJ FLUX := DELETE: PIJ FLUX ;
ECHO "test multicell AT10_LAT (GLOW) CP completed, keff=" keffGLOWCP ;

EVALUATE delCPIC := keffGLOWCP keffGLOWIC - 1E+5 * ;
ECHO "With GLOW: difference between CP and IC keff = " delCPIC ;

SYS := ASM: MACROG TRACKMOC TRACKFILMOC :: ARM EDIT 2 ; 
FLUX := FLU: SYS MACROG TRACKMOC TRACKFILMOC :: EDIT 1 TYPE K ;
GREP: FLUX :: GETVAL 'K-EFFECTIVE' 1 1 1 >>keffGLOWMOC<< ;
ECHO "test multicell AT10_LAT (GLOW) MOC completed, keff=" keffGLOWMOC ;

EVALUATE delMOCIC := keffGLOWMOC keffGLOWIC - 1E+5 * ;
ECHO "With GLOW: difference between MOC and IC keff = " delMOCIC ;

FLUX SYS := DELETE: FLUX SYS ;
TRACKIC TRACKFILIC := DELETE: TRACKIC TRACKFILIC ;
TRACKCP TRACKFILCP := DELETE: TRACKCP TRACKFILCP ;
TRACKMOC TRACKFILMOC := DELETE: TRACKMOC TRACKFILMOC ;


! Assert differences with GEO results 
ECHO "Test IC from IC tracking : GLOW - GEO" ;
ECHO "keffGLOWIC = " keffGLOWIC ;
ECHO "keffGEOIC = " keffGEOIC ;
EVALUATE deltak := keffGLOWIC keffGEOIC - 1E+5 * ;
ECHO "delta k IC (GLOW - GEO) = " deltak "pcm" ;

ECHO "Test PIJ from IC tracking : GLOW - GEO" ;
ECHO "keffGLOWPIJ = " keffGLOWPIJ ;
ECHO "keffGEOPIJ = " keffGEOPIJ ;
EVALUATE deltak := keffGLOWPIJ keffGEOPIJ - 1E+5 * ;
ECHO "delta k PIJ (GLOW - GEO) = " deltak "pcm" ;

ECHO "Test CP from NOIC tracking : GLOW - GEO" ;
ECHO "keffGLOWCP = " keffGLOWCP ;
ECHO "keffGEOCP = " keffGEOCP ;
EVALUATE deltak := keffGLOWCP keffGEOCP - 1E+5 * ;
ECHO "delta k CP (GLOW - GEO) = " deltak "pcm" ;

ECHO "Test MOC from NOIC tracking : GLOW - GEO" ;
ECHO "keffGLOWMOC = " keffGLOWMOC ;
ECHO "keffGEOMOC = " keffGEOMOC ;
EVALUATE deltak := keffGLOWMOC keffGEOMOC - 1E+5 * ;
ECHO "delta k MOC (GLOW - GEO) = " deltak "pcm" ;



*----
ECHO " --- GLOW+MACRO CASES TREATMENT --- " ;
*** TREAT ALL GLOW CASES SECOND ***

TRACKIC TRACKFILIC := SALT: tdtMACRO ::
     EDIT 2
     TITLE "MULTICELL SURFACIC AT10_LAT CASE"
     TISO <<an2d>> <<densur>>
     IC EPSJ 1.0E-6
     ;

TRACKCP TRACKFILCP := SALT: tdtMACRO  ::
     EDIT 2
     TITLE "TISO CP SURFACIC AT10_LAT CASE"
     TISO <<an2d>> <<densur>>
     NOIC
     ;

TRACKMOC TRACKFILMOC := SALT: tdtMACRO  ::
     EDIT 2
     TITLE "TISO MOC SURFACIC AT10_LAT CASE"
     TISO EQW <<an2d>> <<densur>>
     NOIC
     ;

* MOC tracking
TRACKMOC := MCCGT: TRACKMOC TRACKFILMOC ::
  EDIT 0
;

* ---
* Flux calculation
* ---

* FULL PIJ MATRIX RECONSTRUCTION
PIJ := ASM: MACROG TRACKIC TRACKFILIC :: PIJ EDIT 2 ;
FLUX := FLU: PIJ MACROG TRACKIC :: TYPE K ;
GREP: FLUX :: GETVAL 'K-EFFECTIVE' 1 1 1 >>keffGMPIJ<< ;
PIJ FLUX := DELETE: PIJ FLUX ;
ECHO "test multicell AT10_LAT (GLOW+MACRO) FULL PIJ reconstruction completed, keff=" keffGMPIJ ;

* FLUX-CURRENT ITERATION WITHOUT FULL PIJ MATRIX RECONSTRUCTION
PIJ := ASM: MACROG TRACKIC TRACKFILIC :: ARM EDIT 2 ;
FLUX := FLU: PIJ MACROG TRACKIC :: EDIT 4 TYPE K ;
GREP: FLUX :: GETVAL 'K-EFFECTIVE' 1 1 1 >>keffGMIC<< ;
assertS FLUX :: 'K-EFFECTIVE' 1 <<keffGMPIJ>> ;
PIJ FLUX := DELETE: PIJ FLUX ;
ECHO "test multicell AT10_LAT (GLOW+MACRO) FLUX-CURRENT iteration completed, keff=" keffGMIC ;

PIJ := ASM: MACROG TRACKCP TRACKFILCP :: PIJ EDIT 2 ;
FLUX := FLU: PIJ MACROG TRACKCP :: TYPE K ;
GREP: FLUX :: GETVAL 'K-EFFECTIVE' 1 1 1 >>keffGMCP<< ;
PIJ FLUX := DELETE: PIJ FLUX ;
ECHO "test multicell AT10_LAT (GLOW+MACRO) CP completed, keff=" keffGMCP ;

EVALUATE delCPIC := keffGMCP keffGMIC - 1E+5 * ;
ECHO "With GLOW+MACRO: difference between CP and IC keff = " delCPIC ;

SYS := ASM: MACROG TRACKMOC TRACKFILMOC :: ARM EDIT 2 ; 
FLUX := FLU: SYS MACROG TRACKMOC TRACKFILMOC :: EDIT 1 TYPE K ;
GREP: FLUX :: GETVAL 'K-EFFECTIVE' 1 1 1 >>keffGMMOC<< ;
ECHO "test multicell AT10_LAT (GLOW+MACRO) MOC completed, keff=" keffGMMOC ;

EVALUATE delMOCIC := keffGMMOC keffGMIC - 1E+5 * ;
ECHO "With GLOW+MACRO: difference between MOC and IC keff = " delMOCIC ;

FLUX SYS := DELETE: FLUX SYS ;
TRACKIC TRACKFILIC := DELETE: TRACKIC TRACKFILIC ;
TRACKCP TRACKFILCP := DELETE: TRACKCP TRACKFILCP ;
TRACKMOC TRACKFILMOC := DELETE: TRACKMOC TRACKFILMOC ;

! Assert differences with GEO results 
ECHO "Test IC from IC tracking : GLOW+MACRO - GEO" ;
ECHO "keffGMIC = " keffGMIC ;
ECHO "keffGEOIC = " keffGEOIC ;
EVALUATE deltak := keffGMIC keffGEOIC - 1E+5 * ;
ECHO "delta k IC (GLOW+MACRO - GEO) = " deltak " pcm" ;

ECHO "Test PIJ from IC tracking : GLOW+MACRO - GEO" ;
ECHO "keffGMPIJ = " keffGMPIJ ;
ECHO "keffGEOPIJ = " keffGEOPIJ ;
EVALUATE deltak := keffGMPIJ keffGEOPIJ - 1E+5 * ;
ECHO "delta k PIJ (GLOW+MACRO - GEO) = " deltak " pcm" ;

ECHO "Test CP from NOIC tracking : GLOW+MACRO - GEO" ;
ECHO "keffGMCP = " keffGMCP ;
ECHO "keffGEOCP = " keffGEOCP ;
EVALUATE deltak := keffGMCP keffGEOCP - 1E+5 * ;
ECHO "delta k CP (GLOW+MACRO - GEO) = " deltak " pcm" ;

ECHO "Test MOC from NOIC tracking : GLOW+MACRO - GEO" ;
ECHO "keffGMMOC = " keffGMMOC ;
ECHO "keffGEOMOC = " keffGEOMOC ;
EVALUATE deltak := keffGMMOC keffGEOMOC - 1E+5 * ;
ECHO "delta k MOC (GLOW+MACRO - GEO) = " deltak " pcm" ;

END: ;



