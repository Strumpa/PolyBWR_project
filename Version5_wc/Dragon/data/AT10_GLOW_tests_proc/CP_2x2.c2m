*** AT10_2x2 case with CP solution : 
* BWR 2x2 array based on ATRIUM-10 geometry, 
* Generate geometry with GEO:, import tdt_data generated by glow.
* Solve 1 group CP problem to ensure proper material numbering treatment
* issue : GLOW/DRAGON material ordering match, but SALOME re-writes material ids using its internal system.
* Macroscpic cross sections from 24UOX, 32UOX, 45UOX AT10 pins, self-shielded and solved in DRAGON : endfb8r1 - SHEM295
* Homogenized in fuel and condensed to 1 group.



MODULE GEO: MAC: G2S: SALT: ASM: FLU: GREP: DELETE: END: ;
PROCEDURE assertS ;
LINKED_LIST GEOISO GEOSPC MACRO MACROG TRKGEO TRKGLOW PIJ SYS FLUX ;

SEQ_BINARY TRKFILGEO TRKFILGLOW ;
SEQ_ASCII tdt_spc :: FILE 'AT10_2x2_cells_TSPC.dat' ;
SEQ_ASCII tdt_iso :: FILE 'AT10_2x2_cells_TISO.dat' ;
SEQ_ASCII glow_tiso.ps :: FILE 'AT10_2x2_TISO_glow.ps' ;
SEQ_ASCII glow_tspc.ps :: FILE 'AT10_2x2_TSPC_glow.ps' ;

SEQ_ASCII g2s_tdt ;
SEQ_ASCII geo_tiso.ps :: FILE 'AT10_2x2_TISO.ps' ;
SEQ_ASCII geo_tspc.ps :: FILE 'AT10_2x2_TSPC.ps' ;

REAL keffGEO keffGLOW ;
* ---
* TRACKING PARAMTERES
* ---

INTEGER an2d := 18 ;  ! ANGULAR QUADRATURE PARAMETER
REAL densur := 50.0 ; ! DENSITY OF INTEGRATION LINES CM^-1



REAL Rgap Rclad pitch := 0.4520 0.5140 1.295 ;
REAL Rcomb1 Rcomb2 Rcomb3 Rcomb4 := 0.313602 0.396678 0.43227 0.4435 ;

REAL 2_pitch ;
EVALUATE 2_pitch := 2.0 pitch * ;

GEOSPC := GEO: :: CAR2D 2 2
    X- REFL X+ REFL
    Y- REFL Y+ REFL
    CELL
    C1 C2
    C2 C4
    MESHX 0.0 <<pitch>> <<2_pitch>>
    MESHY 0.0 <<pitch>> <<2_pitch>>
    ::: C1 :=  GEO: CARCEL 6 
                RADIUS 0.0 <<Rcomb1>> <<Rcomb2>>
                <<Rcomb3>> <<Rcomb4>> <<Rgap>> <<Rclad>>
                MIX 1 1 1 1 4 5 6
                    MESHX 0.0 <<pitch>> MESHY 0.0 <<pitch>>
    ;
    ::: C2 := GEO: C1 MIX 2 2 2 2 4 5 6
    ;
    ::: C4 := GEO: C1 MIX 3 3 3 3 4 5 6
    ;
;

GEOISO := GEO: :: CAR2D 2 2
    X- ALBE 1.0 X+ ALBE 1.0
    Y- ALBE 1.0 Y+ ALBE 1.0
    CELL
    C1 C2
    C2 C4
    MESHX 0.0 <<pitch>> <<2_pitch>>
    MESHY 0.0 <<pitch>> <<2_pitch>>
    ::: C1 :=  GEO: CARCEL 6 
                RADIUS 0.0 <<Rcomb1>> <<Rcomb2>>
                <<Rcomb3>> <<Rcomb4>> <<Rgap>> <<Rclad>>
                MIX 1 1 1 1 4 5 6
                    MESHX 0.0 <<pitch>> MESHY 0.0 <<pitch>>
    ;
    ::: C2 := GEO: C1 MIX 2 2 2 2 4 5 6
    ;
    ::: C4 := GEO: C1 MIX 3 3 3 3 4 5 6
    ;
;

MACRO := MAC: ::
     EDIT 2 NGRO 1 NMIX 7 NIFI 1
     READ INPUT
     MIX 1 (*24UOX*)
        TOTAL 4.49087471E-01    SCAT 1 1 3.99089456E-01
        NUSIGF 6.24837950E-02      CHI 1.0
      MIX 2 (*32UOX*)
        TOTAL 4.55550849E-01    SCAT 1 1 3.98365676E-01
        NUSIGF 7.73072839E-02      CHI 1.0
      MIX 3 (*45UOX*)
        TOTAL 4.64771211E-01    SCAT 1 1 3.97388905E-01
        NUSIGF 9.81869251E-02      CHI 1.0
    MIX 4 (*GAP*)
        TOTAL 3.00460903E-04  SCAT 1 1 3.00460844E-04
     MIX 5 (*CLAD*)
        TOTAL 0.4029        SCAT 1 1 0.4000
     MIX 6 (*WATER*)
        TOTAL 0.3683        SCAT 1 1 0.3661
;

MACROG := MAC: ::
     EDIT 2 NGRO 1 NMIX 7 NIFI 1
     READ INPUT
     MIX 6 (*24UOX*)
        TOTAL 4.49087471E-01    SCAT 1 1 3.99089456E-01
        NUSIGF 6.24837950E-02      CHI 1.0
      MIX 4 (*32UOX*)
        TOTAL 4.55550849E-01    SCAT 1 1 3.98365676E-01
        NUSIGF 7.73072839E-02      CHI 1.0
      MIX 5 (*45UOX*)
        TOTAL 4.64771211E-01    SCAT 1 1 3.97388905E-01
        NUSIGF 9.81869251E-02      CHI 1.0
    MIX 3 (*GAP*)
        TOTAL 3.00460903E-04  SCAT 1 1 3.00460844E-04
     MIX 2 (*CLAD*)
        TOTAL 0.4029        SCAT 1 1 0.4000
     MIX 1 (*WATER*)
        TOTAL 0.3683        SCAT 1 1 0.3661
;

*** TISO case treatment ***


g2s_tdt geo_tiso.ps := G2S: GEOISO :: ;

glow_tiso.ps := G2S: tdt_iso :: ;

TRKGEO TRKFILGEO := SALT: g2s_tdt ::
     EDIT 2
     TITLE "TISO CP SURFACIC AT10_2x2 CASE - GEO:"
     TISO EQW <<an2d>> <<densur>>
     NOIC
     ;

TRKGLOW TRKFILGLOW := SALT: tdt_iso ::
     EDIT 2
     TITLE "TISO CP SURFACIC AT10_2x2 CASE - GLOW"
     TISO EQW <<an2d>> <<densur>>
     NOIC
     ;

* FULL PIJ MATRIX RECONSTRUCTION
* GEO reference
PIJ := ASM: MACRO TRKGEO TRKFILGEO :: PIJ EDIT 2 ;
FLUX := FLU: PIJ MACRO TRKGEO :: TYPE K ;
GREP: FLUX :: GETVAL 'K-EFFECTIVE' 1 1 1 >>keffGEO<< ;
PIJ FLUX := DELETE: PIJ FLUX ;
ECHO "test AT10_2x2 GEO: / TISO with FULL PIJ reconstruction completed, keff=" keffGEO ;

* GLOW test
PIJ := ASM: MACROG TRKGLOW TRKFILGLOW :: PIJ EDIT 2 ;
FLUX := FLU: PIJ MACROG TRKGLOW :: TYPE K ;
GREP: FLUX :: GETVAL 'K-EFFECTIVE' 1 1 1 >>keffGLOW<< ;
assertS FLUX :: 'K-EFFECTIVE' 1 <<keffGEO>> ;
PIJ FLUX := DELETE: PIJ FLUX ;
ECHO "test AT10_2x2 GLOW / TISO with FULL PIJ reconstruction completed, keff=" keffGLOW ;

g2s_tdt TRKGEO TRKFILGEO TRKGLOW TRKFILGLOW 
    := DELETE: g2s_tdt TRKGEO TRKFILGEO TRKGLOW TRKFILGLOW ;


*** TSPC case treatment ***

g2s_tdt geo_tspc.ps := G2S: GEOSPC :: ;

glow_tspc.ps := G2S: tdt_spc :: ;

TRKGEO TRKFILGEO := SALT: g2s_tdt ::
     EDIT 2
     TITLE "TSPC CP SURFACIC AT10_2x2 CASE - GEO:"
     TSPC EQW2 <<an2d>> <<densur>>
     ;

TRKGLOW TRKFILGLOW := SALT: tdt_spc ::
     EDIT 2
     TITLE "TSPC CP SURFACIC AT10_2x2 CASE - GLOW"
     TSPC EQW2 <<an2d>> <<densur>>
     ;

* FULL PIJ MATRIX RECONSTRUCTION
* GEO reference
PIJ := ASM: MACRO TRKGEO TRKFILGEO :: PIJ EDIT 2 ;
FLUX := FLU: PIJ MACRO TRKGEO :: TYPE K ;
GREP: FLUX :: GETVAL 'K-EFFECTIVE' 1 1 1 >>keffGEO<< ;
PIJ FLUX := DELETE: PIJ FLUX ;
ECHO "test AT10_2x2 GEO: / TSPC with FULL PIJ reconstruction completed, keff=" keffGEO ;

* GLOW test
PIJ := ASM: MACROG TRKGLOW TRKFILGLOW :: PIJ EDIT 2 ;
FLUX := FLU: PIJ MACROG TRKGLOW :: TYPE K ;
GREP: FLUX :: GETVAL 'K-EFFECTIVE' 1 1 1 >>keffGLOW<< ;
assertS FLUX :: 'K-EFFECTIVE' 1 <<keffGEO>> ;
PIJ FLUX := DELETE: PIJ FLUX ;
ECHO "test AT10_2x2 GLOW / TSPC with FULL PIJ reconstruction completed, keff=" keffGLOW ;



g2s_tdt TRKGEO TRKFILGEO TRKGLOW TRKFILGLOW 
    := DELETE: g2s_tdt TRKGEO TRKFILGEO TRKGLOW TRKFILGLOW ;
