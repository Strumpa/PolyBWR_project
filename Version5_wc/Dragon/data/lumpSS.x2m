*----
*
*  TEST CASE lumpSS_new
*  DISTRIBUTED SELF-SHIELDING ON LUMPED ASSEMBLIES
*  295-GROUP JEFF3.1.1 DRAGLIB
*
*  Author: A. Hebert
*
*----
*  Define STRUCTURES and MODULES used
*----
LINKED_LIST GEOM1 GEOM2 GEOM12 TRACK1 TRACK2 TRACK12 LIBRARY LIBRARY2A LIBRARY2B
            LIBRARY2 ASB FLUX EDIBU BURNUP MCOMPO ;
MODULE LIB: GEO: SYBILT: USS: ASM: FLU: COMPO: EDI: EVO: DELETE: END: ;
PROCEDURE assertS ;
REAL P_Zr90     P_Zr91     P_Zr92    P_Zr94    P_Zr96    :=
     51.45E-02  11.22E-02  17.15E-02 17.38E-02 2.80E-02  ;
REAL N_Zr0 := 4.3241E-2 ;
REAL N_Zr90       N_Zr91       N_Zr92       N_Zr94       N_Zr96    :=
     N_Zr0 P_Zr90 *  N_Zr0 P_Zr91 *  N_Zr0 P_Zr92 *  N_Zr0 P_Zr94 *
     N_Zr0 P_Zr96 *  ;
*----
* COMPO INIT
*----
MCOMPO := COMPO: ::
    EDIT 10
    STEP UP 'EDIBU_HOM'
     MAXCAL 5
     COMM 'Evolution burnup - Condensed - Assembly scale homogenized' ENDC
     PARA 'Burnup' IRRA
     ISOT 11 U235 U236 U238 Pu239 Pu240 Pu241 Pu242 Gd155 Gd157 Xe135 Sm149
    INIT
;
*----
* DRAGLIB PROCESSING
*----
LIBRARY := LIB: ::
 EDIT 1
 NMIX 11   (*MAXIMUM OF MATERIAL MIXTURES*)
 CTRA APOL (*APOLLO TYPE TRANSPORT CORRECTION*)
 ANIS 2    (*PROCESS SCATTERING DATA TO P1 ORDER*)
 PT        (*CALENDF TYPE PROBABILITY TABLES*)
*
 DEPL LIB: DRAGON FIL: DLIB_295
 MIXS LIB: DRAGON FIL: DLIB_295
 MIX 1 293.0
    O16     = O16    4.6624E-2
    U235    = U235   7.0803E-4 1
    U238    = U238   2.2604E-2 1
    Pu239   = Pu239  1.0000E-9 1
 MIX 2 COMB 1 1.0
 MIX 3 COMB 1 1.0
 MIX 4 COMB 1 1.0
 MIX 5 293.0
    Zr90    = Zr90   <<N_Zr90>> 2 IRSET PT NONE
    Zr91    = Zr91   <<N_Zr91>> 2 IRSET PT NONE
    Zr92    = Zr92   <<N_Zr92>> 2 IRSET PT NONE
    Zr94    = Zr94   <<N_Zr94>>
    Zr96    = Zr96   <<N_Zr96>>
 MIX 6 293.0 NOEV
    H1      = H1_H2O 4.6892E-2
    O16     = O16    2.3446E-2
 MIX 7 293.0
    O16     = O16    4.6624E-2
    U235    = U235   7.0803E-4 1
    U238    = U238   2.2604E-2 1
    Pu239   = Pu239  1.0000E-9 1
 MIX 8 COMB 7 1.0
 MIX 9 COMB 7 1.0
 MIX 10 COMB 7 1.0
 MIX 11 COMB 5 1.0
 ;
*----
* GEOMETRY DEFINITION AND TRACKING
*----
GEOM1 := GEO: :: CARCEL 6
  X- REFL X+ REFL    Y- REFL Y+ REFL
  MESHX 0.0 1.2  MESHY 0.0 1.2
  RADIUS 0.0 0.288712 0.365195 0.397962 0.4083 0.45 0.5748331
  MIX 1 2 3 4 5 6 6 ;
TRACK1 := SYBILT: GEOM1 ::
  MAXR 20 MAXZ 15000 DP00 QUA2 20 3 ;

GEOM2 := GEO: :: CARCEL 6
  X- REFL X+ REFL    Y- REFL Y+ REFL
  MESHX 0.0 1.2  MESHY 0.0 1.2
  RADIUS 0.0 0.288712 0.365195 0.397962 0.4083 0.45 0.5748331
  MIX 7 8 9 10 11 6 6 ;
TRACK2 := SYBILT: GEOM2 ::
  MAXR 20 MAXZ 15000 DP00 QUA2 20 3 ;

GEOM12 := GEO: :: CAR2D 2 1
  X- REFL X+ REFL    Y- REFL Y+ REFL
  CELL GEOM1 GEOM2
  ::: GEOM1 := GEO: CARCEL 6
      MESHX 0.0 1.2  MESHY 0.0 1.2
      RADIUS 0.0 0.288712 0.365195 0.397962 0.4083 0.45 0.5748331
      MIX 1 2 3 4 5 6 6 ;
  ::: GEOM2 := GEO: CARCEL 6
      MESHX 0.0 1.2  MESHY 0.0 1.2
      RADIUS 0.0 0.288712 0.365195 0.397962 0.4083 0.45 0.5748331
      MIX 7 8 9 10 11 6 6 ;
;
TRACK12 := SYBILT: GEOM12 ::
  MAXR 20 MAXZ 30000 DP00 QUA2 20 3 ;
*----
* RESONANCE SELF SHIELDING OVER EACH CELL
*----
LIBRARY2A := USS: LIBRARY TRACK1 ::
   EDIT 1 MAXST 100 PASS 2 PIJ
   CALC
    REGI W1 U235 ALL
    REGI W1 Pu239 ALL
   ENDC
   ;
LIBRARY2B := USS: LIBRARY TRACK2 ::
   EDIT 1 MAXST 100 PASS 2 PIJ
   CALC
    REGI W2 U235 ALL
    REGI W2 Pu239 ALL
   ENDC
   ;
*----
* CONSTRUCT THE SELF-SHIELDED MICROLIB
*----
LIBRARY2 := LIB: LIBRARY2A ::
  NMIX 11   (*MAXIMUM OF MATERIAL MIXTURES*)
  EDIT 2
  SKIP ! skip the macrolib construction
  CATL
  MIX 1 1
  MIX 2 2
  MIX 3 3
  MIX 4 4
  MIX 5 5
;
LIBRARY2 := LIB: LIBRARY2 LIBRARY ::
  EDIT 2
  SKIP ! skip the macrolib construction
  CATL
  MIX 6 6
;
LIBRARY2 := LIB: LIBRARY2 LIBRARY2B ::
  EDIT 2
  MACR ! activate the macrolib construction
  DEPL LIB: DRAGON FIL: DLIB_295
  CATL
  MIX 7 7
  MIX 8 8
  MIX 9 9
  MIX 10 10
  MIX 11 11
;
LIBRARY2A LIBRARY2B := DELETE: LIBRARY2A LIBRARY2B ;
*----
* FLUX CALCULATION
*----
ASB := ASM: LIBRARY2 TRACK12 :: PIJ ;
FLUX := FLU: LIBRARY2 TRACK12 ASB :: TYPE K ;
assertS FLUX :: K-EFFECTIVE 1 1.329498 ;
*----
* EDITION
*----   
EDIBU := EDI: FLUX LIBRARY2 TRACK12 ::
   EDIT 0
   MICR 11 U235 U236 U238 Pu239 Pu240 Pu241 Pu242 Gd155 Gd157 Xe135 Sm149
   MERG MIX
   1   1   1   1   0   0   1   1   1   1   0  
   COND
   SAVE ON FUEL ;
*----
* BURNUP CALCULATION
*----
BURNUP LIBRARY2 := EVO: LIBRARY2 FLUX TRACK12 ::
   EDIT 3
   DEPL 0.0 10.0 DAY POWR 39.0 EXPM 1.0E15 GLOB
   ;
*----
* COMPO
*----
MCOMPO := COMPO: MCOMPO EDIBU BURNUP LIBRARY2 ::
   EDIT 0
   STEP UP 'EDIBU_HOM'
   SET 0.0 DAY ;
*----
* FLUX CALCULATION
*----
ASB := DELETE: ASB ;
ASB := ASM: LIBRARY2 TRACK12 :: PIJ ;
FLUX := FLU: FLUX LIBRARY2 TRACK12 ASB :: TYPE K ;
assertS FLUX :: K-EFFECTIVE 1 1.272455 ;
*----
* BURNUP FLUX NORMALIZATION AT END-OF-STAGE
*----
BURNUP LIBRARY2 := EVO: BURNUP LIBRARY2 FLUX TRACK12 ::
   EDIT 3
   SAVE 10.0 DAY POWR 39.0
   ;
*----
* EDITION
*----
EDIBU := DELETE: EDIBU ;
EDIBU := EDI: FLUX LIBRARY2 TRACK12 ::
   EDIT 5
   MICR 11 U235 U236 U238 Pu239 Pu240 Pu241 Pu242 Gd155 Gd157 Xe135 Sm149
   MERG MIX
   1   1   1   1   0   0   1   1   1   1   0  
   COND
   SAVE ON FUEL ;
*----
* COMPO
*----
MCOMPO := COMPO: MCOMPO EDIBU BURNUP LIBRARY2 ::
   EDIT 0
   STEP UP 'EDIBU_HOM'
   SET 10.0 DAY ;
ECHO "test lumpSS_new completed" ;
END: ;
