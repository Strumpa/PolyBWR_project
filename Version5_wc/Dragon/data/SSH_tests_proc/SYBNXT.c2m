* Cle2000 procedure : SYBNXT.c2m
* Author : R. Guasch
* Surpose : SYBILT+NXT tracking on pincell of "geoname" 
*           with SSH method "ssh_opt" 
*

************************************************************************
*********************** BEGIN DECLARATION ******************************
************************************************************************
MODULE SYBILT: NXT: MCCGT: ASM: FLU: GREP: DELETE: END: ;
PROCEDURE Geom_C Mix_C1 Mix_C7 SYB_AUTOP1 SYB_AUTOP7 ;
LINKED_LIST GEOM GEOM_SS LIBRARY LIBRARY2 TRACK_SS TRACK SYS FLUX ;
SEQ_BINARY TF_EXC ;
SEQ_ASCII CELL_SS ;
SEQ_ASCII CELL ;


REAL Kinf ;
STRING geo_name ssh_opt ;
:: >>geo_name<< >>ssh_opt<< ;


ECHO "BEGIN SYBSYB.c2m PROCEDURE FOR OPTIONS" geo_name " and " ssh_opt ;


************************************************************************
************************  BEGIN GEOMETRY DEFINITION ********************

GEOM GEOM_SS := Geom_C :: <<geo_name>> ;

*************************** RETRIVE DRAGLIB ****************************
IF geo_name "AT10_UOX" = THEN
    LIBRARY := Mix_C1 :: J311_295 <<ssh_opt>> ;
ELSEIF geo_name "AT10_UOX_Gd2O3" = THEN
    LIBRARY := Mix_C7 :: J311_295 <<ssh_opt>> ;
ENDIF ;

***********************  BEGIN GEOMETRY TRACKING **********************

* ---
* PARAMETERS FOR NXT TRACKING
* ---
INTEGER an2d := 18 ;  ! ANGULAR QUADRATURE PARAMETER
REAL densur := 35.0 ; ! DENSITY OF INTEGRATION LINES CM^-1

* ---
* PARAMETERS FOR MCCGT TRACKING
* ---
* polar angles for MCCGT:
INTEGER nmu := 4 ;

* size allocation for MCCGT:
INTEGER mcu := 50000 ;

* ---
* TRACKING PARAMETERS FOR SYBILT SSH
* ---
INTEGER iqua2_ss := 24 ;
INTEGER nseg_ss := 5 ;

* ---
* FLUX CALC TRACKING
* ---

TRACK TF_EXC := NXT: GEOM ::
  EDIT 0
  ALLG
  ANIS 3
  TSPC EQW2 <<an2d>> <<densur>> REND
;
* MOC tracking
TRACK := MCCGT: TRACK TF_EXC ::
  EDIT 0
  CACB <<nmu>> AAC 80 TMT EPSI 1E-5 MCU <<mcu>>
;


* ---
* SELF-SHIELDING TRACKING
* ---

TRACK_SS := SYBILT: GEOM_SS ::
    TITLE 'TRACKING FOR PINCELL SS'
    MAXR 50 QUA2 <<iqua2_ss>> <<nseg_ss>> DP01 MAXZ 200000 
;

************************ BEGIN SELF-SHIELDING **************************

IF geo_name "AT10_UOX" = THEN
    LIBRARY2 := SYB_AUTOP1 LIBRARY TRACK_SS :: <<ssh_opt>> ;
ELSEIF geo_name "AT10_UOX_Gd2O3" = THEN
    LIBRARY2 := SYB_AUTOP7 LIBRARY TRACK_SS :: <<ssh_opt>> ;
ENDIF ;



************************ BEGIN FLUX CALCULATION ************************
SYS := ASM: LIBRARY2 TRACK :: ARM EDIT 0 ; 

FLUX := FLU: LIBRARY2 SYS TRACK :: 
        EDIT 1 TYPE K ;

GREP: FLUX :: GETVAL 'K-EFFECTIVE  ' 1 1 1 >>Kinf<< ;
ECHO  "END SYBNXT.c2m with : " ssh_opt "calculation with tracking : SSH = SYBILT, FLUX = NXT, MOC Kinf=" Kinf ;


END: ;

QUIT .