* Cle2000 procedure : SYBSYB.c2m
* Author : R. Guasch
* Surpose : SYBILT+SYBILT tracking on pincell of "geoname" 
*           with SSH method "ssh_opt" 
*

************************************************************************
*********************** BEGIN DECLARATION ******************************
************************************************************************
MODULE SYBILT: ASM: FLU: GREP: DELETE: END: ;
PROCEDURE Geom_C Mix_C1 Mix_C7 SYB_AUTOP1 SYB_AUTOP7 ;
LINKED_LIST GEOM GEOM_SS LIBRARY LIBRARY2 TRACK_SS TRACK SYS FLUX ;

SEQ_ASCII CELL_SS ;
SEQ_ASCII CELL ;


REAL Kinf ;
STRING geo_name ssh_opt ;
:: >>geo_name<< >>ssh_opt<< ;


ECHO "BEGIN SYBSYB.c2m PROCEDURE FOR OPTIONS" geo_name "and" ssh_opt ;


************************************************************************
************************  BEGIN GEOMETRY DEFINITION ********************

GEOM GEOM_SS := Geom_C :: <<geo_name>> ;

*************************** RETRIVE DRAGLIB ****************************
IF geo_name "AT10_UOX" = THEN
    LIBRARY := Mix_C1 :: J311_295 <<ssh_opt>> ;
ELSEIF geo_name "AT10_UOX_Gd2O3" = THEN
    LIBRARY := Mix_C7 :: J311_295 <<ssh_opt>> ;
ENDIF ;

***********************  BEGIN GEOMETRY TRACKING **********************

* ---
* TRACKING PARAMETERS FOR PIJ FLX with SYBILT:
* ---
INTEGER iqua2 := 32 ;  ! ANGULAR QUADRATURE PARAMETER
INTEGER nseg := 10 ; ! NUMBER OF INTEGRATION LINES

* ---
* TRACKING PARAMETERS FOR SSH
* ---
INTEGER iqua2_ss := 24 ;
INTEGER nseg_ss := 5 ;

* ---
* FLUX CALC TRACKING
* ---

TRACK := SYBILT: GEOM ::
    MAXR 50 QUA2 <<iqua2>> <<nseg>> DP01 MAXZ 200000
;


* ---
* SELF-SHIELDING TRACKING
* ---

TRACK_SS := SYBILT: GEOM_SS ::
    TITLE 'TRACKING FOR PINCELL SS'
    MAXR 50 QUA2 <<iqua2_ss>> <<nseg_ss>> DP01 MAXZ 200000 
;

************************ BEGIN SELF-SHIELDING **************************

IF geo_name "AT10_UOX" = THEN
    LIBRARY2 := SYB_AUTOP1 LIBRARY TRACK_SS :: <<ssh_opt>> ;
ELSEIF geo_name "AT10_UOX_Gd2O3" = THEN
    LIBRARY2 := SYB_AUTOP7 LIBRARY TRACK_SS :: <<ssh_opt>> ;
ENDIF ;



************************ BEGIN FLUX CALCULATION ************************
SYS := ASM: LIBRARY2 TRACK :: PIJ EDIT 0 ; 

FLUX := FLU: LIBRARY2 SYS TRACK :: 
        EDIT 1 TYPE K ;

GREP: FLUX :: GETVAL 'K-EFFECTIVE  ' 1 1 1 >>Kinf<< ;
ECHO  "END SYBSYB.c2m with : " ssh_opt "calculation with tracking : SSH = SYBILT, FLUX = SYBILT, PIJ Kinf=" Kinf ;
ECHO geo_name ;

END: ;

QUIT .