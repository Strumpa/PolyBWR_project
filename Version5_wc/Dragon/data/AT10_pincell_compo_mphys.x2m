************************************************************************************************************************
*                                                                                                                      *
* Procedure :  AT10_pincell_compo_mphys.x2m                                                                            *
* Purpose   :  Generate COMPO for BWR multi-physics coupled iterations in DONJON5/THM:                                 *
* Author    :  R. Guasch                                                                                               *
*                                                                                                                      *
* CALL      :  ./rdragon AT10_pincell_compo_mphys.x2m                                                                  *
*                                                                                                                      *
************************************************************************************************************************

PROCEDURE Geom_C Geom_C_SALT Mix_UOX TRK_C_SALT TRK_C_SYBNXT TRK_C_NXTNXT Mix_AUTOP1 Mix_AUTOP2 Mix_AUTOP3 ; 

************************************************************************
*                          BEGIN DECLARATION                           *
************************************************************************
MODULE UTL: GREP: DELETE: COMPO: USS: ASM: FLU: EDI: END: ;
LINKED_LIST GEOM GEOM_SS LIBRARY TRACK_SS TRACK SYS FLUX FIG LIBRARY2 ;
LINKED_LIST COMPO EDIOBJ PARAMS ;
SEQ_ASCII CELL_SS ;
SEQ_ASCII CELL ;
SEQ_BINARY TF_EXC ;
SEQ_BINARY TF_EXC_SS ;

STRING geo_name := "AT10_UOX" ; 
STRING ssh_opt := "RSE" ; ! "PT", "RSE", "SUBG"
STRING track_opt := "SALT" ; ! "SYBNXT" "SALT" "NXTNXT"

SEQ_ASCII FIG_SS.ps :: FILE './PIN_AT10_FIG_SS.ps' ;
SEQ_ASCII FIG_FLX.ps :: FILE './PIN_AT10_FIG_FLX.ps' ;
SEQ_ASCII _COMPO :: FILE './COMPO_AT10_MPHYS' ;

REAL Kinf ;

************************************************************************
*                      BEGIN PARAMETER DEFINITION                      *
************************************************************************
INTEGER iAutopMax := 3 ;
INTEGER fuel_t_pts := 7 ;
INTEGER cool_t_pts := 5 ;
INTEGER cool_d_pts := 9 ;
PARAMS := UTL: ::
        CREA
            fuel_temp <<fuel_t_pts>> = 
                500.0 800.0 1000.0 1200.0 1500.0 1800.0 2200.0
        CREA 
            cool_temp <<cool_t_pts>> =
                500.0 600.0 650.0 700.0 750.0
        CREA
            cool_dens <<cool_d_pts>> =
                0.8 0.7 0.6 0.5 0.4 0.3 0.2 0.1 0.01
;



************************************************************************
*                            END DECLARATION                           *
************************************************************************


************************************************************************
*                          GEOMETRY DEFINITION                         *
************************************************************************
IF track_opt "SALT" = THEN
        GEOM GEOM_SS := Geom_C_SALT :: <<geo_name>> ; 
ELSEIF track_opt "NXTNXT" = THEN
        GEOM GEOM_SS := Geom_C :: <<geo_name>> ;
ELSEIF track_opt "SYBNXT" = THEN
        GEOM GEOM_SS := Geom_C :: <<geo_name>> ;

ENDIF ;

************************************************************************
*                             TRACKING                                 *
************************************************************************
IF track_opt "SALT" = THEN 
        CELL FIG_FLX.ps TRACK TF_EXC CELL_SS FIG_SS.ps TRACK_SS TF_EXC_SS := TRK_C_SALT GEOM GEOM_SS ;
ELSEIF track_opt "SYBNXT" = THEN
        TRACK TF_EXC TRACK_SS := TRK_C_SYBNXT GEOM GEOM_SS ;
ELSEIF track_opt "NXTNXT" = THEN
        TRACK TF_EXC TRACK_SS TF_EXC_SS := TRK_C_NXTNXT GEOM GEOM_SS ;
ENDIF ;


************************************************************************
*                          COMPO INITIALIZATION                        *
************************************************************************
COMPO := COMPO: ::
    EDIT 0
    STEP UP 'EDIPARAM'
    MAXCAL 5
    COMM 'Multiphysics pincell calculation' ENDC
    !PARA 'Burnup' IRRA
    PARA 'TFuel' VALU REAL
    PARA 'TCool' VALU REAL
    PARA 'DCool' VALU REAL
    ISOT 11 U235 U236 U238 Pu239 Pu240 Pu241 Pu242 Gd155 Gd157 Xe135 Sm149
    INIT
;
************************************************************************
*                       MAIN LOOP OVER SCENARIOS                       *
************************************************************************
REAL TFuel ;
REAL TCool ;
REAL DCool ;
INTEGER Tfuel_step := 0 ;
INTEGER Tcool_step := 0 ;
INTEGER Dcool_step := 0 ;
WHILE Tfuel_step fuel_t_pts < DO
    EVALUATE Tfuel_step := Tfuel_step 1 + ;
    WHILE Tcool_step cool_t_pts < DO
        EVALUATE Tcool_step := Tcool_step 1 + ;
        WHILE Dcool_step cool_d_pts < DO
            EVALUATE Dcool_step := Dcool_step 1 + ;
            ECHO "DCool_step =" Dcool_step ", Tcool_step =" Tcool_step ", Tfuel_step=" Tfuel_step ;
            GREP: PARAMS :: GETVAL 'fuel_temp' <<Tfuel_step>> >>TFuel<< ;
            GREP: PARAMS :: GETVAL 'cool_temp' <<Tcool_step>> >>TCool<< ;
            GREP: PARAMS :: GETVAL 'cool_dens' <<Dcool_step>> >>DCool<< ;
            ECHO "Begin State point calculation TFuel =" TFuel ", TCool =" TCool ", H2O density =" DCool ; 

            LIBRARY := Mix_UOX :: J311_295 <<ssh_opt>> <<TFuel>> <<TCool>> <<DCool>> ;
            ECHO "after LIB" ;
            IF track_opt "SALT" = THEN
                    LIBRARY2 := Mix_AUTOP1 LIBRARY TRACK_SS TF_EXC_SS :: <<ssh_opt>> <<track_opt>> ;
            ELSEIF track_opt "SYBNXT" = THEN
                    LIBRARY2 := Mix_AUTOP2 LIBRARY TRACK_SS :: <<ssh_opt>> <<track_opt>> ;
            ELSEIF track_opt "NXTNXT" = THEN
                    LIBRARY2 := Mix_AUTOP3 LIBRARY TRACK_SS TF_EXC_SS :: <<ssh_opt>> <<track_opt>> ;
            ENDIF ;


            SYS := ASM: LIBRARY2 TRACK TF_EXC :: ARM EDIT 0 ; 

            FLUX := FLU: LIBRARY2 SYS TRACK TF_EXC :: 
                    EDIT 1 TYPE K B1 SIGS ;

            GREP: FLUX :: GETVAL 'K-EFFECTIVE  ' 1 1 1 >>Kinf<< ;
            ECHO "End of State point calculation TFuel =" TFuel ", TCool =" TCool ", H2O density =" DCool ; 
            ECHO track_opt " tracking, MOC on AT10 C1: Kinf=" Kinf ;

            ************************************************************************
            *  2-GROUPS ENERGY CONDENSATION AND HOMOGENISATION                     *
            ************************************************************************
            EDIOBJ := EDI: FLUX LIBRARY2 TRACK ::
                    EDIT 0
                    MICR 11 U235 U236 U238 Pu239 Pu240 Pu241 Pu242 Gd155 Gd157 Xe135 Sm149
                    MERG COMP
                    COND 0.625
                    SAVE ON EDIPARAM 
                ;
            
            ************************************************************************
            *  INITIAL MULTICOMPO FEEDING                                          *
            ************************************************************************
            COMPO := COMPO: COMPO EDIOBJ LIBRARY ::
                    EDIT 2
                    STEP UP 'EDIPARAM'
                    'TFuel' <<TFuel>>
                    'TCool' <<TCool>>
                    'DCool' <<DCool>>
                ;
            LIBRARY LIBRARY2 := DELETE: LIBRARY LIBRARY2 ;
            FLUX := DELETE: FLUX ;
            SYS := DELETE: SYS ;
            EDIOBJ := DELETE: EDIOBJ ;
        ENDWHILE ;
        EVALUATE Dcool_step := 0 ;
    ENDWHILE ;
    EVALUATE Tcool_step := 0 ;
ENDWHILE ;

_COMPO := COMPO ;

END: ;
QUIT .

