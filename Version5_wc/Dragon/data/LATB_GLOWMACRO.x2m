*** AT10_LAT_MODEBOX case with CP/IC/MOC solution : 
* BWR LATTICEB array based on ATRIUM-10 geometry, 
* Presence of moderator box
* import tdt_data generated by glow.
* Solve 1 group CP/IC/MOC problem to ensure proper material numbering treatment
* issue : GLOW/DRAGON material ordering match, but SALOME re-writes material ids using its internal system.
* Macroscpic cross sections from ATRIUM-10 assembly pins. 
* self-shielded and solved in DRAGON : endfb8r1 - SHEM295
* Homogenized in fuel and condensed to 1 group.


MODULE GEO: MAC: G2S: SALT: MCCGT: ASM: FLU: GREP: DELETE: END: ;
PROCEDURE assertS ;
LINKED_LIST GEOISO GEOSPC MACRO MACROG TRACKIC TRACKCP TRACKMOC PIJ SYS FLUX ;

SEQ_BINARY TRACKFILIC TRACKFILCP TRACKFILMOC ;
SEQ_ASCII tdt_isoM :: FILE 'AT10_lattice_MODEBOX_TISO_MACRO.dat' ;
SEQ_ASCII glowMACRO.ps :: FILE 'AT10_lattice_MODEBOX_TISO_glow_MACRO.ps' ;


REAL keffGEOCP keffGEOIC keffGEOMOC ;
REAL keffGLOWCP keffGLOWIC keffGLOWMOC ;
REAL delCPIC delMOCIC ;

* ---
* TRACKING PARAMTERES
* ---
ECHO "BEGIN AT10_LAT_MODEBOX multicell CASE" ;

INTEGER an2d := 24 ;  ! ANGULAR QUADRATURE PARAMETER
REAL densur := 100.0 ; ! DENSITY OF INTEGRATION LINES CM^-1


!#  1 - MODERATOR
!#  2 - MODERATOR_BOX
!#  3 - COOLANT
!#  4 - CLAD
!#  5 - GAP
!#  6 - 24UOX
!#  7 - 32UOX
!#  8 - 42UOX
!#  9 - 45UOX
!# 10 - 42Gd
!# 11 - 45Gd
!# 12 - 50UOX
!# 13 - 48UOX

MACROG := MAC: ::
     EDIT 2 NGRO 1 NMIX 13 NIFI 1
     READ INPUT
     MIX 6 (*24UOX*)
        TOTAL 4.43890840E-01    SCAT 1 1 3.99300873E-01
        NUSIGF 5.23696020E-02      CHI 1.0

      MIX 7 (*32UOX*)
        TOTAL 4.47801262E-01    SCAT 1 1 3.98593932E-01
        NUSIGF 6.20270856E-02      CHI 1.0

      MIX 8 (*42UOX*)
        TOTAL 4.50964361E-01    SCAT 1 1 3.97878230E-01
        NUSIGF 7.01253936E-02      CHI 1.0

      MIX 9 (*45UOX*)
        TOTAL 4.52631325E-01    SCAT 1 1 3.98060858E-01
        NUSIGF 7.29189515E-02      CHI 1.0

      MIX 13 (*48UOX*)
        TOTAL 4.53984380E-01    SCAT 1 1 3.97542506E-01
        NUSIGF 7.79897496E-02      CHI 1.0

      MIX 12 (*50UOX*)
        TOTAL 4.53644007E-01    SCAT 1 1 3.96933615E-01
        NUSIGF 7.77277201E-02      CHI 1.0
      
      MIX 11 (*45Gd*)
        TOTAL 5.22946239E-01    SCAT 1 1 3.99194658E-01
        NUSIGF 3.58763784E-02      CHI 1.0
      
      MIX 10 (*42Gd*)
        TOTAL 5.12946239E-01    SCAT 1 1 3.99194658E-01
        NUSIGF 3.48763784E-02      CHI 1.0
    
      MIX 5 (*GAP*)
        TOTAL 3.12748462E-01  SCAT 1 1 3.10926110E-01
      MIX 4 (*CLAD*)
        TOTAL 2.88040959E-04        SCAT 1 1 2.88040930E-04
      MIX 3 (*COOLANT*)
        TOTAL 7.66823292E-01        SCAT 1 1 7.65839636E-01
      MIX 1 (*MODERATOR*)
        TOTAL 7.66823292E-01        SCAT 1 1 7.65839636E-01
      MIX 2 (*MODERATOR BOX*)
        TOTAL 2.88040959E-04        SCAT 1 1 2.88040930E-04
;


*** TISO case treatment ***

glowMACRO.ps := G2S: tdt_isoM :: ;

*----
ECHO " --- GLOW+MACRO CASES TREATMENT --- " ;
*** TREAT ALL GLOW CASES SECOND ***

TRACKIC TRACKFILIC := SALT: tdt_isoM ::
     EDIT 2
     TITLE "MULTICELL SURFACIC AT10_LAT_MODEBOX GLOW+MACRO CASE"
     TISO <<an2d>> <<densur>>
     IC EPSJ 1.0E-6
     ;

TRACKCP TRACKFILCP := SALT: tdt_isoM ::
     EDIT 2
     TITLE "TISO CP SURFACIC AT10_LAT_MODEBOX GLOW+MACRO CASE"
     TISO <<an2d>> <<densur>>
     NOIC
     ;

TRACKMOC TRACKFILMOC := SALT: tdt_isoM ::
     EDIT 2
     TITLE "TISO MOC SURFACIC AT10_LAT_MODEBOX GLOW+MACRO CASE"
     TISO EQW <<an2d>> <<densur>>
     NOIC
     ;

* MOC tracking
TRACKMOC := MCCGT: TRACKMOC TRACKFILMOC ::
  EDIT 0
;

* ---
* Flux calculation
* ---

* FULL PIJ MATRIX RECONSTRUCTION
PIJ := ASM: MACROG TRACKIC TRACKFILIC :: PIJ EDIT 2 ;
FLUX := FLU: PIJ MACROG TRACKIC :: TYPE K ;
GREP: FLUX :: GETVAL 'K-EFFECTIVE' 1 1 1 >>keffGLOWCP<< ;
PIJ FLUX := DELETE: PIJ FLUX ;
ECHO "test multicell AT10_LAT_MODEBOX (GLOW+MACRO) FULL PIJ reconstruction completed, keff=" keffGLOWCP ;

* FLUX-CURRENT ITERATION WITHOUT FULL PIJ MATRIX RECONSTRUCTION
PIJ := ASM: MACROG TRACKIC TRACKFILIC :: ARM EDIT 2 ;
FLUX := FLU: PIJ MACROG TRACKIC :: EDIT 4 TYPE K ;
GREP: FLUX :: GETVAL 'K-EFFECTIVE' 1 1 1 >>keffGLOWIC<< ;
assertS FLUX :: 'K-EFFECTIVE' 1 <<keffGLOWCP>> ;
PIJ FLUX := DELETE: PIJ FLUX ;
ECHO "test multicell AT10_LAT_MODEBOX (GLOW+MACRO) FLUX-CURRENT iteration completed, keff=" keffGLOWIC ;

PIJ := ASM: MACROG TRACKCP TRACKFILCP :: PIJ EDIT 2 ;
FLUX := FLU: PIJ MACROG TRACKCP :: TYPE K ;
GREP: FLUX :: GETVAL 'K-EFFECTIVE' 1 1 1 >>keffGLOWCP<< ;
PIJ FLUX := DELETE: PIJ FLUX ;
ECHO "test multicell AT10_LAT_MODEBOX (GLOW+MACRO) CP completed, keff=" keffGLOWCP ;

EVALUATE delCPIC := keffGLOWCP keffGLOWIC - 1E+5 * ;
ECHO "With GLOW+MACRO: difference between CP and IC keff = " delCPIC ;

SYS := ASM: MACROG TRACKMOC TRACKFILMOC :: ARM EDIT 2 ; 
FLUX := FLU: SYS MACROG TRACKMOC TRACKFILMOC :: EDIT 1 TYPE K ;
GREP: FLUX :: GETVAL 'K-EFFECTIVE' 1 1 1 >>keffGLOWMOC<< ;
ECHO "test multicell AT10_LAT_MODEBOX (GLOW+MACRO) MOC completed, keff=" keffGLOWMOC ;

EVALUATE delMOCIC := keffGLOWMOC keffGLOWIC - 1E+5 * ;
ECHO "With GLOW+MACRO: difference between MOC and IC keff = " delMOCIC ;

FLUX SYS := DELETE: FLUX SYS ;
TRACKIC TRACKFILIC := DELETE: TRACKIC TRACKFILIC ;
TRACKCP TRACKFILCP := DELETE: TRACKCP TRACKFILCP ;
TRACKMOC TRACKFILMOC := DELETE: TRACKMOC TRACKFILMOC ;

ECHO "END OF AT10_LAT_MODEBOX multicell CASE" ;
END: ;




