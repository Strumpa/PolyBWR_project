* Deck TRTM_TDCM32
* CLE-2000 procedure
* Author : R. Guasch, adapted from Dragon5 test case salmacro_proc/TDCM32.c2m
* Getting 3 level geometry as input, processing with SALT: and saving tracks with TLM
* Making use of assertS
*

* ---
* INPUT & OUTPUT PARAMETERS
* ---
PARAMETER GEOM ::
::: LINKED_LIST GEOM ; ;


*----
*  Define STRUCTURES and MODULES used
*----
LINKED_LIST MACRO TRACK PIJ FLUX ;
MODULE MAC: G2S: SALT: TLM: ASM: FLU: END: ;
SEQ_BINARY TRACK_FIL ;
SEQ_ASCII Fic_SAL ;
SEQ_ASCII Fic_SAL.ps :: FILE './TDCM32_FIG.ps' ;
PROCEDURE assertS ;

SEQ_ASCII TLM_F :: FILE 'tracks_SALT_TDCM32.m' ;

*
MACRO := MAC: ::
     EDIT 2 NGRO 1 NMIX 5 NIFI 1
     READ INPUT
     MIX 1 (*COMBUSTIBLE*)
        TOTAL 0.3652    SCAT 1 1 0.3234
        NUSIGF 0.05564      CHI 1.0
     MIX 2 (*GAINE*)
        TOTAL 0.4029        SCAT 1 1 0.4000
     MIX 3 (*EAU*)
        TOTAL 0.3683        SCAT 1 1 0.3661
;
Fic_SAL Fic_SAL.ps := G2S: GEOM ;

TRACK TRACK_FIL := SALT: Fic_SAL ::
     EDIT 1
     TSPC 4 10.0
     LONG
;
TLM_F := TLM: TRACK TRACK_FIL ::
  EDIT 10
  NTPO 1
  DIRECTIONS
  DIR 0
;

PIJ := ASM: MACRO TRACK TRACK_FIL :: PIJ ;

FLUX := FLU: MACRO PIJ TRACK TRACK_FIL :: TYPE K ;

assertS FLUX :: 'K-EFFECTIVE' 1 1.040070 ;

ECHO "test TDCM32 completed + TLM tracking saved" ;
END: ;
