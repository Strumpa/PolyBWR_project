*----
*  TEST CASE TRDR49
*  5 X 5 PWR ASSEMBLY WITH MULTICELL SURFACIC APPROXIMATION
*  172-GROUP JEF2.2 DRAGLIB
*
*----
*  Define STRUCTURES and MODULES used
*----
LINKED_LIST
  ASSMB DISCR LIBRARY LIBRARY2 CP CALC ;
MODULE
  LIB: GEO: G2S: SYBILT: SALT: USS: ASM: FLU: DELETE: END: ;
PROCEDURE assertS ;
SEQ_ASCII Fic_SAL :: FILE './Fic_SAL_TCDR49.tdt' ;
SEQ_ASCII Fic_EPS.eps :: FILE './Fic_EPS_TCDR49.eps' ;
SEQ_BINARY DISCR_FIL ;
*----
*  Microscopic cross sections from Jef 2.2 Draglib
*----
LIBRARY := LIB: ::
  NMIX 8 CTRA WIMS
  SUBG
  MIXS LIB: DRAGON FIL: DLIB_J2
  MIX 1 579.9
    H1H2O    = H1_H2O   4.76690E-2   O16H2O   = O16      2.38345E-2
    B10      = B10      4.71444E-6   B11      = B11      1.90959E-5
  MIX 2 579.9                        O16      = O16      3.06711E-4
    Cr52     = Cr52     7.54987E-5   Fe56     = Fe56     1.47624E-4
    Zr91     = Zr0      4.18621E-2
  MIX 3 579.9
    H1H2O    = H1_H2O   4.65292E-2   O16H2O   = O16      2.32646E-2
                                     Cr52     = Cr52     4.79927E-5
    Fe56     = Fe56     4.45845E-5   Ni58     = Ni58     1.13521E-4
                                     MoNat    = Mo95     4.03755E-6
                                     Al27     = Al27     2.35231E-6
    Mn55     = Mn55     4.15901E-7
    B10      = B10      4.60867E-6   B11      = B11      1.86674E-5
    Zr91     = Zr0      8.92427E-4
  MIX 4 579.9                        O16      = O16      2.87335E-4
    Cr52     = Cr52     7.07291E-5   Fe56     = Fe56     1.38298E-4
    Zr91     = Zr0      3.92175E-2
  MIX 5 579.9
    H1H2O    = H1_H2O   4.71346E-2   O16H2O   = O16      2.35673E-2
                                     Cr52     = Cr52     2.24991E-5
    Fe56     = Fe56     2.09013E-5   Ni58     = Ni58     5.32188E-5
                                     MoNat    = Mo95     1.89281E-6
                                     Al27     = Al27     1.10277E-6
    Mn55     = Mn55     1.94976E-7
    B10      = B10      4.66484E-6   B11      = B11      1.88950E-5
    Zr91     = Zr0      4.18372E-4
  MIX 6 579.9
    H1H2O    = H1_H2O   4.71676E-2   O16H2O   = O16      2.35838E-2
                                     Cr52     = Cr52     2.11122E-5
    Fe56     = Fe56     1.96130E-5   Ni58     = Ni58     4.99383E-5
                                     MoNat    = Mo95     1.77614E-6
                                     Al27     = Al27     1.03479E-6
    Mn55     = Mn55     1.82957E-7
    B10      = B10      4.66791E-6   B11      = B11      1.89074E-5
    Zr91     = Zr0      3.92583E-4
  MIX 7 579.9
    H1H2O    = H1_H2O   4.72020E-2   O16H2O   = O16      2.36010E-2
                                     Cr52     = Cr52     1.96591E-5
    Fe56     = Fe56     1.82630E-5   Ni58     = Ni58     4.65011E-5
                                     MoNat    = Mo95     1.65389E-6
                                     Al27     = Al27     9.63569E-7
    Mn55     = Mn55     1.70365E-7
    B10      = B10      4.67110E-6   B10      = B10      1.89203E-5
    Zr91     = Zr0      3.65562E-4
  MIX 8 933.6                        O16      = O16      4.49355E-2
    U235     = U235     7.39237E-4 1 
    U238     = U238     2.17285E-2 1 
  ;
*----
*  Geometry ASSMB : a 17 X 17 normal PWR assembly
*  contains  C1 : cell without fuel
*            C2 : normal fuel cell
*            C3 : peripheral cell
*            C4 : corner cell
*----
ASSMB := GEO: :: CAR2D 3 3
  X- DIAG X+ ALBE 1.0 Y- SYME Y+ DIAG
  CELL C1 C2 C3
          C2 C3
             C4
  ::: C1 := GEO: CARCEL 2
    MESHX 0.0 1.26472 MESHY 0.0 1.26472
    RADIUS 0.0 0.572435 0.613142  MIX 1 2 3 ;
  ::: C2 := GEO: C1  RADIUS 0.0 0.412660 0.474364  MIX 8 4 5 ;
  ::: C3 := GEO: C2  MESHX 0.0 1.31472 MIX 8 4 6 ;
  ::: C4 := GEO: C3  MESHY 0.0 1.31472  MIX 8 4 7 ;
  ;
*----
*  Self-Shielding calculation SYBIL
*  Transport calculation      SYBIL
*  Flux calculation for B1 homogeneous leakage
*----
DISCR := SYBILT: ASSMB ::
  TITLE 'TCDR49: 5 X 5 MULTICELL 5x5 CASE WITH SYBIL'
  MAXR 100 QUA2 6 3 ;
LIBRARY2 := USS: LIBRARY DISCR :: EDIT 2 PASS 2 ARM ;
CP := ASM: LIBRARY2 DISCR :: ARM ;
CALC := FLU: CP LIBRARY2 DISCR :: TYPE K B1 SIGS ;
assertS CALC :: 'K-INFINITY' 1 1.233395 ;
DISCR LIBRARY2 CP CALC := DELETE: DISCR LIBRARY2 CP CALC ;
*----
*  Self-Shielding calculation SALT WITH MULTICELL SURFACIC
*  Transport calculation      SALT WITH MULTICELL SURFACIC
*  Flux calculation for B1 homogeneous leakage
*----
Fic_SAL Fic_EPS.eps := G2S: ASSMB :: MACRO ;

DISCR DISCR_FIL := SALT: Fic_SAL ::
     EDIT 3
     TITLE "TCDR49: MULTICELL SURFACIC 5x5 CASE"
     TISO 8 40.0
     IC EPSJ 1.0E-5
     ;
LIBRARY2 := USS: LIBRARY DISCR DISCR_FIL :: EDIT 2 PASS 2 ARM ;
CP := ASM: LIBRARY2 DISCR DISCR_FIL :: EDIT 1 ARM ;
CALC := FLU: CP LIBRARY2 DISCR :: TYPE K B1 SIGS ;
assertS CALC :: 'K-INFINITY' 1 1.232901 ;
ECHO "test TCDR49 completed" ;
END: ; 
QUIT "LIST" .
