* CLE-2000 procedure to perform a homogeneous pincell calculation 
* Self-shielding calculation with SUBG method and USS: module

PARAMETER XSM :: ::: XSM_FILE XSM ; ;


MODULE GEO: LIB: SYBILT: USS: ASM: FLU: EDI: GREP: ;

LINKED_LIST GEOM LIB_SUBG LIB2_SUBG TRACK CP CALC ;

REAL Kinf ;


STRING correl ;
:: >>correl<< ;

GEOM := GEO: :: HOMOGE 
        MIX 1
      ;

TRACK := SYBILT: GEOM ::
    TITLE 'HOM_UOX benchmark case'
    MAXR 2
    ;

ECHO "Correlation on/off : " correl ;
IF correl "CORR" = THEN
  LIB_SUBG := LIB: ::
      EDIT 1
      NMIX 1    ! MAXIMUM OF MATERIAL MIXTURES
      ANIS 1
      SUBG

      MIXS LIB: DRAGON FIL: J311_295

      MIX 1 750.0
        U234  = U234   3.482587E-06 CORR 1
        U235  = U235   3.664503E-04 CORR 1
        U238  = U238   7.765571E-03 CORR 1
        H1    = H1_H2O     2.497844E-02
        O16   = O16    2.955339E-02
    ;

ELSEIF correl "noCORR" = THEN
  LIB_SUBG := LIB: ::
      EDIT 1
      NMIX 1    ! MAXIMUM OF MATERIAL MIXTURES
      SUBG 
      ANIS 1

      MIXS LIB: DRAGON FIL: J311_295

      MIX 1 750.0
        U234  = U234   3.482587E-06 1
        U235  = U235   3.664503E-04 1
        U238  = U238   7.765571E-03 1
        H1    = H1_H2O     2.497844E-02
        O16   = O16    2.955339E-02
    ;
ENDIF ;


LIB2_SUBG := USS: LIB_SUBG TRACK :: EDIT 2 GRMIN 52 PASS 3 ;

CP := ASM: LIB2_SUBG TRACK :: EDIT 1 PIJ ;

CALC := FLU: CP LIB2_SUBG TRACK :: EDIT 1 TYPE K EXTE 5 ;

GREP: CALC :: GETVAL 'K-INFINITY  ' 1 1 1 >>Kinf<< ;
ECHO 'Kinf SUBG = ' Kinf ;

XSM := EDI: CALC LIB2_SUBG TRACK ::
     EDIT 2 SAVE
     MERG REGI 1
     SAVE MICR 3 U234 U235 U238
    ;
QUIT "LIST" .