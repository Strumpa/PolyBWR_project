*----
*
*  TEST CASE fbr
*  Junjie Rao and Xingjie Peng
*
*----
*  Define STRUCTURES and MODULES used
*----
LINKED_LIST GEOM TRACK LIBRARY ASB FLUX ;
MODULE LIB: GEO: SYBILT: TONE: ASM: FLU: DELETE: END: ;
PROCEDURE assertS ;
*
GEOM := GEO: :: TUBE 3
  R+ REFL
  RADIUS 0.0 0.3236 0.3859 0.53703
  SPLITR 2 1 5
  MIX 1 2 3 ;
TRACK := SYBILT: GEOM ::
  MAXR 20 QUA1 16 ;
*----
* USS
*----
LIBRARY := LIB: ::
 EDIT 1
 NMIX 3    (*MAXIMUM OF MATERIAL MIXTURES*)
 CTRA APOL (*APOLLO TYPE TRANSPORT CORRECTION*)
 ANIS 2
*
 MIXS LIB: DRAGON FIL: DLIB_315
 MIX 1 300.0 (*COMB*)
    U235    = U235   3.08220E-5 1
    U238    = U238   1.82440E-2 1
    Pu239   = Pu239  2.81470E-3 1
 MIX 2 300.0 (*GAIN1*)
    Cr52    = Cr52   8.68256E-3 2
    Fe56    = Fe56   6.39008E-2 2
 MIX 3 300.0 (*MODE1*)
    Na23    = Na23   2.22720E-2
 ;
LIBRARY := TONE: LIBRARY TRACK :: EDIT 3 TRAN PIJ ;
ASB := ASM: LIBRARY TRACK :: PIJ ;
FLUX := FLU: LIBRARY TRACK ASB :: TYPE K ;
assertS FLUX :: K-EFFECTIVE 1 1.466528 ;
LIBRARY ASB FLUX := DELETE: LIBRARY ASB FLUX ;
ECHO "test fbr_tone completed" ;
END: ;
