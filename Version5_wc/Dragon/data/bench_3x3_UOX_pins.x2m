* bench_3x3_UOX.x2m, CLE-2000 script to call Dragon5
* Purpose : Refining calculation scheme for BWR ATRIUM-10 type heterogeneous assemlies.
* 3x3 pins, no Gd loaded pins.
* Reference Serpen2 case : bench_3x3_UOX_mc
* No Burnup calculation 


****************************************************************************
************************** VARIABLES DEFINITION ****************************
****************************************************************************

MODULE GEO: G2S: NXT: SYBILT: SALT: MCCGT: LIB: USS: ASM: FLU: GREP: EDI: COMPO: DELETE: END: ;
LINKED_LIST GEOM_3x3 GEOM_3x3_SS LIBRARY LIBRARY2 TRACK_SS TRACK SYS FLUX EDIRATES COMPO ;

SEQ_ASCII Geo_G2S GeoSS_G2S ;
SEQ_ASCII _COMPO :: FILE './COMPO_bench_3x3_UOX_REFL_REFL' ;
SEQ_ASCII FIG_main.ps :: FILE './FIG_main_bench3x3.ps' ;
SEQ_ASCII FIG_SS.ps :: FILE './FIG_selfshielding.ps' ;

SEQ_BINARY TF_EXC TF_EXC_SS ;

STRING Library := "endfb8r1_295" ;

REAL Kinf ;

****************************************************************************
******************* GEOMETRY DEFINTION OF bench 3x3 UOX ********************
****************************************************************************
REAL Pitch_C := 1.29500 ;
REAL Rgap Rclad := 0.4520 0.5140 ;
REAL Rcomb1 Rcomb2 Rcomb3 Rcomb4 ;
EVALUATE Rcomb4 := 0.4435 ;
EVALUATE Rcomb1 := 0.313602 ;
EVALUATE Rcomb2 := 0.396678 ; 
EVALUATE Rcomb3 := 0.43227 ;

!Mix numbers for C1 fuel 
INTEGER COMB0101 COMB0102 COMB0103 COMB0104 ;
EVALUATE COMB0101 COMB0102 COMB0103 COMB0104 := 1 2 3 4 ;

!Mix numbers for C2 fuel 
INTEGER COMB0201 COMB0202 COMB0203 COMB0204 ;
EVALUATE COMB0201 COMB0202 COMB0203 COMB0204 := 5 6 7 8 ;

!Mix numbers for C3 fuel 
INTEGER COMB0301 COMB0302 COMB0303 COMB0304 ;
EVALUATE COMB0301 COMB0302 COMB0303 COMB0304 := 9 10 11 12 ;

!Mix numbers for C4 fuel 
INTEGER COMB0401 COMB0402 COMB0403 COMB0404 ;
EVALUATE COMB0401 COMB0402 COMB0403 COMB0404 := 13 14 15 16 ;

!Mix numbers for C6 fuel 
INTEGER COMB0601 COMB0602 COMB0603 COMB0604 ;
EVALUATE COMB0601 COMB0602 COMB0603 COMB0604 := 17 18 19 20 ;

! Pin level common mixes : Clad, Gap, Coolant (intra-pin water) :

INTEGER GAP CLAD COOL ;
EVALUATE GAP CLAD COOL := 21 22 23 ;

! Multiples of cell pitch : used to define 2nd level geometry mesh 
REAL 2_Pitch_C 3_Pitch_C 4_Pitch_C ;
EVALUATE 2_Pitch_C := Pitch_C 2.0 * ;
EVALUATE 3_Pitch_C := Pitch_C 3.0 * ;
EVALUATE 4_Pitch_C := Pitch_C 4.0 * ;



GEOM_3x3_SS := GEO: :: CAR2D 3 3
    X- REFL X+ REFL 
    Y- REFL Y+ REFL
    CELL
    C1 C2 C3
    C2 C4 C1
    C3 C1 C6
    MESHX 0.0 <<Pitch_C>> <<2_Pitch_C>> <<3_Pitch_C>>
    MESHY 0.0 <<Pitch_C>> <<2_Pitch_C>> <<3_Pitch_C>>
    ::: C1 :=  GEO: CARCEL 6 
                RADIUS 0.0 <<Rcomb1>> <<Rcomb2>>
                <<Rcomb3>> <<Rcomb4>> <<Rgap>> <<Rclad>>
                MIX <<COMB0101>> <<COMB0102>> <<COMB0103>> <<COMB0104>>
                    <<GAP>> <<CLAD>> <<COOL>> 
                    MESHX 0.0 <<Pitch_C>> MESHY 0.0 <<Pitch_C>>
    ;
    ::: C2 := GEO: C1 MIX <<COMB0201>> <<COMB0202>> <<COMB0203>> <<COMB0204>>
                            <<GAP>> <<CLAD>> <<COOL>>
    ;
    ::: C3 := GEO: C1 MIX <<COMB0301>> <<COMB0302>> <<COMB0303>> <<COMB0304>>
                            <<GAP>> <<CLAD>> <<COOL>>
    ;  
    ::: C4 := GEO: C1 MIX <<COMB0401>> <<COMB0402>> <<COMB0403>> <<COMB0404>>
                            <<GAP>> <<CLAD>> <<COOL>>
    ;
    ::: C6 := GEO: C1 MIX <<COMB0601>> <<COMB0602>> <<COMB0603>> <<COMB0604>>
                            <<GAP>> <<CLAD>> <<COOL>>
    ;
;


GEOM_3x3 := GEO: :: CAR2D 3 3
    X- REFL X+ REFL 
    Y- REFL Y+ REFL
    CELL
    C1 C2 C3
    C2 C4 C1
    C3 C1 C6
    MESHX 0.0 <<Pitch_C>> <<2_Pitch_C>> <<3_Pitch_C>>
    MESHY 0.0 <<Pitch_C>> <<2_Pitch_C>> <<3_Pitch_C>>
    ::: C1 :=  GEO: CARCEL 6 
                SECT 4 6
                RADIUS 0.0 <<Rcomb1>> <<Rcomb2>>
                <<Rcomb3>> <<Rcomb4>> <<Rgap>> <<Rclad>>
                MIX <<COMB0101>> <<COMB0102>> <<COMB0103>> <<COMB0104>>
                    <<GAP>> <<CLAD>>
                    <<COOL>> <<COOL>> <<COOL>> <<COOL>>
                    <<COOL>> <<COOL>> <<COOL>> <<COOL>>
                    <<COOL>> <<COOL>> <<COOL>> <<COOL>>  
                    MESHX 0.0 <<Pitch_C>> MESHY 0.0 <<Pitch_C>>
    ;
    ::: C2 := GEO: C1 MIX <<COMB0201>> <<COMB0202>> <<COMB0203>> <<COMB0204>>
                            <<GAP>> <<CLAD>>
                            <<COOL>> <<COOL>> <<COOL>> <<COOL>>
                            <<COOL>> <<COOL>> <<COOL>> <<COOL>>
                            <<COOL>> <<COOL>> <<COOL>> <<COOL>>  
    ;
    ::: C3 := GEO: C1 MIX <<COMB0301>> <<COMB0302>> <<COMB0303>> <<COMB0304>>
                            <<GAP>> <<CLAD>>
                            <<COOL>> <<COOL>> <<COOL>> <<COOL>>
                            <<COOL>> <<COOL>> <<COOL>> <<COOL>>
                            <<COOL>> <<COOL>> <<COOL>> <<COOL>>  
    ;  
    ::: C4 := GEO: C1 MIX <<COMB0401>> <<COMB0402>> <<COMB0403>> <<COMB0404>>
                            <<GAP>> <<CLAD>> 
                            <<COOL>> <<COOL>> <<COOL>> <<COOL>>
                            <<COOL>> <<COOL>> <<COOL>> <<COOL>>
                            <<COOL>> <<COOL>> <<COOL>> <<COOL>>
    ;
    ::: C6 := GEO: C1 MIX <<COMB0601>> <<COMB0602>> <<COMB0603>> <<COMB0604>>
                            <<GAP>> <<CLAD>> 
                            <<COOL>> <<COOL>> <<COOL>> <<COOL>>
                            <<COOL>> <<COOL>> <<COOL>> <<COOL>>
                            <<COOL>> <<COOL>> <<COOL>> <<COOL>>
    ;
;

****************************************************************************
******************** LIBRARY DEFINTION OF bench 3x3 UOX ********************
****************************************************************************


LIBRARY := LIB: ::
    EDIT 0
    NMIX 50    ! MAXIMUM OF MATERIAL MIXTURES 
    ANIS 2
    PT CALENDF 4
    ADED 4 NELAS N4N N2N N3N

  DEPL LIB: DRAGON FIL: <<Library>>

  MIXS LIB: DRAGON FIL: <<Library>>
  ! C1 fuel compo "AT10_24UOX" :
  MIX <<COMB0101>> 750.0  
    O16     = O16    4.66705E-02
    U234    = U234   5.15910E-06 1
    U235    = U235   5.67035E-04 1 
    U238    = U238   2.27631E-02 1 
  MIX <<COMB0102>> COMB <<COMB0101>> 1.0
  MIX <<COMB0103>> COMB <<COMB0101>> 1.0
  MIX <<COMB0104>> COMB <<COMB0101>> 1.0

  !C2 fuel compo "AT10_32UOX" :
  MIX <<COMB0201>> 750.0
    O16     = O16    4.667480E-02
    U234    = U234   7.039170E-06 1
    U235    = U235   7.560370E-04 1 
    U238    = U238   2.257430E-02 1 
  MIX <<COMB0202>> COMB <<COMB0201>> 1.0
  MIX <<COMB0203>> COMB <<COMB0201>> 1.0
  MIX <<COMB0204>> COMB <<COMB0201>> 1.0
     
  !C3 fuel compo "AT10_42UOX" :
  MIX <<COMB0301>> 750.0
    O16     = O16    4.667960E-02
    U234    = U234   9.163680E-06 1
    U235    = U235   9.686590E-04 1 
    U238    = U238   2.236200E-02 1 
  MIX <<COMB0302>> COMB <<COMB0301>> 1.0
  MIX <<COMB0303>> COMB <<COMB0301>> 1.0
  MIX <<COMB0304>> COMB <<COMB0301>> 1.0

  ! C4 fuel compo "AT10_45UOX" :
  MIX <<COMB0401>> 750.0
    O16     = O16    4.668150E-02
    U234    = U234   9.991530E-06 1
    U235    = U235   1.051340E-03 1 
    U238    = U238   2.227940E-02 1 
  MIX <<COMB0402>> COMB <<COMB0401>> 1.0
  MIX <<COMB0403>> COMB <<COMB0401>> 1.0
  MIX <<COMB0404>> COMB <<COMB0401>> 1.0

  ! C6 fuel compo "AT10_50UOX" :
  MIX <<COMB0601>> 750.0
    O16     = O16    4.668410E-02
    U234    = U234   1.117530E-05 1
    U235    = U235   1.169460E-03 1 
    U238    = U238   2.216140E-02 1 
  MIX <<COMB0602>> COMB <<COMB0601>> 1.0
  MIX <<COMB0603>> COMB <<COMB0601>> 1.0
  MIX <<COMB0604>> COMB <<COMB0601>> 1.0


  MIX <<CLAD>> 559.0 NOEV
    Zr90  = Zr90   2.18317E-02 6
    Zr91  = Zr91   4.76096E-03 6
    Zr92  = Zr92   7.27723E-03 6
    Zr94  = Zr94   7.37482E-03 6
    Zr96  = Zr96   1.18812E-03 6
    Fe56  = Fe56   8.74901E-05 
    Ni60  = Ni60   9.69262E-06
    Fe58  = Fe58   2.68895E-07
    Sn112  = Sn112   4.67343E-06
    Sn116  = Sn116   7.00535E-05
    Cr50  = Cr50   3.29613E-06
    Sn114  = Sn114   3.17985E-06
    Sn117  = Sn117   3.70020E-05
    Sn122  = Sn122   2.23073E-05
    Sn124  = Sn124   2.78961E-05
    Cr54  = Cr54   1.79408E-06
    Ni62  = Ni62   1.34344E-06
    Cr53  = Cr53   7.20746E-06
    Sn115  = Sn115   1.63812E-06
    Ni61  = Ni61   4.21331E-07
    Ni64  = Ni64   3.42085E-07
    Cr52  = Cr52   6.35631E-05
    Sn120  = Sn120   1.56970E-04
    Ni58  = Ni58   2.51627E-05
    Fe54  = Fe54   5.57337E-06
    Fe57  = Fe57   2.02053E-06
    
    Sn118  = Sn118   1.16691E-04
    Sn119  = Sn119   4.13864E-05

  MIX <<GAP>> 750.0 NOEV
    He4      = He4 1.50456E-04

  MIX <<COOL>> 559.0 NOEV
    H1      = H1_H2O 4.94546E-02 ! 0% void Coolant 
    O16     = O16    2.47298E-02

;

****************************************************************************************
********************* TRAKING GEOM_2x2/_SS FOR FLUX/SSH CALCULATION ********************
****************************************************************************************

* ---
* TRACKING PARAMTERES
* ---

INTEGER an2d := 24 ;  ! ANGULAR QUADRATURE PARAMETER 
REAL densur := 50.0 ; ! DENSITY OF INTEGRATION LINES CM^-1

* ---
* TRACKING PARAMETERS FOR SSH
* ---

* if using SALT: 

INTEGER an2d_SS := 24 ;
REAL densur_SS := 50.0 ;

* if using SYBILT:
INTEGER iqua2 := 32 ;
INTEGER nseg := 10 ;

* polar angles for MCCGT:
INTEGER nmu := 4 ;

* size allocation for MCCGT:
INTEGER mcu := 50000 ;

* ---
* FLUX CALC TRACKING
* ---
Geo_G2S FIG_main.ps := G2S: GEOM_3x3 :: ;


TRACK TF_EXC := SALT: Geo_G2S ::
  EDIT 1
  ALLG
  BATCH 100
  ANIS 2
  TISO EQW2 <<an2d>> <<densur>> REND LONG
;


* MOC tracking
TRACK := MCCGT: TRACK TF_EXC ::
  EDIT 1
  CACB <<nmu>> AAC 80 TMT EPSI 1E-5 MCU <<mcu>> ! tried CACB but is limited to isotropic ?
;

* ---
* SELF-SHIELDING TRACKING
* ---
* switch to SYBILT to test rates

* ---
* SELF-SHIELDING TRACKING
* ---

TRACK_SS := SYBILT: GEOM_3x3_SS ::
    TITLE 'TRACKING FOR bench 3x3 UOX'
    MAXR 100 QUA2 <<iqua2>> <<nseg>> DP01 MAXZ 750000 
;


****************************************************************************************
********************* BEGIN SELF-SHIELDING CALCULATION WITH USS/RSE ********************
****************************************************************************************


LIBRARY2 := USS: LIBRARY TRACK_SS ::
        EDIT 1 PASS 4 MAXST 50
      ;

SYS := ASM: LIBRARY2 TRACK TF_EXC :: ARM EDIT 0 ; 

FLUX := FLU: LIBRARY2 SYS TRACK TF_EXC :: 
             EDIT 1 TYPE K  
      ;
GREP: FLUX :: GETVAL 'K-EFFECTIVE  ' 1 1 1 >>Kinf<< ;
ECHO "SS tracking = SYBILT and FLX tracking = SALT TSPC, MOC on bench_3x3_UOX: Kinf=" Kinf ;

**********************************************************************************************************
********************          EXPORTS FOR POST-TREATMENT OF REACTION RATES               *****************
**********************************************************************************************************
COMPO := COMPO: ::
    EDIT 1
    STEP UP 'EDIBU_HOM'
     MAXCAL 5
     COMM 'Reaction rates- Condensed, Homogenized over all fuel cells' ENDC
     ISOT 2 U235 U238
    INIT
    STEP UP 'HOM1g'
     MAXCAL 5
     COMM 'Reaction rates- Condensed, Homogenized' ENDC
     ISOT 2 U235 U238
    INIT
    STEP UP 'HOM2g'
     MAXCAL 5
     COMM 'Reaction rates - Condensed 2gr, Homogenized' ENDC
     ISOT 2 U235 U238
    INIT
  ;

**** homogenization over all fuel cells
  EDIRATES := EDI: FLUX LIBRARY2 TRACK ::
            EDIT 1
            MICR 2 U235 U238
            MERG MIX 
            1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 0 0 0
            COND
            SAVE ON EDIBU_HOM
        ;

COMPO := COMPO: COMPO EDIRATES LIBRARY2 ::
            EDIT 1
            STEP UP 'EDIBU_HOM'
        ;

EDIRATES := DELETE: EDIRATES ;

***** Cell-wise homogenization
*** Condense all rates on 1 group :
EDIRATES := EDI: FLUX LIBRARY2 TRACK ::
        EDIT 1
        MICR 2 U235 U238
        MERG MIX 
        1 1 1 1 2 2 2 2 3 3 3 3 4 4 4 4 5 5 5 5 0 0 0
        COND
        SAVE ON HOM1g
        ;

COMPO := COMPO: COMPO EDIRATES LIBRARY2 ::
        EDIT 1
        STEP UP 'HOM1g'
        ;

EDIRATES := DELETE: EDIRATES ;


*** Condense rates to 2 groups with division between groups at 0.625 eV

EDIRATES := EDI: FLUX LIBRARY2 TRACK ::
        EDIT 1
        MICR 2 U235 U238
        MERG MIX 
        1 1 1 1 2 2 2 2 3 3 3 3 4 4 4 4 5 5 5 5 0 0 0
        COND 0.625
        SAVE ON HOM2g 
        ;

COMPO := COMPO: COMPO EDIRATES LIBRARY2 ::
            EDIT 1
            STEP UP 'HOM2g'
        ;

EDIRATES := DELETE: EDIRATES ;


_COMPO := COMPO ;

QUIT "LIST" .

