*** Gd3x3BL case with CP/IC/MOC solution : 
* BWR 3x3 array based on ATRIUM-10 geometry, 
* All 24UOX, 45Gd in Bottom Left position
* import tdt_data generated by glow.
* Solve 1 group CP/IC/MOC problem.
* Macroscpic cross sections from 24UOX and 45Gd AT10 pins, 
* self-shielded and solved in DRAGON : endfb8r1 - SHEM295
* Homogenized in fuel and condensed to 1 group.


MODULE GEO: MAC: G2S: SALT: MCCGT: ASM: FLU: GREP: DELETE: END: ;
PROCEDURE assertS ;
LINKED_LIST GEOISO GEOSPC MACRO MACROG TRACKIC TRACKCP TRACKMOC PIJ SYS FLUX ;

SEQ_BINARY TRACKFILIC TRACKFILCP TRACKFILMOC ;

SEQ_ASCII tdtMACRO :: FILE '3x3_Gd_BL_TISO_MACRO.dat' ;
SEQ_ASCII glowMACRO.ps :: FILE '3x3_Gd_BL_GLOW_TISO_MACRO.ps' ;

SEQ_ASCII g2s_tdt g2s_tdt_IC ;
SEQ_ASCII geo_tiso.ps glow_tiso.ps ;
SEQ_ASCII geoTisoIC.ps ;

REAL keffGEOIC keffGEOPIJ keffGEOCP  keffGEOMOC ;
REAL keffGLOWCP keffGLOWPIJ keffGLOWIC keffGLOWMOC ;
REAL keffGMCP keffGMPIJ keffGMIC keffGMMOC ; 
REAL delCPIC delMOCIC deltak ;
* ---
* TRACKING PARAMTERES
* ---

INTEGER an2d := 24 ;  ! ANGULAR QUADRATURE PARAMETER
REAL densur := 50.0 ; ! DENSITY OF INTEGRATION LINES CM^-1


REAL Rgap Rclad pitch := 0.4520 0.5140 1.295 ;
REAL Rcomb1 Rcomb2 Rcomb3 Rcomb4 := 0.313602 0.396678 0.43227 0.4435 ;
REAL RcombGd1 RcombGd2 RcombGd3 RcombGd4 RcombGd5 RcombGd6 
         := 0.19834 0.28049 0.34353 0.39668 0.43227 0.4435 ;

REAL 2_pitch 3_pitch ;
EVALUATE 2_pitch := 2.0 pitch * ;
EVALUATE 3_pitch := 3.0 pitch * ;

GEOISO := GEO: :: CAR2D 3 3
    X- ALBE 1.0 X+ ALBE 1.0 
    Y- ALBE 1.0 Y+ ALBE 1.0 
    CELL
    C7 C1 C1
    C1 C1 C1
    C1 C1 C1
    MESHX 0.0 <<pitch>> <<2_pitch>> <<3_pitch>>
    MESHX 0.0 <<pitch>> <<2_pitch>> <<3_pitch>>
    ::: C1 :=  GEO: CARCEL 6 
                RADIUS 0.0 <<Rcomb1>> <<Rcomb2>>
                <<Rcomb3>> <<Rcomb4>> <<Rgap>> <<Rclad>>
                MIX 1 1 1 1 3 4 5
                MESHX 0.0 <<pitch>> MESHY 0.0 <<pitch>>
    ;
    ::: C7 := GEO: CARCEL 8
              RADIUS 0.0 <<RcombGd1>> <<RcombGd2>> <<RcombGd3>> <<RcombGd4>>
                          <<RcombGd5>> <<RcombGd6>> <<Rgap>> <<Rclad>> 
              MIX 2 2 2 2 2 2 3 4 5
              MESHX 0.0 <<pitch>> MESHY 0.0 <<pitch>>
    ;
    
;

MACRO := MAC: ::
    EDIT 2 NGRO 1 NMIX 5 NIFI 1
    READ INPUT

    MIX 1 (*24UOX*)
        TOTAL 4.41963434E-01    SCAT 1 1 3.98291796E-01
        NUSIGF 5.10632172E-02      CHI 1.0

    MIX 2 (*45Gd*)
        TOTAL 5.11014581E-01    SCAT 1 1 3.98968190E-01
        NUSIGF 3.49357314E-02      CHI 1.0

    MIX 3 (*GAP*)
        TOTAL 3.00460903E-04  SCAT 1 1 3.00460844E-04
    
    MIX 4 (*CLAD*)
        TOTAL 0.4029        SCAT 1 1 0.4000
    
    MIX 5 (*WATER*)
        TOTAL 0.3683        SCAT 1 1 0.3661
;


MACROG := MAC: ::
    EDIT 2 NGRO 1 NMIX 5 NIFI 1
    READ INPUT

    MIX 4 (*24UOX*)
        TOTAL 4.41963434E-01    SCAT 1 1 3.98291796E-01
        NUSIGF 5.10632172E-02      CHI 1.0

    MIX 5 (*45Gd*)
        TOTAL 5.11014581E-01    SCAT 1 1 3.98968190E-01
        NUSIGF 3.49357314E-02      CHI 1.0

    MIX 3 (*GAP*)
        TOTAL 3.00460903E-04  SCAT 1 1 3.00460844E-04
    
    MIX 2 (*CLAD*)
        TOTAL 0.4029        SCAT 1 1 0.4000
    
    MIX 1 (*MODERATOR*)
        TOTAL 0.3683        SCAT 1 1 0.3661
;


*** TISO case treatment ***


g2s_tdt geo_tiso.ps := G2S: GEOISO :: ;
g2s_tdt_IC geoTisoIC.ps := G2S: GEOISO :: MACRO ;

glow_tiso.ps := G2S: tdtMACRO :: ;

*** TREAT ALL GEO: CASES FIRST ***

TRACKIC TRACKFILIC := SALT: g2s_tdt_IC ::
     EDIT 2
     TITLE "MULTICELL SURFACIC 3x3_Gd_BL GEO CASE"
     TISO <<an2d>> <<densur>>
     IC EPSJ 1.0E-7
     ;

TRACKCP TRACKFILCP := SALT: g2s_tdt ::
     EDIT 2
     TITLE "TISO CP SURFACIC 3x3_Gd_BL GEO CASE"
     TISO <<an2d>> <<densur>>
     ;

TRACKMOC TRACKFILMOC := SALT: g2s_tdt ::
     EDIT 2
     TITLE "TISO MOC SURFACIC 3x3_Gd_BL GEO CASE"
     TISO EQW <<an2d>> <<densur>>
     ;

* MOC tracking
TRACKMOC := MCCGT: TRACKMOC TRACKFILMOC ::
  EDIT 0
;


* ---
* Flux calculation
* ---

* FULL PIJ MATRIX RECONSTRUCTION
PIJ := ASM: MACRO TRACKIC TRACKFILIC :: PIJ EDIT 2 ;
FLUX := FLU: PIJ MACRO TRACKIC :: 
    EDIT 2 TYPE K EXTE 100 1.0E-7 THER 10 1.0E-6 ;
GREP: FLUX :: GETVAL 'K-EFFECTIVE' 1 1 1 >>keffGEOPIJ<< ;
PIJ FLUX := DELETE: PIJ FLUX ;
ECHO "test multicell 3x3_Gd_BL FULL PIJ reconstruction completed, keff=" keffGEOPIJ ;

* FLUX-CURRENT ITERATION WITHOUT FULL PIJ MATRIX RECONSTRUCTION
PIJ := ASM: MACRO TRACKIC TRACKFILIC :: ARM EDIT 2 ;
FLUX := FLU: PIJ MACRO TRACKIC :: 
    EDIT 4 TYPE K EXTE 100 1.0E-7 THER 10 1.0E-6 ;
GREP: FLUX :: GETVAL 'K-EFFECTIVE' 1 1 1 >>keffGEOIC<< ;
assertS FLUX :: 'K-EFFECTIVE' 1 <<keffGEOPIJ>> ;
PIJ FLUX := DELETE: PIJ FLUX ;
ECHO "test multicell 3x3_Gd_BL FLUX-CURRENT iteration completed, keff=" keffGEOIC ;

PIJ := ASM: MACRO TRACKCP TRACKFILCP :: PIJ EDIT 2 ;
FLUX := FLU: PIJ MACRO TRACKCP :: 
    EDIT 2 TYPE K EXTE 100 1.0E-7 THER 10 1.0E-6 ;
GREP: FLUX :: GETVAL 'K-EFFECTIVE' 1 1 1 >>keffGEOCP<< ;
PIJ FLUX := DELETE: PIJ FLUX ;
ECHO "test multicell 3x3_Gd_BL CP completed, keff=" keffGEOCP ;

EVALUATE delCPIC := keffGEOCP keffGEOIC - 1E+5 * ;
ECHO "With GEO: difference between CP and IC keff = " delCPIC ;

SYS := ASM: MACRO TRACKMOC TRACKFILMOC :: ARM EDIT 2 ; 
FLUX := FLU: SYS MACRO TRACKMOC TRACKFILMOC :: 
    EDIT 1 TYPE K EXTE 100 1.0E-7 THER 10 1.0E-6 ;
GREP: FLUX :: GETVAL 'K-EFFECTIVE' 1 1 1 >>keffGEOMOC<< ;
ECHO "test multicell 3x3_Gd_BL MOC completed, keff=" keffGEOMOC ;

EVALUATE delMOCIC := keffGEOMOC keffGEOIC - 1E+5 * ;
ECHO "With GEO: difference between MOC and IC keff = " delMOCIC ;


FLUX SYS := DELETE: FLUX SYS ;
TRACKIC TRACKFILIC := DELETE: TRACKIC TRACKFILIC ;
TRACKCP TRACKFILCP := DELETE: TRACKCP TRACKFILCP ;
TRACKMOC TRACKFILMOC := DELETE: TRACKMOC TRACKFILMOC ;

*----
ECHO " --- GLOW CASES TREATMENT --- " ;
*** TREAT ALL GLOW CASES SECOND ***

TRACKIC TRACKFILIC := SALT: tdtMACRO ::
     EDIT 2
     TITLE "MULTICELL SURFACIC 3x3_Gd_BL GLOW CASE"
     TISO <<an2d>> <<densur>>
     IC EPSJ 1.0E-7
     ;

TRACKCP TRACKFILCP := SALT: tdtMACRO ::
     EDIT 2
     TITLE "TISO CP SURFACIC 3x3_Gd_BL GLOW CASE"
     TISO <<an2d>> <<densur>>
     NOIC
     ;

TRACKMOC TRACKFILMOC := SALT: tdtMACRO ::
     EDIT 2
     TITLE "TISO MOC SURFACIC 3x3_Gd_BL GLOW CASE"
     TISO EQW <<an2d>> <<densur>>
     NOIC
     ;

* MOC tracking
TRACKMOC := MCCGT: TRACKMOC TRACKFILMOC ::
  EDIT 0
;

* ---
* Flux calculation
* ---

* FULL PIJ MATRIX RECONSTRUCTION
PIJ := ASM: MACROG TRACKIC TRACKFILIC :: PIJ EDIT 2 ;
FLUX := FLU: PIJ MACROG TRACKIC :: 
    EDIT 2 TYPE K EXTE 100 1.0E-7 THER 10 1.0E-6 ;
GREP: FLUX :: GETVAL 'K-EFFECTIVE' 1 1 1 >>keffGLOWPIJ<< ;
PIJ FLUX := DELETE: PIJ FLUX ;
ECHO "test multicell 3x3_Gd_BL (GLOW) FULL PIJ reconstruction completed, keff=" keffGLOWPIJ ;

* FLUX-CURRENT ITERATION WITHOUT FULL PIJ MATRIX RECONSTRUCTION
PIJ := ASM: MACROG TRACKIC TRACKFILIC :: ARM EDIT 2 ;
FLUX := FLU: PIJ MACROG TRACKIC :: 
    EDIT 4 TYPE K EXTE 100 1.0E-7 THER 10 1.0E-6 ;
GREP: FLUX :: GETVAL 'K-EFFECTIVE' 1 1 1 >>keffGLOWIC<< ;
assertS FLUX :: 'K-EFFECTIVE' 1 <<keffGLOWPIJ>> ;
PIJ FLUX := DELETE: PIJ FLUX ;
ECHO "test multicell 3x3_Gd_BL (GLOW) FLUX-CURRENT iteration completed, keff=" keffGLOWIC ;

PIJ := ASM: MACROG TRACKCP TRACKFILCP :: PIJ EDIT 2 ;
FLUX := FLU: PIJ MACROG TRACKCP :: 
    TYPE K EXTE 100 1.0E-7 THER 10 1.0E-6 ;
GREP: FLUX :: GETVAL 'K-EFFECTIVE' 1 1 1 >>keffGLOWCP<< ;
PIJ FLUX := DELETE: PIJ FLUX ;
ECHO "test multicell 3x3_Gd_BL (GLOW) CP completed, keff=" keffGLOWCP ;

EVALUATE delCPIC := keffGLOWCP keffGLOWIC - 1E+5 * ;
ECHO "With GLOW: difference between CP and IC keff = " delCPIC ;

SYS := ASM: MACROG TRACKMOC TRACKFILMOC :: ARM EDIT 2 ; 
FLUX := FLU: SYS MACROG TRACKMOC TRACKFILMOC ::
    EDIT 1 TYPE K EXTE 100 1.0E-7 THER 10 1.0E-6 ;
GREP: FLUX :: GETVAL 'K-EFFECTIVE' 1 1 1 >>keffGLOWMOC<< ;
ECHO "test multicell 3x3_Gd_BL (GLOW) MOC completed, keff=" keffGLOWMOC ;

EVALUATE delMOCIC := keffGLOWMOC keffGLOWIC - 1E+5 * ;
ECHO "With GLOW: difference between MOC and IC keff = " delMOCIC ;

FLUX SYS := DELETE: FLUX SYS ;
TRACKIC TRACKFILIC := DELETE: TRACKIC TRACKFILIC ;
TRACKCP TRACKFILCP := DELETE: TRACKCP TRACKFILCP ;
TRACKMOC TRACKFILMOC := DELETE: TRACKMOC TRACKFILMOC ;

! Assert differences with GEO results 
ECHO "Test IC from IC tracking : GLOW - GEO" ;
ECHO "keffGLOWIC = " keffGLOWIC ;
ECHO "keffGEOIC = " keffGEOIC ;
EVALUATE deltak := keffGLOWIC keffGEOIC - 1E+5 * ;
ECHO "delta k IC (GLOW - GEO) = " deltak "pcm" ;

ECHO "Test PIJ from IC tracking : GLOW - GEO" ;
ECHO "keffGLOWPIJ = " keffGLOWPIJ ;
ECHO "keffGEOPIJ = " keffGEOPIJ ;
EVALUATE deltak := keffGLOWPIJ keffGEOPIJ - 1E+5 * ;
ECHO "delta k PIJ (GLOW - GEO) = " deltak "pcm" ;

ECHO "Test CP from NOIC tracking : GLOW - GEO" ;
ECHO "keffGLOWCP = " keffGLOWCP ;
ECHO "keffGEOCP = " keffGEOCP ;
EVALUATE deltak := keffGLOWCP keffGEOCP - 1E+5 * ;
ECHO "delta k CP (GLOW - GEO) = " deltak "pcm" ;

ECHO "Test MOC from NOIC tracking : GLOW - GEO" ;
ECHO "keffGLOWMOC = " keffGLOWMOC ;
ECHO "keffGEOMOC = " keffGEOMOC ;
EVALUATE deltak := keffGLOWMOC keffGEOMOC - 1E+5 * ;
ECHO "delta k MOC (GLOW - GEO) = " deltak "pcm" ;

END: ;

