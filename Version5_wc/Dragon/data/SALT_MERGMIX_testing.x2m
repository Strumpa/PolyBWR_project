* Cle2000 script
* SALT_MERGMIX_testing.x2m
* Purpose : tracking dummy geometries with SALT using MERGMIX option
* Geometries tested display a diagonal symmetry --> optential source of error ?
* GEOM_C and GEOM_LVL1 are 1 level geometries.
* GEOM_LVL2 and GEOM_LVL3 are 2 and 3 level geometries defined using the GEO: module.

! General data definition
!Assembly scale data

REAL Pitch_A W_gap := 15.24 0.75 ; ! Assembly pitch and water gap thickness
REAL Box_out Box_in Box_thi := 13.74 13.4 0.17 ; ! Outer box outer, inner sides and thickness
REAL Chan_out Chan_in Chan_thi := 3.5 3.34 0.08 ; ! Channel box outer, inner sides and thickness
REAL Pitch_C := 1.29500 ;

! Multiples of cell pitch : used to define 1st level geometry mesh 
REAL 2_Pitch_C 3_Pitch_C 4_Pitch_C ;
EVALUATE 2_Pitch_C := Pitch_C 2.0 * ;
EVALUATE 3_Pitch_C := Pitch_C 3.0 * ;
EVALUATE 4_Pitch_C := Pitch_C 4.0 * ;

! X1 = Water gap + outer box thickness +extra water : in box water not a multiple of Pitch_C
REAL XtraMode ;
EVALUATE XtraMode := Box_in 10.0 Pitch_C * - 2.0 / ;

!Meshing points for macro geometry :
REAL X1 X2 X3 X4  ;
EVALUATE X1 := W_gap Box_thi + XtraMode + ;
EVALUATE X2 := 4.0 Pitch_C * X1 + ;
EVALUATE X3 := 3.0 Pitch_C * X2 + ;
EVALUATE X4 := 3.0 Pitch_C * X3 + ;  

REAL Y1 Y2 Y3 Y4 ;
EVALUATE Y1 := X1 ;
EVALUATE Y2 := 4.0 Pitch_C * Y1 + ;
EVALUATE Y3 := 3.0 Pitch_C * Y2 + ;
EVALUATE Y4 := 3.0 Pitch_C * Y3 + ;

REAL L1 L2 L3 ;
EVALUATE L1 := X2 X1 - ;
EVALUATE L2 := X3 X2 - ;
EVALUATE L3 := X4 X3 - ;

REAL H1 H2 H3 ;
EVALUATE H1 := Y2 Y1 - ;
EVALUATE H2 := Y3 Y2 - ;
EVALUATE H3 := Y4 Y3 - ;

REAL x_step y_step ; ! allows for submeshing of in-assembly box water
! in-assembly box water included in geom level with outer assembly water and box
EVALUATE x_step := W_gap Box_thi + ;
EVALUATE y_step := XtraMode Box_thi + ;

! inner CHaNneL box related parameters, used to define 2nd level CHNL Geometry
! CHNL = channel box + surroudning water to fit with pin CARCEL edges 
REAL XCHNL1 XCHNL2 XCHNL3 XCHNL4 XCHNL5 ;
EVALUATE XCHNL1 := L2 Chan_out - 2.0 / ;
EVALUATE XCHNL2 := XCHNL1 Chan_thi + ;
EVALUATE XCHNL3 := XCHNL2 Chan_in + ;
EVALUATE XCHNL4 := XCHNL3 Chan_thi + ;
EVALUATE XCHNL5 := L2 ;

! UOX Pincell scale data

REAL Rgap Rclad pitch := 0.4520 0.5140 1.29500 ;
REAL Rcomb1 Rcomb2 Rcomb3 Rcomb4 ;
EVALUATE Rcomb4 := 0.4435 ;
EVALUATE Rcomb1 := 0.313602 ;
EVALUATE Rcomb2 := 0.396678 ; 
EVALUATE Rcomb3 := 0.43227 ;

! UOX + Gd Pincell scale data

REAL RcombGd1 RcombGd2 RcombGd3 RcombGd4 RcombGd5 RcombGd6 ;
EVALUATE RcombGd1 RcombGd2 RcombGd3 RcombGd4 RcombGd5 RcombGd6 
         := 0.19834 0.28049 0.34353 0.39668 0.43227 0.4435 ;

INTEGER an2d := 18 ;  ! ANGULAR QUADRATURE PARAMETER
REAL densur := 35.0 ; ! DENSITY OF INTEGRATION LINES CM^-1


* Step 1 : simplified PINCELL with sectorized moderator : using MERGMIX should regroup the sectorized regions
* as they all have mix number = 7


MODULE GEO: SALT: G2S: TLM ;
LINKED_LIST GEOM_C GEOM_LVL1 GEOM_LVL2 GEOM_LVL3 ;
LINKED_LIST TRACK_C ;
SEQ_ASCII Geo_G2S_C ;
SEQ_BINARY TF_EXC_C ;
SEQ_ASCII FIG_C.ps :: FILE './MergMix_test_FIG_C.ps' ;

GEOM_C := GEO: :: CARCEL 3 
    X- REFL X+ REFL
    Y- REFL Y+ REFL
    SECT 4 3
    RADIUS 0.0 0.4435 0.4520 0.5140
    MIX 1 2 3 7 7 7 7 7 7 7 7 7 7 7 7
    MESHX 0.0 1.295 MESHY 0.0 1.295
;

! Converting to surfacic definition with G2S: 

Geo_G2S_C FIG_C.ps := G2S: GEOM_C :: ;

TRACK_C TF_EXC_C := SALT: Geo_G2S_C ::
  EDIT 3
  TSPC EQW2 <<an2d>> <<densur>> REND
  MERGMIX
;


! Step 2 : 1st level geometry test with DIAG condition

LINKED_LIST TRACK_1 ;
SEQ_ASCII Geo_G2S_LVL1 ;
SEQ_BINARY TF_EXC_1 ;
SEQ_ASCII FIG_LVL1.ps :: FILE './MergMix_test_FIG_LVL1.ps' ;


GEOM_LVL1 := GEO: :: CAR2D 5 5 
    EDIT 1
    X- DIAG X+ REFL
    Y- REFL Y+ DIAG
    MIX
    1  2  3  5  5 
       6  7  5  9
          10 11 12
             13 14
                15
    MESHX 0.0 <<X1>> <<X2>> <<X3>> <<X4>> <<Pitch_A>>
    MESHY 0.0 <<Y1>> <<Y2>> <<Y3>> <<Y4>> <<Pitch_A>>
;
! Using dummy mix numbers for 1st level geom.
ECHO "1 level GEOM_A generation done" ;

! Converting to surfacic definition with G2S: 

Geo_G2S_LVL1 FIG_LVL1.ps := G2S: GEOM_LVL1 :: ;

TRACK_1 TF_EXC_1 := SALT: Geo_G2S_LVL1 ::
  EDIT 3
  TSPC EQW2 <<an2d>> <<densur>> REND
  MERGMIX
;

! Step 3 : 2 levels geometry test with DIAG condition

LINKED_LIST TRACK_2 ;
SEQ_ASCII Geo_G2S_LVL2 ;
SEQ_BINARY TF_EXC_2 ;
SEQ_ASCII FIG_LVL2.ps :: FILE './MergMix_test_FIG_LVL2.ps' ;


GEOM_LVL2 := GEO: :: CAR2D 5 5 
    EDIT 1
    X- DIAG X+ REFL
    Y- REFL Y+ DIAG
    CELL
    WBC1 WB1 WB2 WB3 WBC2
         C01 C02 C03 WR1
            CHNL C04 WR2
                 C05 WR3
                     WTC

    MESHX 0.0 <<X1>> <<X2>> <<X3>> <<X4>> <<Pitch_A>>
    MESHY 0.0 <<Y1>> <<Y2>> <<Y3>> <<Y4>> <<Pitch_A>>
        ::: WTC := GEO: CAR2D 3 3
        MESHX 0.0 <<XtraMode>> <<y_step>> <<X1>>
        MESHY 0.0 <<XtraMode>> <<y_step>> <<Y1>>
        MIX 7 60 70 
             60 60 70 
            70 70 70
    ;
    ::: WBC1 := GEO: CAR2D 3 3 
        MESHX 0.0 <<W_gap>> <<x_step>> <<X1>>
        MESHY 0.0 <<W_gap>> <<x_step>> <<Y1>>
        MIX 70 70 70 
            70 60 60
            70 60 7
    ;
    ::: WR3 := GEO: CAR2D 3 1
        MESHX 0.0 <<XtraMode>> <<y_step>> <<X1>>
        MESHY 0.0 <<L3>>
        MIX 7 60 70
    ;
    ::: WR2 := GEO: CAR2D 3 1
        MESHX 0.0 <<XtraMode>> <<y_step>> <<X1>>
        MESHY 0.0 <<L2>>
        MIX 7 60 70
    ; 
    ::: WR1 := GEO: CAR2D 3 1
        MESHX 0.0 <<XtraMode>> <<y_step>> <<X1>>
        MESHY 0.0 <<L1>>
        MIX 7 60 70
    ; 
    ::: WB1 := GEO: CAR2D 1 3
        MESHX 0.0 <<L1>>
        MESHY 0.0 <<W_gap>> <<x_step>> <<Y1>>
        MIX 70 60 7
    ;
    ::: WB2 := GEO: CAR2D 1 3
        MESHX 0.0 <<L2>>
        MESHY 0.0 <<W_gap>> <<x_step>> <<Y1>>
        MIX 70 60 7
    ;
    ::: WB3 := GEO: CAR2D 1 3
        MESHX 0.0 <<L3>>
        MESHY 0.0 <<W_gap>> <<x_step>> <<Y1>>
        MIX 70 60 7
    ;
    ::: WBC2 := GEO: CAR2D 3 3
        MESHX 0.0 <<XtraMode>> <<y_step>> <<X1>>
        MESHY 0.0 <<W_gap>> <<x_step>> <<X1>>
        MIX 70 70 70 
            60  60 70 
            7 60 70 
    ;
    ::: CHNL := GEO: CAR2D 5 5
        MESHX 0.0 <<XCHNL1>> <<XCHNL2>> <<XCHNL3>> <<XCHNL4>> <<XCHNL5>>
        MESHY 0.0 <<XCHNL1>> <<XCHNL2>> <<XCHNL3>> <<XCHNL4>> <<XCHNL5>>
        MIX 7 7 7 7 7 
            7 60 60 60 7
            7 60 70 60 7
            7 60 60 60 7
            7 7 7 7 7
    ;
    ::: C01 := GEO: CAR2D 4 4
        MIX 1 2 3 4
            1 2 3 4
            1 2 3 4
            1 2 3 4 
        MESHX 0.0 <<Pitch_C>> <<2_Pitch_C>> <<3_Pitch_C>> <<4_Pitch_C>>
        MESHY 0.0 <<Pitch_C>> <<2_Pitch_C>> <<3_Pitch_C>> <<4_Pitch_C>>
    ;
    ::: C02 := GEO: CAR2D 3 4 
        MIX 1 2 3
            1 2 3
            1 2 3
            1 2 3  
        MESHX 0.0 <<Pitch_C>> <<2_Pitch_C>> <<3_Pitch_C>> 
        MESHY 0.0 <<Pitch_C>> <<2_Pitch_C>> <<3_Pitch_C>> <<4_Pitch_C>>

    ;
    ::: C03 := GEO: CAR2D 3 4
        MIX 1 2 3
            1 2 3
            1 2 3
            1 2 3
        MESHX 0.0 <<Pitch_C>> <<2_Pitch_C>> <<3_Pitch_C>> 
        MESHY 0.0 <<Pitch_C>> <<2_Pitch_C>> <<3_Pitch_C>> <<4_Pitch_C>>
    ;
    ::: C04 := GEO: CAR2D 3 3 
        MIX 1 2 3
            1 2 3
            1 2 3
        MESHX 0.0 <<Pitch_C>> <<2_Pitch_C>> <<3_Pitch_C>>
        MESHY 0.0 <<Pitch_C>> <<2_Pitch_C>> <<3_Pitch_C>>
    ;
    ::: C05 := GEO: CAR2D 3 3
        MIX 1 2 3
            1 2 3
            1 2 3
        MESHX 0.0 <<Pitch_C>> <<2_Pitch_C>> <<3_Pitch_C>>
        MESHY 0.0 <<Pitch_C>> <<2_Pitch_C>> <<3_Pitch_C>>
    ;
;

! Converting to surfacic definition with G2S: 

Geo_G2S_LVL2 FIG_LVL2.ps := G2S: GEOM_LVL2 :: ;

TRACK_2 TF_EXC_2 := SALT: Geo_G2S_LVL2 ::
  EDIT 3
  TSPC EQW2 <<an2d>> <<densur>> REND
  MERGMIX
;


! Step 4 : 3 levels geometry test with DIAG condition

LINKED_LIST TRACK_3 ;
SEQ_ASCII Geo_G2S_LVL3 ;
SEQ_BINARY TF_EXC_3 ;
SEQ_ASCII FIG_LVL3.ps :: FILE './MergMix_test_FIG_LVL3.ps' ;


GEOM_LVL3 := GEO: :: CAR2D 5 5 
    EDIT 1
    X- DIAG X+ REFL
    Y- REFL Y+ DIAG
    CELL
    WBC1 WB1 WB2 WB3 WBC2
          C01 C02 C03 WR1
             CHNL C04 WR2
                  C05 WR3
                      WTC

    MESHX 0.0 <<X1>> <<X2>> <<X3>> <<X4>> <<Pitch_A>>
    MESHY 0.0 <<Y1>> <<Y2>> <<Y3>> <<Y4>> <<Pitch_A>>
    ::: WTC := GEO: CAR2D 3 3
        MESHX 0.0 <<XtraMode>> <<y_step>> <<X1>>
        MESHY 0.0 <<XtraMode>> <<y_step>> <<Y1>>
        MIX 7 60 70 
             60 60 70 
            70 70 70
    ;
    ::: WBC1 := GEO: CAR2D 3 3 
        MESHX 0.0 <<W_gap>> <<x_step>> <<X1>>
        MESHY 0.0 <<W_gap>> <<x_step>> <<Y1>>
        MIX 70 70 70 
            70 60 60
             70 60 7
    ;
    ::: WR3 := GEO: CAR2D 3 1
        MESHX 0.0 <<XtraMode>> <<y_step>> <<X1>>
        MESHY 0.0 <<L3>>
        MIX 7 60 70
    ;
    ::: WR2 := GEO: CAR2D 3 1
        MESHX 0.0 <<XtraMode>> <<y_step>> <<X1>>
        MESHY 0.0 <<L2>>
        MIX 7 60 70
    ; 
    ::: WR1 := GEO: CAR2D 3 1
        MESHX 0.0 <<XtraMode>> <<y_step>> <<X1>>
        MESHY 0.0 <<L1>>
        MIX 7 60 70
    ; 
    ::: WB1 := GEO: CAR2D 1 3
        MESHX 0.0 <<L1>>
        MESHY 0.0 <<W_gap>> <<x_step>> <<Y1>>
        MIX 70 60 7
      ;
    ::: WB2 := GEO: CAR2D 1 3
        MESHX 0.0 <<L2>>
        MESHY 0.0 <<W_gap>> <<x_step>> <<Y1>>
        MIX 70 60 7
    ;
    ::: WB3 := GEO: CAR2D 1 3
        MESHX 0.0 <<L3>>
        MESHY 0.0 <<W_gap>> <<x_step>> <<Y1>>
        MIX 70 60 7
    ;
    ::: WBC2 := GEO: CAR2D 3 3
        MESHX 0.0 <<XtraMode>> <<y_step>> <<X1>>
        MESHY 0.0 <<W_gap>> <<x_step>> <<X1>>
        MIX 70 70 70 
            60  60 70 
            7 60 70 
    ;
    ECHO "After Sides and corners" ;
    ::: C01 := GEO: CAR2D 4 4
        EDIT 1
        CELL
        C1 C2 C3 C5
        C2 C4 C7 C6 
        C3 C7 C6 C6
        C5 C6 C6 C6
        MESHX 0.0 <<Pitch_C>> <<2_Pitch_C>> <<3_Pitch_C>> <<4_Pitch_C>>
        MESHY 0.0 <<Pitch_C>> <<2_Pitch_C>> <<3_Pitch_C>> <<4_Pitch_C>>
        ::: C1 :=  GEO: CARCEL 6 
                   RADIUS 0.0 <<Rcomb1>> <<Rcomb2>>
                   <<Rcomb3>> <<Rcomb4>> <<Rgap>> <<Rclad>>
                   MIX 1 2 3 4
                       5 6 7 
                       MESHX 0.0 <<pitch>> MESHY 0.0 <<pitch>>
        ;
        ::: C2 := GEO: C1 MIX 21 22 23 24
                              5 6 7
        ;
        ::: C3 := GEO: C1 MIX 31 32 33 34
                              5 6 7 
        ;
        ::: C4 := GEO: C1 MIX 41 42 43 44
                              5 6 7 
        ;
        ::: C5 := GEO: C1 MIX 51 52 53 54
                              5 6 7 
        ; 
        ::: C6 := GEO: C1 MIX 61 62 63 64
                              5 6 7
        ;
        ::: C7 := GEO: CARCEL 8
                  RADIUS 0.0 <<RcombGd1>> <<RcombGd2>> <<RcombGd3>> <<RcombGd4>>
                             <<RcombGd5>> <<RcombGd6>> <<Rgap>> <<Rclad>> 
                  MIX 71 72 73 74 75 76
                      5 6 7
                  MESHX 0.0 <<pitch>> MESHY 0.0 <<pitch>>
        ; 
    ;
    ECHO "C01 done" ;
    ::: C02 := GEO: CAR2D 3 4 
        CELL
        C6 C5 C4
        C7 C6 C6
        C6 C7 C6
        C6 C6 C7
        MESHX 0.0 <<Pitch_C>> <<2_Pitch_C>> <<3_Pitch_C>> 
        MESHY 0.0 <<Pitch_C>> <<2_Pitch_C>> <<3_Pitch_C>> <<4_Pitch_C>>
        ::: C6 := GEO: CARCEL 6 
                   RADIUS 0.0 <<Rcomb1>> <<Rcomb2>>
                   <<Rcomb3>> <<Rcomb4>> <<Rgap>> <<Rclad>>
                   MIX 41 42 43 44
                   5 6 7
                   MESHX 0.0 <<pitch>> MESHY 0.0 <<pitch>>
        ;
        ::: C5 := GEO: C6 MIX 51 52 53 54
                               5 6 7 
        ; 
        ::: C4 := GEO: C6 MIX 61 62 63 64
                              5 6 7
        ;
        ::: C7 := GEO: CARCEL 8
                  RADIUS 0.0 <<RcombGd1>> <<RcombGd2>> <<RcombGd3>> <<RcombGd4>>
                             <<RcombGd5>> <<RcombGd6>> <<Rgap>> <<Rclad>> 
                  MIX 71 72 73 74 75 76
                      5 6 7
                  MESHX 0.0 <<pitch>> MESHY 0.0 <<pitch>>
        ; 
    ;
    ECHO "C02 done" ;
    ::: C03 := GEO: CAR2D 3 4
        CELL
        C3 C2 C1
        C7 C4 C2
        C6 C7 C3
        C6 C5 C4
        MESHX 0.0 <<Pitch_C>> <<2_Pitch_C>> <<3_Pitch_C>> 
        MESHY 0.0 <<Pitch_C>> <<2_Pitch_C>> <<3_Pitch_C>> <<4_Pitch_C>>
        ::: C1 :=  GEO: CARCEL 6 
                   RADIUS 0.0 <<Rcomb1>> <<Rcomb2>>
                   <<Rcomb3>> <<Rcomb4>> <<Rgap>> <<Rclad>>
                   MIX 1 2 3 4
                   5 6 7
                   MESHX 0.0 <<pitch>> MESHY 0.0 <<pitch>>
        ;
        ::: C2 := GEO: C1 MIX 21 22 23 24
                              5 6 7
        ;
        ::: C3 :=  GEO: C1 MIX 31 32 33 34
                               5 6 7
        ;
        ::: C4 :=  GEO: C1 MIX 41 42 43 44
                               5 6 7 
        ;
        ::: C5 :=  GEO: C1 MIX 51 52 53 54
                               5 6 7 
        ; 
        ::: C6 := GEO: C1 MIX 61 62 63 64
                              5 6 7
        ;
        ::: C7 := GEO: CARCEL 8
                  RADIUS 0.0 <<RcombGd1>> <<RcombGd2>> <<RcombGd3>> <<RcombGd4>>
                             <<RcombGd5>> <<RcombGd6>> <<Rgap>> <<Rclad>> 
                  MIX 71 72 73 74 75 76
                      5 6 7
                  MESHX 0.0 <<pitch>> MESHY 0.0 <<pitch>>
        ; 
    ;
    ECHO "C03 done" ;
    ::: C04 := GEO: CAR2D 3 3 
        CELL
        C4 C6 C4
        C3 C7 C4 
        C4 C4 C4
        MESHX 0.0 <<Pitch_C>> <<2_Pitch_C>> <<3_Pitch_C>>
        MESHY 0.0 <<Pitch_C>> <<2_Pitch_C>> <<3_Pitch_C>>
        ::: C3 :=  GEO: CARCEL 6 
                   RADIUS 0.0 <<Rcomb1>> <<Rcomb2>>
                   <<Rcomb3>> <<Rcomb4>> <<Rgap>> <<Rclad>>
                   MIX 31 32 33 34
                   5 6 7
                   MESHX 0.0 <<pitch>> MESHY 0.0 <<pitch>>
        ;
        ::: C4 :=  GEO: C3 MIX 41 42 43 44
                               5 6 7
        ;
        ::: C6 := GEO: C3 MIX 61 62 63 64
                              5 6 7
        ;
        ::: C7 := GEO: CARCEL 8
                  RADIUS 0.0 <<RcombGd1>> <<RcombGd2>> <<RcombGd3>> <<RcombGd4>>
                             <<RcombGd5>> <<RcombGd6>> <<Rgap>> <<Rclad>> 
                  MIX 71 72 73 74 75 76
                      5 6 7
                  MESHX 0.0 <<pitch>> MESHY 0.0 <<pitch>> 
        ; 
    ;
    ECHO "C04 done" ;
    ::: C05 := GEO: CAR2D 3 3
        CELL
        C4 C8 C3
        C8 C4 C2 
        C3 C2 C1
        MESHX 0.0 <<Pitch_C>> <<2_Pitch_C>> <<3_Pitch_C>>
        MESHY 0.0 <<Pitch_C>> <<2_Pitch_C>> <<3_Pitch_C>>
        ::: C1 :=  GEO: CARCEL 6 
                   RADIUS 0.0 <<Rcomb1>> <<Rcomb2>>
                   <<Rcomb3>> <<Rcomb4>> <<Rgap>> <<Rclad>>
                   MIX 1 2 3 4
                   5 6 7 
                   MESHX 0.0 <<pitch>> MESHY 0.0 <<pitch>>
        ;
        ::: C2 := GEO: C1 MIX 21 22 23 24
                              5 6 7
        ;
        ::: C3 :=  GEO: C1 MIX 31 32 33 34
                               5 6 7 
        ;
        ::: C4 :=  GEO: C1 MIX 41 42 43 44
                               5 6 7
        ;
         ::: C8 := GEO: CARCEL 8
                  RADIUS 0.0 <<RcombGd1>> <<RcombGd2>> <<RcombGd3>> <<RcombGd4>>
                             <<RcombGd5>> <<RcombGd6>> <<Rgap>> <<Rclad>> 
                  MIX 81 82 83 84 85 86 
                      5 6 7
                  MESHX 0.0 <<pitch>> MESHY 0.0 <<pitch>>
        ; 
    ;
    ECHO "C05 done" ;
    ::: CHNL := GEO: CAR2D 5 5
        MESHX 0.0 <<XCHNL1>> <<XCHNL2>> <<XCHNL3>> <<XCHNL4>> <<XCHNL5>>
        MESHY 0.0 <<XCHNL1>> <<XCHNL2>> <<XCHNL3>> <<XCHNL4>> <<XCHNL5>>
        MIX 7 7 7 7 7 
            7 60 60 60 7
            7 60 70 60 7
            7 60 60 60 7
            7 7 7 7 7
     ;
     ECHO "CHNL done" ;
;

! Converting to surfacic definition with G2S: 

Geo_G2S_LVL3 FIG_LVL3.ps := G2S: GEOM_LVL3 :: ;

TRACK_3 TF_EXC_3 := SALT: Geo_G2S_LVL3 ::
  EDIT 3
  TSPC EQW2 <<an2d>> <<densur>> REND
  MERGMIX
;
