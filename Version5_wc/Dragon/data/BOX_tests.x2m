*** AT10_LAT_MODEBOX case with CP/IC/MOC solution : 
* BWR LATTICEB array based on ATRIUM-10 geometry, 
* Presence of moderator box
* import tdt_data generated by glow.
* Solve 1 group CP/IC/MOC problem to ensure proper material numbering treatment
* issue : GLOW/DRAGON material ordering match, but SALOME re-writes material ids using its internal system.
* Macroscpic cross sections from ATRIUM-10 assembly pins. 
* self-shielded and solved in DRAGON : endfb8r1 - SHEM295
* Homogenized in fuel and condensed to 1 group.


MODULE GEO: MAC: G2S: SALT: MCCGT: ASM: FLU: GREP: DELETE: END: ;
PROCEDURE assertS ;
LINKED_LIST GEOISO MACRO MACROG TRACKIC TRACKCP TRACKMOC PIJ SYS FLUX ;

SEQ_BINARY TRACKFILIC TRACKFILCP TRACKFILMOC ;
SEQ_ASCII g2s_tdt :: FILE 'AT10_BOX_g2s.dat' ;
SEQ_ASCII geo_tiso.ps :: FILE 'AT10_BOX_GEO.ps' ;
SEQ_ASCII tdt_box :: FILE 'AT10_BOX_TISO.dat' ;
SEQ_ASCII box.ps :: FILE 'AT10_BOX_glow.ps' ;
SEQ_ASCII tdt_cells :: FILE 'AT10_cells_around_BOX_TISO.dat' ;
SEQ_ASCII cellsbox.ps :: FILE 'AT10_cells_around_BOX_TISO_glow.ps' ;

REAL keffGLOWCP keffGLOWIC keffGLOWMOC ;
REAL delCPIC delMOCIC ;


* --------------------------------
*    DIMENSIONS DEFINITION
* --------------------------------
!Assembly scale data

REAL Pitch_A W_gap := 15.24 0.75 ; ! Assembly pitch and water gap thickness
REAL Box_out Box_in Box_thi := 13.74 13.4 0.17 ; ! Outer box outer, inner sides and thickness
REAL Chan_out Chan_in Chan_thi := 3.5 3.34 0.08 ; ! Channel box outer, inner sides and thickness


! UOX Pincell scale data

REAL Rgap Rclad pitch := 0.4520 0.5140 1.29500 ;
REAL Rcomb1 Rcomb2 Rcomb3 Rcomb4 ;
EVALUATE Rcomb4 := 0.4435 ;
EVALUATE Rcomb1 := 0.313602 ;
EVALUATE Rcomb2 := 0.396678 ; 
EVALUATE Rcomb3 := 0.43227 ;
REAL RCool := 0.58075 ; ! radius half way between outer clad and pincell side.

REAL RcombGd1 RcombGd2 RcombGd3 RcombGd4 RcombGd5 RcombGd6 ;
EVALUATE RcombGd1 RcombGd2 RcombGd3 RcombGd4 RcombGd5 RcombGd6 
        := 0.19834 0.28049 0.34353 0.39668 0.43227 0.4435 ;

! Evaluating dependencies

! Multiples of cell pitch : used to define 2nd level geometry mesh 
REAL 2_pitch 3_pitch ;
EVALUATE 2_pitch := pitch 2.0 * ;
EVALUATE 3_pitch := pitch 3.0 * ;

! LLat = total lattice length
REAL LLat := pitch 10.0 * ;


! X1 = Water gap + outer box thickness +extra water : in box water not a multiple of pitch
REAL XtraCool ;
EVALUATE XtraCool := Box_in 10.0 pitch * - 2.0 / ;

!Meshing points for macro geometry :
REAL X1 X2 X3 X4  ;
! X2 = X1 + 4*pitch
! X3 = X2 + 3*pitch
! X4 = X3 + 3*pitch
EVALUATE X1 := W_gap Box_thi + XtraCool + ;
EVALUATE X2 := 4.0 pitch * X1 + ;
EVALUATE X3 := 3.0 pitch * X2 + ;
EVALUATE X4 := 3.0 pitch * X3 + ;  

REAL Y1 Y2 Y3 Y4 ;
EVALUATE Y1 := X1 ;
EVALUATE Y2 := 4.0 pitch * Y1 + ;
EVALUATE Y3 := 3.0 pitch * Y2 + ;
EVALUATE Y4 := 3.0 pitch * Y3 + ;

REAL L1 L2 L3 ;
EVALUATE L1 := X2 X1 - ;
EVALUATE L2 := X3 X2 - ;
EVALUATE L3 := X4 X3 - ;

REAL H1 H2 H3 ;
EVALUATE H1 := Y2 Y1 - ;
EVALUATE H2 := Y3 Y2 - ;
EVALUATE H3 := Y4 Y3 - ;

REAL x_step y_step ; ! allows for submeshing of in-assembly box water
! in-assembly box water included in geom level with outer assembly water and box
EVALUATE x_step := W_gap Box_thi + ;
EVALUATE y_step := XtraCool Box_thi + ;

! inner CHaNneL box related parameters, used to define 2nd level CHNL Geometry
! CHNL = channel box + surroudning water to fit with pin CARCEL edges 
REAL XCHNL1 XCHNL2 XCHNL3 XCHNL4 XCHNL5 ;
EVALUATE XCHNL1 := L2 Chan_out - 2.0 / ;
EVALUATE XCHNL2 := XCHNL1 Chan_thi + ;
EVALUATE XCHNL3 := XCHNL2 Chan_in + ;
EVALUATE XCHNL4 := XCHNL3 Chan_thi + ;
EVALUATE XCHNL5 := L2 ;

REAL X1sym := pitch XCHNL2 - ;
REAL X2sym := pitch XCHNL1 - ;



GEOISO := GEO: :: CAR2D 3 3 
    X- ALBE 1.0 X+ ALBE 1.0
    Y- ALBE 1.0 Y+ ALBE 1.0
    CELL
    W1 WB W2
    WL W0 WR
    W3 WT W4
    MESHX 0.0 <<pitch>> <<2_pitch>> <<3_pitch>>
    
    MESHY 0.0 <<pitch>> <<2_pitch>> <<3_pitch>>
    ::: W1 := GEO: CAR2D 3 3
       MESHX 0.0 <<XCHNL1>> <<XCHNL2>> <<pitch>>
       MESHY 0.0 <<XCHNL1>> <<XCHNL2>> <<pitch>>
       MIX 11 11 11
           11 12 12
           11 12 13 ;
    ::: WB := GEO: CAR2D 1 3
       MESHX 0.0 <<pitch>>
       MESHY 0.0 <<XCHNL1>> <<XCHNL2>> <<pitch>>
       MIX 11 12 13 ;
   ::: W2 := GEO: CAR2D 3 3
       MESHX 0.0 <<X1sym>> <<X2sym>> <<pitch>>
       MESHY 0.0 <<XCHNL1>> <<XCHNL2>> <<pitch>>
       MIX 11 11 11
           12 12 11
           13 12 11 ;
   ::: WL := GEO: CAR2D 3 1
       MESHX 0.0 <<XCHNL1>> <<XCHNL2>> <<pitch>>
       MESHY 0.0 <<pitch>>
       MIX 11 12 13 ;
   ::: W0 := GEO: CAR2D 1 1
       MESHX 0.0 <<pitch>>
       MESHY 0.0 <<pitch>>
        MIX 13 ;
   ::: WR := GEO: CAR2D 3 1
       MESHX 0.0 <<X1sym>> <<X2sym>> <<pitch>>
       MESHY 0.0 <<pitch>>
       MIX 13 12 11 ;
   ::: W3 := GEO: CAR2D 3 3
       MESHX 0.0 <<XCHNL1>> <<XCHNL2>> <<pitch>>
       MESHY 0.0 <<X1sym>> <<X2sym>> <<pitch>>
       MIX 11 12 13
           11 12 12
           11 11 11 ;
   ::: WT := GEO: CAR2D 1 3
       MESHX 0.0 <<pitch>>
       MESHY 0.0 <<X1sym>> <<X2sym>> <<pitch>>
       MIX 13 12 11 ;
   ::: W4 := GEO: CAR2D 3 3
        MESHX 0.0 <<X1sym>> <<X2sym>> <<pitch>>
        MESHY 0.0 <<X1sym>> <<X2sym>> <<pitch>>
        MIX 13 12 11
            12 12 11
            11 11 11 ;
;

* ---
* TRACKING PARAMTERES
* ---
ECHO "BEGIN AT10_LAT_MODEBOX multicell CASE" ;

INTEGER an2d := 24 ;  ! ANGULAR QUADRATURE PARAMETER
REAL densur := 100.0 ; ! DENSITY OF INTEGRATION LINES CM^-1


*** TISO case treatment ***

g2s_tdt geo_tiso.ps := G2S: GEOISO :: ;
box.ps := G2S: tdt_box :: ;

*----
ECHO " --- GLOW CASE TREATMENT --- " ;
*** TREAT GLOW CASE  ***

TRACKIC TRACKFILIC := SALT: tdt_box ::
     EDIT 3
     TITLE "MULTICELL SURFACIC BOX GLOW CASE"
     TISO <<an2d>> <<densur>>
     IC EPSJ 1.0E-6
     ;

TRACKCP TRACKFILCP := SALT: tdt_box ::
     EDIT 2
     TITLE "TISO CP SURFACIC BOX GLOW CASE"
     TISO <<an2d>> <<densur>>
     NOIC
     ;

TRACKMOC TRACKFILMOC := SALT: tdt_box ::
     EDIT 2
     TITLE "TISO MOC SURFACIC BOX GLOW CASE"
     TISO EQW <<an2d>> <<densur>>
     NOIC
     ;

* MOC tracking
TRACKMOC := MCCGT: TRACKMOC TRACKFILMOC ::
  EDIT 0
;


TRACKIC TRACKFILIC := DELETE: TRACKIC TRACKFILIC ;
TRACKCP TRACKFILCP := DELETE: TRACKCP TRACKFILCP ;
TRACKMOC TRACKFILMOC := DELETE: TRACKMOC TRACKFILMOC ;


*----
ECHO " --- GEO CASE TREATMENT --- " ;

*** TREAT GEO CASE  ***

TRACKIC TRACKFILIC := SALT: g2s_tdt ::
     EDIT 2
     TITLE "MULTICELL SURFACIC BOX GEO CASE"
     TISO <<an2d>> <<densur>>
     IC EPSJ 1.0E-6
     ;

TRACKCP TRACKFILCP := SALT: g2s_tdt ::
     EDIT 2
     TITLE "TISO CP SURFACIC BOX GEO CASE"
     TISO <<an2d>> <<densur>>
     NOIC
     ;

TRACKMOC TRACKFILMOC := SALT: g2s_tdt ::
     EDIT 2
     TITLE "TISO MOC SURFACIC BOX GEO CASE"
     TISO EQW <<an2d>> <<densur>>
     NOIC
     ;

* MOC tracking
TRACKMOC := MCCGT: TRACKMOC TRACKFILMOC ::
  EDIT 0
;


END: ;




