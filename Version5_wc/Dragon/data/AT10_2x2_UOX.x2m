* AT10_2x2_UOX.x2m, CLE-2000 script to call Dragon5
* Purpose : Refining calculation scheme for BWR ATRIUM-10 type heterogeneous assemlies.
* 2x2 pins, no Gd loaded pins.
* Reference Serpen2 case : AT10_2x2_UOX_mc
* No Burnup calculation 


****************************************************************************
************************** VARIABLES DEFINITION ****************************
****************************************************************************

MODULE GEO: G2S: NXT: SYBILT: SALT: TLM: MCCGT: LIB: USS: ASM: FLU: GREP: END: ;
LINKED_LIST GEOM_2x2 GEOM_2x2_SS LIBRARY LIBRARY2 TRACK_SS TRACK SYS FLUX ;

SEQ_ASCII Geo_G2S GeoSS_G2S ;

SEQ_ASCII FIG_main.ps :: FILE './FIG_main.ps' ;
SEQ_ASCII FIG_SS.ps :: FILE './FIG_selfshielding.ps' ;

SEQ_BINARY TF_EXC TF_EXC_SS ;
SEQ_ASCII TLM_F1 :: FILE 'tracks_SALT_FLX_2x2_UOX.m' ;
SEQ_ASCII TLM_F2 :: FILE 'tracks_SALT_SSH_2x2_UOX.m' ;

STRING Library := "J311_295" ;
STRING ssh_option := "VVER_ALL" ;

REAL Kinf ;

****************************************************************************
********************** GEOMETRY DEFINTION OF 2x2 UOX ***********************
****************************************************************************
REAL Pitch_C := 1.29500 ;
REAL Rgap Rclad := 0.4520 0.5140 ;
REAL Rcomb1 Rcomb2 Rcomb3 Rcomb4 ;
EVALUATE Rcomb4 := 0.4435 ;
EVALUATE Rcomb1 := 0.313602 ;
EVALUATE Rcomb2 := 0.396678 ; 
EVALUATE Rcomb3 := 0.43227 ;

!Mix numbers for C1 fuel 
INTEGER COMB0101 COMB0102 COMB0103 COMB0104 ;
EVALUATE COMB0101 COMB0102 COMB0103 COMB0104 := 1 2 3 4 ;

!Mix numbers for C2 fuel 
INTEGER COMB0201 COMB0202 COMB0203 COMB0204 ;
EVALUATE COMB0201 COMB0202 COMB0203 COMB0204 := 21 22 23 24 ;


!Mix numbers for C4 fuel 
INTEGER COMB0401 COMB0402 COMB0403 COMB0404 ;
EVALUATE COMB0401 COMB0402 COMB0403 COMB0404 := 41 42 43 44 ;

! Pin level common mixes : Clad, Gap, Coolant (intra-pin water) :

INTEGER GAP CLAD COOL ;
EVALUATE GAP CLAD COOL := 5 6 7 ;

! Multiples of cell pitch : used to define 2nd level geometry mesh 
REAL 2_Pitch_C 3_Pitch_C 4_Pitch_C ;
EVALUATE 2_Pitch_C := Pitch_C 2.0 * ;
EVALUATE 3_Pitch_C := Pitch_C 3.0 * ;
EVALUATE 4_Pitch_C := Pitch_C 4.0 * ;



GEOM_2x2_SS := GEO: :: CAR2D 2 2
    X- REFL X+ REFL 
    Y- REFL Y+ REFL
    CELL
    C1 C2
    C2 C4
    MESHX 0.0 <<Pitch_C>> <<2_Pitch_C>>
    MESHX 0.0 <<Pitch_C>> <<2_Pitch_C>>
    ::: C1 :=  GEO: CARCEL 6 
                RADIUS 0.0 <<Rcomb1>> <<Rcomb2>>
                <<Rcomb3>> <<Rcomb4>> <<Rgap>> <<Rclad>>
                MIX <<COMB0101>> <<COMB0102>> <<COMB0103>> <<COMB0104>>
                    <<GAP>> <<CLAD>> <<COOL>> 
                    MESHX 0.0 <<Pitch_C>> MESHY 0.0 <<Pitch_C>>
    ;
    ::: C2 := GEO: C1 MIX <<COMB0201>> <<COMB0202>> <<COMB0203>> <<COMB0204>>
                            <<GAP>> <<CLAD>> <<COOL>>
    ;
    ::: C4 := GEO: C1 MIX <<COMB0401>> <<COMB0402>> <<COMB0403>> <<COMB0404>>
                            <<GAP>> <<CLAD>> <<COOL>>
    ;
;


GEOM_2x2 := GEO: :: CAR2D 2 2
    X- REFL X+ REFL 
    Y- REFL Y+ REFL
    CELL
    C1 C2
    C2 C4
    MESHX 0.0 <<Pitch_C>> <<2_Pitch_C>>
    MESHX 0.0 <<Pitch_C>> <<2_Pitch_C>>
    ::: C1 :=  GEO: CARCEL 6 
                SECT 4 6
                RADIUS 0.0 <<Rcomb1>> <<Rcomb2>>
                <<Rcomb3>> <<Rcomb4>> <<Rgap>> <<Rclad>>
                MIX <<COMB0101>> <<COMB0102>> <<COMB0103>> <<COMB0104>>
                    <<GAP>> <<CLAD>>
                    <<COOL>> <<COOL>> <<COOL>> <<COOL>>
                    <<COOL>> <<COOL>> <<COOL>> <<COOL>>
                    <<COOL>> <<COOL>> <<COOL>> <<COOL>>  
                    MESHX 0.0 <<Pitch_C>> MESHY 0.0 <<Pitch_C>>
    ;
    ::: C2 := GEO: C1 MIX <<COMB0201>> <<COMB0202>> <<COMB0203>> <<COMB0204>>
                            <<GAP>> <<CLAD>> <<COOL>> <<COOL>> <<COOL>> <<COOL>>
                            <<COOL>> <<COOL>> <<COOL>> <<COOL>>
                            <<COOL>> <<COOL>> <<COOL>> <<COOL>>
    ;
    ::: C4 := GEO: C1 MIX <<COMB0401>> <<COMB0402>> <<COMB0403>> <<COMB0404>>
                            <<GAP>> <<CLAD>> 
                            <<COOL>> <<COOL>> <<COOL>> <<COOL>>
                            <<COOL>> <<COOL>> <<COOL>> <<COOL>>
                            <<COOL>> <<COOL>> <<COOL>> <<COOL>>
    ;
;

****************************************************************************
*********************** LIBRARY DEFINTION OF 2x2 UOX ***********************
****************************************************************************


LIBRARY := LIB: ::
    EDIT 0
    NMIX 100    ! MAXIMUM OF MATERIAL MIXTURES
    RSE 1.0E-03
    ANIS 3

  DEPL LIB: DRAGON FIL: <<Library>>

  MIXS LIB: DRAGON FIL: <<Library>>
  ! C1 fuel compo "AT10_24UOX" :
  MIX <<COMB0101>> 750.0
    O16     = O16    4.66705E-02
    U234    = U234   5.15910E-06 1
    U235    = U235   5.67035E-04 1 
    U238    = U238   2.27631E-02 1 
  MIX <<COMB0102>> COMB <<COMB0101>> 1.0
  MIX <<COMB0103>> COMB <<COMB0101>> 1.0
  MIX <<COMB0104>> COMB <<COMB0101>> 1.0

  !C2 fuel compo "AT10_32UOX" :
  MIX <<COMB0201>> 750.0
    O16     = O16    4.667480E-02
    U234    = U234   7.039170E-06 1
    U235    = U235   7.560370E-04 1 
    U238    = U238   2.257430E-02 1 
  MIX <<COMB0202>> COMB <<COMB0201>> 1.0
  MIX <<COMB0203>> COMB <<COMB0201>> 1.0
  MIX <<COMB0204>> COMB <<COMB0201>> 1.0


  ! C4 fuel compo "AT10_45UOX" :
  MIX <<COMB0401>> 750.0
    O16     = O16    4.668150E-02
    U234    = U234   9.991530E-06 1
    U235    = U235   1.051340E-03 1 
    U238    = U238   2.227940E-02 1 
  MIX <<COMB0402>> COMB <<COMB0401>> 1.0
  MIX <<COMB0403>> COMB <<COMB0401>> 1.0
  MIX <<COMB0404>> COMB <<COMB0401>> 1.0


  MIX <<CLAD>> 559.0 NOEV
    Zr90  = Zr90   2.18317E-02  2
    Zr91  = Zr91   4.76096E-03  2
    Zr92  = Zr92   7.27723E-03 2
    Zr94  = Zr94   7.37482E-03 2
    Zr96  = Zr96   1.18812E-03  2
    Ni60  = Ni60   9.69262E-06
    Fe58  = Fe58   2.68895E-07
    Sn112  = Sn112   4.67343E-06
    Sn116  = Sn116   7.00535E-05
    Cr50  = Cr50   3.29613E-06
    Sn114  = Sn114   3.17985E-06
    Sn117  = Sn117   3.70020E-05
    Sn122  = Sn122   2.23073E-05
    Sn124  = Sn124   2.78961E-05
    Cr54  = Cr54   1.79408E-06
    Ni62  = Ni62   1.34344E-06
    Cr53  = Cr53   7.20746E-06
    Sn115  = Sn115   1.63812E-06
    Ni61  = Ni61   4.21331E-07
    Ni64  = Ni64   3.42085E-07
    Cr52  = Cr52   6.35631E-05
    Sn120  = Sn120   1.56970E-04
    Ni58  = Ni58   2.51627E-05
    Fe54  = Fe54   5.57337E-06
    Fe57  = Fe57   2.02053E-06
    Fe56  = Fe56   8.74901E-05
    Sn118  = Sn118   1.16691E-04
    Sn119  = Sn119   4.13864E-05

  MIX <<GAP>> 750.0 NOEV
    He4      = He4 1.50456E-04

  MIX <<COOL>> 559.0 NOEV
    H1      = H1_H2O 4.94546E-02 ! 0% void Coolant 
    O16     = O16    2.47298E-02

;

****************************************************************************************
********************* TRAKING GEOM_2x2/_SS FOR FLUX/SSH CALCULATION ********************
****************************************************************************************

* ---
* TRACKING PARAMTERES
* ---

INTEGER an2d := 18 ;  ! ANGULAR QUADRATURE PARAMETER 
REAL densur := 40.0 ; ! DENSITY OF INTEGRATION LINES CM^-1

INTEGER an2d_ss := 8 ;  ! ANGULAR QUADRATURE PARAMETER 
REAL densur_ss := 25.0 ; ! DENSITY OF INTEGRATION LINES CM^-1 

INTEGER iqua2 := 24 ;
INTEGER nseg := 5 ;

* polar angles for MCCGT:
INTEGER nmu := 4 ;

* size allocation for MCCGT:
INTEGER mcu := 50000 ;

* ---
* FLUX CALC TRACKING
* ---
Geo_G2S FIG_main.ps := G2S: GEOM_2x2 :: ;

TRACK TF_EXC := SALT: Geo_G2S ::
  EDIT 1
  ALLG
  ANIS 3
  TSPC EQW2 <<an2d>> <<densur>> REND LONG
;

TLM_F1 := TLM: TRACK TF_EXC ::
  EDIT 10
  NTPO 1
  DIRECTIONS
  DIR 0
;

* MOC tracking
TRACK := MCCGT: TRACK TF_EXC ::
  EDIT 1
  CACB <<nmu>> AAC 80 TMT EPSI 1E-5 MCU <<mcu>> ! tried CACB but is limited to isotropic ?
;

* ---
* SELF-SHIELDING TRACKING
* ---
GeoSS_G2S FIG_SS.ps := G2S: GEOM_2x2_SS :: ;

TRACK_SS TF_EXC_SS := SALT: GeoSS_G2S ::
  EDIT 1
  ALLG
  TSPC <<an2d_ss>> <<densur_ss>> REND
  MERGMIX
;

* MOC tracking for ssh ?
TRACK_SS := MCCGT: TRACK_SS TF_EXC_SS ::
  EDIT 1
  CACB <<nmu>> AAC 80 TMT EPSI 1E-5 MCU <<mcu>> ! tried CACB but is limited to isotropic ?
;



****************************************************************************************
********************* BEGIN SELF-SHIELDING CALCULATION WITH USS/RSE ********************
****************************************************************************************


LIBRARY2 := USS: LIBRARY TRACK_SS TF_EXC_SS ::
        EDIT 1 PASS 1 MAXST 20 ARM
      ;

SYS := ASM: LIBRARY2 TRACK TF_EXC :: ARM EDIT 0 ; 

FLUX := FLU: LIBRARY2 SYS TRACK TF_EXC :: 
             EDIT 1 TYPE K  
      ;
GREP: FLUX :: GETVAL 'K-EFFECTIVE  ' 1 1 1 >>Kinf<< ;
ECHO "SS tracking = SALT and FLX tracking = SALT TSPC, MOC on 2x2_UOX: Kinf=" Kinf ;
END: ;
