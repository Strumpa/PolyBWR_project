************************************************************************************************************************
*                                                                                                                      *
* Procedure :  MPHYS_3D_MOC_REFL.x2m                                                                                   *
* Purpose   :  Generate REFLECTOR COMPO for BWR multi-physics coupled iterations in DONJON5/THM:                       * 
*              --> Axial layers to model bottom and top axial light water reflectors                                   *
* Author    :  R. Guasch                                                                                               *
*                                                                                                                      *
* CALL      :  ./rdragon MPHYS_3D_MOC_REFL.x2m                                                                         *
*                                                                                                                      *
************************************************************************************************************************

************************************************************************
*                          BEGIN DECLARATION                           *
************************************************************************
PROCEDURE Mix_UOX ;
MODULE GEO: LIB: SYBILT: NXT: MCCGT: UTL: GREP: DELETE: COMPO: USS: ASM: FLU: EDI: END: ;
LINKED_LIST GEOM3D GEOMSS1 GEOMSS2 GEOMSS3 GEOMSS4 ;
LINKED_LIST LIBRARY LIBRARY2 LIBRARYH1 LIBRARYH2 LIBRARYH3 LIBRARYH4 ;
LINKED_LIST TRACKSS1 TRACKSS2 TRACKSS3 TRACKSS4 TRACK SYS FLUX  ;
LINKED_LIST COMPO EDIOBJ PARAMS ;
SEQ_BINARY TF_EXC ;

SEQ_ASCII _COMPO :: FILE './COMPO_REFL_3DMOC_4Zones_splitz5' ;

REAL Kinf ;

STRING Library := "J311_295" ;

* Idea : 3D representation of fuel channel, with lower and upper axial reflectors
* Use 3D MOC approach with "representative" fuel properties in order to get axial reflector XS

* Use CARCELZ based geometry with ~ a few axial slices with T_fuel(z) and most importantly rho_m(z).
**************************************** APPROXIMATIVE TH PARAMS ************************************

REAL TFuel1 TFuel2 TFuel3 TFuel4 :=  961.2 635.8 573.3 562.7 ;
REAL TCool_in TCool1 TCool2 TCool3 TCool4 :=  543.15 560.4 562.5 561.8 561.2 ;
REAL cool_dens_in cool_dens1 cool_dens2 cool_dens3 cool_dens4 :=  0.732 0.440 0.258 0.244 0.241 ;

**************************************** GEOMETRICAL PARAMETERS *************************************

REAL Rcomb1 Rcomb2 Rcomb3 Rcomb4 :=  0.313602 0.396678 0.43227 0.4435 ;
REAL Rgap Rclad pitch := 0.4520 0.5140 1.295 ;
REAL Rmode := 0.64 ;
REAL A_pitch := 15.24 ;

* According to "Burnup Credit Evaluation for BWR Spent Fuel from Full Core Calculations" - Detkina et al. 2020
* bottom/top axial reflector ~ 2*assembly size, use this as height for the reflectors
REAL Hfuel := 380.0 ; ! total active core height = fuel rod height
REAL Hb :=  Hfuel 4.0 / ; !height of fuel bundle
REAL Hrefl := 2.0 A_pitch * ;
REAL H2 := Hrefl Hb + ;
REAL H3 := H2 Hb + ;
REAL H4 := H3 Hb + ;
REAL H5 := H4 Hb + ;
REAL Htot := H5 Hrefl + ;


******************************************** MIX NUMBERING ****************************************
INTEGER MODE_l MODE_u := 1 2 ;
INTEGER COMB0101 COMB0102 COMB0103 COMB0104 GAP1 CLAD1 MODE1 := 3 4 5 6 7 8 9 ;
INTEGER COMB0201 COMB0202 COMB0203 COMB0204 GAP2 CLAD2 MODE2 := 10 11 12 13 14 15 16 ;
INTEGER COMB0301 COMB0302 COMB0303 COMB0304 GAP3 CLAD3 MODE3 := 17 18 19 20 21 22 23 ;
INTEGER COMB0401 COMB0402 COMB0403 COMB0404 GAP4 CLAD4 MODE4 := 24 25 26 27 28 29 30 ;


******************************************* 3D Geometry definition *****************************************

GEOM3D := GEO: :: CARCELZ 7 6
    EDIT 1
    X- REFL X+ REFL
    Y- REFL Y+ REFL
    Z- REFL Z+ REFL
    RADIUS 0.0 <<Rcomb1>> <<Rcomb2>> <<Rcomb3>> <<Rcomb4>> <<Rgap>> <<Rclad>> <<Rmode>>
    MIX 
        <<MODE_l>> <<MODE_l>> <<MODE_l>> <<MODE_l>> <<MODE_l>> <<MODE_l>> <<MODE_l>> <<MODE_l>> !Lower lwtr reflector
        <<COMB0101>> <<COMB0102>> <<COMB0103>> <<COMB0104>> <<GAP1>> <<CLAD1>> <<MODE1>> <<MODE1>> !First fuel slice
        <<COMB0201>> <<COMB0202>> <<COMB0203>> <<COMB0204>> <<GAP2>> <<CLAD2>> <<MODE2>> <<MODE2>> !Second fuel slice
        <<COMB0301>> <<COMB0302>> <<COMB0303>> <<COMB0304>> <<GAP3>> <<CLAD3>> <<MODE3>> <<MODE3>> !Third fuel slice
        <<COMB0401>> <<COMB0402>> <<COMB0403>> <<COMB0404>> <<GAP4>> <<CLAD4>> <<MODE4>> <<MODE4>> !Fourth fuel slice
        <<MODE_u>> <<MODE_u>> <<MODE_u>> <<MODE_u>> <<MODE_u>> <<MODE_u>> <<MODE_u>> <<MODE_u>> !Lower lwtr reflector
    MESHX 0.0 <<pitch>> 
    MESHY 0.0 <<pitch>>
    MESHZ 0.0 <<Hrefl>> <<H2>> <<H3>> <<H4>> <<H5>> <<Htot>>
    SPLITZ 5 1 1 1 1 5
  ; 

**************************************** 2D Self-shielding geometries *****************************************

GEOMSS1 := GEO: :: CARCEL 6
    X- REFL X+ REFL
    Y- REFL Y+ REFL
    RADIUS 0.0 <<Rcomb1>> <<Rcomb2>> <<Rcomb3>> <<Rcomb4>> <<Rgap>> <<Rclad>>
    MIX <<COMB0101>> <<COMB0102>> <<COMB0103>> <<COMB0104>> <<GAP1>> <<CLAD1>> <<MODE1>> !First fuel slice
    MESHX 0.0 <<pitch>> 
    MESHY 0.0 <<pitch>>
;

GEOMSS2 := GEO: :: CARCEL 6
    X- REFL X+ REFL
    Y- REFL Y+ REFL
    RADIUS 0.0 <<Rcomb1>> <<Rcomb2>> <<Rcomb3>> <<Rcomb4>> <<Rgap>> <<Rclad>>
    MIX <<COMB0201>> <<COMB0202>> <<COMB0203>> <<COMB0204>> <<GAP2>> <<CLAD2>> <<MODE2>> !Second fuel slice
    MESHX 0.0 <<pitch>> 
    MESHY 0.0 <<pitch>>
;

GEOMSS3 := GEO: :: CARCEL 6
    X- REFL X+ REFL
    Y- REFL Y+ REFL
    RADIUS 0.0 <<Rcomb1>> <<Rcomb2>> <<Rcomb3>> <<Rcomb4>> <<Rgap>> <<Rclad>>
    MIX <<COMB0301>> <<COMB0302>> <<COMB0303>> <<COMB0304>> <<GAP3>> <<CLAD3>> <<MODE3>> !Third fuel slice
    MESHX 0.0 <<pitch>> 
    MESHY 0.0 <<pitch>>
;

GEOMSS4 := GEO: :: CARCEL 6
    X- REFL X+ REFL
    Y- REFL Y+ REFL
    RADIUS 0.0 <<Rcomb1>> <<Rcomb2>> <<Rcomb3>> <<Rcomb4>> <<Rgap>> <<Rclad>>
    MIX <<COMB0401>> <<COMB0402>> <<COMB0403>> <<COMB0404>> <<GAP4>> <<CLAD4>> <<MODE4>> !Fourth fuel slice
    MESHX 0.0 <<pitch>> 
    MESHY 0.0 <<pitch>>
;
********************************************* LIBRARY ************************************************

* Calculation of moderator data
* AVOGADRO's number
REAL A := 6.022094E-1 ; ! Normalisation des concentrations par 10E-24
REAL M_H2O := 15.9994 2.0 1.00794 * + ;

* Water in lower reflector
REAL N_H2O_in := cool_dens_in A * M_H2O / ;
REAL N_O_in := N_H2O_in ;
REAL N_H_in := 2.0 N_H2O_in * ;

* Height 1 = water in first fuel slice
REAL N_H2O_1 := cool_dens1 A * M_H2O / ;
REAL N_O_1 := N_H2O_1 ;
REAL N_H_1 := 2.0 N_H2O_1 * ;

* Height 2 = water in second fuel slice
REAL N_H2O_2 := cool_dens2 A * M_H2O / ;
REAL N_O_2 := N_H2O_2 ;
REAL N_H_2 := 2.0 N_H2O_2 * ;

* Height 3 = water in third fuel slice
REAL N_H2O_3 := cool_dens3 A * M_H2O / ;
REAL N_O_3 := N_H2O_3 ;
REAL N_H_3 := 2.0 N_H2O_3 * ;

* Height 4 = water in fourth fuel slice
REAL N_H2O_4 := cool_dens4 A * M_H2O / ;
REAL N_O_4 := N_H2O_4 ;
REAL N_H_4 := 2.0 N_H2O_4 * ;

LIBRARY := LIB: ::
    EDIT 1
    NMIX 30    ! MAXIMUM OF MATERIAL MIXTURES
    PT CALENDF 4
    ANIS 3
    MIXS LIB: DRAGON FIL: <<Library>>

! C1 fuel compo "AT10_24UOX" in silce 1:
    MIX <<COMB0101>> <<TFuel1>>
        O16     = O16    4.66705E-02
        U234    = U234   5.15910E-06 1
        U235    = U235   5.67035E-04 1 
        U238    = U238   2.27631E-02 1 
    MIX <<COMB0102>> COMB <<COMB0101>> 1.0
    MIX <<COMB0103>> COMB <<COMB0101>> 1.0
    MIX <<COMB0104>> COMB <<COMB0101>> 1.0

! C1 fuel compo "AT10_24UOX" in silce 2:
    MIX <<COMB0201>> <<TFuel2>>
        O16     = O16    4.66705E-02
        U234    = U234   5.15910E-06 1
        U235    = U235   5.67035E-04 1 
        U238    = U238   2.27631E-02 1 
    MIX <<COMB0202>> COMB <<COMB0201>> 1.0
    MIX <<COMB0203>> COMB <<COMB0201>> 1.0
    MIX <<COMB0204>> COMB <<COMB0201>> 1.0

! C1 fuel compo "AT10_24UOX" in silce 3:
    MIX <<COMB0301>> <<TFuel3>>
        O16     = O16    4.66705E-02
        U234    = U234   5.15910E-06 1
        U235    = U235   5.67035E-04 1 
        U238    = U238   2.27631E-02 1 
    MIX <<COMB0302>> COMB <<COMB0301>> 1.0
    MIX <<COMB0303>> COMB <<COMB0301>> 1.0
    MIX <<COMB0304>> COMB <<COMB0301>> 1.0

! C1 fuel compo "AT10_24UOX" in silce 4:
    MIX <<COMB0401>> <<TFuel4>>
        O16     = O16    4.66705E-02
        U234    = U234   5.15910E-06 1
        U235    = U235   5.67035E-04 1 
        U238    = U238   2.27631E-02 1 
    MIX <<COMB0402>> COMB <<COMB0401>> 1.0
    MIX <<COMB0403>> COMB <<COMB0401>> 1.0
    MIX <<COMB0404>> COMB <<COMB0401>> 1.0

* Cladding around H1 fuel
    MIX <<CLAD1>> <<TCool1>> NOEV
        Cr50  = Cr50   3.29613E-06
        Cr52  = Cr52   6.35631E-05
        Cr53  = Cr53   7.20746E-06
        Cr54  = Cr54   1.79408E-06
        Fe54  = Fe54   5.57337E-06
        Fe56  = Fe56   8.74901E-05 2
        Fe57  = Fe57   2.02053E-06
        Fe58  = Fe58   2.68895E-07
        Ni58  = Ni58   2.51627E-05
        Ni60  = Ni60   9.69262E-06
        Ni61  = Ni61   4.21331E-07
        Ni62  = Ni62   1.34344E-06
        Ni64  = Ni64   3.42085E-07
        Zr90  = Zr90   2.18317E-02 2
        Zr91  = Zr91   4.76096E-03 2
        Zr92  = Zr92   7.27723E-03 2
        Zr94  = Zr94   7.37482E-03 2
        Zr96  = Zr96   1.18812E-03 2
        Sn112  = Sn112   4.67343E-06
        Sn114  = Sn114   3.17985E-06
        Sn115  = Sn115   1.63812E-06
        Sn116  = Sn116   7.00535E-05
        Sn117  = Sn117   3.70020E-05
        Sn118  = Sn118   1.16691E-04
        Sn119  = Sn119   4.13864E-05
        Sn120  = Sn120   1.56970E-04 
        Sn122  = Sn122   2.23073E-05
        Sn124  = Sn124   2.78961E-05
        O16  = O16   3.08132E-04
        O17  = O17   1.17376E-07
    
* Cladding around H2 fuel
    MIX <<CLAD2>> <<TCool2>> NOEV
        Cr50  = Cr50   3.29613E-06
        Cr52  = Cr52   6.35631E-05
        Cr53  = Cr53   7.20746E-06
        Cr54  = Cr54   1.79408E-06
        Fe54  = Fe54   5.57337E-06
        Fe56  = Fe56   8.74901E-05 2
        Fe57  = Fe57   2.02053E-06
        Fe58  = Fe58   2.68895E-07
        Ni58  = Ni58   2.51627E-05
        Ni60  = Ni60   9.69262E-06
        Ni61  = Ni61   4.21331E-07
        Ni62  = Ni62   1.34344E-06
        Ni64  = Ni64   3.42085E-07
        Zr90  = Zr90   2.18317E-02 2
        Zr91  = Zr91   4.76096E-03 2
        Zr92  = Zr92   7.27723E-03 2
        Zr94  = Zr94   7.37482E-03 2
        Zr96  = Zr96   1.18812E-03 2
        Sn112  = Sn112   4.67343E-06
        Sn114  = Sn114   3.17985E-06
        Sn115  = Sn115   1.63812E-06
        Sn116  = Sn116   7.00535E-05
        Sn117  = Sn117   3.70020E-05
        Sn118  = Sn118   1.16691E-04
        Sn119  = Sn119   4.13864E-05
        Sn120  = Sn120   1.56970E-04 
        Sn122  = Sn122   2.23073E-05
        Sn124  = Sn124   2.78961E-05
        O16  = O16   3.08132E-04
        O17  = O17   1.17376E-07

* Cladding around H3 fuel
    MIX <<CLAD3>> <<TCool3>> NOEV
        Cr50  = Cr50   3.29613E-06
        Cr52  = Cr52   6.35631E-05
        Cr53  = Cr53   7.20746E-06
        Cr54  = Cr54   1.79408E-06
        Fe54  = Fe54   5.57337E-06
        Fe56  = Fe56   8.74901E-05 2
        Fe57  = Fe57   2.02053E-06
        Fe58  = Fe58   2.68895E-07
        Ni58  = Ni58   2.51627E-05
        Ni60  = Ni60   9.69262E-06
        Ni61  = Ni61   4.21331E-07
        Ni62  = Ni62   1.34344E-06
        Ni64  = Ni64   3.42085E-07
        Zr90  = Zr90   2.18317E-02 2
        Zr91  = Zr91   4.76096E-03 2
        Zr92  = Zr92   7.27723E-03 2
        Zr94  = Zr94   7.37482E-03 2
        Zr96  = Zr96   1.18812E-03 2
        Sn112  = Sn112   4.67343E-06
        Sn114  = Sn114   3.17985E-06
        Sn115  = Sn115   1.63812E-06
        Sn116  = Sn116   7.00535E-05
        Sn117  = Sn117   3.70020E-05
        Sn118  = Sn118   1.16691E-04
        Sn119  = Sn119   4.13864E-05
        Sn120  = Sn120   1.56970E-04 
        Sn122  = Sn122   2.23073E-05
        Sn124  = Sn124   2.78961E-05
        O16  = O16   3.08132E-04
        O17  = O17   1.17376E-07

* Cladding around H4 fuel
    MIX <<CLAD4>> <<TCool4>> NOEV
        Cr50  = Cr50   3.29613E-06
        Cr52  = Cr52   6.35631E-05
        Cr53  = Cr53   7.20746E-06
        Cr54  = Cr54   1.79408E-06
        Fe54  = Fe54   5.57337E-06
        Fe56  = Fe56   8.74901E-05 2
        Fe57  = Fe57   2.02053E-06
        Fe58  = Fe58   2.68895E-07
        Ni58  = Ni58   2.51627E-05
        Ni60  = Ni60   9.69262E-06
        Ni61  = Ni61   4.21331E-07
        Ni62  = Ni62   1.34344E-06
        Ni64  = Ni64   3.42085E-07
        Zr90  = Zr90   2.18317E-02 2
        Zr91  = Zr91   4.76096E-03 2
        Zr92  = Zr92   7.27723E-03 2
        Zr94  = Zr94   7.37482E-03 2
        Zr96  = Zr96   1.18812E-03 2
        Sn112  = Sn112   4.67343E-06
        Sn114  = Sn114   3.17985E-06
        Sn115  = Sn115   1.63812E-06
        Sn116  = Sn116   7.00535E-05
        Sn117  = Sn117   3.70020E-05
        Sn118  = Sn118   1.16691E-04
        Sn119  = Sn119   4.13864E-05
        Sn120  = Sn120   1.56970E-04 
        Sn122  = Sn122   2.23073E-05
        Sn124  = Sn124   2.78961E-05
        O16  = O16   3.08132E-04
        O17  = O17   1.17376E-07

* Expansion Gaps, heights 1 to 4    
    MIX <<GAP1>> <<TFuel1>> NOEV
        He4      = He4 1.50456E-04
    MIX <<GAP2>> <<TFuel2>> NOEV
        He4      = He4 1.50456E-04
    MIX <<GAP3>> <<TFuel3>> NOEV
        He4      = He4 1.50456E-04
    MIX <<GAP4>> <<TFuel4>> NOEV
        He4      = He4 1.50456E-04

* Moderator in fuel slice 1
    MIX <<MODE1>> <<TCool1>> NOEV
        H1      = H1_H2O <<N_H_1>>  
        O16     = O16    <<N_O_1>> 
* Moderator in lower reflector
    MIX <<MODE_l>> <<TCool_in>> NOEV
        H1      = H1_H2O <<N_H_in>>  
        O16     = O16    <<N_O_in>> 
* Moderator in fuel slice 2
    MIX <<MODE2>> <<TCool2>> NOEV
        H1      = H1_H2O <<N_H_2>>  
        O16     = O16    <<N_O_2>> 

* Moderator in fuel slice 3    
    MIX <<MODE3>> <<TCool3>> NOEV
        H1      = H1_H2O <<N_H_3>>  
        O16     = O16    <<N_O_3>> 

* Moderator in fuel slice 4
    MIX <<MODE4>> <<TCool4>> NOEV
        H1      = H1_H2O <<N_H_4>>  
        O16     = O16    <<N_O_4>>
* Moderator in upper reflector
    MIX <<MODE_u>> COMB <<MODE4>> 1.0
    
;

************************************************* SELF-SHIELDING ************************************************
* 1. TRACKING :

TRACKSS1 := SYBILT: GEOMSS1 ::
    TITLE 'TRACKING AXIAL SLICE 1'
    MAXR 50 QUA2 24 5 DP01 MAXZ 200000 
;


TRACKSS2 := SYBILT: GEOMSS2 ::
    TITLE 'TRACKING AXIAL SLICE 2'
    MAXR 50 QUA2 24 5 DP01 MAXZ 200000 
;


TRACKSS3 := SYBILT: GEOMSS3 ::
    TITLE 'TRACKING AXIAL SLICE 3'
    MAXR 50 QUA2 24 5 DP01 MAXZ 200000 
;


TRACKSS4 := SYBILT: GEOMSS4 ::
    TITLE 'TRACKING AXIAL SLICE 4'
    MAXR 50 QUA2 24 5 DP01 MAXZ 200000 
;


* 2. 2D self-shielding calculation for each fuel slice

LIBRARY2 := USS: LIBRARY TRACKSS1 ::
          EDIT 0
          PASS 3
;

LIBRARY2 := USS: LIBRARY TRACKSS2 LIBRARY2 ::
          EDIT 0
          PASS 3
;

LIBRARY2 := USS: LIBRARY TRACKSS3 LIBRARY2 ::
          EDIT 0
          PASS 3
;

LIBRARY2 := USS: LIBRARY TRACKSS4 LIBRARY2 ::
          EDIT 0
          PASS 3
;

*************************************** 3D NXT+MCCGT TRACKING FOR MOC solution ***************************************

* NXT tracking

INTEGER an2d := 16 ;  ! ANGULAR QUADRATURE PARAMETER
REAL densur := 55.0 ; ! DENSITY OF INTEGRATION LINES CM^-1

TRACK TF_EXC := NXT: GEOM3D ::
  EDIT 1
  ANIS 3
  ALLG
  TISO EQW <<an2d>> <<densur>> REND
;

* MOC tracking
TRACK := MCCGT: TRACK TF_EXC ::
  EDIT 1
  CACB 4 AAC 80 TMT EPSI 1E-5 MCU 50000
;

********************************************* FLUX SOLUTION USING 3D-MOC ************************************************

SYS := ASM: LIBRARY2 TRACK TF_EXC :: ARM EDIT 1 ; 

FLUX := FLU: LIBRARY2 SYS TRACK TF_EXC :: 
             EDIT 1 TYPE K B1 SIGS
      ;
GREP: FLUX :: GETVAL 'K-EFFECTIVE  ' 1 1 1 >>Kinf<< ;
ECHO "SSH tracking = SYBILT and FLX tracking = NXT TISO, 3D-MOC on AT10_24UOX_3D : Kinf=" Kinf ;


********************************************* EDI: and COMPO: for exportation **********************************************


COMPO := COMPO: ::
    EDIT 0
    STEP UP 'EDIREFL'
    MAXCAL 5
    COMM 'AXIAL (Lower/Upper) Reflector calculation, 3D-MOC' ENDC
    PARA 'POS' VALU CHAR
    INIT
;

EDIOBJ := EDI: FLUX LIBRARY2 TRACK ::
            EDIT 0
            MERG MIX 1 0 0 0 0 0 0 0 0 0  
                     0 0 0 0 0 0 0 0 0 0
                     0 0 0 0 0 0 0 0 0 0
            COND 0.625
            SAVE ON EDIREFL
        ;
COMPO := COMPO: COMPO EDIOBJ LIBRARY ::
        EDIT 2
        STEP UP 'EDIREFL'
        POS 'Lower'
;
EDIOBJ := DELETE: EDIOBJ ;

EDIOBJ := EDI: FLUX LIBRARY2 TRACK ::
            EDIT 0
            MERG MIX 0 1 0 0 0 0 0 0 0 0  
                     0 0 0 0 0 0 0 0 0 0
                     0 0 0 0 0 0 0 0 0 0
            COND 0.625
            SAVE ON EDIREFL
        ;
COMPO := COMPO: COMPO EDIOBJ LIBRARY ::
        EDIT 2
        STEP UP 'EDIREFL'
        POS 'Upper'
;

_COMPO := COMPO ;

END: ;
QUIT .

