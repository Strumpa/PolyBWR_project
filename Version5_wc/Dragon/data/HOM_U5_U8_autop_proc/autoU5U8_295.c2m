* CLE-2000 procedure to perform a homogeneous pincell calculation 
* Self-shielding calculation with UFG method and AUTO: module

PARAMETER XSM :: ::: XSM_FILE XSM ; ;

STRING ssh_method ;
:: >>ssh_method<< ;

MODULE GEO: LIB: SYBILT: AUTO: ASM: FLU: EDI: GREP: ;

LINKED_LIST GEOM LIB_AUTO LIB2_AUTO TRACK CP CALC ;

REAL Kinf ;

GEOM := GEO: :: HOMOGE 
        MIX 1
      ;

TRACK := SYBILT: GEOM ::
    TITLE 'HOM_U5_U8 benchmark case'
    MAXR 2
    ;


LIB_AUTO := LIB: ::
    EDIT 1
    NMIX 1    ! MAXIMUM OF MATERIAL MIXTURES
    <<ssh_method>>
    ANIS 1

    MIXS LIB: DRAGON FIL: J311_295

    MIX 1 750.0
      U235  = U235   3.664503E-04 1
      U238  = U238   7.765571E-03 1
      H1    = H1     2.497844E-02
      O16   = O16    2.955339E-02
  ;

LIB2_AUTO := AUTO: LIB_AUTO TRACK :: EDIT 2 GRMIN 52 MAXT 100000 ;

CP := ASM: LIB2_AUTO TRACK :: EDIT 1 PIJ ;

CALC := FLU: CP LIB2_AUTO TRACK :: EDIT 1 TYPE K EXTE 5 ;

GREP: CALC :: GETVAL 'K-INFINITY  ' 1 1 1 >>Kinf<< ;
ECHO 'Kinf AUTO = ' Kinf ;

XSM := EDI: CALC LIB2_AUTO TRACK ::
     EDIT 2 SAVE
     MERG REGI 1
     SAVE MICR 2 U235 U238 
    ;

QUIT "LIST" .