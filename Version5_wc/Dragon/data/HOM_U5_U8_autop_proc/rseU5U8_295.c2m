* CLE-2000 procedure to perform a homogeneous pincell calculation 
* Self-shielding calculation with RSE method and USS: module

PARAMETER XSM :: ::: XSM_FILE XSM ; ;

MODULE GEO: LIB: SYBILT: USS: ASM: FLU: EDI: GREP: ;

LINKED_LIST GEOM LIB_RSE LIB2_RSE TRACK CP CALC ;

REAL Kinf ;

STRING correl ;
:: >>correl<< ;

GEOM := GEO: :: HOMOGE 
        MIX 1
      ;

TRACK := SYBILT: GEOM ::
    TITLE 'HOM_U5_U8 benchmark case'
    MAXR 2
    ;

ECHO "Correlation on/off : " correl ;
IF correl "CORR" = THEN
  LIB_RSE := LIB: ::
      EDIT 1
      NMIX 1    ! MAXIMUM OF MATERIAL MIXTURES
      RSE 5.0E-2
      ANIS 1

      MIXS LIB: DRAGON FIL: J311_295

      MIX 1 750.0
        U235  = U235   3.664503E-04 CORR 1
        U238  = U238   7.765571E-03 CORR 1
        H1    = H1_H2O     2.497844E-02
        O16   = O16    2.955339E-02
    ;

ELSEIF correl "noCORR" = THEN
  LIB_RSE := LIB: ::
      EDIT 1
      NMIX 1    ! MAXIMUM OF MATERIAL MIXTURES
      RSE 5.0E-2
      ANIS 1

      MIXS LIB: DRAGON FIL: J311_295

      MIX 1 750.0
        U235  = U235   3.664503E-04 1
        U238  = U238   7.765571E-03 1
        H1    = H1_H2O     2.497844E-02
        O16   = O16    2.955339E-02
    ;
ENDIF ;

LIB2_RSE := USS: LIB_RSE TRACK :: EDIT 1 PASS 3 ;

CP := ASM: LIB2_RSE TRACK :: EDIT 1 PIJ ;

CALC := FLU: CP LIB2_RSE TRACK :: EDIT 1 TYPE K EXTE 10 ;

GREP: CALC :: GETVAL 'K-INFINITY  ' 1 1 1 >>Kinf<< ;

ECHO 'Kinf RSE = ' Kinf ;

XSM := EDI: CALC LIB2_RSE TRACK ::
     EDIT 2 SAVE
     MERG REGI 1
     SAVE MICR 2 U235 U238 
    ;
QUIT "LIST" .