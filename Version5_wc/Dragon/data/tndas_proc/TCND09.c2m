*----
*  TEST CASE TCND09
*  MOSTELLER BENCHMARK FOR 1-D ANNULAR CELL
*  NDAS 89-GROUP CROSS-SECTION LIBRARY
*  REFLECTIVE AND VOID BC
*
*  REF: None
*
*----
*  Define STRUCTURES and MODULES used
*----
LINKED_LIST
  MOSTELA MOSTELV VOLMAT LIBRARY PIJ FLUX OUT ;
MODULE
  LIB: GEO: SYBILT: SHI: ASM: FLU: EDI: DELETE: END: ;
PROCEDURE assertS ;
*----
*  Microscopic cross sections from file in NDAS format
*----
LIBRARY := LIB: ::
  NMIX 3 CTRA WIMS
  MIXS LIB: NDAS FIL: E65LIB6.idx:E65LIB6.sdb
  MIX 1 600.0                           O16   = O16   4.61309E-2
    U235     = U235     1.66078E-4 1 
    U238     = U238     2.28994E-2 1 
  MIX 2 600.0
    Zr91     = ZR91     3.83243E-2
  MIX 3 600.0
    H1H2O    = H1H2O    4.42326E-2   O16H2O   = O16   2.21163E-2
    B10      = B10      2.02223E-6      B11   = B11   8.19107E-6
  ;
*----
*  Geometry MOSTELA : Annular cell with reflective BC
*           MOSTELV : Annular cell with void BC
*----
MOSTELA := GEO: :: TUBE 3
  RADIUS 0.0 0.39306 0.45802 0.71206
  SPLITR     2       1       1
  MIX        1       2       3
  R+ REFL ;
MOSTELV := GEO: MOSTELA ::
  R+ VOID ;
*----
*  Self-Shielding calculation SYBIL
*  Transport calculation      SYBIL
*  Flux calculation for keff
*----
VOLMAT := SYBILT: MOSTELA ::
  TITLE 'TCND09: SYBIL TRACK MOSTELLER BENCHMARK REFLECTIVE BC '
  MAXR 4 QUA1 5 ;
LIBRARY := SHI: LIBRARY VOLMAT :: EDIT 0 NOLJ ;
PIJ := ASM: LIBRARY VOLMAT ;
FLUX := FLU: PIJ LIBRARY VOLMAT ::
  TYPE K ;
assertS FLUX :: 'K-EFFECTIVE' 1 0.7690296 ;
OUT := EDI: FLUX LIBRARY VOLMAT ::
  EDIT 4 MERG MIX 1 2 3 COND 4.0 SAVE ;
PIJ VOLMAT := DELETE: PIJ VOLMAT ;
VOLMAT := SYBILT: MOSTELV ::
  TITLE 'TCND09: SYBIL TRACK MOSTELLER BENCHMARK VOID BC '
  MAXR 4 QUA1 5 ;
PIJ := ASM: LIBRARY VOLMAT ;
FLUX := FLU: FLUX PIJ LIBRARY VOLMAT ::
  TYPE K ;
assertS FLUX :: 'K-EFFECTIVE' 1 9.919909E-03 ;
OUT := EDI: OUT FLUX LIBRARY VOLMAT ::
  EDIT 4 MERG MIX 1 2 3 COND 4.0 SAVE ;
OUT FLUX PIJ LIBRARY VOLMAT := DELETE:
  OUT FLUX PIJ LIBRARY VOLMAT  ;
ECHO "test TCND09 completed" ;
END: ;  
QUIT "LIST" .
