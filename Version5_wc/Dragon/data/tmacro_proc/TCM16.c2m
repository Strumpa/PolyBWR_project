*----
*  TEST CASE TCM16
*  1D LINEARLY ANISOTROPIC BENCHMARK; DIAMOND DIFFERENCING; SN/PN
*  ONE-GROUP MACROLIB-DEFINED CROSS SECTIONS
*----
*  Define STRUCTURES and MODULES used
*----
LINKED_LIST GEOM MACRO TRACK SYSTEM FLUX EDIT ;
MODULE GEO: MAC: SNT: BIVACT: ASM: FLU: DELETE: END: ;
INTEGER s := 1 ;
INTEGER d := 1 ;
PROCEDURE assertS ;
*
GEOM := GEO: :: CAR1D  10
           X- VOID X+ VOID
           MIX  3 2 2 1 1 1 1 2 2 3
           MESHX -160.0 -120.0 -80.0 -40.0 -20.0 0.0 20.0 40.0 80.0 120.0 160.0
           SPLITX <<s>> <<s>> <<s>> <<s>> <<s>> <<s>> <<s>> <<s>> <<s>> <<s>>
           ;
MACRO := MAC: ::
 EDIT 2 NGRO 1 ANIS 2 NMIX 3 NIFI 1
 READ INPUT
 MIX     1
     TOTAL  0.025
      SCAT  1 1 0.013
            1 1 0.0
     NUSIGF 0.0155   CHI 1.0
 MIX     2
     TOTAL  0.025
      SCAT  1 1 0.024
            1 1 0.006
 MIX     3
     TOTAL  0.075
      SCAT  1 1 0.0
            1 1 0.0
;
*----
* SN (slab) - HIGH ORDER DIAMOND DIFFERENCE
*----
TRACK := SNT: GEOM ::
      TITLE 'ENE6101 BENCHMARK IN 1D SLAB GEOMETRY'
      EDIT 2 MAXR 2500 DIAM <<d>> SN 4 SCAT 2 ;
SYSTEM := ASM: MACRO TRACK ::
      EDIT 1 ARM ;
FLUX :=  FLU: MACRO TRACK SYSTEM ::
      EDIT 2 TYPE K EXTE 100 5.0E-8 ;
assertS FLUX :: 'K-EFFECTIVE' 1 0.9947010 ;
TRACK SYSTEM FLUX := DELETE: TRACK SYSTEM FLUX ;
*----
* PN (slab)
*----
TRACK := BIVACT: GEOM ::
      TITLE 'ANISOTROPIC NSE BENCHMARK 1D, 1 GROUP'
      EDIT 2 MAXR 2500 DUAL 1 3 SPN 5 SCAT 2 ;
SYSTEM := ASM: MACRO TRACK ::
      EDIT 1 ARM ;
FLUX :=  FLU: MACRO TRACK SYSTEM ::
      EDIT 2 TYPE K EXTE 100 5.0E-8 ;
assertS FLUX :: 'K-EFFECTIVE' 1 0.9947010 ;
GEOM TRACK SYSTEM FLUX := DELETE: GEOM TRACK SYSTEM FLUX ;
*
ECHO "test TCM16 completed" ;
END: ;
QUIT "LIST" .
