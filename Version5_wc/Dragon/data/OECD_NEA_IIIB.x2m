*** Cle-2000 procedure to call DRAGON5 and treat OECD/NEA Phase IIIB BWR assembly benchmark.
* Author : R. Guasch
* Ref. : "OECD/NEA Burnup Credit Criticality Benchmarks Phase IIIB: 
* Burnup Calculations of BWR Fuel Assemblies for Storage and Transport"
* Hiroshi OKUNO, Yoshitaka NAITO* and Kenya SUYAMA - JAERI-Research, 
* Report NEA/NSC/DOC(2002)2, 2002.

*********************
* PROCEDURES
*********************

PROCEDURE GEO_8th GEO_8th_A GEO_4th TRK_8th TRK_4th MIX_A ; 

*********************
* MODULE IMPORTS
*********************
MODULE GEO: G2S: SALT: LIB: USS: MCCGT: ASM: FLU: EDI: COMPO: GREP: DELETE: ;

*********************************
* DRAGON VARIABLES DECLARATIONS
*********************************

LINKED_LIST GEOM GEOMSSH TRK TRKSSH LIBRARY LIBRARY2 ;
LINKED_LIST SYS FLUX EDIRATES COMPO ;
SEQ_ASCII _COMPO :: FILE './_CPO_OECD_NEA_PHASE_IIIB_J311_295_WR_SPLITR_NOSECT_inrs1_eighth' ;
SEQ_BINARY TF_EXC TF_EXC_SS ;
SEQ_ASCII GEO_G2S :: FILE 'Fic_SAL_PhaseIIIB.tdt' ;
SEQ_ASCII GEOSSH_G2S :: FILE 'Fic_SAL_PhaseIIIB_SSH.tdt' ;
REAL Kinf ;

STRING symmetry := "eighth" ; ! symmetry of the assembly : "eighth", "quarter", or "full"
STRING exec_case := "FALSE" ; ! 'TRUE' to execute the case, 'FALSE' to skip it
STRING Library := "J311_295" ; ! name of the library to use, e.g. "endfb8r1_295", "J311_295"

* ---
* TRACKING PARAMTERES
* ---
INTEGER an2d := 24 ;  ! ANGULAR QUADRATURE PARAMETER 
REAL densur := 75.0 ; ! DENSITY OF INTEGRATION LINES CM^-1

INTEGER an2d_ss := 12 ;  ! ANGULAR QUADRATURE PARAMETER #18
REAL densur_ss := 35.0 ; ! DENSITY OF INTEGRATION LINES CM^-1 #30

* polar angles for MCCGT:
INTEGER nmu := 4 ;

* size allocation for MCCGT:
INTEGER mcu := 50000 ;

*---
* COMPOSITION options
*---
STRING compos_opt := "VOID_0" ; ! options for composition, e.g. "no

********************************************************************
*       BEGIN 1 Level calculation scheme
********************************************************************

IF symmetry 'eighth' = THEN

    ! Define the flux and self-shielding geometry
    GEOM GEOMSSH := GEO_8th_A ;
    ! Call to tracking module
    GEO_G2S TRK TF_EXC GEOSSH_G2S TRKSSH TF_EXC_SS := TRK_8th GEOM GEOMSSH 
    :: <<an2d>> <<an2d_ss>> <<densur>> <<densur_ss>> <<nmu>> <<mcu>> ;

ELSEIF symmetry 'quarter' = THEN
    GEOM GEOMSSH := GEO_4th ;
    GEO_G2S TRK TF_EXC GEOSSH_G2S TRKSSH TF_EXC_SS := TRK_4th GEOM GEOMSSH
    :: <<an2d>> <<an2d_ss>> <<densur>> <<densur_ss>> <<nmu>> <<mcu>> ;

ELSEIF symmetry 'full' = THEN
!    GEOM GEOMSSH := GEO_A ;
    ECHO "Not implemented yet" ;

ENDIF ;

IF exec_case 'TRUE' = THEN

************************************************
*    LIBRARY DEFINITION : Microlib
************************************************
ECHO "Library definition for Microlib with table of probabilities" ;
LIBRARY := MIX_A :: <<Library>> <<compos_opt>> ;
************************************************
*    USS LIBRARY DEFINITION : Self-Shielding
************************************************

LIBRARY2 := USS: LIBRARY TRKSSH TF_EXC_SS ::
        EDIT 1 PASS 4
        ;
************************************************
*          BEGIN MOC FLUX CALCULATION                                       
************************************************

SYS := ASM: LIBRARY2 TRK TF_EXC :: ARM EDIT 0 ; 

FLUX := FLU: LIBRARY2 SYS TRK TF_EXC :: 
            EDIT 1 TYPE K  
    ;
GREP: FLUX :: GETVAL 'K-INFINITY  ' 1 1 1 >>Kinf<< ;

ECHO "OECD/NEA Phase IIIB VOID0, t=0 calculation completed, Kinf = " Kinf ;



**********************************************************************************************************
********************          EXPORTS FOR POST-TREATMENT OF REACTION RATES               *****************
**********************************************************************************************************
COMPO := COMPO: ::
EDIT 1
STEP UP 'EDI_HOM'
    MAXCAL 5
    COMM 'Reaction rates- Condensed, Homogenized over all fuel cells' ENDC
    ISOT 6 U234 U235 U236 U238 Gd155 Gd157
INIT
STEP UP 'HOM2g'
    MAXCAL 5
    COMM 'Reaction rates - Condensed 2gr, Homogenized' ENDC
    ISOT 6 U234 U235 U236 U238 Gd155 Gd157
INIT
;


*** Condense rates to 2 groups with division between groups at 0.625 eV

EDIRATES := EDI: FLUX LIBRARY2 TRK ::
    EDIT 1
    MICR 6 U234 U235 U236 U238 Gd155 Gd157
    MERG MIX 
        1 1 1 1 ! F010401 F010402 F010403 F010404
        2 2 2 2 ! F020301 F020302 F020303 F020304
        3 3 3 3 ! F030201 F030202 F030203 F030204
        4 4 4 4 ! F040201 F040202 F040203 F040204
        5 5 5 5 ! F050201 F050202 F050203 F050204
        6 6 6 6 6 6 ! F06Gd01 F06Gd02 F06Gd03 F06Gd04 F06Gd05 F06Gd06
        7 7 7 7 ! F070101 F070102 F070103 F070104
        8 8 8 8 ! F080101 F080102 F080103 F080104
        9 9 9 9 ! F090101 F090102 F090103 F090104
        0 0 0 0 0

    COND 0.625
    SAVE ON HOM2g 
    ;

COMPO := COMPO: COMPO EDIRATES LIBRARY2 ::
        EDIT 1
        STEP UP 'HOM2g'
    ;

EDIRATES := DELETE: EDIRATES ;

**** homogenization over all fuel cells
EDIRATES := EDI: FLUX LIBRARY2 TRK ::
    EDIT 1
    MICR 6 U234 U235 U236 U238 Gd155 Gd157
    MERG MIX 
        1 1 1 1 ! F010401 F010402 F010403 F010404
        1 1 1 1 ! F020301 F020302 F020303 F020304
        1 1 1 1 ! F030201 F030202 F030203 F030204
        1 1 1 1 ! F040201 F040202 F040203 F040204
        1 1 1 1 ! F050201 F050202 F050203 F050204
        1 1 1 1 1 1  ! F06Gd01 F06Gd02 F06Gd03 F06Gd04 F06Gd05 F06Gd06
        1 1 1 1 ! F070101 F070102 F070103 F070104
        1 1 1 1 ! F080101 F080102 F080103 F080104
        1 1 1 1 ! F090101 F090102 F090103 F090104
        1 1 1 1 1  ! CLAD WROD COOL MODE BOX
        
    SAVE ON EDIBU_HOM
    ;

COMPO := COMPO: COMPO EDIRATES LIBRARY2 ::
    EDIT 1
    STEP UP 'EDI_HOM'
;

_COMPO := COMPO ;

ENDIF ;


    