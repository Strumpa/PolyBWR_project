*** Cle-2000 procedure to call DRAGON5 and treat OECD/NEA Phase IIIB BWR assembly benchmark.
* Author : R. Guasch
* Ref. : "OECD/NEA Burnup Credit Criticality Benchmarks Phase IIIB: 
* Burnup Calculations of BWR Fuel Assemblies for Storage and Transport"
* Hiroshi OKUNO, Yoshitaka NAITO* and Kenya SUYAMA - JAERI-Research, 
* Report NEA/NSC/DOC(2002)2, 2002.


*********************
* MODULE IMPORTS
*********************
MODULE GEO: G2S: SALT: ;

*********************************
* DRAGON VARIABLES DECLARATIONS
*********************************

LINKED_LIST GEOM GEOMSSH TRK TRKSSH ;
SEQ_BINARY TF_EXC TF_EXC_SS ;
SEQ_ASCII GEO_G2S :: FILE 'Fic_SAL_PhaseIIIB.tdt' ;
SEQ_ASCII GEOSSH_G2S :: FILE 'Fic_SAL_PhaseIIIB_SSH.tdt' ;
SEQ_ASCII FigMain.ps :: FILE 'Fig_PhaseIIIB.ps' ;
SEQ_ASCII FigSSH.ps :: FILE 'Fig_PhaseIIIB_SSH.ps' ;
**********************************
* VARIABLE INITIALIZATION
**********************************
* MIX NUMBERING :
* mix variables are identified by a "position number" and a "rod id" ie composition number. 
* According to Ref. the numbering goes 01_04, 02_03, 03_02, 04_02,  
*                                             05_02, 06_Gd, 07_01,
*                                                    08_01, 09_01,
*                                                              WR,
* This coresponds to the upper diagonal of the top left corner of the assembly.
* Using symmetries, the lower diagonal of the upper right corner is deduced and defined here.
* Numbering of fuel rods is done according to the following scheme :
* F<pos_id><rod_id><ring_id>
! fuel, position 01 (symmetrical equivalent), composition 04, ring number
INTEGER F010401 F010402 F010403 F010404 := 1 2 3 4 ; 
INTEGER F020301 F020302 F020303 F020304 := 5 6 7 8 ; ! fuel, position 02, composition 03
INTEGER F030201 F030202 F030203 F030204 := 9 10 11 12 ; ! fuel, position 03, composition 02
INTEGER F040201 F040202 F040203 F040204 := 13 14 15 16 ; ! fuel, position 04, composition 02
INTEGER F050201 F050202 F050203 F050204 := 17 18 19 20 ; ! fuel, position 05, composition 02
INTEGER F06Gd01 F06Gd02 F06Gd03 F06Gd04 F06Gd05 F06Gd06 := 21 22 23 24 25 26 ; ! fuel, position 06, composition Gd
INTEGER F070101 F070102 F070103 F070104 := 27 28 29 30 ; ! fuel, position 07, composition 01
INTEGER F080101 F080102 F080103 F080104 := 31 32 33 34 ; ! fuel, position 08, composition 01
INTEGER F090101 F090102 F090103 F090104 := 35 36 37 38 ; ! fuel, position 09, composition 01
INTEGER CLAD := 10 ; ! clad, composition 10
INTEGER MODE := 11 ; ! moderating water, composition 11 : in water rod and out-of-assembly water
INTEGER COOL := 12 ; ! coolant water, composition 12 : in assembly water
INTEGER WROD := 13 ; ! cladding of water rod, composition 13
INTEGER BOX := 14 ; ! channel box, composition 14

* Geometrical data

* Assembly pitch
REAL Pitch_A := 15.24 ; ! cm
REAL Pitch_A2 := Pitch_A 2.0 /  ; ! cm
* 1/2 thickness of water gap (between x=0 and channel box)
REAL w_gap := 0.846 ; ! cm
* channel box thickness
REAL chan_thi := 0.254 ; ! cm
REAL xlim := chan_thi w_gap + ; ! cm, x limit of the geometry in frame of R1/R2/TC
* cell pitch
REAL Pitch_C := 1.63 ; ! cm
REAL 2_Pitch_C := Pitch_C 2.0 * ; ! cm
REAL 3_Pitch_C := Pitch_C 3.0 * ; ! cm
REAL 4_Pitch_C := Pitch_C 4.0 * ; ! cm
* Fuel rod radius
REAL fuelRad := 0.529 ; ! cm
* Santamarina radii for UOX cells : 4 subdivisions amouting for 50, 80, 95 and 100% of the fuel rod volume
REAL fRad1 fRad2 fRad3 := 0.374 0.473 0.516 ;
* Santamarina radii for Gd cells : 6 subdivisions amouting for 20, 40, 60, 80, 95 and 100% of the fuel rod volume
REAL fRadGd1 fRadGd2 fRadGd3 fRadGd4 fRadGd5 := 0.237 0.335 0.409 0.473 0.516 ;

* Clad radius
REAL cladRad := 0.615 ; ! cm
* outer water rod radius
REAL waterRad_o := 1.6 ; ! cm
* inner water rod radius
REAL waterRad_i := 1.5 ; ! cm

REAL disp_wr := Pitch_C 2.0 / -1.0 * ; ! cm, displacement of water rod with respect to center of its bounding CARCEL
*** 8th symmetry : SYME at -Y, DIAG at -X, +Y and REFL at +X 

* ---
* TRACKING PARAMTERES
* ---

INTEGER an2d := 18 ;  ! ANGULAR QUADRATURE PARAMETER 
REAL densur := 45.0 ; ! DENSITY OF INTEGRATION LINES CM^-1

INTEGER an2d_ss := 8 ;  ! ANGULAR QUADRATURE PARAMETER #18
REAL densur_ss := 25.0 ; ! DENSITY OF INTEGRATION LINES CM^-1 #30

* polar angles for MCCGT:
INTEGER nmu := 4 ;

* size allocation for MCCGT:
INTEGER mcu := 50000 ;


**************************************************
*               GEOMETRY DEFINITIONS
**************************************************

GEOMSSH := GEO: :: CAR2D 3 3 
    EDIT 1
    X- DIAG X+ REFL
    Y- REFL Y+ DIAG
    CELL WR LAT1 R1 
            LAT2 R2 
                 TC 
    MESHX 0.0 <<Pitch_C>> <<4_Pitch_C>> <<Pitch_A2>> 
    MESHX 0.0 <<Pitch_C>> <<4_Pitch_C>> <<Pitch_A2>>
    ::: WR := GEO: CARCEL 2
            MESHX 0.0 <<Pitch_C>>
            MESHY 0.0 <<Pitch_C>>
            RADIUS 0.0 <<waterRad_i>> <<waterRad_o>>
            OFFCENTER <<disp_wr>> <<disp_wr>>
            MIX <<MODE>> <<WROD>> <<COOL>> ! water, clad, intra-assembly water
        ;
    ::: LAT1 := GEO: CAR2D 3 1 
        MESHX 0.0 <<Pitch_C>> <<2_Pitch_C>> <<3_Pitch_C>>
        MESHY 0.0 <<Pitch_C>>
        CELL C11 C12 C21
        ::: C11 := GEO: CARCEL 5
                MESHX 0.0 <<Pitch_C>>
                MESHY 0.0 <<Pitch_C>>
                RADIUS 0.0 <<fRad1>> <<fRad2>> <<fRad3>> <<fuelRad>> <<cladRad>>
                MIX <<F090101>> <<F090102>> <<F090103>> <<F090104>> <<CLAD>> <<COOL>> 
                ! fuel, clad, intra-assembly water
            ;
        ::: C12 := GEO: C11 MIX <<F070101>> <<F070102>> <<F070103>> <<F070104>> <<CLAD>> <<COOL>> ;
        ::: C21 := GEO: C11 MIX <<F040201>> <<F040202>> <<F040203>> <<F040204>> <<CLAD>> <<COOL>> ;
    ;
    ::: LAT2 := GEO: CAR2D 3 3
        MESHX 0.0 <<Pitch_C>> <<2_Pitch_C>> <<3_Pitch_C>>
        MESHY 0.0 <<Pitch_C>> <<2_Pitch_C>> <<3_Pitch_C>>
        CELL C13 CG1 C22 
             CG1 C23 C31 
             C22 C31 C41
        ::: C13 := GEO: CARCEL 5
                MESHX 0.0 <<Pitch_C>>
                MESHY 0.0 <<Pitch_C>>
                RADIUS 0.0 <<fRad1>> <<fRad2>> <<fRad3>> <<fuelRad>> <<cladRad>>
                MIX <<F080101>> <<F080102>> <<F080103>> <<F080104>> <<CLAD>> <<COOL>>
                ! fuel, clad, intra-assembly water
            ;
        
        ::: C22 := GEO: C13 MIX <<F030201>> <<F030202>> <<F030203>> <<F030204>> <<CLAD>> <<COOL>> ;
        ::: C23 := GEO: C13 MIX <<F050201>> <<F050202>> <<F050203>> <<F050204>> <<CLAD>> <<COOL>> ;
        ::: C31 := GEO: C13 MIX <<F020301>> <<F020302>> <<F020303>> <<F020304>> <<CLAD>> <<COOL>> ;
        ::: C41 := GEO: C13 MIX <<F010401>> <<F010402>> <<F010403>> <<F010404>> <<CLAD>> <<COOL>> ;

        ::: CG1 := GEO: CARCEL 7
                MESHX 0.0 <<Pitch_C>>
                MESHY 0.0 <<Pitch_C>>
                RADIUS 0.0 <<fRadGd1>> <<fRadGd2>> <<fRadGd3>> <<fRadGd4>> <<fRadGd5>> <<fuelRad>> <<cladRad>>
                MIX <<F06Gd01>> <<F06Gd02>> <<F06Gd03>> <<F06Gd04>> <<F06Gd05>> <<F06Gd06>> <<CLAD>> <<COOL>> 
                ! Gd fuel, clad, intra-assembly water
        ; 
    ;
    ::: R1 := GEO: CAR2D 2 1
        MESHX 0.0 <<chan_thi>> <<xlim>>
        MESHY 0.0 <<Pitch_C>>
        MIX <<BOX>> <<MODE>> ! channel box, out-of-assembly water
    ;
    ::: R2 := GEO: CAR2D 2 1
        MESHX 0.0 <<chan_thi>> <<xlim>>
        MESHY 0.0 <<3_Pitch_C>>
        MIX <<BOX>> <<MODE>> ! channel box, out-of-assembly water
    ;
    ::: TC := GEO: CAR2D 2 2
        MESHX 0.0 <<chan_thi>> <<xlim>>
        MESHY 0.0 <<chan_thi>> <<xlim>>
        MIX <<BOX>> <<MODE>> <<BOX>> <<MODE>> ! channel box corner surrounded by out-of-assembly water
    ;
;


GEOM := GEO: :: CAR2D 3 3 
    EDIT 1
    X- DIAG X+ REFL
    Y- REFL Y+ DIAG
    CELL WR LAT1 R1 
            LAT2 R2 
                 TC 
    MESHX 0.0 <<Pitch_C>> <<4_Pitch_C>> <<Pitch_A2>> 
    MESHX 0.0 <<Pitch_C>> <<4_Pitch_C>> <<Pitch_A2>>
    ::: WR := GEO: CARCEL 2
            MESHX 0.0 <<Pitch_C>>
            MESHY 0.0 <<Pitch_C>>
            RADIUS 0.0 <<waterRad_i>> <<waterRad_o>>
            OFFCENTER <<disp_wr>> <<disp_wr>>
            MIX <<MODE>> <<WROD>> <<COOL>> ! water, clad, intra-assembly water
        ;
    ::: LAT1 := GEO: CAR2D 3 1 
        MESHX 0.0 <<Pitch_C>> <<2_Pitch_C>> <<3_Pitch_C>>
        MESHY 0.0 <<Pitch_C>>
        CELL C11 C12 C21
        ::: C11 := GEO: CARCEL 5
                SECT 4 5 
                MESHX 0.0 <<Pitch_C>>
                MESHY 0.0 <<Pitch_C>>
                RADIUS 0.0 <<fRad1>> <<fRad2>> <<fRad3>> <<fuelRad>> <<cladRad>>
                MIX <<F090101>> <<F090102>> <<F090103>> <<F090104>> <<CLAD>> 
                        <<COOL>> <<COOL>> <<COOL>> <<COOL>>
                        <<COOL>> <<COOL>> <<COOL>> <<COOL>>
                        <<COOL>> <<COOL>> <<COOL>> <<COOL>>  ! fuel, clad, intra-assembly water
            ;
        ::: C12 := GEO: C11 MIX <<F070101>> <<F070102>> <<F070103>> <<F070104>> <<CLAD>> 
                        <<COOL>> <<COOL>> <<COOL>> <<COOL>>
                        <<COOL>> <<COOL>> <<COOL>> <<COOL>>
                        <<COOL>> <<COOL>> <<COOL>> <<COOL>>  
            ;
        ::: C21 := GEO: C11 MIX <<F040201>> <<F040202>> <<F040203>> <<F040204>> <<CLAD>> 
                        <<COOL>> <<COOL>> <<COOL>> <<COOL>>
                        <<COOL>> <<COOL>> <<COOL>> <<COOL>>
                        <<COOL>> <<COOL>> <<COOL>> <<COOL>>  
            ;
    ;
    ::: LAT2 := GEO: CAR2D 3 3
        MESHX 0.0 <<Pitch_C>> <<2_Pitch_C>> <<3_Pitch_C>>
        MESHY 0.0 <<Pitch_C>> <<2_Pitch_C>> <<3_Pitch_C>>
        CELL C13 CG1 C22 
             CG1 C23 C31 
             C22 C31 C41
        ::: C13 := GEO: CARCEL 5
                SECT 4 5 
                MESHX 0.0 <<Pitch_C>>
                MESHY 0.0 <<Pitch_C>>
                RADIUS 0.0 <<fRad1>> <<fRad2>> <<fRad3>> <<fuelRad>> <<cladRad>>
                MIX <<F080101>> <<F080102>> <<F080103>> <<F080104>> <<CLAD>> 
                        <<COOL>> <<COOL>> <<COOL>> <<COOL>>
                        <<COOL>> <<COOL>> <<COOL>> <<COOL>>
                        <<COOL>> <<COOL>> <<COOL>> <<COOL>>  ! fuel, clad, intra-assembly water
            ;
        
        ::: C22 := GEO: C13 MIX <<F030201>> <<F030202>> <<F030203>> <<F030204>> <<CLAD>>
                        <<COOL>> <<COOL>> <<COOL>> <<COOL>>
                        <<COOL>> <<COOL>> <<COOL>> <<COOL>>
                        <<COOL>> <<COOL>> <<COOL>> <<COOL>>  
            ;
        ::: C23 := GEO: C13 MIX <<F050201>> <<F050202>> <<F050203>> <<F050204>> <<CLAD>> 
                        <<COOL>> <<COOL>> <<COOL>> <<COOL>>
                        <<COOL>> <<COOL>> <<COOL>> <<COOL>>
                        <<COOL>> <<COOL>> <<COOL>> <<COOL>>  
            ;
        ::: C31 := GEO: C13 MIX <<F020301>> <<F020302>> <<F020303>> <<F020304>> <<CLAD>> 
                        <<COOL>> <<COOL>> <<COOL>> <<COOL>>
                        <<COOL>> <<COOL>> <<COOL>> <<COOL>>
                        <<COOL>> <<COOL>> <<COOL>> <<COOL>>  
            ;
        ::: C41 := GEO: C13 MIX <<F010401>> <<F010402>> <<F010403>> <<F010404>> <<CLAD>> 
                        <<COOL>> <<COOL>> <<COOL>> <<COOL>>
                        <<COOL>> <<COOL>> <<COOL>> <<COOL>>
                        <<COOL>> <<COOL>> <<COOL>> <<COOL>>  
            ;

        ::: CG1 := GEO: CARCEL 7
                SECT 4 7
                MESHX 0.0 <<Pitch_C>>
                MESHY 0.0 <<Pitch_C>>
                RADIUS 0.0 <<fRadGd1>> <<fRadGd2>> <<fRadGd3>> <<fRadGd4>> <<fRadGd5>> <<fuelRad>> <<cladRad>>
                MIX <<F06Gd01>> <<F06Gd02>> <<F06Gd03>> <<F06Gd04>> <<F06Gd05>> <<F06Gd06>> <<CLAD>> 
                        <<COOL>> <<COOL>> <<COOL>> <<COOL>>
                        <<COOL>> <<COOL>> <<COOL>> <<COOL>>
                        <<COOL>> <<COOL>> <<COOL>> <<COOL>>  ! Gd fuel, clad, intra-assembly water
        ; 
    ;
    ::: R1 := GEO: CAR2D 2 1
        MESHX 0.0 <<chan_thi>> <<xlim>>
        MESHY 0.0 <<Pitch_C>>
        MIX <<BOX>> <<MODE>> ! channel box, out-of-assembly water
    ;
    ::: R2 := GEO: CAR2D 2 1
        MESHX 0.0 <<chan_thi>> <<xlim>>
        MESHY 0.0 <<3_Pitch_C>>
        MIX <<BOX>> <<MODE>> ! channel box, out-of-assembly water
    ;
    ::: TC := GEO: CAR2D 2 2
        MESHX 0.0 <<chan_thi>> <<xlim>>
        MESHY 0.0 <<chan_thi>> <<xlim>>
        MIX <<BOX>> <<MODE>> <<BOX>> <<MODE>> 
        ! channel box corner surrounded by out-of-assembly water
    ;
;

! USE MACRO KEYWORD TO INCLUDE MACRO GEOM INFO FOR MULTICELL TRACKING
GEOSSH_G2S FigSSH.ps := G2S: GEOMSSH :: 
! MACRO 
 ;
GEO_G2S FigMain.ps := G2S: GEOM :: ;

! TISO only available option for multicell tracking
! "SAL100: TISO option is incompatible with the surfacic file"
! Check : this probably has something to do with the splitting of cells along the diagonal
TRKSSH TF_EXC_SS := SALT: GEOSSH_G2S ::
  EDIT 10
  ALLG 
  BATCH 100
  TSPC EQW2 <<an2d_ss>> <<densur_ss>> REND
  MERGMIX
;

TRK TF_EXC := SALT: GEO_G2S ::
  EDIT 10
  ALLG 
  BATCH 750
  ANIS 3
  TSPC EQW2 <<an2d>> <<densur>> REND
  LONG
;