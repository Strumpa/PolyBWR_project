*----
* case ASSBLY_CASEA_1level_apex_boron_xsm
* 281-group eighth assembly in DRAGON -- 1-level scheme
* production of an Apex file for reflector feeding
* Author: A. Hebert
*----
HDF5_FILE MyApex :: FILE './MyApex.hdf' ;
LINKED_LIST DONNEES GEOSS GEON2 TRACKSS TRACKN2 LIBRARY LIBRARY2 SYS FLUX2 EDIOBJ ;
MODULE GREP: SYBILT: G2S: SALT: MCCGT: USS: ASM: FLU: EDI: UTL: DELETE: LIB: APX: END: ;
REAL Rcomb1 Rcomb2 Rcomb3 Rcomb4 R_int_TG R_ext_TG R_int_TI R_ext_TI
     R_int_G R_ext_G Cote CoteL RmodeN1 Lame pbore ;
SEQ_ASCII ASSBLY_CASEA :: FILE './ASSBLY_CASEA_1level_g2s.txt' ;
SEQ_ASCII FIGASB_CASEA :: FILE './ASSBLY_CASEA_1level_g2s.eps' ;
SEQ_BINARY TF_EXC ;
INTEGER an2d := 8 ;
REAL densur := 20.0 ;
PROCEDURE Geo_SS Geo_N2 Mix_UOX ;
PROCEDURE assertS ;
 
************************************************************************
*  BEGIN DECLARATION                                                   *
************************************************************************
INTEGER iBoreMax := 5 ;
DONNEES := UTL: ::
    CREA
          PPMBORE <<iBoreMax>> =
            0.0     350.0     700.0    1000.0    1400.0
    ;
************************************************************************
*  END DECLARATION                                                     *
************************************************************************

************************************************************************
*  BEGIN GEOMETRY                                                      *
************************************************************************
EVALUATE Rcomb4 := 0.4096 ;
EVALUATE Rcomb1 := 0.5 SQRT Rcomb4 * ;
EVALUATE Rcomb2 := 0.8 SQRT Rcomb4 * ;
EVALUATE Rcomb3 := 0.95 SQRT Rcomb4 * ;
EVALUATE R_int_TG := 0.5605 ;
EVALUATE R_ext_TG := 0.6225 ;
EVALUATE R_int_TI := 0.5725 ;
EVALUATE R_ext_TI := 0.6225 ;
EVALUATE R_int_G := 0.4180 ;
EVALUATE R_ext_G := 0.4750 ;
EVALUATE Cote := 1.26 ;
EVALUATE CoteL := 1.302 ;
EVALUATE Lame := CoteL Cote - ;
EVALUATE RmodeN1 := 0.670 ;

GEOSS := Geo_SS :: <<Rcomb1>> <<Rcomb2>> <<Rcomb3>> <<Rcomb4>>
                      <<R_int_TG>> <<R_ext_TG>> <<R_int_TI>> 
                      <<R_ext_TI>> <<R_int_G>> <<R_ext_G>> <<Cote>>
                      <<CoteL>> ;

GEON2 := Geo_N2 :: <<Rcomb1>> <<Rcomb2>> <<Rcomb3>> <<Rcomb4>>
                      <<R_int_TG>> <<R_ext_TG>> <<R_int_TI>> 
                      <<R_ext_TI>> <<R_int_G>> <<R_ext_G>> <<Cote>>
                      <<Lame>> ;
ASSBLY_CASEA FIGASB_CASEA := G2S: GEON2 ;

************************************************************************
*  BEGIN TRACKING                                                      *
************************************************************************
! Level One
TRACKSS := SYBILT: GEOSS ::
  EDIT 0
  MAXR 500
  MAXZ  1000000
  TITLE 'TRACKING FOR ASSEMBLY SS'
  QUA2 20 3
  DP01  ;
! MOC tracking for flux calculation
TRACKN2 TF_EXC := SALT: ASSBLY_CASEA ::
  EDIT 3
  ALLG
  TSPC EQW2 <<an2d>> <<densur>> REND
;
TRACKN2 := MCCGT: TRACKN2 TF_EXC ::
  CACB 4 AAC 80 TMT EPSI 1E-5 MCU 10000 ;

************************************************************************
*  END TRACKING                                                        *
************************************************************************

************************************************************************
*  BEGIN MyApex INITIALIZATION                                         *
************************************************************************
  MyApex := APX: ::
    EDIT 10
    NOML pincell_apex
    PARA 'BoronPPM' VALE FLOT
    MACR  TOUT
    REAC  TOTA N2N FISS CHI NUFI ENER LEAK DIFF SCAT ABSO
    ;
************************************************************************
*  END MyApex INITIALIZATION                                           *
************************************************************************

************************************************************************
*  BRANCH BORON LOOP                                                   *
************************************************************************
ECHO "$$$ Branch Boron loop" ;
STRING Library := "D281CEAV5" ;
REAL dens_mod := 0.85 ;

INTEGER iBoreStep := 0 ;
WHILE iBoreStep iBoreMax < DO
  EVALUATE iBoreStep := iBoreStep 1 + ;
  GREP: DONNEES :: GETVAL 'PPMBORE' <<iBoreStep>> >>pbore<< ;
  ECHO "Boron step: " iBoreStep "/" iBoreMax " Boron=" pbore " ppm" ;

************************************************************************
*  BEGIN LIBRARY READING                                               *
************************************************************************
  LIBRARY := Mix_UOX :: <<Library>> <<dens_mod>> <<pbore>> ;

************************************************************************
*  END LIBRARY READING                                                 *
************************************************************************

************************************************************************
*  RESONANCE SELF-SHIELDING                                            *
************************************************************************
  LIBRARY2 := USS: LIBRARY TRACKSS :: EDIT 1 PASS 2 ARM GRMAX 93
    
   CALC REGI W1 U235 ALL

        REGI W1 U238 3
        REGI W2 U238 4
        REGI W3 U238 5
        REGI W4 U238 6

        REGI W1 U238 9
        REGI W2 U238 10
        REGI W3 U238 11
        REGI W4 U238 12

        REGI W1 U238 94
        REGI W2 U238 95
        REGI W3 U238 96
        REGI W4 U238 97

        REGI W1 U238 114
        REGI W2 U238 115
        REGI W3 U238 116
        REGI W4 U238 117

        REGI W1 U238 122
        REGI W2 U238 123
        REGI W3 U238 124
        REGI W4 U238 125

        REGI W1 U238 126
        REGI W2 U238 127
        REGI W3 U238 128
        REGI W4 U238 129

        REGI W1 U238 154
        REGI W2 U238 155
        REGI W3 U238 156
        REGI W4 U238 157

        REGI W1 U238 158
        REGI W2 U238 159
        REGI W3 U238 160
        REGI W4 U238 161
               
   ENDC ;  

************************************************************************
*  BEGIN MOC FLUX CALCULATION                                          *
************************************************************************
  SYS := ASM: LIBRARY2 TRACKN2 TF_EXC :: ARM EDIT 1 ;    

  IF iBoreStep 1 = THEN     
    FLUX2 := FLU: LIBRARY2 SYS TRACKN2 TF_EXC :: EDIT 1 TYPE B B1 SIGS ;
  ELSE
    FLUX2 := FLU: FLUX2 LIBRARY2 SYS TRACKN2 TF_EXC :: EDIT 1 TYPE B B1 SIGS ;
  ENDIF ;

  EDIOBJ := EDI: FLUX2 LIBRARY2 TRACKN2 :: EDIT 5                              
    MERG COMP
    MICR RES
    SAVE ON COND2                                                        
    ;

************************************************************************
*  MyApex FEEDING                                                       *
************************************************************************
  MyApex := APX: MyApex EDIOBJ ::
      EDIT 2
      BoronPPM <<pbore>> ;

  LIBRARY LIBRARY2 SYS EDIOBJ := DELETE: LIBRARY LIBRARY2 SYS EDIOBJ ;
ENDWHILE ; ! boron loop
************************************************************************
*  END OF BORON LOOP                                                   *
************************************************************************
************************************************************************
************************************************************************

assertS FLUX2 :: K-INFINITY 1 0.9571407 ;
ECHO "test ASSBLY_CASEA_1level_apex_boron_xsm completed" ;
END: ;
