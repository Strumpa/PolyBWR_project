************************************************************************************************************************
*                                                                                                                      *
* Procedure :  TRK_C.c2m --> TRK_C_SALT.c2m                                                                            *
* Purpose   :  Tracking and figure printing for pin cells                                                              *
* Author    :  R. Guasch adapted from L.Fede                                                                           *
*                                                                                                                      *
* CALL      :                                                                                                          *
* UOX_TBH FIG1.ps TRACK TF_EXC UOX_TBH_SS FIG_SS.ps TRACK_SS TF_EXC_SS := TRK_C_SALT GEOM GEOM_SS                      *
*                                                                                                                      *
************************************************************************************************************************


* ---
* INPUT & OUTPUT PARAMETERS
* ---
PARAMETER UOX_TBH    FIG1.ps   TRACK    TF_EXC
          UOX_TBH_SS FIG_SS.ps TRACK_SS TF_EXC_SS
          GEOM       GEOM_SS ::
::: SEQ_ASCII UOX_TBH ;
::: SEQ_ASCII FIG1.ps ;
::: LINKED_LIST TRACK ;
::: SEQ_BINARY TF_EXC ;
::: SEQ_ASCII UOX_TBH_SS ;
::: SEQ_ASCII FIG_SS.ps ;
::: LINKED_LIST TRACK_SS ;
::: SEQ_BINARY TF_EXC_SS ;
::: LINKED_LIST GEOM ;
::: LINKED_LIST GEOM_SS ; ;

* ---
* STRUCTURES AND MODULES
* ---
MODULE G2S: MCCGT: SALT: END: ;


INTEGER an2d := 24 ;  ! ANGULAR QUADRATURE PARAMETER
REAL densur := 50.0 ; ! DENSITY OF INTEGRATION LINES CM^-1

INTEGER an2d_SS := 8 ;
REAL densur_SS := 15.0 ;

* polar angles for MCCGT:
INTEGER nmu := 4 ;

* size allocation for MCCGT:
INTEGER mcu := 50000 ;

* ---
* FLUX CALC TRACKING
* ---
UOX_TBH FIG1.ps := G2S: GEOM :: ;

TRACK TF_EXC := SALT: UOX_TBH ::
  EDIT 0
  ALLG
  ANIS 3
  TSPC EQW2 <<an2d>> <<densur>> REND
;

* MOC tracking
TRACK := MCCGT: TRACK TF_EXC ::
  EDIT 0
  CACB <<nmu>> AAC 80 TMT EPSI 1E-5 MCU <<mcu>>
;

* ---
* SELF-SHIELDING TRACKING
* ---
UOX_TBH_SS FIG_SS.ps := G2S: GEOM_SS :: ;

TRACK_SS TF_EXC_SS := SALT: UOX_TBH_SS ::
  EDIT 1
  ALLG
  TSPC <<an2d_SS>> <<densur_SS>> REND
  MERGMIX
;

TRACK_SS := MCCGT: TRACK_SS TF_EXC_SS ::
  EDIT 0
  CACB <<nmu>> AAC 80 TMT EPSI 1E-5 MCU <<mcu>>
;

END: ;
QUIT .
