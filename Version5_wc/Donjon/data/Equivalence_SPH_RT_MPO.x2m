*----
*  TEST CASE Equivalence_SPH_RT_MPO
*  17 X 17 PWR ASSEMBLY + WATER BLADES WITH TRIVAC
*  Input cross section data from Mpo
*----
*  Define STRUCTURES and MODULES used
*----
LINKED_LIST ASSMB TRACK SYSTEM MACRO2 MACRO OPTIM FLUX ;
HDF5_FILE MPOinit_run :: FILE './quick-mpo-oneParam-17-diag.hdf' ;
MODULE GEO: SPH: TRIVAT: TRIVAA: FLUD: UTL: DELETE: END: ;
INTEGER IterEmax := 1000 ;
PROCEDURE SPHPicardRT ;
PROCEDURE assertS ;

REAL side1 := 1.26502 ;
REAL side2 := side1 1.26502 + ;
REAL side3 := side2 1.26502 + ;
REAL side4 := side3 1.26502 + ;
REAL side5 := side4 1.26502 + ;
REAL side6 := side5 1.26502 + ;
REAL side7 := side6 1.26502 + ;
REAL side8 := side7 1.26502 + ;
REAL side9 := side8 1.26502 + ;
REAL side10 := side9 0.0488 + ;
ASSMB := GEO: :: CAR2D 10 10 (*ASSEMBLY 19 X 19*)
          X- DIAG X+ REFL
          Y- SYME Y+ DIAG
          MESHX 0.0 <<side1>> <<side2>> <<side3>> <<side4>> <<side5>>
                <<side6>> <<side7>> <<side8>> <<side9>> <<side10>>
          MIX  1  2  3  4  5  6  7  8  9 10
                 11 12 13 14 15 16 17 18 19
                    20 21 22 23 24 25 26 27
                       28 29 30 31 32 33 34
                          35 36 37 38 39 40
                             41 42 43 44 45
                                46 47 48 49
                                   50 51 52
                                      53 54
                                         55
;

TRACK := TRIVAT: ASSMB ::
     EDIT 1
     MAXR 100  (*MAXIMUM OF 40 REGIONS*)
     TITLE '17x17 FUEL ASSEMBLY'
     DUAL 1 3 ;
     
UTL: TRACK :: DIR ;

*----
* Perform fixed point SPH equivalence
*----
MACRO2 := SPH: MPOinit_run :: EDIT 1 STEP AT 1 MACRO OFF LEAK ;
MACRO OPTIM := SPHPicardRT MACRO2 ASSMB TRACK
   :: 0.0 10.0 1.0E-6 <<IterEmax>> ;
ECHO "control variables at convergence" ;
UTL: OPTIM :: IMPR 'VAR-VALUE' * ;
MACRO2 := DELETE: MACRO2 ;

*----
* Write SPH factors on MPO file
*----
MPOinit_run := SPH: MPOinit_run OPTIM :: EDIT 1 STEP AT 1
   IDEM SPOP EQUI 'UDF_EQV_MINOS_FLXV' ;
OPTIM := DELETE: OPTIM ;

*----
* Verification calculation
*----
SYSTEM := TRIVAA: MACRO TRACK ;
FLUX := FLUD: SYSTEM TRACK :: EDIT 2 ADI 3 EXTE 1.E-07 200 ;
assertS FLUX :: 'K-INFINITY' 1 1.1930146 ;

ECHO "Equivalence_SPH_RT_MPO completed" ;
END: ;
