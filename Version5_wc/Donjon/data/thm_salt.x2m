*=====================================================================*
* FILE:           thm_salt.x2m
* TYPE:           DONJON input file
* AUTHOR:         Cristian Garrido Tamm (cristian.garrido@idom.com)
* PURPOSE:        Single pincell surrounded by FLiBe as coolant to
*                 test the Molten Salt thermalhydraulics in THM
* CREATION DATE:  Wednesday Aug 16, 2023 21:13:01 CEST
* LAST MODIFIED:  Sunday Sep 17, 2023 21:44:30 CEST
*=====================================================================*
*---
* DEFINE MODULES
*---
MODULE      GEO: RESINI: TRIVAT: TRIVAA: GREP: FIND0: NCR: FLUD: FLPOW:
            MACINI: USPLIT: TINST: UTL: DELETE: ABORT: THM: END: ;
*---
* DEFINE PROCEDURES
*---
PROCEDURE   assertS ;
*---
* DEFINE DATA STRUCTURES
*---
LINKED_LIST Geom Track Flux Cpo Matex Lib MicroF MacroF Macro2 Fmap
            System Power Burnup Thm ;
*---
* DEFINE VARIABLES
*---
* Constants
STRING DOUBLE_LINE := 
"====================================================================" ;
STRING SINGLE_LINE := 
"--------------------------------------------------------------------" ;
LOGICAL True False := $True_L $False_L ;
STRING FILENAME := 'thm_salt' ;
INTEGER MaxR := 10000 ;
* Input data
REAL maxh := 80.0 ;
REAL Cote := 1.26  ;
REAL z1 := maxh 20.0 / ;
REAL z2 := maxh 20.0 / z1 + ;
REAL z3 := maxh 20.0 / z2 + ;
REAL z4 := maxh 20.0 / z3 + ;
REAL z5 := maxh 20.0 / z4 + ;
REAL z6 := maxh 20.0 / z5 + ;
REAL z7 := maxh 20.0 / z6 + ;
REAL z8 := maxh 20.0 / z7 + ;
REAL z9 := maxh 20.0 / z8 + ;
REAL z10 := maxh 20.0 / z9 + ;
REAL z11 := maxh 20.0 / z10 + ;
REAL z12 := maxh 20.0 / z11 + ;
REAL z13 := maxh 20.0 / z12 + ;
REAL z14 := maxh 20.0 / z13 + ;
REAL z15 := maxh 20.0 / z14 + ;
REAL z16 := maxh 20.0 / z15 + ;
REAL z17 := maxh 20.0 / z16 + ;
REAL z18 := maxh 20.0 / z17 + ;
REAL z19 := maxh 20.0 / z18 + ;
REAL z20 := maxh 20.0 / z19 + ;
* REAL Fuelpwr := 38.425 ; ! W/g ou kW/kg ou MW/t
REAL Fuelpwr := 380.425 ; ! W/g ou kW/kg ou MW/t
REAL Mass := 4.655149E-3 ; ! kg
REAL Tfuel := 1200.15 ; (*fuel temperature (K)*)
REAL Tcool := 1000.15 ; (*coolant temperature (K)*)
* Options
* Variables
INTEGER istep := 0 ; ! step counter
REAL t tend dt := 0.0 1.0 0.1 ; ! time, end time, time step (s)
REAL Toutlet ;

*---
* BEGIN CALCULATION
*---
ECHO DOUBLE_LINE ;
ECHO 'BEGIN INPUT ' FILENAME ;
ECHO DOUBLE_LINE ;

REAL   powi := Fuelpwr Mass * 1.E3 / ;
ECHO "total reactor power=" powi "MW" ;

Geom := GEO: :: CAR3D 1 1 20
   X- REFL X+ REFL    Y- REFL Y+ REFL    Z- REFL Z+ REFL
   MESHX 0.0 <<Cote>>
   MESHY 0.0 <<Cote>>
   MESHZ 0.0 <<z1>> <<z2>> <<z3>> <<z4>> <<z5>> <<z6>> <<z7>> <<z8>>
         <<z9>> <<z10>> <<z11>> <<z12>> <<z13>> <<z14>> <<z15>> <<z16>>
         <<z17>> <<z18>> <<z19>> <<z20>>
   MIX
   PLANE 1
      1
   PLANE 2  SAME 1
   PLANE 3  SAME 1
   PLANE 4  SAME 1
   PLANE 5  SAME 1
   PLANE 6  SAME 1
   PLANE 7  SAME 1
   PLANE 8  SAME 1
   PLANE 9  SAME 1
   PLANE 10 SAME 1
   PLANE 11 SAME 1
   PLANE 12 SAME 1
   PLANE 13 SAME 1
   PLANE 14 SAME 1
   PLANE 15 SAME 1
   PLANE 16 SAME 1
   PLANE 17 SAME 1
   PLANE 18 SAME 1
   PLANE 19 SAME 1
   PLANE 20 SAME 1
;

Geom Matex := USPLIT: Geom :: NGRP 2 MAXR <<MaxR>>
               NFUEL 1  FMIX  1
            ;

*--
* Fuel map definition
*--
Fmap Matex := RESINI: Matex ::
      ::: GEO: CAR3D 1 1 20
                EDIT  0
                X- REFL X+ REFL    Y- REFL Y+ REFL    Z- REFL Z+ REFL
   MESHX 0.0 <<Cote>>
   MESHY 0.0 <<Cote>>
   MESHZ 0.0 <<z1>> <<z2>> <<z3>> <<z4>> <<z5>> <<z6>> <<z7>> <<z8>>
         <<z9>> <<z10>> <<z11>> <<z12>> <<z13>> <<z14>> <<z15>> <<z16>>
         <<z17>> <<z18>> <<z19>> <<z20>>
   MIX
   PLANE 1
      1
   PLANE 2  SAME 1
   PLANE 3  SAME 1
   PLANE 4  SAME 1
   PLANE 5  SAME 1
   PLANE 6  SAME 1
   PLANE 7  SAME 1
   PLANE 8  SAME 1
   PLANE 9  SAME 1
   PLANE 10 SAME 1
   PLANE 11 SAME 1
   PLANE 12 SAME 1
   PLANE 13 SAME 1
   PLANE 14 SAME 1
   PLANE 15 SAME 1
   PLANE 16 SAME 1
   PLANE 17 SAME 1
   PLANE 18 SAME 1
   PLANE 19 SAME 1
   PLANE 20 SAME 1
;
!
NXNAME '01' NYNAME  'A'
NCOMB 1
B-ZONE 1

ADD-PARAM PNAME 'T-FUEL' PARKEY 'TFA' GLOBAL
ADD-PARAM PNAME 'T-COOL' PARKEY 'TCA' GLOBAL
BTYPE INST-BURN
INST-BVAL CHAN 0.0
REACTOR-POW <<powi>> AXIAL-PFORM 1.0 1.0 1.0 1.0 1.0 1.0 1.0 1.0 1.0 1.0
                                 1.0 1.0 1.0 1.0 1.0 1.0 1.0 1.0 1.0 1.0
SET-PARAM 'T-FUEL' <<Tfuel>>
SET-PARAM 'T-COOL' <<Tcool>>
FUEL WEIGHT <<Mass>>
;

* UTL: Fmap :: STEP UP PARAM STEP AT 4 DIR IMPR P-NAME * ;
*--
* THM single-stage calculation
*--
Thm Fmap := THM: Fmap ::
    EDIT 5
    FLUID SALT LiF-BeF2 0.66-0.34 
    FPUISS 0.93
    INLET 101325.0 1000.15
*    INLET-Q 7.9770045E-5 (*m2*) 0.375 (*inlet mass flow rate kg/s*)
    SPEED 3.0
    ASSMB 1 0
    RADIUS 4.55063E-3 4.60169E-3 5.00184E-3 5.00184E-3 (* m *)
    RODMESH 15 20
    MONO
    POWER-LAW 1E3 12
    0.0 0.5
    0.1 1.0
    0.2 2.0
    0.3 3.0
    0.4 4.0
    0.5 5.0
    0.6 5.1
    0.7 5.2
    0.8 5.3
    0.9 5.4
    1.0 5.5
    2.0 5.5
;

WHILE t tend <= DO
  EVALUATE istep := istep 1 + ;
  ECHO "istep=" istep "t=" t "dt=" dt ;

  Thm Fmap := THM: Thm Fmap ::
    EDIT 5
    FLUID SALT LiF-BeF2 0.66-0.34 
    TIME <<t>> <<dt>>
  ;
  EVALUATE t := t dt + ;
ENDWHILE ;

*--
* Dump THM object and Asserts
*--
UTL: Thm :: DIR DUMP ;
assertS Thm :: 'AVG-T-FUEL' 1 1.412878E3 ;
assertS Thm :: 'AVG-T-COOL' 1 1.016132E3 ;

*---
* END CALCULATION
*---
ECHO DOUBLE_LINE ;
ECHO 'END INPUT ' FILENAME ;
ECHO DOUBLE_LINE ;

END: ;
QUIT .

