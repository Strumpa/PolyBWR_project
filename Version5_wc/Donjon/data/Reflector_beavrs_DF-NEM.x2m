*---
*  Nodal Expansion Reflector DF-NEM equivalence prodedure
*---
LINKED_LIST Geom FVol Param MACRO ;
STRING nameFeed := "assbly_caseA_mpo_boron.hdf" ;
STRING nameRefl := "reflector_beavrs_DF-NEM_mpo.hdf" ;
STRING Palier := "BEAVRS" ;
STRING htype := "DF-NEM" ;
STRING LibType := "CLA99CEA93" ;
STRING NuclData := "CLA99CEA93:CLA99CEA93_SS" ;
STRING OptSph := "NOSP" ;
STRING OptNoal := "ALBE" ;
STRING OptSS := "TONE" ;
MODULE GEO: UTL: MCR: END: ;
HDF5_FILE MpoRefl :: FILE <<nameRefl>> ;
PROCEDURE REFL_MPO ;
!
Geom := GEO: ::
   CAR1D 12
   X- REFL X+ VOID
   MESHX -43.0 -21.5 -5.0 0.0 0.005 2.2225 3.9825 9.6975 10.1975 15.9125 21.5 21.505
         35.9125
   MIX     1  1  1     2        3         4         3          5          6          5
           !  fuel   gap       ss    ss+h2o        ss        h2o  ss_shield        h2o
                 7            8
           !   gap          h2o
   SPLITX  20 15 20  1         25        24        10          5          6          5
                 1            5
;

* Define volume fractions in 8 mixtures
* mix=1 : feeding assembly (first fraction is always set to 0)
* mix=2 : fuel-reflector gap
* mix=7 : right-most gap to compute albedo
* mix=8 : residual right reflector
INTEGER nbZones := 8 ;
REAL fvacier := 0.05 ;
REAL fveau := 1.0 fvacier - ;
FVol := UTL: ::
                  ! MIX  1  2  3  4           5  6  7  8
CREA 'H2O' <<nbZones>> = 0. 0. 0. <<fveau>>   1. 0. 1. 1.
CREA 'SS ' <<nbZones>> = 0. 1. 1. <<fvacier>> 0. 1. 0. 0.
;
UTL: FVol :: DUMP ;

* Define feeding assembly parameters (0 to 3 parameters allowed)
Param := UTL: :: CREA PARAMVALU 1 = 975.
                 STEP UP 'PARAMNAME' NEW LIST 1
                 CREA 1 2 = 'C-BO' 'RE  ' ;
UTL: Param :: DUMP ;

MpoRefl := REFL_MPO Geom FVol Param :: <<nameFeed>> <<nameRefl>> <<Palier>> <<htype>>
                            <<LibType>> <<NuclData>> <<OptSph>> <<OptNoal>>
                            <<OptSS>> ;

************************************************************************
* MPO extrapolation of the reflector at 350 ppm Boron
************************************************************************
REAL densB1 := 350.0 ;
MACRO :=  MCR:  MpoRefl :: EDIT 3 MACRO NMIX 1
   MPO MpoRefl output_0
   MIX  1 FROM 2 SET 'C-BORE' <<densB1>> SET 'Method' <<htype>> SET 'Palier' <<Palier>> ENDMIX
;
UTL: MACRO :: DIR DUMP ;
ECHO "test Reflector_beavrs_DF-NEM.x2m completed" ;
END: ;
